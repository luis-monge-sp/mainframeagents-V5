*********************************************************************
***                                                              ****
***   SA-AGENT VERSION 4.0.XX                                    ****
***                                                              ****
***   (C) Copyright 1999-2004 BMC Software, Inc.                 ****
***   All rights reserved.                                       ****
***                                                              ****
***********************************************************************
*                                                                     *
* MACRO  NAME : CTSRRSTB                                              *
*                                                                     *
* DESCRIPTION : MACRO to BE USED by customer customization of RRSF    *
*               support table                                         *
*                                                                     *
* FUNCTION:     Table has three parts: Header, Security Entries,      *
*               and all-zero last Entry, all generated by this macro  *
*                                                                     *
* ATTRIBUTES  : The data-only module created by this macro is linked  *
*               with CTSEVX01 exit as =V(CTSRSSTB)                    *
*                                                                     *
* RESTRICTIONS : Module generated with this macro must be linked with *
*                RENT attribute of CTSEVX01                           *
*                                                                     *
*                Turning ON flag WTODBG1 must be done with care       *
*                only upon demand of Control-SA development.          *
*                This flag cause debug display of password change     *
*                information on z/OS console(s)                       *
*                                                                     *
* INPUT :        Example of data-only module created with this macro. *
*                PRINT GEN                                            *
*                CTSRRSTB START=YES,CTSDUMMY=$#@$$@#$                 *
*                CTSRRSTB SRCUSER=STCUSER5,SRCNODE=OS35               *
*                CTSRRSTB SRCUSER=STCUSER9,SRCNODE=MVS9               *
*                CTSRRSTB END                                         *
*                END                                                  *
*                                                                     *
*                In above example, the dummy user $#@$$@#$ is         *
*                used in RRSF directed ALTUSER command that envelops  *
*                password change msg between Control-SA components.   *
*                This should never be a real customer-RACF user.      *
*                                                                     *
*                In above example, the security is specified by two   *
*                elements: Source-Node where non-managed AONI runs,   *
*                and the Source RACF user under which AONI runs. Only *
*                these two source user & node combinations are allowed*
*                to direct ALTUSER special command to CTSEVX01 exit   *
*                in managed node.                                     *
*                                                                     *
*                Security can have lesser granularity, by specifying  *
*                just Source Users, or it cand have higher granularity*
*                by specifying also the Local-User to which ALTUSER   *
*                command can be directed, as per these 2 examples:    *
*                CTSRRSTB SRCUSER=STCRRSF                             *
*                CTSRRSTB SRCUSER=STCRRSF9,SRCNODE=MVS9,LCLUSER=Q17W  *
*                                                                     *
*                However, in one table, the highest number of         *
*                elements (0,1,2 or 3) in any entry is discovered,    *
*                placed in the table header, and only entries with    *
*                that highest number of elements are searched in at   *
*                CTSEVX01 exit run-time.                              *
*                                                                     *
* CHANGE ACTIVITY :                                                   *
*                                                                     *
*    19/02/2002  CREATED                                              *
*                                                                     *
***********************************************************************
*
***********************************************************************
         MACRO
&CTSRRSL CTSRRSTB &SRCUSER=*,&SRCNODE=*,&LCLUSER=*,&TYPE=CSECT,        &
               &END=NO,&START=NO,&CTSDUMY=CTSDUMMY,&WTODBG1=0
         GBLA &CTSIX,&CTSHIGH,ENTRYC
         LCLA &CTSCNT,&LENDUMY
         AIF  ('&TYPE' EQ 'CSECT').CHKSTRT
CTSRRSTB DSECT             Used by CTSEVX01 exit code
EYECATCH DS   CL11         eye catch constant
HDRFLAGS DS   X            table header flags
WTOYES   EQU X'01'         turn on debug wto
LENDUMMY DS   H            length of dummy user
CTSDUMMY DS   CL8          dummy user, otherwise never a real user
ENTRYC   DS   H            counter of security entries in the table
ENTRYH   DS   H            highest (0,1,2,or 3) fields in any entry
ENTRYF   EQU  *            first entry here
ENTRYLEN EQU  26           entry length (also header length)
.CHKSTRT AIF ('&START' NE 'YES').CHKLAST
&LENDUMY SETA   K'&CTSDUMY
&CTSRRSL CSECT             start actual table
EYECATCH DC  CL11'RRSF TABLE:'
HDRFLAGS DC  AL1(&WTODBG1)
LENDUMMY DC  H'&LENDUMY'
CTSDUMMY DC  CL8'&CTSDUMY'
ENTRYC   DS  H
ENTRYH   DS  H
ENTRYLEN EQU 26
ENTRYF   EQU *
         MEXIT
.CHKLAST AIF ('&END' EQ 'YES').ENTRLST
&CTSIX   SETA &CTSIX+1
&CTSCNT  SETA 3
.CKLUSER AIF ('&LCLUSER' NE '*').CKSNODE
&CTSCNT  SETA &CTSCNT-1
.CKSNODE AIF ('&SRCNODE' NE '*').CKSUSER
&CTSCNT  SETA &CTSCNT-1
.CKSUSER AIF ('&SRCUSER' NE '*').ENTRYDC
&CTSCNT  SETA &CTSCNT-1
.ENTRYDC ANOP
ENTRY&CTSIX DC H'&CTSCNT' number of fields in this entry 0,1,2 or 3
            DC CL8'&SRCUSER',CL8'&SRCNODE',CL8'&LCLUSER'
&ENTRYC  SETA &ENTRYC+1
         AIF (&CTSHIGH LT &CTSCNT).CTSWAP
         MEXIT
.CTSWAP  ANOP
&CTSHIGH SETA &CTSCNT
         MEXIT
.ENTRLST ANOP
         ORG  ENTRYC
         DC H'&ENTRYC'          number of security entries in table
         ORG  ENTRYH
         DC H'&CTSHIGH'         highest number of fields in any entry
         ORG
ENTRYL   DC XL(ENTRYLEN)'00'
         MEND
