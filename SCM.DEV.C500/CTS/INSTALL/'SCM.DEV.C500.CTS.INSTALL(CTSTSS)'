//%JOBNAME%SD JOB %JOBCARD%                                             00010000
//********************************************************************* 00020000
//*                                                                   * 00030000
//*  THIS JOB PROVIDES THE REQUIRED CONNECTOR-FOR-TSS SECURITY        * 00040000
//*  DEFINITIONS OF CA-TOP-SECRET (TSS)                               * 00050000
//*                                                                   * 00060000
//*  THE TSS COMMANDS IN THIS JOB ARE ONLY EXAMPLES. PLEASE REVIEW    * 00061000
//*  THEM CAREFULLY AND CHANGE ACCORDING TO SITE SECURITY STANDARDS   * 00062000
//*  BEFORE SUBMITTING THE JOB.                                       * 00063000
//*                                                                   * 00064000
//*                                                                   * 00065000
//********************************************************************* 00066000
//*                                                                     00067000
//*  1. THIS JOB HAS TO RUN UNDER A USER ID THAT IS ALLOWED TO DEFINE   00068000
//*     SECURITY DEFINITIONS.                                           00069000
//*                                                                     00069100
//*  2. ASSIGNING GID AND UID VALUES                                    00069200
//*     THERE ARE SEVERAL WAYS TO ASSIGN THE VALUE FOR GID AND UID      00069300
//*     DEPENDING ON Z/OS AND TSS VERSIONS.                             00069400
//*     2.1 - ASSIGN A UNIQUE NUMBER BY SPECIFYING                      00069500
//*              GID(GGGGGG) OR UID(UUUUUU)                             00069600
//*           WHERE GGGGGG AND UUUUUU ARE UNIQUE GID AND UID VALUES.    00069700
//*     2.2 - ASK TSS TO ASSIGN THE FIRST FREE NUMBER AS GID AND UID    00069800
//*           USING THE AUTOUID AND AUTOGID FEATURES, BY SPECIFYING     00069900
//*              UID(?)[,RANGE(<-LOW->,<-HIGH->)] OR                    00070000
//*              GID(?)[,RANGE(<-LOW->,<-HIGH->)]                       00080000
//*           WHERE <-LOW-> AND <-HIGH-> ARE THE LOW AND HIGH VALUES    00090000
//*           FOR THE RANGE FROM WHICH THE UID AND GID SHOULD BE        00100000
//*           SELECTED. IF RANGE IS NOT SPECIFIED, TSS PICKS A NUMBER   00110000
//*           FROM THE DEFAULT RANGE,                                   00111000
//*     2.3 - USE DEFAULT GID AND UID:                                  00112000
//*           DEFINE ACIDS WITH DEFAULT OMVS ATTRIBUTES INCLUDING UID   00113000
//*           OR GID.                                                   00114000
//*           SET THESE ACIDS AS DEFAULT USING THE "OMVSGRP" AND        00115000
//*           "OMVSUSR" CONTROL OPTIONS OR THE "MODLUSER" CONTROL       00116000
//*           OPTION (DEPENDING ON TSS AND/OR Z/OS VERSIONS).           00117000
//*           THE GID AND UID PARAMETERS SHOULD BE OMITTED FROM THE     00118000
//*           TSS COMMANDS BELOW.                                       00119000
//*     2.4 - ASK THE SYSTEM TO ASSIGN UID AND GID WHEN UNIX SYSTEM     00120000
//*           SERVICES ARE REQUESTED. THIS FEATURE IS CONTROLLED BY     00130000
//*           THE "UNIQUSER" CONTROL OPTION.                            00140000
//*           THE GID AND UID PARAMETERS SHOULD BE OMITTED FROM THE     00150000
//*           TSS COMMANDS BELOW.                                       00160000
//*     A DETAILED DESCRIPTION OF THE DIFFERENT WAYS TO ASSIGN THE      00170000
//*     UID AND GID CAN BE FOUND IN THE "UNIX SECURITY" CHAPTER IN      00180000
//*     "CA-TOP-SECRET FOR Z/OS COOKBOOK" AND IN THE CONTROL            00190000
//*     OPTIONS DESCRIPTION IN "CA-TOP-SECRET FOR Z/OS CONTROL          00200000
//*     OPTIONS GUIDE".                                                 00210000
//*                                                                     00220000
//********************************************************************* 00230000
//*                                                                     00240000
//TSOBATCH  EXEC PGM=%IKJEFT01%                                         00250000
//SYSLBC    DD  DISP=SHR,DSN=%BRODCAST%                                 00260000
//SYSTSPRT  DD  SYSOUT=%HOLDCLASS%                                      00270000
//SYSTSIN   DD   *                                                      00280000
                                                                        00290000
   /*==========================  1  ===========================*/       00300000
   /*   1. DEFINE SA-AGENT AS MULTIUSER FACILITY TO TSS.       */       00310000
   /*      CTSA FACILITY IS ASSOCIATED WITH AGENT VIA PGM=CTS. */       00320000
   /*                                                          */       00330000
   /*      FIND A DUMMY FACILITY ENTRY AND REPLACE <-N-> WITH  */       00340000
   /*      THE DUMMY FACILITY ENTRY NUMBER TO BE REPLACED.     */       00350000
   /*                                                          */       00360000
   /*      NOTE:                                               */       00360100
   /*      THIS DEFINITION IS REQUIRED ONLY ONCE, ON THE FIRST */       00361000
   /*      TIME CONNECTOR-FOR-TSS IS INSTALLED IN THE SYSTEM.  */       00362000
   /*      SUBSEQUENT INSTALLATIONS CAN USE THE SAME FACILITY. */       00363000
   /*      THEREFORE, BEFORE RUNNING THE JOB, CHECK IF THE     */       00364000
   /*      CTSA FACILITY IS ALREADY DEFINED, WITH THE REQURIED */       00365000
   /*      ATTRIBUTES. IF IT IS ALREADY DEFINED, DEACTIVATE    */       00366000
   /*      THE COMMANDS BELOW THAT DEFINE IT.                  */       00367000
   /*                                                          */       00369100
   /*      *** ATTENTION ***                                   */       00370000
   /*      THE DEFINITIONS BELOW SHOULD BE COPIED TO THE       */       00380000
   /*      CA-TOP SECRET PARAMETERS MEMBER TO BE RETAINED      */       00390000
   /*      AFTER THE NEXT IPL.                                 */       00400000
   /*                                                          */       00410000
                                                                        00420000
   TSS MODIFY FAC(USER<-N->=NAME=CTSA)                                  00430000
   TSS MODIFY FAC(CTSA=PGM=CTS)                                         00440000
   TSS MODIFY FAC(CTSA=SHRPRF,ASUBM,NOABEND,MULTIUSER)                  00450000
   TSS MODIFY FAC(CTSA=LUMSG,NOSTMSG,SIGN(M),NORNDPW,NOXDEF)            00460000
   TSS MODIFY FAC(CTSA=NOAUDIT,WARNPW,ACTIVE,NOTSOC)                    00470000
   TSS MODIFY FAC(CTSA=MODE=FAIL)                                       00480000
   /*==========================================================*/       00490000
                                                                        00549000
   /*==========================  2  ===========================*/       00550000
   /* 2. CREATE SCA ACID FOR CONNECTOR-FOR-TSS STARTED TASKS.  */       00560000
                                                                        00570000
   TSS CREATE(%PROCPREFS%ACID) -                                        00580000
       TYPE(SCA) PASSWORD(NOPW) FACILITY(STC) -                         00590000
       NAME('SA-AGENT STARTED TASKS')                                   00600000
   /*==========================================================*/       00601000
                                                                        00602000
   /*==========================  3  ===========================*/       00603000
   /* 3. ADD THE CTSA FACILITY AS MASTFAC                      */       00604000
   /*    THIS REQUIRES OPTION(72) OF TSS PARAMETERS TO BE      */       00605000
   /*    ACTIVE.                                               */       00606000
                                                                        00607000
   TSS ADD(%PROCPREFS%ACID) MASTFAC(CTSA)                               00608000
   /*==========================================================*/       00609000
                                                                        00610000
   /*==========================  4  ===========================*/       00611000
   /* 4. CREATE A DEPARTMENT CONTAINER AND A GROUP TO HOLD     */       00612000
   /*    CONNECTOR-FOR-TSS STARTED TASKS.                      */       00613000
   /*    THE GROUP ASSIGNED TO CTSGATE MUST HAVE A GID VALUE.  */       00614000
   /*    CHOOSE AND SET THE GID VALUE ACCORDING TO THE         */       00615000
   /*    COMMENTS AT THE BEGINNIG OF THIS JOB.                 */       00616000
                                                                        00617000
   TSS CREATE(%PROCPREFS%DEPT) -                                        00618000
       TYPE(DEPARTMENT) NAME('SA-AGENT DEPARTMENT')                     00619000
                                                                        00620000
   TSS CREATE(%PROCPREFS%GROUP) -                                       00630000
       TYPE(GROUP) NAME('SA-AGENT GROUP') DEPT(%PROCPREFS%DEPT)         00640000
                                                                        00650000
   TSS ADDTO(%PROCPREFS%GROUP) GID(<-GID->)                             00660000
   /*==========================================================*/       00670000
                                                                        00680000
   /*==========================  5  ===========================*/       00690000
   /* 5. ASSIGN THE GROUP AND UNIX SYSTEM SERVICES ATTRIBUTES  */       00700000
   /*    TO THE CONNECTOR-FOR-TSS ACID.                        */       00710000
   /*                                                          */       00720000
   /*    5.1 CTSGATE ACID MUST BE DEFINED AS A UNIX SYSTEM     */       00730000
   /*        SERVICES USER:                                    */       00740000
   /*        - ASSIGN A UNIX SYSTEM SERVICES GROUP             */       00750000
   /*        - ASSIGN A UNIX SYSTEM SERVICES UID TO THE USER.  */       00760000
   /*          CHOOSE THE UID VALUE ACCORDING TO THE COMMENTS  */       00770000
   /*          AT THE BEGINNIG OF THIS JOB.                    */       00780000
   /*        - ASSIGN THE USER TO A DEFAULT GROUP              */       00790000
   /*                                                          */       00800000
   /*    5.2. THE VALUE SET OR DEFAULETD FOR THE OEFILEP       */       00810000
   /*         PARAMETER FOR THE CTSGATE STARTED TASK ACID      */       00820000
   /*         MUST BE GREATER THAN 9.                          */       00821000
   /*         THE DEFAULT VALUE FOR  OEFILEP IS THE VALUE      */       00822000
   /*         SET FOR MAXFILEPROC IN BPXPRMXX SYSTEM PARAMETER */       00823000
   /*         MEMBER.                                          */       00824000
   /*         IF THE DEFAULT IS LESS THAN 9, SET THE OEFILEP   */       00825000
   /*         ATTRIBUTE TO CTSGATE ACID BY ACTIVATING THE      */       00826000
   /*         ADDTO FOR OEFILEP COMMAND BELOW AND SETTING THE  */       00827000
   /*         APPROPRIATE VALUE FOR OEFILEP.  THE COMMONLY     */       00828000
   /*         RECOMMENDED VALUE IS 256.                        */       00829000
                                                                        00830000
   TSS ADDTO(%PROCPREFS%ACID) UID(<-UID->) -                            00831000
       DFLTGRP(%PROCPREFS%GROUP) GROUP(%PROCPREFS%GROUP)                00832000
   /* TSS ADDTO(%PROCPREFS%ACID) OEFILEP(255)     */                    00833000
                                                                        00834000
   /*==========================================================*/       00835000
                                                                        00836000
   /*==========================  6  ===========================*/       00837000
   /* 6. ASSOCIATE CONNECTOR-FOR-TSS STARTED TASKS WITH        */       00838000
   /*    CONNECTOR-FOR-TSS ACID.                               */       00839000
                                                                        00840000
   TSS ADDTO(STC) PROCNAME(%PROCPREFS%GATE) ACID(%PROCPREFS%ACID)       00850000
   TSS ADDTO(STC) PROCNAME(%PROCPREFS%ACS) ACID(%PROCPREFS%ACID)        00860000
   TSS ADDTO(STC) PROCNAME(%PROCPREFS%ACD) ACID(%PROCPREFS%ACID)        00870000
   TSS ADDTO(STC) PROCNAME(%PROCPREFS%AONI) ACID(%PROCPREFS%ACID)       00880000
   TSS ADDTO(STC) PROCNAME(%PROCPREFS%OFLI) ACID(%PROCPREFS%ACID)       00890000
   /*==========================================================*/       00900000
                                                                        00910000
   /*==========================  7  ===========================*/       00920000
   /* 7. GRANT CONNECTOR-FOR-TSS ACID WITH AUTHORITY TO LIST   */       00930000
   /*    ALL DATA                                              */       00940000
                                                                        00950000
   TSS ADMIN(%PROCPREFS%ACID) ACID(INFO,REPORT,AUDIT)                   00960000
   TSS ADMIN(%PROCPREFS%ACID) MISC8(LISTSTC,LISTRDT)                    00970000
   TSS ADMIN(%PROCPREFS%ACID) MISC9(GENERIC)                            00980000
   TSS ADMIN(%PROCPREFS%ACID) RESOURCE(INFO,REPORT)                     00990000
   TSS ADMIN(%PROCPREFS%ACID) DATA(ALL,PROFILE,ACIDS,PASSWORD)          01000000
   /*==========================================================*/       01001000
                                                                        01002000
   /*==========================  8  ===========================*/       01003000
   /* 8. GRANT CTSGATE WITH AUTHOITY TO USE TCP/IP STACK.      */       01004000
   /*                                                          */       01005000
   /*    THIS PERMISSION IS REQUIRED ONLY WHEN TSS SERVAUTH    */       01006000
   /*    RESOURCE CLASS IS DEFINED TO PROTECT TCP/IP RESOURCES */       01007000
   /*    FROM UNAUTHORIZED ACCESS.                             */       01008000
   /*                                                          */       01009000
                                                                        01010000
   /* TSS PERMIT(%PROCPREFS%ACID) -                            */       01020000
   /*    SERVAUTH(EZB.STACKACCESS.<-SYSNAME->.<-TCPIPID->)     */       01030000
   /*==========================================================*/       01030100
                                                                        01030300
   /*==========================  9  ===========================*/       01030400
   /* 9. ADD THE CTSA FACILITY TO THE USERS                    */       01030500
   /*    USE THE COMMAND APPROPRIATE ACCORDNG TO THE Z/OS      */       01030600
   /*    VERSION.                                              */       01030700
                                                                        01030800
   /*    IN Z/OS 2.1 AND BELOW:                                */       01030900
   /*    ASSIGN CTSA FACILITY AUTHORIZATION TO MANAGED SYSTEM  */       01031000
   /*    ADMINISTRATORS.                                       */       01031100
   /*    REPLACE <-MS-ADMIN-> IN THE COMMAND BELOW WITH THE    */       01031200
   /*    MANAGED SYSTEM ADMINISTRATOR ACID.                    */       01031300
   /*    IF MULTIPLE MANAGED SYSTEM ADMINISTRATORS ARE USED,   */       01031400
   /*    ADD COMMAND SHOULD BE ISSUED FOR EACH ONE OF THEM.    */       01031500
   /*                                                                   01031600
   /* TSS ADD(<-MS-ADMIN->) FACILITY(CTSA)                     */       01031700
   /*                                                                   01031800
                                                                        01031900
   /*    IN Z/OS 2.2 AND ABOVE THE CTSA FACILITY SHOULD BE SET */       01032000
   /*    TO ALL ACID:                                          */       01032100
   /*                                                                   01032200
   /* TSS ADD(ALL) FACILITY(CTSA)                              */       01032300
   /*==========================================================*/       01032400
                                                                        01032500
   /*==========================  10  ==========================*/       01032600
   /* 10. PROTECT CONNECTOR-FOR-TSS DATASETS                   */       01032700
                                                                        01032800
   TSS ADDTO(%PROCPREFS%ACID) DSN(%ILPREFS%.%ILVERS%) -                 01032900
       NOPERMIT                                                         01033000
   TSS ADDTO(%PROCPREFS%ACID) DSN(%OLPREFS%.%OLVERS%) -                 01033100
       NOPERMIT                                                         01033200
                                                                        01033300
   TSS PERMIT(ALL) DSN(%ILPREFS%.%ILVERS%.CLIST) -                      01033400
       ACC(R)                                                           01033500
   TSS PERMIT(ALL) DSN(%OLPREFS%.%OLVERS%.DIAGLVL) -                    01034000
       ACC(R)                                                           01035000
   TSS PERMIT(ALL) DSN(%OLPREFS%.%OLVERS%.USER.CLIST) -                 01036000
       ACC(R)                                                           01037000
   TSS PERMIT(ALL) DSN(%OLPREFS%.%OLVERS%.ENCREXT) -                    01038000
       ACC(N)                                                           01039000
   TSS PERMIT(ALL) DSN(%OLPREFS%.%OLVERS%.ENCRINT) -                    01040000
       ACC(N)                                                           01050000
                                                                        01060000
   /*   PERMIT ONLY WHEN STCJOBS ARE USED.                     */       01070000
   /*   IN THE LAST PERMIT, REPLCE <-MS-ADMIN-> WITH           */       01080000
   /*   MANAGED SYSTEM ADMINISTRATOR USERID.                   */       01090000
   /*   IF MULTIPLE MANAGED SYSTEM ADMINISTRATORS ARE USED     */       01100000
   /*   A PERMIT COMMAND SHOULD BE ADDED FOR EACH ONE OF THEM. */       01110000
                                                                        01120000
   TSS PERMIT(ALL) DSN(%OLPREFS%.%OLVERS%.EXECOUT.*) -                  01130000
       ACC(N)                                                           01140000
   TSS PERMIT(%PROCPREFS%ACID) -                                        01150000
       DSN(%OLPREFS%.%OLVERS%.EXECOUT.*) -                              01160000
       ACC(A)                                                           01170000
   TSS PERMIT(<-MS-ADMIN->) -                                           01180000
       DSN(%OLPREFS%.%OLVERS%.EXECOUT.*) -                              01181000
       ACC(UPDATE)                                                      01182000
                                                                        01183000
   /*   PERMIT ONLY WHEN STCJOBS ARE USED AND CTSBATCH         */       01184000
   /*   UTILITY WILL BE USED.                                  */       01184100
   /*   REPLACE <-BATCH-USERID-> WITH THE USERID OF THE        */       01184200
   /*   USER THAT WILL BE RUNNING CTSBATCH.                    */       01184300
   /*   REPLACE <-BATCH-ADMIN-> WITH THE USERID OF THE         */       01184400
   /*   ADMINISTRATOR PROVIDED TO CTSBATCH.                    */       01184500
   /*                                                          */       01184600
   TSS PERMIT(<-BATCH-USERID->) -                                       01184700
       DSN(%OLPREFS%.%OLVERS%.EXECOUT.%PROCPREFS%BATCH) -               01184800
       ACC(A)                                                           01184900
   TSS PERMIT(<-BATCH-ADMIN->) -                                        01185000
       DSN(%OLPREFS%.%OLVERS%.EXECOUT.%PROCPREFS%BATCH) -               01185100
       ACC(UPDATE)                                                      01185200
   /*==========================================================*/       01185300
                                                                        01185400
   /*==========================  11  ==========================*/       01185500
   /* 11. PERMIT ALL CONNECTOR-FOR-TSS INSTALLATION AND        */       01185600
   /*     OPERATION LIBRARIES TO BE ACCESSED BY                */       01185700
   /*     CONNECTOR-FOR-TSS STARTED TASKS                      */       01185800
                                                                        01185900
   TSS PERMIT(%PROCPREFS%ACID) DSN(%ILPREFS%.%ILVERS%.) ACC(A)          01186000
   TSS PERMIT(%PROCPREFS%ACID) DSN(%OLPREFS%.%OLVERS%.) ACC(A)          01187000
   /*==========================================================*/       01188000
                                                                        01189000
   /*==========================  12  ==========================*/       01190000
   /* 12. PERMIT MANAGED SYSTEM ADMINISTRATOR TO USE THE NOPW  */       01200000
   /*     OPTION WHEN ADDING A NEW USERID TO TSS.              */       01210000
   /*     DECIDE WHETHER ADDTO OR PERMIT SHOULD BE USED.       */       01220000
   /*                                                                   01230000
   /*   REPLACE <-MS-ADMIN-> WITH MANAGED SYSTEM ADMINISTRATOR */       01240000
   /*   ACID.                                                  */       01250000
   /*   IF MULTIPLE MANAGED SYSTEM ADMINISTRATORS ARE USED     */       01260000
   /*   ADD OR PERMIT COMMANDS SHOULD BE ADDED FOR EACH ONE.   */       01270000
                                                                        01280000
   /* TSS ADDTO(<-MS-ADMIN->) CASECAUT(TSSCMD.USER.REPLACE.NOPW) */     01290000
   /* TSS ADDTO(<-MS-ADMIN->) CASECAUT(TSSCMD.USER.ADDTO.NOPW)   */     01300000
   /* TSS ADDTO(<-MS-ADMIN->) CASECAUT(TSSCMD.USER.CREATE.NOPW)  */     01301000
   /*                                                            */     01302000
   /*   -- OR --                                                 */     01303000
   /*                                                            */     01304000
   /* TSS PERMIT(<-MS-ADMIN->) ACCESS(UPDATE) -                  */     01305000
   /*     CASECAUT(TSSCMD.USER.REPLACE.NOPW)                     */     01306000
   /* TSS PERMIT(<-MS-ADMIN->) ACCESS(UPDATE) -                  */     01307000
   /*     CASECAUT(TSSCMD.USER.ADDTO.NOPW)                       */     01308000
   /* TSS PERMIT(<-MS-ADMIN->) ACCESS(UPDATE) -                  */     01309000
   /*     CASECAUT(TSSCMD.USER.CREATE.NOPW)                      */     01310000
   /*==========================================================*/       01310100
/*                                                                      01310200
//                                                                      01310300
