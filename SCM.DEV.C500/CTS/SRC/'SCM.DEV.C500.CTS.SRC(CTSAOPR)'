*
****************************************************************
****                                                        ****
****     CONTROL-SA  RELEASE 1.3.0                          ****
****                                                        ****
****     LICENCED MATERIAL - NEW DIMENSION SOFTWARE LTD.    ****
****                                                        ****
****************************************************************
*
****************************************************************
*                                                              *
* TITLE            : CTSAOPR                                   *
*                                                              *
* FILE NAME        : CTSAOPR                                   *
*                                                              *
* AUTHOR           : ALEX SHVARTSMAN                           *
*                                                              *
* CREATION DATE    : 31/08/94                                  *
*                                                              *
* DESCRIPTION      : ISSUE OPERATOR COMMANDS                   *
*                                                              *
*                                                              *
* INPUT            : PDEBUG       -  DEBUG LEVEL               *
*                    PCMDLEN      -  COMMAND LEN               *
*                    PCMD         -  COMMAND                   *
*                                                              *
* OUTPUT           : NONE                                      *
*                                                              *
* RETURN VALUE     : VIA REG.15                                *
*                                                              *
* ASSUMPTIONS AND                                              *
*   CONSIDERATIONS :                                           *
*                                                              *
****************************************************************
*
****************************************************************
* MOD.ID   WHO      WHEN     DESCRIPTION                       *
* -------- -------- -------- --------------------------------- *
*                   DD/MM/YY                                   *
*                                                              *
****************************************************************
*
PARM     DSECT
PDEBUG   DS    A                       ADDR DEBUG LEVEL ( F )
PCMDLEN  DS    A                       ADDR COMMAND LEN
PCMD     DS    A                       ADDR COMMAND
*
CTSAOPR  CSECT
CTSAOPR  AMODE 31
CTSAOPR  RMODE ANY
         BEGIN *
         CTSEQUR
         CTSLEVEL
*
         LR    R10,R1              ADDR PARAMETER LIST
         USING PARM,R10
*
         L     R2,PDEBUG           R2 -> DEBUG LEVEL (4 BYTES)
         XC    DEBUGL,DEBUGL       STORE DEBUG LEVEL
         ICM   R2,15,0(R2)         DEBUG LEVEL
         BNP   NODEBUG             SKIP DEBUG LVL IF MINUS
         STC   R2,DEBUGL           STORE DEBUG LEVEL (1 BYTE)
NODEBUG  DS    0H
         CTSADBG 'ENTER',                                              *
               LEVEL=(DEBUGL,1,2,3,4,5)
*
         XC    RESERVED,RESERVED
         L     R2,PCMDLEN              ADDR COMMAND LEN
         L     R2,0(R2)                COMMAND LEN
         L     R3,PCMD
         BCTR  R2,0                    DECR FOR EX COMMAND
         EX    R2,MOVECMD              MOVE COMMAND TO BUFFER
         LA    R2,1(R2)
         LA    R2,4(R2)                DESCRIPTOR LENGTH
         STH   R2,LENGTH
         CTSADBG LEVEL=(DEBUGL,1,2,3,4,5),                             *
               'COMMAND BUFFER = ~,_',(LENGTH,COMMAND,30)
*
         MODESET MODE=SUP,KEY=ZERO
         XR    R0,R0                   CLEAR CONSOLE ID                 00850500
         LA    R1,CMDBUF               POINT TO COMMAND BUFFER          00850600
         SVC   34                      ISSUE COMMAND                    00850700
         LR    R5,R15
         MODESET MODE=PROB,KEY=NZERO
         LR    R15,R5
*
*-----------------------------------------------------------------
*                  EXIT
*-----------------------------------------------------------------
         DS    0H
         CTSADBG LEVEL=(DEBUGL,1,2,3,4,5),                             *
               'EXITING WITH RC = #',(?R15)
*
         BRTRN 0
*
*---------------------------------------------------------------------*
*        CONSTANTS                                                    *
*---------------------------------------------------------------------*
         LTORG
DEBUGL   DS    X                       DEBUG LEVEL
MOVECMD  MVC   COMMAND(*-*),0(R3)      MOVE COMMAND TO BUFFER
CMDBUF   DS   0F                       COMMAND BUFFER
LENGTH   DS   H                                                         01000000
RESERVED DS   H                                                         01010000
COMMAND  DS   CL128                                                     01020000
         END
