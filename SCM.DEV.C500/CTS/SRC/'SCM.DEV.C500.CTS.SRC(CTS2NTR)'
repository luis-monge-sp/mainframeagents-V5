          START
****************************************************************
****                                                        ****
****     CONTROL-SA  RELEASE 2.1.0                          ****
****                                                        ****
****     LICENCED MATERIAL - NEW DIMENSION SOFTWARE LTD.    ****
****                                                        ****
****************************************************************
*
******************************************************************
*                                                                *
* TITLE            : ACF2 INFOSTOR RECORDS EXTRACTION (ACNTRY)   *
*                                                                *
* FILE NAME        : CTS2NTR                                     *
*                                                                *
* AUTHOR           : YISHAY YOVEL                                *
*                                                                *
* CREATION DATE    : 22/06/97                                    *
*                                                                *
* DESCRIPTION      : GET INFOSTOR RECORDS                        *
*                                                                *
* PARAMETERS       :                                             *
*                                                                *
* INPUT             RECORD STRUCTURE                             *
*                   CURRENT_RECORD_STRUCTURE                     *
*                                                                *
* OUTPUT            LAST LID TO UPDATE RECORD                    *
*                   LAST TOD TO UPDATE RECORD                    *
*                   ARE BUFFER LENGTH                            *
*                   ARE BUFFER ADDRESS                           *
*                   RC                                           *
*                   MSG                                          *
*                                                                *
* DEPENDENCIES     : ACCVT - ACF2 CVT CONTROL BLOCK              *
*                    ACNTRY - INFOSTORAGE ACCESS API             *
*                                                                *
* CHANGE ACTIVITY :                                              *
* 27/04/05 IS0258   AL DROP INTERNAL STCKCONV-USE SYS1.MACLIB VER*
* 01/11/16 SAS2IBMT ST SAS/C TO IBM C CONVERSION PROJECT:        *
*                      THIS ROUTINE SHOULD RETURN RC (R15) 0     *
* 14/10/24 IS10188  AL MOVE OUR MODULES ABOVE 16M LINE           *
******************************************************************
*                                                              *
****************************************************************
****************************************************************
*        PARAMETERS BLOCK                                      *
****************************************************************
*
PARM       DSECT
PARMREC    DS    A                       RECORD STRUCTURE
PARMCUR    DS    A                       CURRENT_RECORD_STRUCTURE
PARMLID    DS    A                       LAST LID TO UPDATE RECORD
PARMTOD    DS    A                       LAST TOD TO UPDATE RECORD
PARMBUFL   DS    A                       ARE BUFFER LENGTH
PARMBUF    DS    A                       ARE BUFFER ADDRESS
PARMRC     DS    A                       RC
PARMMSG    DS    A                       MSG
NULL       EQU   C' '
YES        EQU   C'Y'
****************************************************************
* RECORD STRUCTURE: SPECIFYING WHAT SHOULD BE RETRIEVED        *
****************************************************************
RECSTRU    DSECT
RS_SYSID_MASKED DS  C        IS SYSID MASKED (Y/N)
RS_RECID_MASKED DS  C        IS RECID MASKED (Y/N)
RS_SYSID_LENGTH DS  H        SYSID COMPONENT LENGTH
RS_RECID_LENGTH DS  H        RECID COMPONENT LENGTH
RS_FULL_KEY     DS  0CL44
RS_CLASS        DS  C        RECORD CLASS
RS_TYPE         DS  CL3      RECORD TYPE
RS_KEY          DS  CL40     SYSID+RECID
*
CTS2NTR    CSECT
CTS2NTR    AMODE ANY                                            IS10188
CTS2NTR    RMODE ANY                                            IS10188
           BEGIN
           CTSEQUR
           LR    R11,R1
           USING PARM,R11
****************************************************************
* RESET PARAMTER BLOCK FOR ACFSVC CALL TYPE ACNTRY             *
****************************************************************
           XC    ACNTRY(ACNTBLKL),ACNTRY
           XC    SVCMSG(L'SVCMSG),SVCMSG
****************************************************************
* BUILD PARAMTER BLOCK FOR ISSUING A REQUEST                   *
****************************************************************
BUILD_BLOCK DS    0H
           MVI   ACNTFCN,ACNTFC0A    - ACNTRY REQUEST
           MVI   ACNTSFCN,ACNF$FRT   - RETURN FORMATTED RECORD
           MVC   ACNTMSG,=A(SVCMSG)  - SVC MSG
           L     R4,PARMREC
           USING RECSTRU,R4
           CLI   RS_SYSID_MASKED,YES
           BNE   NO_SYSID_MASK
           OI    ACNTFLGC,ACNC$MSI   - YES, SET IT
           OI    ACNTFLGC,ACNC$SEQ   - SET INDEX-SEQ PROCESSING
NO_SYSID_MASK  DS 0H
           CLI   RS_RECID_MASKED,YES
           BNE   NO_RECID_MASK
           OI    ACNTFLGC,ACNC$MRI   - YES, SET IT
           OI    ACNTFLGC,ACNC$SEQ   - SET INDEX-SEQ PROCESSING
NO_RECID_MASK  DS 0H
*
BUILD_KEY_FIELD DS 0H
           MVC   ACNTSCLS,RS_CLASS
           MVC   ACNTSTYP,RS_TYPE
           MVC   ACNTSIRI,RS_KEY
*
           MVC   ACNTPSIL,RS_SYSID_LENGTH
           MVC   ACNTPRIL,RS_RECID_LENGTH
*
           L     R4,PARMCUR
           CLI   RS_FULL_KEY,NULL
           BE    NO_CURRENT_KEY
           MVC   ACNTLAST,RS_FULL_KEY
NO_CURRENT_KEY DS 0H
           MVC   ACNTAREB,PARMBUF   - SET ARE BUFFER
           L     R5,PARMBUFL
           MVC   ACNTAREL,0(R5)     - SET ARE BUFFER LENGTH
****************************************************************
* OBATIN ACCVT ADDRESSABILITY                                  *
****************************************************************
           ACFINCVT R6,NONE=EXIT_NOACF2
           USING  ACCVT,R6
****************************************************************
* ISSUE API CALL TO ACF2                                       *
****************************************************************
           ACFSVC ACNTRY,TYPE=A,NONE=EXIT_NOACF2,CVT=HAVE
           B     *+4(R15)
           B     EXIT_OK
           B     EXIT_ACCESS_DENIED
           B     EXIT_CONTROL_BLOCK_ERROR
           B     EXIT_NOACF2
           B     EXIT_RC
****************************************************************
* ACCESS DENIED : USUALLY, EOF                                 *
****************************************************************
EXIT_ACCESS_DENIED DS 0H
           LA    R15,4
           B     EXIT_RC
****************************************************************
* CONTROL_BLOCK_ERROR : INTERNAL ERROR                         *
****************************************************************
EXIT_CONTROL_BLOCK_ERROR DS 0H
           LA    R15,8
           B     EXIT_RC
****************************************************************
* ACF2 NOT ACTIVE                                              *
****************************************************************
EXIT_NOACF2        DS   0H
           LA    R15,12
           B     EXIT_RC
****************************************************************
* CALL SUCCESSFULL                                             *
****************************************************************
EXIT_OK    DS   0H
           L    R4,PARMCUR
           MVC  RS_FULL_KEY,ACNTLAST
           MVC  RS_SYSID_LENGTH,ACNTPSIL
           MVC  RS_RECID_LENGTH,ACNTPRIL
           L    R4,PARMLID
           MVC  0(L'ACNTLLID,R4),ACNTLLID
****************************************************************
* PREPARE FOR LAST_UPDATE_TOD PROCESSING, PERFORM ACF2 ADJUST  *
* OF TIMESTAMP (BASED ON ACF2 OPTS CPUTIME: GMT/LOCAL          *
****************************************************************
ADJUST_ACF2_TIMESTAMP DS 0H
           CLI  ACCCGMT,X'00'    - IF GMT NO ADJUST
           BE   NO_ADJUST
           L    R5,16(0,0)       - GET CVT ADDRESS
           L    R6,CVTTZ-CVT(,R5)
           A    R6,ACNTLTOD
           ST   R6,ACNTLTOD
****************************************************************
* CONVERT ACF2 STCK TO TOD FORMAT                              *
****************************************************************
NO_ADJUST  DS   0H
           STCKCONV  STCKVAL=ACNTLTOD,CONVVAL=CONVTIME,                X
               DATETYPE=YYYYDDD,TIMETYPE=BIN
           L    R4,PARMTOD
           L    R5,CONVTIME+8
           SLL  R5,4
           ST   R5,0(R4)
           NC   0(4,R4),=X'00FFFFFF'
           OI   3(R4),X'0F'
           MVC  4(4,R4),CONVTIME
           XR   R15,R15
           B    EXIT_RC
****************************************************************
* EXIT WITH RC                                                 *
****************************************************************
EXIT_RC    DS   0H
           L    R14,PARMRC
           ST   R15,0(R14)
           L    R14,PARMMSG
           MVC  0(124,R14),SVCMSG+4
           BRTRN 0                                             SAS2IBMT
           LTORG
SVCMSG     DS    CL128
CONVTIME   DS    D
           DS    D
           ACNTRY DSECT=NO
           ACCVT
           CVT  DSECT=YES
           END
