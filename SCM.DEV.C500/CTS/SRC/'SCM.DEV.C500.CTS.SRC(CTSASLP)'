         TITLE 'CTSASLP - SLEEP FOR A FEW SECONDS'                      00040000
*                                                                       00050000
****************************************************************        00060000
****                                                        ****        00070000
****     CONTROL-SA  RELEASE 4.0.00                         ****        00080000
****                                                        ****        00090000
****     (C) COPYRIGHT 2015 SAILPOINT TECHNOLOGIES, INC.,   ****        00100000
****     ALL RIGHTS RESERVED.                               ****        00110000
****                                                        ****        00110000
****************************************************************        00120000
*                                                                       00130000
****************************************************************        00140000
*                                                              *        00150000
* TITLE            : WAIT FOR THE REQUESTED AMOUNT OF TIME.    *        00160000
*                                                              *        00170000
* FILE NAME        : CTSASLP                                   *        00180000
*                                                              *        00190000
* AUTHOR           : NURIT YAHALOM                             *        00200000
*                                                              *        00210000
* CREATION DATE    : 23/01/17                                  *        00220000
*                                                              *        00230000
* DESCRIPTION      : THIS ROUTINE REPLACES THE SLEEP() C       *        00240000
*                    BECUASE THE SLEEP() C ROUTINE NEEDS THE   *        00230000
*                    POSIX RUNTIME OPTION, WHICH WE ARR NOT    *        00230000
*                    USING.                                    *        00230000
*                                                              *        00230000
*                    THIS ROUTINE RECIEVES THE AMOUNT OF       *        00270000
*                    SECONDS TO WAIT AND ISSUES STIMERM TO     *        00250000
*                    WAIT.                                     *        00250000
*                                                              *        00250000
* PARAMETERS       :  1. TIME TO WAIT. (F)                     *        00240000
*                  :  2. TIME TYPE (C)                         *        00240000
*                        S - SECONDS.                          *        00250000
*                        H - HUNDREDTH OF SECONDS.             *        00250000
*                  :  3. DEBUG LEVEL (F)                       *        00250000
*                                                              *        00250000
* ERTURN CODES     :  0 - OK.                                  *        00240000
*                  :  4 - TIME TO WAIT IS 0.                   *        00240000
*                  :  8 - PARAMETERS ERROR.                    *        00240000
*                  : 12 - GETMAIN FAILED.                      *        00240000
*                                                              *        00250000
*                                                              *        00250000
* ASSUMPTIONS AND                                              *        00260000
*   CONSIDERATIONS :                                           *        00270000
*                                                              *        00304000
****************************************************************        00310000
*                                                                       00320000
****************************************************************        00330000
* MOD.ID   WHO      WHEN     DESCRIPTION                       *        00340000
* -------- -------- -------- --------------------------------- *        00350000
*          SAS2IBMN 23/01/17 INITIAL VERSION                   *
****************************************************************        00380000
CTSASLP  CSECT                                                          00390000
CTSASLP  RMODE ANY                                                      00400000
CTSASLP  AMODE ANY                                                      00410000
*
         USING CTSASLP,R15
         SAVE  (14,12),,*                                               00530000
         CTSLEVEL
*
         LR    R12,R15                 SET BASE REGISTER.
         DROP  R15
         USING CTSASLP,R12
*
*
**   ALLOCATE WORK AREA AND CHAIN SAVE AREAS.
*
         LR    R10,R1                  R10 -> PARMS LIST.
         USING PRM,R10                 R10 POINTS TO INPUT PARMS
         GETMAIN RC,LV=WALEN,LOC=24
         CHI   R15,0
         JNE   RC12                    GETMAIN FAILED.
*
         ST    R13,4(,R1)
         ST    R1,8(,R13)
         LR    R13,R1
         USING WA,R13
*
**   TAKE DEBUG LEVEL.
*
         MVI   DBGLEVEL,0              ZERO DEBUG LEVEL.
         L     R4,PRMDBG               R4 -> DEBUG LEVEL (4 BYTES)
         LA    R4,0(,R4)               CLEAR AMODE BIT.
         CHI   R4,0                    DEBUG LEVEL PROVIDED ?
         JE    AFTDBGL
         L     R4,0(,R4)               R4 = DEBUG LEVEL
         CHI   R4,0                    VALID DEBUG LEVEL ?
         JNH   AFTDBGL                 ..NO - NO DEBUG.
         STC   R4,DBGLEVEL             STORE DEBUG LEVEL (1 BYTE)
AFTDBGL  DS    0H
*
**   TAKE TIME TO WAIT
*
         L     R4,PRMTIME              R4 -> TIME
         CTSADBG LEVEL=(DBGLEVEL,1,2,3,4,5,6),                         X00885600
               'TIME PARM = ~ -> #',(?R4,0(R4)),                       X00885600
               RAREA=RAREA,DBGAREA=DBGAREA
         LA    R4,0(,R4)               CLEAR AMODE BIT.
         CHI   R4,0                    TIME PROVIDED ?
         JE    RC8                     ..NO - ERROR.
         L     R4,0(,R4)
         CHI   R4,0                    TIME = 0 ?
         JE    RC4                     DO NOT WAIT.
*
**   TAKE TIME TYPE
*
         L     R5,PRMTMTYP             R5 -> TIME TYPE
         CTSADBG LEVEL=(DBGLEVEL,1,2,3,4,5,6),                         X00885600
               'TIME TYPE PARM = ~ -> _',(?R5,0(R5),1),                X00885600
               RAREA=RAREA,DBGAREA=DBGAREA
         LA    R5,0(,R5)               CLEAR AMODE BIT.
         CHI   R5,0                    TIME TYPE PROVIDED ?
         JE    RC8                     ..NO - ERROR.
*
*
         CLI   0(R5),C'H'              TIME IN HUNDRESTH OF SECONDS ?
         JE    WAIT                    ...YES - GO WAIT.
         CLI   0(R5),C'S'              TIME IN SECONDS ?
         JNE   RC8                     ..NO - PARAMETER ERROR.
         MHI   R4,100                  SET TIME IN HUNDREDTH OF SECONDS
*
**   WAIT
*
WAIT     DS    0H
         ST    R4,WAITIME
         CTSADBG LEVEL=(DBGLEVEL,1,2,3,4,5,6),                         X00885600
               'WAIT # HANDREDTH OF SECONDS',(WAITIME),                X00885600
               RAREA=RAREA,DBGAREA=DBGAREA
         MVC   STIMERL(STIMERLL),STIMERM
         STIMERM SET,BINTVL=WAITIME,WAIT=YES,ID=ID,MF=(E,STIMERL)
*
**   SET RETURN CODE AND RETURN
*
RC0      DS    0H                      OK
         LHI   R15,0
         J     RETURN
RC4      DS    0H                      TIME TO WAIT = 0.
         LHI   R15,4
         J     RETURN
RC8      DS    0H                      PARAMETERS ERROR.
         LHI   R15,8
         J     RETURN
RC12     DS    0H                      GETMAIN FAILED.
         LHI   R15,12
         J     OUT
*
RETURN   DS    0H
         CTSADBG LEVEL=(DBGLEVEL,1,2,3,4,5,6),                         X00885600
               'RETURN: RC = #',(?R15),                                X00885600
               RAREA=RAREA,DBGAREA=DBGAREA
         LR    R5,R15                  SAVE RC
         LR    R1,R13                  R1 -> ALLOCTED AREA.
         L     R13,4(,R13)
         FREEMAIN RC,LV=WALEN,A=(R1)
         LR    R15,R5              RESTORE RC.
*
OUT      DS    0H
         RETURN (14,12),RC=(15)
         DROP  ,
         LTORG ,
*
**   CONSTANTS
*
STIMERM  STIMERM SET,MF=L
*
**   WORK AREA
*
WA       DSECT
SA       DS    18F
*
DBGLEVEL DS    A
ID       DS    A                   STIMER ID.
WAITIME  DS    A                   WAIT TIME.
*
STIMERL  STIMERM SET,MF=L
STIMERLL EQU   *-STIMERL
*
         DS    0D
RAREA    DS    XL512               USED BY CTSADBG.
DBGAREA  DS    XL2048                                                   01950100
WALEN    EQU   *-WA
*
**   INTERNAL DSECTS
*
         CTSEQUR ,
*
PRM      DSECT
PRMTIME  DS    A           F       -> SECONDS TO WAIT.
PRMTMTYP DS    A           C       -> S(CONDS) / H(HANDREDTH OF SEC)
PRMDBG   DS    A           F       -> DEBUF LEVEL.
*
         END
