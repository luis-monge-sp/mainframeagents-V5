****************************************************************
****                                                        ****
****     CONTROL-SA  RELEASE 3.2.00                         ****
****                                                        ****
****     LICENSED MATERIAL - EAGLEEYE CONTROL SOFTWARE LTD. ****
****                                                        ****
****************************************************************
*
****************************************************************
*                                                              *
* TITLE            : CTSAOLT                                   *
*                                                              *
* FILE NAME        : CTSAOLT                                   *
*                                                              *
* AUTHOR           : AVNER LIVNY                               *
*                                                              *
* CREATION DATE    : 11 FEB 2004                      (IS0225) *
*                                                              *
* DESCRIPTION      : RETURN OS LOCAL DATE & TIME INCLUDING     *
*                    MILLISECONDS.                             *
*                    USED BY CTSCMVS & CTSOMVS.                *
*                                                              *
****************************************************************
*                                                              *
* INPUT:             NONE                                      *
* -----                                                        *
*                                                              *
* OUTPUT:            PYEAR                                     *
* -------            PMONTH                                    *
*                    PDAY                                      *
*                    PHH                                       *
*                    PMM                                       *
*                    PSS                                       *
*                    PMILLI                                    *
*                                                              *
* ALL RETURNED VALUES ARE POINTER TO SHORT (BINARY 2 BYTES).   *
*                                                              *
* CHANGE ACTIVITY :                                            *
*                                                              *
****************************************************************
*SAS2IBMN NY 17/01/17 CHANGE RMODE TO ANY (NO NEED FOR 24)     *
****************************************************************
         START
R0       EQU   0                       TEMPORARY WORK REGISTER
R1       EQU   1                       PARMS AT ENTRY
R2       EQU   2
R3       EQU   3                       WORK
R4       EQU   4                       WORK
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11                      PARAMETERS LIST
R12      EQU   12                      PROGRAM BASE REGISTER
R13      EQU   13                      CALLER'S SAVE AREA
R14      EQU   14                      RETURN ADDRESS
R15      EQU   15                      RETURN CODE (ALWAYS 0)
*
CTSAOLT  CSECT
CTSAOLT  AMODE 31
*SAS2IBMN CTSAOLT  RMODE 24
CTSAOLT  RMODE ANY                                             SAS2IBMN
*
****************************************************************
*        INITIALIZE
****************************************************************
*
*        SAVE REGISTERS AND ESTABLISH ADDRESSABILITY
*
         SAVE  (14,12),,CTSAOLT-&SYSDATE-&SYSTIME
         LR    R12,R15
         USING CTSAOLT,R12
         CTSLEVEL ,
         LR    R11,R1
         USING PARMS,R11
****************************************************************
* RETURNED TIME & DATE IS : HMSTMJ00YYMD0000 (HEX FORMAT)
*                           HMSHIU00YYMD0000
*
* TIME                                  DATE
* ----                                  ----
* HH=HOUR                               YYYY=YEAR
* MM=MINUTE                             MM  =MONTH
* SS=SECOND                             DD  =DAY
* T=TENTHS OF A SECOND
* H=HUNDREDTH OF A SECOND
* M=MILLISECOND
* I=TEN THOUSANDS OF A SECOND
* J=HUNDRED THOUSANDS OF A SECOND
* T=MICROSECONDS
****************************************************************
         TIME  DEC,TIMEDATE,ZONE=LT,LINKAGE=SYSTEM,DATETYPE=YYYYMMDD
***   HH HANDLING WITHIN HHMMSS...
         XR    R3,R3
         IC    R3,TIMEDATE        = HH I.E. 14 FOR 2:00 PM
         SLL   R3,8               R3 = 00001400
         IC    R3,DECSIGN         R3 = 000014F0
         SRL   R3,4               R3 = 0000014F
         XC    D,D                CLR DEC AREA
         ST    R3,D+4
         CVB   R3,D               R3 = 0000000E
         L     R4,PHH             -> HH
         STH   R3,0(R4)           SET REURN VALUE
***   MM HANDLING WITHIN HHMMSS...
         XR    R3,R3
         IC    R3,TIMEDATE+1      = MM I.E. 25
         SLL   R3,8               R3 = 00002500
         IC    R3,DECSIGN         R3 = 000025F0
         SRL   R3,4               R3 = 0000025F
         XC    D,D                CLR DEC AREA
         ST    R3,D+4
         CVB   R3,D               R3 = 00000019
         L     R4,PMM             -> MM
         STH   R3,0(R4)           SET REURN VALUE
***   SS HANDLING WITHIN HHMMSS...
         XR    R3,R3
         IC    R3,TIMEDATE+2      = SS I.E. 25
         SLL   R3,8               R3 = 00002500
         IC    R3,DECSIGN         R3 = 000025F0
         SRL   R3,4               R3 = 0000025F
         XC    D,D                CLR DEC AREA
         ST    R3,D+4
         CVB   R3,D               R3 = 00000019
         L     R4,PSS             -> SS
         STH   R3,0(R4)           SET REURN VALUE
***  THM HANDLING WITHIN HHMMSSTHM...
         XR    R3,R3
         ICM   R3,3,TIMEDATE+3      =THM I.E. 987X
         SLL   R3,4               R3 = 00098700
         IC    R3,DECSIGN         R3 = 000987F0
         SRL   R3,4               R3 = 0000987F
         XC    D,D                CLR DEC AREA
         ST    R3,D+4
         CVB   R3,D               R3 = 00000???
         L     R4,PMILLI          -> MILLI SECONDS
         STH   R3,0(R4)           SET REURN VALUE
*** YYYY HANDLING WITHIN YYYYMMDD...
         XR    R3,R3
         ICM   R3,3,TIMEDATE+8      YYYY I.E. 2004
         SLL   R3,8               R3 = 00200400
         IC    R3,DECSIGN         R3 = 002004F0
         SRL   R3,4               R3 = 0002004F
         XC    D,D                CLR DEC AREA
         ST    R3,D+4
         CVB   R3,D               R3 = 00000???
         L     R4,PYEAR           -> YEAR
         STH   R3,0(R4)           SET REURN VALUE
***   MM HANDLING WITHIN YYYYMMDD...
         XR    R3,R3
         IC    R3,TIMEDATE+10     = MM I.E. 12
         SLL   R3,8               R3 = 00001200
         IC    R3,DECSIGN         R3 = 000012F0
         SRL   R3,4               R3 = 0000012F
         XC    D,D                CLR DEC AREA
         ST    R3,D+4
         CVB   R3,D               R3 = 0000000C
         L     R4,PMONTH          -> MONTH
         STH   R3,0(R4)           SET REURN VALUE
***   DD HANDLING WITHIN YYYYMMDD...
         XR    R3,R3
         IC    R3,TIMEDATE+11     = DD I.E. 31
         SLL   R3,8               R3 = 00003100
         IC    R3,DECSIGN         R3 = 000031F0
         SRL   R3,4               R3 = 0000031F
         XC    D,D                CLR DEC AREA
         ST    R3,D+4
         CVB   R3,D               R3 = 0000001F
         L     R4,PDAY            -> DAY
         STH   R3,0(R4)           SET REURN VALUE
****************************************************************
*        RETURN TO CALLER
****************************************************************
RETURN   DS    0H
         XR    R15,R15                 RC = 0 ALWAYS
         RETURN (14,12),T,RC=(15)      RESTORE REGISTERS AND RETURN
****************************************************************
*        CONSTANTS
****************************************************************
         LTORG ,
D        DS    D
TIMEDATE DS    CL16         TIME AND DATE RETURNED
DECSIGN  DC    X'F0'
PARMS    DSECT
PYEAR    DS    A
PMONTH   DS    A
PDAY     DS    A
PHH      DS    A
PMM      DS    A
PSS      DS    A
PMILLI   DS    A
         END
