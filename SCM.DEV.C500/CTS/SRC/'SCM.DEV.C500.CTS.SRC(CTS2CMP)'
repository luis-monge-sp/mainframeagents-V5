****************************************************************
****                                                        ****
****     CONTROL-SA  RELEASE 3.0.0                          ****
****                                                        ****
****     LICENCED MATERIAL - NEW DIMENSION SOFTWARE LTD.    ****
****                                                        ****
****************************************************************
*
****************************************************************
*                                                              *
* TITLE            : ACF2 RULES COMPILE                        *
*                                                              *
* FILE NAME        : CTS2CMP                                   *
*                                                              *
* AUTHOR           : GUY SHAVITT                               *
*                                                              *
* CREATION DATE    : 26/01/98                                  *
*                                                              *
* DESCRIPTION      : COMPILE RULESET AND INSERT/REPLACE IT     *
*                                                              *
* PARAMETERS       :                                           *
*                                                              *
* INPUT            : KEY  - RESOURCE KEY                       *
*                  : TYPE - RESOURCE TYPE                      *
*                    FREP - REPLACE RULESET FLAG               *
*                    RULP - MAXRULE/MAXRSRC PARM               *
*                    DDN  - DDNAME OF INPUT FILE               *
*                                                              *
* OUTPUT           : BUF  - 32K RULE OBJECT BUFFER             *
*                    RC   - RETURN CODE                        *
*                    MSG  - ERR MSG                            *
* DEPENDENCIES     : ACFCMPLR - ACF2 RULE COMPILE ROUTINE      *
*                                                              *
* CHANGE ACTIVITY :                                            *
* 01/11/16 SAS2IBMT ST SAS/C TO IBM C CONVERSION PROJECT:      *
*                     THIS ROUTINE SHOULD RETURN RC (R15) 0    *
*                                                              *
****************************************************************
****************************************************************
*        PARAMETERS BLOCK                                      *
****************************************************************
*
PARM       DSECT
PARMKEY    DS    A                       RESOURCE KEY
PARMTYPE   DS    A                       RESOURCE TYPE
PARMFREP   DS    A                       REPLACE RULESET FLAG
PARMRULP   DS    A                       MAXRULE/MAXRSRC PARM
PARMDDN    DS    A                       DDNAME OF INPUT FILE
PARMBUF    DS    A                       RULE OBJECT BUFFER ADDRESS
PARMRC     DS    A                       RC
PARMMSG    DS    A                       MSG
*
CTS2CMP    CSECT
CTS2CMP    AMODE 31
CTS2CMP    RMODE ANY
           BEGIN
           CTSEQUR
           LR    R11,R1
           USING PARM,R11
****************************************************************
*  COMPILE THE RULE SOURCE                                     *
****************************************************************
           MVC  SRCDD,PARMDDN
           MVC  OBJBUF,PARMBUF
           L    R4,PARMRULP
           CLI  0(R4),X'00'
           BE   SKIPMXR
           MVC  PRML_LEN,=FL4'19'
           MVC  MAX_PARM,0(R4)
SKIPMXR    DS   0H
           LA   R1,CMPLPRM
           LINK EP=ACFCMPLR
           LTR  R15,R15
           BZ   CMPL_OK
           LA   R15,6
           B    EXIT_RC
****************************************************************
*  INSERT OR UPDATE ?                                          *
****************************************************************
CMPL_OK    DS   0H
           MVC   OP,=CL3'INS'
           L     R4,PARMFREP
           CLI   0(R4),C'Y'          REPLACE RULE OBJECT ?
           BNE   CHK_TYPE
           MVC   OP,=CL3'UPD'
****************************************************************
*  ROUTE TO ACCESS/RESOURCE RULES ROUTINE                      *
****************************************************************
CHK_TYPE   DS   0H
           L     R4,PARMTYPE
           CLC   0(3,R4),=CL3'DSN'   IS IT AN ACCESS RULE ?
           BNE   RESRULE
****************************************************************
*  ACCESS RULE HANDLING                                        *
****************************************************************
           L     R4,PARMKEY
           L     R5,PARMBUF
           CALL CTS2RUL,(OP,                                           X
               (R4),                                                   X
               DUMMY,                                                  X
               SPECIFIC,                                @WS2333        X
               (R5),                                                   X
               DUMMY,                                                  X
               DUMMY,                                                  X
               RC,                                                     X
               ERRMSG)
           L     R15,RC
           B     EXIT_RC
****************************************************************
*  RESOURCE RULE HANDLING                                      *
****************************************************************
RESRULE    DS   0H
           L     R4,PARMKEY
           L     R5,PARMBUF
           L     R6,PARMTYPE
           CALL CTS2GRS,(OP,                                           X
               (R4),                                                   X
               (R6),                                                   X
               DUMMY,                                                  X
               SPECIFIC,                                   @WS2333     X
               (R5),                                                   X
               DUMMY,                                                  X
               DUMMY,                                                  X
               DUMMY,                                                  X
               DUMMY,                                                  X
               RC,                                                     X
               ERRMSG)
           L     R15,RC
****************************************************************
*  RETURN WITH RC                                              *
****************************************************************
EXIT_RC    DS   0H
           L    R14,PARMRC
           ST   R15,0(R14)
           L    R14,PARMMSG
           MVC  0(124,R14),ERRMSG
           BRTRN 0                                             SAS2IBMT
ERRMSG     DS    CL128
OP         DS    CL3
           DS    0F
RC         DS    F
CMPLPRM    DS    0F
SRCDD      DS    A
MSGDD      DC    A(MSG_DD)
OBJBUF     DS    A
OBJLEN     DC    A(BUF_LEN)
NULL1      DC    A(0)
LISTADDR   DC    A(PARMLIST)
LISTLEN    DC    A(PRML_LEN)
MSG_DD     DC    CL8'ACF2MSG'
BUF_LEN    DC    FL4'32768'
PRML_LEN   DC    FL4'06'
PARMLIST   DC    C'NOLIST '
MAX_PARM   DS    CL12
PARMTRAIL  DC    CL1' '
DUMMY      DS    CL8
SPECIFIC   DC    CL1'S'
           END
