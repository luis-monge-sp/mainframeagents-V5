         START
*                                                                       00000901
****************************************************************        00000201
****                                                        ****        00000301
****     CONTROL-SA  RELEASE 1.3.0                          ****        00000401
****                                                        ****        00000501
****     LICENCED MATERIAL - NEW DIMENSION SOFTWARE LTD.    ****        00000601
****                                                        ****        00000701
****************************************************************        00000801
*                                                                       00000901
****************************************************************        00001001
*                                                              *        00001101
* TITLE            : CTSASUB                                   *        00001201
*                                                              *        00001301
* FILE NAME        : CTSASUB                                   *        00001401
*                                                              *        00001501
* AUTHOR           : DORON COHEN                               *        00001601
*                                                              *        00001701
* CREATION DATE    : 07/08/94                                  *        00001801
*                                                              *        00001901
* DESCRIPTION      : COMMAND PROCESSOR TO SUBMIT A JOB FROM    *        00002001
*                    A SPECIFIED DDCARD (OR CTSJOBIN - DEFAULT *        00002101
*                    DDCARD )                                  *        00002101
*                                                              *        00002101
* ASSUMPTIONS AND                                              *        00002201
*   CONSIDERATIONS :                                           *        00002301
*                                                              *        00002401
****************************************************************        00002501
****************************************************************        00002701
* MOD.ID   WHO      WHEN     DESCRIPTION                       *        00002801
* -------- -------- -------- --------------------------------- *        00002901
* IS10188  NURITY   06/10/24 MOVE OUR MODULES ABOVE 16M LINE   *
****************************************************************        00002701
*                                                                       00002601
TIOT     DSECT                                                          @PS0331
         IEFTIOT1                                                       @PS0331
*                                                                       00002601
CTSASUB  CSECT
*IS10188 CTSASUB  AMODE 24
*IS10188 CTSASUB  RMODE 24
CTSASUB  AMODE ANY                                             IS10188
CTSASUB  RMODE ANY                                             IS10188
*                                                                       00020001
***************************************************************
*        INITIALIZE                                           *
***************************************************************
*
         BEGIN
         CTSEQUR
         CTSLEVEL
         LR    R10,R1
         XC    FLAGS,FLAGS
         XC    RC,RC
*
         USING CPPL,R10
         L     R9,CPPLCBUF
*                                                                       00020001
         XC    DCBAREA,DCBAREA         NO DCB AREA.            IS10188
         GETMAIN RC,LV=DCBWALN,LOC=24  ALLOCATE AREA FOR DCBS  IS10188
         CHI   R15,0                   OK ?                    IS10188
         BNE   ERRGETM                 ..NO - ERROR.           IS10188
         LR    R5,R1                   R5 -> DCB AREA.         IS10188
         ST    R5,DCBAREA              SAVE AREA ADDRESS.      IS10188  188
         MVC   INDCB-DCBWA(INDCBLN,R5),IN MOVE DCB TO 24 BIT.  IS10188
         MVC   INRDCB-DCBWA(INRDCBLN,R5),INTRDR MOVE TO 24 BIT IS10188
         LA    R6,INDCB-DCBWA(,R5)     R6 -> INPUT DCB.        IS10188
         LA    R7,INRDCB-DCBWA(,R5)    R7 -> INTRDR DCB.       IS10188
*
***************************************************************
*        CALC DDNAME LENGTH                                   *
***************************************************************
*
         LA    R3,4(R9)              R3 -> START OF COMMAND
         AH    R3,2(R9)              R3 -> START OF DDCARD
         LR    R4,R9                 R4 -> START OF BUFFER
         AH    R4,0(R9)              R4 -> END OF BUFFER
*
         LR    R1,R3                 R1 -> START DDNAME
         XR    R2,R2                 RESET DDNAME LENGTH
LOOPCLEN DS    0H
         CR    R1,R4                 END OF BUFFER ?
         BNL   ENDCLEN               YES, SO END OF DDNAME
         CLI   0(R1),C' '            END OF DDNAME ?
         BE    ENDCLEN               YES, OK
         LA    R2,1(R2)              INCREASE DDNAME LENGTH
         LA    R1,1(R1)              NEXT CHARACTER
         B     LOOPCLEN              YA, LOOP LOOP LOOP
ENDCLEN  DS    0H
*
***************************************************************
*        ISOLATE DDNAME
***************************************************************
*
         LTR   R2,R2                 DDNAME = NULL ?
         BNP   SKIPREP               NO, USE DEFAULT
         CH    R2,=H'8'              TOO LONG ?
         BL    SKIPLEN               NO, OK
         LA    R2,8                  YES, TRUNCATE
SKIPLEN  DS    0H
         MVC   DDCARD,=CL8' '        RESET DDNAME IN DCB
         BCTR  R2,0                  PREPARE TO EXECUTE
         MVC   DDCARD(*-*),0(R3)     DUMMY CODE FOR EXECUTE
         EX    R2,*-6
*
         TR    DDCARD,LWR2UPR
         TR    DDCARD,SUPNPRT
*
***************************************************************
*        REPLACE DDNAME IN INPUT DCB
***************************************************************
*
*IS10188 LA    R4,IN
         LR    R4,R6                   R4 -> IN DCB            IS10188
         USING IHADCB,R4
         MVC   DCBDDNAM,DDCARD
         DROP  R4
SKIPREP  DS    0H
*                                                                       08750000
***************************************************************         @PS0331
*        SEARCH FOR INTRDR DD IN JOB                                    @PS0331
*        IF FOUND - USE IT FOR OUTPUT                                   @PS0331
*        IF NOT   - FAIL THE REQUEST.                          IS10188  @PS0331
*
*        ***  IS10188   ***
*        DYNAMIC ALLOCATION FOR INTRDR IS REMOVED BECAUSE IT   IS10188
*        REQUIRES AUTHORIZATION.  USUALLY ALLOCATION IS NOT    IS10188
*        REQUIRED BECAUSE WE HAVE THE INTRDR DD STATEMENT IN   IS10188
*        OUR PROCEDURES. INSTEAD OF ALLOCATING THE DD WE NOW   IS10188
*        ISSUE A MESSAGE ASKING THE CUSTOMER TO ADD  THE DD    IS10188
*        IN CASE IT DOES NOT EXIST.                            IS10188
*        THIS WILL SAVE THE NEED TO HANDLE AUTHORIZATION       IS10188
*        ISSUES IF/WHEN THE DD DOESNOT EXIST.                  IS10188
*
***************************************************************         @PS0331
*                                                                       @PS0331
*IS10188 MVC   DDNAME1,INTRDRDD                                         @PS0331
*                                                                       @PS0331
         EXTRACT TIOTA,FIELDS=TIOT                                      @PS0331
         L     R4,TIOTA                                                 @PS0331
         USING TIOT,R4                                                  @PS0331
*                                                                       @PS0331
         XR    R0,R0                                                    @PS0331
FTIOT    ICM   R0,1,TIOELNGH                                            @PS0331
*IS10188 BE    DODYNAM                                                  @PS0331
         BE    ERRNORDR                                        IS10188  @PS0331
         CLC   TIOEDDNM,INTRDRDD                                        @PS0331
         BE    PUTDD                                                    @PS0331
         AR    R4,R0                                                    @PS0331
         B     FTIOT                                                    @PS0331
*                                                                       @PS0331
*
***************************************************************
*IS10188 ALLOCATE INTRDR DYNAMICALLY                                    08770000
***************************************************************
*IS10188                                                                08750000
*IS10188  DODYNAM  DS    0H                                             @PS0331
*IS10188                                                                08750000
*IS10188 MVC   DDNAME1,=CL8'        '                                   @PS0331
*IS10188 LA    R1,ALCSVC99                                              08710000
*IS10188 SVC   99                                                       08730000
*IS10188 OC    ALCERRCD,ALCERRCD
*IS10188 BNZ   ERRALCO
*IS10188 OI    FLAGS,FLGALCO
*IS10188 MVC   DELDDNM1,DDNAME1
*
***************************************************************
*        REPLACE INTRDR DDNAME IN OUTPUT DCB
***************************************************************
*
PUTDD    DS    0H                                                       @PS0331
*
*IS1088  LA    R4,INTRDR
         LR    R4,R7                   R4 -> INTRDR DDCB       IS10188
*IS10188 USING IHADCB,R4
*IS10188 MVC   DCBDDNAM,DDNAME1
         DROP  R4
*
***************************************************************
*        OPEN INPUT FILE AND INTRDR
***************************************************************
*
*IS10188 OPEN  (IN,INPUT)
*IS10188 LA    R4,IN               POINT TO DCB
         OPEN  ((R6),INPUT),MODE=31                            IS10188
         LR    R4,R6               POINT TO DCB                IS10188
         USING IHADCB,R4           ESTABLISH ADDRESSABILITY
         TM    DCBOFLGS,DCBOFOPN   OPEN SUCCESSFULLY COMPLETED ?
         BZ    ERROPENI            IF NOT -
         OI    FLAGS,FLGOPNI
         DROP  R4
*
*IS10188 OPEN  (INTRDR,OUTPUT)
*IS10188 LA    R4,IN               POINT TO DCB
         OPEN  ((R7),OUTPUT),MODE=31                           IS10188
         LR    R4,R7               POINT TO DCB                IS10188
         USING IHADCB,R4           ESTABLISH ADDRESSABILITY
         TM    DCBOFLGS,DCBOFOPN   OPEN SUCCESSFULLY COMPLETED ?
         BZ    ERROPENO            IF NOT -
         OI    FLAGS,FLGOPNO
         DROP  R4
*
***************************************************************
*        COPY INPUT FILE TO INTRDR
***************************************************************
*
GETLOOP  DS    0H
*IS10188 GET   IN,INLINE
*IS10188 PUT   INTRDR,INLINE
         GET   (R6),INLINE                                     IS10188
         PUT   (R7),INLINE                                     IS10188
         B     GETLOOP
*
***************************************************************
*        ERRORS
***************************************************************
*
ERROPENI DS    0H
         MVC   RC,=F'8'                                                 @PS0331
         B     SOF
*
ERROPENO DS    0H
         MVC   RC,=F'12'                                                @PS0331
         B     SOF
*IS10188 IERRALCO  DS    0H
*IS10188 CTSADBG TYPE=WTO,'DYNAMIC ALLOCATION FAILED FOR INTRDR. SVC99 X@PS0331
               RC=~',ALCERRCD                                           @PS0331
*IS10188 MVC   RC,=F'16'                                                @PS0331
*IS10188 B     SOF
ERRNORDR DS    0H                                              IS10188
         CTSADBG TYPE=WTO,'INTRDR DD STATEMENT IS MISSING'     IS10188  @PS0331
         MVC   RC,=F'16'                                       IS10188  @PS0331
         B     SOF                                             IS10188
ERRFREO  DS    0H
         MVC   RC,=F'4'                                                 @PS0331
         B     SOF
ERRGETM  DS    0H                                              IS10188
         MVC   RC,=F'20'                                       IS10188  @PS0331
         B     SOF                                             IS10188
*
***************************************************************
*        END OF JOB FILE
***************************************************************
*
SOF      DS    0H
*                                                                       08750000
***************************************************************
*        CLOSE OUTPUT FILE                                              08770000
***************************************************************
*                                                                       08240000
         TM    FLAGS,FLGOPNO
         BZ    SKIPCLSO
         NI    FLAGS,255-FLGOPNO
*IS10188 CLOSE INTRDR
         CLOSE ((R7),),MODE=31                                 IS10188  E ((R7))
SKIPCLSO DS    0H
*                                                                       08750000
***************************************************************
*        CLOSE INPUT FILE                                               08770000
***************************************************************
*                                                                       08240000
         TM    FLAGS,FLGOPNI
         BZ    SKIPCLSI
         NI    FLAGS,255-FLGOPNI
*IS10188 CLOSE IN
         CLOSE ((R6),),MODE=31                                 IS10188
SKIPCLSI DS    0H
*                                                                       08750000
***************************************************************
*IS10188 DEALLOCATE INTRDR                                              08770000
***************************************************************
*IS10188                                                                08240000
*IS10188 TM    FLAGS,FLGALCO
*IS10188 BZ    SKIPFREO
*IS10188 NI    FLAGS,255-FLGALCO
*IS10188 LA    R1,DELSVC99                                              08800000
*IS10188 SVC   99                                                       08820000
*IS10188 OC    DELERRCD,DELERRCD
*IS10188 BNZ   ERRFREO
*IS10188 SKIPFREO DS    0H
*                                                              IS10188  08750000
***************************************************************IS10188
*        FREE THE DCB AREA.                                    IS10188  08770000
***************************************************************IS10188
*                                                              IS10188  08750000
         L     R1,DCBAREA              R1 -> DCB AREA          IS10188  A ALLOCA
         CHI   R1,0                    ALLOCATED ?             IS10188
         BE    SKIPFREM                ..NO - NO NEED TO FREE. IS10188
         FREEMAIN RC,LV=DCBWALN,A=(R1)                         IS10188
         CHI   R15,0                   FREE DONE ?             IS10188
         BE    SKIPFREM                                        IS10188
         CTSADBG TYPE=WTO,'FREEMAIN FAILED.  RC = #',(?R15)    IS10188  @PS0331
SKIPFREM DS    0H                                              IS10188
*
***************************************************************
*        FINISH                                                         08770000
***************************************************************
*                                                                       08240000
         L     R15,RC
         BRTRN (15)
*
***************************************************************
*        CONSTANTS
***************************************************************
*
INTRDRDD DC    CL8'INTRDR  '                                            @PS0331
*
LWR2UPR  DC    256AL1(*-LWR2UPR)
         ORG   LWR2UPR+C'A'-X'40'
         DC    C'ABCDEFGHI'
         ORG   LWR2UPR+C'J'-X'40'
         DC    C'JKLMNOPQR'
         ORG   LWR2UPR+C'S'-X'40'
         DC    C'STUVWXYZ'
         ORG   ,
SUPNPRT  DC    256AL1(*-SUPNPRT)
         ORG   SUPNPRT
         DC    64C' '
         ORG   ,
         LTORG
         DROP  ,
*
*IS10188
***************************************************************
*IS10188 DYNAMIC ALLOCATION WORKAREA
***************************************************************
*IS10188                                                                10970000
*IS10188 +-------------------------------------------------------*      11130000
*IS10188 Ý      SVC99 FOR ALLOCATION                                    11140000
*IS10188 +-------------------------------------------------------*      11150000
*IS10188                                                                10970000
*IS10188  ALCSVC99 DS    0F               SVC 99 PARAMETR BLOCK MASK
*IS10188  ALCPARMP DC    X'80',AL3(ALCVERB)
*IS10188  ALCVERB  DC    XL4'14010000'
*IS10188  ALCERRCD DC    HL2'0'
*IS10188  ALCINFO  DC    HL2'0'
*IS10188  ALCPTRA  DC    A(ALCPTRS,0,0)
*IS10188  ALCPTRS  DS    0F
*IS10188  ALCDD    DC    A(DDNAME)
*IS10188  ALCCLASS DC    A(CLASS)
*IS10188  ALCDS    DC    A(SYSOUT)
*IS10188  ALCPTEND DC    X'80000000'
*IS10188  *
*IS10188  ALCTEXT  DS    0F
*IS10188  DDNAME   DC    AL2(DALRTDDN),AL2(1),AL2(8)
*IS10188  DDNAME1  DC    CL8' '
*IS10188  CLASS    DC    AL2(DALSYSOU),AL2(0),AL2(1)
*IS10188  CLASS1   DC    CL1'X'
*IS10188  SYSOUT   DC    AL2(DALSPGNM),AL2(1),AL2(8)
*IS10188  SYSOUT1  DC    CL8'INTRDR'
*IS10188  ALC99END EQU   *
*IS10188                                                                11120000
*IS10188 +-------------------------------------------------------*      10980000
*IS10188 Ý      SVC99 FOR DEALLOCATION                                  10990000
*IS10188 +-------------------------------------------------------*      11000000
*IS10188                                                                11120000
*IS10188  DELSVC99 DS    0F
*IS10188  DELPARMP DC    X'80',AL3(DELVERB)
*IS10188  DELVERB  DC    XL4'14020000'
*IS10188  DELERRCD DC    HL2'0'
*IS10188  DELINFO  DC    HL2'0'
*IS10188  DELPTRA  DC    A(DELPTRS,0,0)
*IS10188  *
*IS10188  DELPTRS  DS    0F
*IS10188  DELDD    DC    A(DELDDNM)
*IS10188  DELPTEND DC    X'80000000'
*IS10188  DELTEXT  DS    0F
*IS10188  DELDDNM  DC    AL2(DALDDNAM),AL2(1),AL2(8)
*IS10188  DELDDNM1 DC    CL8' '
*IS10188  DEL99END EQU   *
*
***************************************************************
*        WORKAREA
***************************************************************
TIOTA    DS    F                   TIOT ADDR                            @PS0331
RC       DC    F'0'
FLAGS    DC    X'00'
FLGOPNI  EQU   B'10000000'
FLGOPNO  EQU   B'01000000'
*IS10188 FLGALCO  EQU   B'00100000'
*
DDCARD   DC    CL8' '
DCBAREA  DC    A(0)                                            IS10188
*IS10188 IN       DCB   DDNAME=CTSJOBIN,DSORG=PS,MACRF=GM,EODAD=SOF
*IS10188 INTRDR   DCB   DDNAME=INTRDR,DSORG=PS,MACRF=PM,LRECL=80,
IN       DCB   DDNAME=CTSJOBIN,DSORG=PS,MACRF=GM,DCBE=INDCBE   IS10188
INDCBLN  EQU   *-IN                                            IS10188
INDCBE   DCBE  RMODE31=BUFF,EODAD=SOF                          IS10188
INTRDR   DCB   DDNAME=INTRDR,DSORG=PS,MACRF=PM,LRECL=80,       IS10188 >
               DCBE=INRDCBE                                    IS10188
INRDCBLN EQU   *-INTRDR                                        IS10188
INRDCBE  DCBE  RMODE31=BUFF                                    IS10188
INLINE   DC    1024C' '
*
DCBWA    DSECT                                                 IS10188
INDCB    DS    XL(INDCBLN)             AREA FOR IN DCB.        IS10188
INRDCB   DS    XL(INRDCBLN)            AREA FOR INTRDR DCB.    IS10188
DCBWALN  EQU   *-DCBWA                                         IS10188
***************************************************************
*        CONTROL BLOCKS MAPPING                               *
***************************************************************
*
         PRINT NOGEN
*IS10188 IEFZB4D0 ,                                                     01450000
*IS10188 IEFZB4D2 ,                                                     01460000
         IKJCPPL
         IKJECT
         DCBD  DSORG=PS,DEVD=DA
         END
