         TITLE 'CTSASCS - START CTSACS WITH EONUM'
*
****************************************************************
****                                                        ****
****     CONTROL-SA  RELEASE 3.3.01                         ****
****                                                        ****
****     LICENCED MATERIAL - BMC INC.                       ****
****                                                        ****
****************************************************************
*
****************************************************************
*                                                              *
* TITLE            : START CTSACS WHILE PROVIDING A UNIQUE     *
*                    NUMBER FOR EXECOUT DATASET NAME.          *
*                                                              *
* FILE NAME        : CTSASCS                                   *
*                                                              *
* AUTHOR           : NURIT YAHALOM                             *
*                                                              *
* CREATION DATE    : 31.03.16                                  *
*                                                              *
* DESCRIPTION      : START CTSACS WHILE PROVIDING A UNIQUE     *
*                    NUMBER FOR EXECOUT DATASET NAME.          *
*                                                              *
*                    IF THE NUMBER WAS NOT PROVIDED (IT IS     *
*                    XX, WHICH IS A DEFUALT VALUE), START      *
*                    CTSCACS AGAIN PROVIDING A UNIQUE NUMBER   *
*                    AND RETURN A RETURN CODE OF 1.            *
*                                                              *
*                    IF THE NUMBER WAS PROVIDED (PROBABLY BY   *
*                    A PREVIOUS RUN OF THIS PROGRAM), SET A    *
*                    RETURN CODE OF 0 ALLOWING THE NEXT STEPS  *
*                    ACTIVATING THE STANDAR CTSACS TO EXECUTE.
*                                                              *
*                    THIS PROGRMA IS INVOKED BY STCJOBS MEMBER *
*                    CTSACS.                                   *
*                                                              *
*                    INPUT PARAMETER VIA PARM=                 *
*                    E=<2-CHARACTERS>,T='<CTSGATE-TOKEN>'      *
*                                                              *
*                                                              *
* ASSUMPTIONS AND                                              *
*   CONSIDERATIONS : SEE CCID BS10048 FOR IMPLEMETATION        *        00270000
*                    CONSIDERATIONS.                           *
*                                                              *
****************************************************************
*
****************************************************************
* MOD.ID   WHO      WHEN     DESCRIPTION                       *
* -------- -------- -------- --------------------------------- *
* BS10048   NURITY  31.03.16 NEW ROUTINE.                      *
*                                                              *
****************************************************************
*
CTSASCS  CSECT
CTSASCS  AMODE 31
CTSASCS  RMODE ANY
         SAVE  (14,12)
         LR    R12,R15
         USING CTSASCS,R12
*
         MVC   8(4,R13),=A(SA)
         ST    R13,SA+4
         LA    R13,SA
*
         LR    R3,R1
         L     R3,0(,R3)
         LH    R4,0(,R3)           R4 = PARAMETER LENGTH.
         LA    R3,2(,R3)           R3 -> PARAMETER.
*
**   PARAMETER = E=XX,T='TOKEN-PASSED-FROM-GATE'
**       -- OR --
**   PARAMETER = E=NN,T='TOKEN-PASSED-FROM-GATE'
**     WHERE NN IS A UNIQUE NUMBER.
*
         CLC   0(2,R3),=C'E='
         BNE   SETRC16
         CLC   4(3,R3),=C',T='
         BNE   SETRC16
*
         CLC   2(2,R3),=C'XX'      DEFAULT EONUM ?
         BNE   SETRC0              ..NO - THEN THIS IS THE START WE
*                                         ISSUED, WITH A UNIQUE NUMBER
*                                         SO SET A RETURN CODE OF 0.
*
*
**   ISSUE START WITH A UNIQUE NUMBER
*
*
         LA    R3,7(,R3)           R3 -> TOKEN DATA.
         AHI   R4,-7               R4 = TOKEN LENGTH.
*
         L     R1,PSATOLD-PSA      POINT TO TCB
         LHI   R15,0
         ICM   R15,B'0111',TCBJSTCA-TCB(R1) R15 -> JSTCB
         ICM   R15,B'0111',TCBJSCBB-TCB(R15) R15 -> JSCB
         L     R15,JSCBCSCB-IEZJSCB(,R15) R15 -> CSCB.
         MVC   NAME,CHCLS-CSCB(R15)
         MVC   ID,CHKEY-CSCB(R15)
*
         MVC   CMDVAR(8),NAME      COPY NAME.
         LA    R5,CMDVAR           R5 -> COMMAND BUFFER.
         LA    R6,8
LK4ENDNM DS    0H
         CLI   0(R5),C' '
         BNH   ENDNM
         LA    R5,1(,R5)
         BCT   R6,LK4ENDNM
ENDNM    DS    0H
         MVI   0(R5),C'.'
         LA    R5,1(,R5)
*
         MVC   0(8,R5),ID        COPY ID.
         LA    R6,8
LK4ENDID DS    0H
         CLI   0(R5),C' '
         BNH   ENDID
         LA    R5,1(,R5)
         BCT   R6,LK4ENDID
ENDID    DS    0H
         LA    R7,0(,R5)
         AHI   R7,-2
         MVC   0(7,R5),=C',EONUM='
         MVC   7(2,R5),0(R7)       SET ID AS UNIQUE NUMBER.
         LA    R5,9(,R5)
*
         MVC   0(8,R5),=C',TOKEN='''
         LA    R5,8(,R5)
*
         AHI   R4,-1
         EX    R4,MVTOKEN          MOVE TOKEN TO START COMMAND.
         B     *+L'*+L'MVTOKEN
MVTOKEN  MVC   0(0,R5),0(R3)
         LA    R5,1(R4,R5)
         MVC   0(2,R5),=C''' '
         LA    R5,2(,R5)
*
         LA    R3,CMD
         SR    R5,R3               R5 = CMD LENGTH
         STH   R5,CMDLEN
*
**   ISSUE THE START COMMAND
**     S NAME.ID,EONUM=NN,TOKEN='TOKEN-RECEIED-FROM-GATE'
*
         MODESET KEY=ZERO,MODE=SUP
         SLR   R2,R2
         MGCRE TEXT=CMDA,CONSID=(R2),MF=(E,MGCREL)
         LR    R5,R15
         MODESET KEY=NZERO,MODE=PROB
         LTR   R15,R5              RESTORE RC AND CHECK MGCRE RC:
         BZ    SETRC1              0 (START OK)... SET RC = 1.
         B     SOF                 OTHERWISE... RETURH WITH MGCRE RC.
*
**   SET RETURN CODES AND RETURN.
*
SETRC16  DS    0H                   PARAMETER ERROR.
         LHI   R15,16
         B     SOF
SETRC1   DS    0H                   NEW CTSACS WAS STARTED.
         LHI   R15,1
         B     SOF
SETRC0   DS    0H                   CONTINUE EXECUTING THE CTSACS PROC.
         LHI   R15,0
         B     SOF
*
SOF      DS    0H
         L     R13,SA+4
         RETURN (14,12),RC=(15)
         LTORG ,
         DROP  ,
SA       DS    18F
*
NAME     DC    CL8' '
ID       DC    CL8' '
*
CMDA     DC    A(CMDLEN)
CMDLEN   DC    H'0'
CMD      DC    C'S '
CMDVAR   DC    CL250' '
CMDEND   EQU   *
*
MGCREL   MGCRE MF=L
*
         YREGS ,
         IHAPSA
         IKJTCB
*
         IEZJSCB ,
*
CSCB     DSECT
         IEECHAIN ,
         END
