          START
*                                                                       00000101
****************************************************************        00000201
****                                                        ****        00000301
****     CONTROL-SA  RELEASE 1.3.0                          ****        00000401
****                                                        ****        00000501
****     LICENCED MATERIAL - NEW DIMENSION SOFTWARE LTD.    ****        00000601
****                                                        ****        00000701
****************************************************************        00000801
*                                                                       00000901
****************************************************************        00001001
*                                                              *        00001101
* TITLE            : CHECK RACF UP/DOWN                        *        00001201
*                                                              *        00001301
* FILE NAME        : CTSFCST                                   *        00001401
*                                                              *        00001501
* AUTHOR           : ALEXANDER SHVARTSMAN                      *        00001601
*                                                              *        00001701
* CREATION DATE    : 26/07/94                                  *        00001801
*                                                              *        00001901
* DESCRIPTION      : THE PROGRAM CHECKS RACF STATUS            *        00002001
*                    UP/DOWN                                   *        00002101
*                                                              *        00002101
* INPUT            : DEBUG       -  DEBUG LEVEL                *
*                                                              *
* OUTPUT           : NONE                                     *
*                                                              *
* RETURN VALUE     : VIA REG.15                                *
*                    0           -  O.K.                       *
*                    8           -  RACF DEACTIVATED BY RVARY  *
*                   16           -  RACF DOWN                  *
*                                                              *
* ASSUMPTIONS AND                                              *        00002201
*   CONSIDERATIONS : NONE                                      *        00002301
*                                                              *        00002401
* MOD.ID   WHO      WHEN     DESCRIPTION                       *        00300000
* -------- -------- -------- --------------------------------- *        00310000
* IS10188  AVNERL   06/10/24 MOVE OUR MODULES ABOVE 16M LINE   *
****************************************************************        00002501
*                                                                       00002601
PARM     DSECT
DEBUG    DS    A                       DEBUG LEVEL
***************************************************************
*
***************************************************************
*-----------------------------------------------------------------
*    OFFSETS IN ICHPRCVT DSECT FOR SOME FIELDS
*-----------------------------------------------------------------
OFFID    EQU   0                       RCVTID   FIELD
OFFSTAT  EQU   X'35'                   RCVTSTAT FIELD
OFFFLGS  EQU   X'99'                   RCVTFLGS FIELD
OFFVERS  EQU   X'AC'                   RACFVERS FIELD
OFFVRNM  EQU   X'268'                  RCVTVRMN FIELD
*-----------------------------------------------------------------
CTSFCST  CSECT
CTSFCST  AMODE 31
*IS10188 CTSFCST  RMODE 24       (FOR CTSADBG)
CTSFCST  RMODE ANY                                           IS10188
*
*        INITALIZE
*
         BEGIN *,R12
         CTSEQUR
         CTSLEVEL
*
         LR    R11,R1                  ADDR OF PARAMETER LIST
         USING PARM,R11
         L     R4,DEBUG                R4 -> DEBUG LEVEL (4 BYTES)
         XC    DEBUGC,DEBUGC           STORE DEBUG LEVEL
         ICM   R4,15,0(R4)             DEBUG LEVEL
         BNP   NODEBUG                 SKIP DEBUG LVL IF MINUS
         STC   R4,DEBUGC               STORE DEBUG LEVEL (1 BYTE)
NODEBUG  DS    0H
         CTSADBG 'CTSACST ENTER',LEVEL=(DEBUGC,1,2,3,4,5)
*
         L     R10,CVTPTR              LOAD ADDR CVT
         USING CVT,R10                 ADDRESSABILITY CVT DSECT
*
         L     R8,CVTRAC               LOAD ADDR CVT RACF
         LTR   R8,R8                   ADDR SUPPLIED ?
         BZ    RET8                    NO, RACF 'DOWN'
*-------------------------------------------------------------------
*     PRINT RACF CVT AREA ACCORDING DEBUG LEVEL
*-------------------------------------------------------------------
         LR    R9,R8
         LA    R9,1000(R9)
         CTSADBG TYPE=SNAP,ID=1,HDR='** RACF CVT **',                  1
               ADDR=(0(R8),0(R9)),LEVEL=(DEBUGC,4,5)
*
*-------------------------------------------------------------------
*    CHECK RACF STATUS
*-------------------------------------------------------------------
         USING RCVT,R8                 ADDRESSABILITY CVT RACF DSECT
*
         CTSADBG 'RCVTID=_',(RCVTID,4),LEVEL=(DEBUGC,1,2,3,4,5)
*---------------------------------------------------------------
*     BLOCK ID  - MUST BE 'RCVT'
*---------------------------------------------------------------
         CLC   =CL4'RCVT',OFFID(R8)    CHECK BLOCK ID
         BNE   RET16                   INVALID BLOCK ID, RACF 'DOWN'
*
         XR    R15,R15
         IC    R15,RCVTSTAT
         CTSADBG 'RCVTSTAT=~',(?R15),LEVEL=(DEBUGC,1,2,3,4,5)
*----------------------------------------------------------------
*     STATUS FLAG
*----------------------------------------------------------------
         TM    OFFSTAT(R8),RCVTRNA     RACF ACTIVE ?
         BO    RET16                   N, RACF 'DOWN'
*
         XR    R15,R15
         IC    R15,RCVTFLGS
         CTSADBG 'RCVTFLGS=~',(?R15),LEVEL=(DEBUGC,1,2,3,4,5)
*-----------------------------------------------------------------
*     RVARY COMMAND FLAG
*-----------------------------------------------------------------
         TM    OFFFLGS(R8),RCVTROFF    RACF HAS DEACTIATED
*                                      BY RVARY CMD
         BO    RET8                    Y, RACF 'DOWN'
         B     RET0
***********************************************************************
*
*--------------------------------------------------------------------
*    RETURN CODES
*--------------------------------------------------------------------
RET0     DS    0H                      0-O.K
         SR    R15,R15
         B     RET
*
RET8     DS    0H                      8 -RACF DEACTIVATED BY RVARY CMD
         LA    R15,8
         B     RET
*
RET16    DS    0H                      16-RACF DOWN
         LA    R15,16
         B     RET
*
RET      DS    0H
         CTSADBG 'CTSACST EXITING WITH RC=~',(?R15),                   1
               LEVEL=(DEBUGC,1,2,3,4,5)
         BRTRN (15)
         LTORG
DEBUGC   DS    X                       DEBUG LEVEL
         DROP  ,
***************************************************************
*                                                             *
*        SYSTEM CONTROL BLOCKS MAPPING                        *
*                                                             *
***************************************************************
         PRINT GEN
         CVT   DSECT=YES,LIST=YES
         ICHPRCVT
         END
