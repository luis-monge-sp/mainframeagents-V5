         START
****************************************************************
****                                                        ****
****     MF CONNECTOR RELEASE 4.0.00                        ****
****                                                        ****
****     (C) COPYRIGHT 2023 SAILPOINT TECHNOLOGIES, INC.,   ****
****     ALL RIGHTS RESERVED.                               ****
****                                                        ****
****************************************************************
*
****************************************************************
*                                                              *
* TITLE            : INTERMIDIATOR FOR CALLING ACF00RBS        *
*                                                              *
* FILE NAME        : CTS2RBS                                   *
*                                                              *
* AUTHOR           : THOMAS SEGEV                              *
*                                                              *
* CREATION DATE    : 31/07/23                                  *
*                                                              *
* DESCRIPTION      : CALLED FROM C  PROVIDED IN                *
*                                                              *
* PARAMETERS       : FUNC: CALL - LOAD AND CALL ACF00RBS       *
*                  :              (IF ALREADY LOADED JUST CALL)*
*                  :       FREE - FREE THE RETURNED BLOCK      *
*                  : INP : INPUT PARAMETERS FOR ACF00RBS       *
*                  :       TO BE PASSED IN R1 (XROLE)          *
*                  : RET@: IN/OUT: ADDRESS OF POINTER TO       *
*                  :       OUTPUT BLOCK FROM ACF00RBS (XROLTB) *
*                  : RBS@: IN/OUT ADDRESS OF POINTER           *
*                  :       TO ACF00RBS. 1ST TIME POINTER IS 0. *
*                  :       THE POINTER IS SET AFTER LOADING    *
*                  :       AND SO NO NEED TO LOAD ON SUBSEQUENT*
*                  :       CALLS                               *
*                  : PRMDBG: ADDRESS OF DBGLVL                 *
*                  : RETLEN: SIZE OF AREA TO FREE              *
*                  : RETSP : SUBPOOL OF AREA TO FREE           *
*                                                              *
****************************************************************
* NEW     TS 31/07/23                                          *
* BS10140  THOMAS   08/09/24 NO X-ROL ROLES SHOULD BE OK       *
* IS10188  AVNERL   06/10/24 MOVE OUR MODULES ABOVE 16M LINE   *
****************************************************************
*
PARM     DSECT
FUNC     DS    A         CALL/FREE
INP      DS    A         INPUT BLOCK OF PARAMETERS FOR ACF00RBS
RET@     DS    A         ADDRESS OF POINTER TO RETURNED BLOCK
RBS@     DS    A         ADDRESS OF POINTER TO ACF00RBS
PRMDBG   DS    A         ADDRESS OF DBGLVL
RETLEN   DS    F         RETURNED AREA LENGTH TO FREE
RETSP    DS    F         RETURNED AREA SUBPOOL
*
CTS2RBS  CSECT
CTS2RBS  AMODE 31
*IS10188 CTS2RBS  RMODE 24
CTS2RBS  RMODE ANY                                              IS10188
         BEGIN
         CTSEQUR
         CTSLEVEL CTS2RBS
*
         L     R10,0(,R1)
         USING PARM,R10
*
         MVI   DBGLEVEL,0
         L     R1,PRMDBG
         LA    R1,0(,R1)
         CHI   R1,0
         JE    NODEBUG
         L     R1,0(,R1)
         CHI   R1,0
         JNH   NODEBUG
         STC   R1,DBGLEVEL
NODEBUG  DS    0H
         LT    R7,RBS@
         JZ    ERRRBS@3
         LT    R7,FUNC
         JZ    ERRFUNC1
         CLC   0(4,R7),=CL4'CALL'
         JE    CALL
         CLC   0(4,R7),=CL4'FREE'
         JE    FREE
         J     ERRFUNC2
*
CALL     DS    0H
         L     R7,INP
         USING XROLE,R7
         L     R1,XRLSRCID
         MVC   NAME(8),0(R1)
         DROP  R7 ,
         CTSADBG 'GET ROLES FOR _',(NAME,8),LEVEL=(DBGLEVEL,1),        X
               DBGAREA=DBGAREA,RAREA=RAREA
         CTSADBG TYPE=SNAP,ID=1,HDR='INPUT XROLE',LEVEL=(DBGLEVEL,1),  X
               ADDR=(0(R7),XRLLN-1(R7)),                               X
               DBGAREA=DBGAREA,RAREA=RAREA
         L     R15,RBS@
         LA    R15,0(,R15)
         L     R15,0(,R15)
         LA    R15,0(,R15)
         LTR   R15,R15
         JNZ   LOADED
         LOAD  EP=ACF00RBS,ERRET=ERRLOAD
         LR    R15,R0
         L     R8,RBS@
         ST    R15,0(,R8)
LOADED   DS    0H
*
         L     R1,INP
         BASR  R14,R15
*                                  R1 -> XROLTB
         LTR   R15,R15                                          BS10140
         JZ    RCGOOD                                           BS10140
         CTSADBG ' ACF00RBS: RC = #',LEVEL=(DBGLEVEL,1),(?R15), BS10140X
               DBGAREA=DBGAREA,RAREA=RAREA                      BS10140
         CHI   R15,12  RC 12 IS NO ACF2 OR NO ROLES (X-ROLES)   BS10140
*                      NO ACF2 SHOULD BE HANDLED EARLIER        BS10140
         JNE   RCBAD                                            BS10140
         LHI   R15,0                                            BS10140
         J     RET                                              BS10140
RCBAD    DS    0H                                               BS10140
         J     RET                                              BS10140
RCGOOD   DS    0H                                               BS10140
         LR    R9,R1
         CTSADBG ' ACF00RBS: RC = # OUTPUT AT = ~',LEVEL=(DBGLEVEL,1), X
               (?R15,?R9),                                             X
               DBGAREA=DBGAREA,RAREA=RAREA
         L     R8,RET@
         ST    R9,0(,R8)
         USING XROLTB,R9
         LH    R7,XRLTBLN
         CLC   =X'FFFF',XRLTBLN
         JNE   GOTLEN
         L     R7,XRLTBFLN
         DROP  R9 ,
GOTLEN   DS    0H
         AR    R7,R9
         AHI   R7,-1
         CTSADBG TYPE=SNAP,ID=1,HDR='XROLTB',LEVEL=(DBGLEVEL,1),       X
               ADDR=(0(R9),0(R7)),                                     X
               DBGAREA=DBGAREA,RAREA=RAREA
         J     RET
*
FREE     DS    0H
*
         LHI   R15,0
         LT    R0,RETLEN
         JZ    RET20
         LT    R1,RET@
         JZ    RET21
         LT    R1,0(,R1)
         JZ    RET22
         L     R2,RETSP
         STORAGE RELEASE,LENGTH=(R0),ADDR=(R1),SP=(R2),                X
               COND=YES,LINKAGE=SYSTEM
*
         LTR   R15,R15
         JZ    RET
         LR    R3,R15
         WTO   'CTSXXXX: CTS2RBS - RELEASE OF ROLES AREA FAILED'
         LR    R15,R3

         CTSADBG TYPE=PRTDBG,                                          X
               'ROLES AREA RELEASE FAILED: ~ / #   RC = #',            X
               (?R1,?R0,?R15),                                         X
               RAREA=RAREA,DBGAREA=DBGAREA
         J     RET
*
ERRFUNC1 DS    0H
         LHI   R15,1
         J     RET
ERRFUNC2 DS    0H
         LHI   R15,2
         J     RET
ERRRBS@3 DS    0H
         LHI   R15,3
         J     RET
ERRLOAD  DS    0H
         WTO   'CTSXXXX: CTS2RBS - LOAD OF ACF00RBS FAILED'
         LHI   R15,12
         J     RET
*
RET20    DS    0H
         LHI   R15,20
         J     RET
RET21    DS    0H
         LHI   R15,21
         J     RET
RET22    DS    0H
         LHI   R15,22
         J     RET
RET      DS    0H
         BRTRN (15)                    RETURN RC
DBGLEVEL DS    XL1                     DEBUG LEVEL FOR THIS ROUTINE.
NAME     DS    CL8
         LTORG ,
         DS    0F
RAREA    DS    XL512
DBGAREA  DS    XL2048
         XROL   ,
         XROLTB ,
         END
