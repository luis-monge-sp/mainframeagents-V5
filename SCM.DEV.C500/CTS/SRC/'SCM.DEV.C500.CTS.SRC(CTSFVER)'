          START
*                                                                       00000101
****************************************************************        00000201
****                                                        ****        00000301
****     CONTROL-SA  RELEASE 1.3.0                          ****        00000401
****                                                        ****        00000501
****     LICENCED MATERIAL - NEW DIMENSION SOFTWARE LTD.    ****        00000601
****                                                        ****        00000701
****************************************************************        00000801
*                                                                       00000901
****************************************************************        00001001
*                                                              *        00001101
* TITLE            : GET RACF VERSION, RELEASE                 *        00001201
*                                                              *        00001301
* FILE NAME        : CTSFVER                                   *        00001401
*                                                              *        00001501
* AUTHOR           : ALEXANDER SHVARTSMAN                      *        00001601
*                                                              *        00001701
* CREATION DATE    : 08/06/95                                  *        00001801
*                                                              *        00001901
* DESCRIPTION      : THE PROGRAM RETURNS RACF VERSION, RELEASE *        00002001
*                                                              *        00002101
* INPUT            : DEBUG       -  DEBUG LEVEL                *
*                                                              *
* OUTPUT           : DEBUG       -  DEBUG LEVEL                *
*                    RACFVERN    -  RACF REAL VER NAME   CL4   *
*                                                              *
* RETURN VALUE     : VIA REG.15                                *
*                    0           -  O.K.                       *
*                   16           -  RACF DEACTIVATED BY RVARY  *
*                   20           -  RACF DOWN                  *
*                                                              *
* ASSUMPTIONS AND                                              *        00002201
*   CONSIDERATIONS : CALLER MUST BE AUTHORIZED                 *        00002301
*                                                              *        00002401
****************************************************************        00002501
*
****************************************************************
* MOD.ID   WHO      WHEN     DESCRIPTION                       *
* -------- -------- -------- --------------------------------- *
* WS2004   AS       13/05/97 RACF OVERRIDE VERSION PROCESSING
* BS2625   AVNERL   28/10/03 KEEP VER EVEN IF NOT FOUND        *
* IS0272   MICHAEL  10/06/04  UPDATE C31B LIBS WITH OLD FIXES  *
* IS10188  AVNERL   06/10/24 MOVE OUR MODULES ABOVE 16M LINE   *
****************************************************************
*                                                                       00002601
PARM     DSECT
DEBUG    DS    A                       DEBUG LEVEL
RACFVERN DS    A                       RACF REAL VER NAME     CL4
***************************************************************
*
***************************************************************
*-----------------------------------------------------------------
*    OFFSETS IN ICHPRCVT DSECT FOR SOME FIELDS
*-----------------------------------------------------------------
OFFID    EQU   0                       RCVTID   FIELD
OFFSTAT  EQU   X'35'                   RCVTSTAT FIELD
OFFFLGS  EQU   X'99'                   RCVTFLGS FIELD
OFFVERS  EQU   X'AC'                   RACFVERS FIELD
OFFVRNM  EQU   X'268'                  RCVTVRMN FIELD
*-----------------------------------------------------------------
CTSFVER  CSECT
CTSFVER  AMODE 31
*IS10188 CTSFVER  RMODE 24       (FOR CTSADBG)
CTSFVER  RMODE ANY                                             IS10188
*
*        INITALIZE
*
         BEGIN *,R12
         CTSEQUR
         CTSLEVEL
*
         LR    R11,R1                  ADDR OF PARAMETER LIST
         USING PARM,R11
         L     R4,DEBUG                R4 -> DEBUG LEVEL (4 BYTES)
         XC    DEBUGC,DEBUGC           STORE DEBUG LEVEL
         ICM   R4,15,0(R4)             DEBUG LEVEL
         BNP   NODEBUG                 SKIP DEBUG LVL IF MINUS
         STC   R4,DEBUGC               STORE DEBUG LEVEL (1 BYTE)
NODEBUG  DS    0H
         CTSADBG 'ENTER',LEVEL=(DEBUGC,1,2,3,4,5)
*                                                               WS2004
*  IF RACF VERSION PREVIOUSLY DETERMINED, RETURN IT             WS2004
*                                                               WS2004
         CLI   WRACFV,0                CHECK INTERNAL VAR       WS2004
         BZ    FVERL                   LOAD IOA PARAMETERS      WS2004
         L     R5,RACFVERN                                      WS2004
         CLI   WRACFV,X'FF'            VER NOT FOUND WAS SET?   BS2625
         BNE   SETVER                  NO,SET REAL ACF2 VER     BS2625
         XC    0(4,R5),0(R5)           YES, SET VER = 0000      BS2625
         B     RET16                        AND SET RC=16       BS2625
SETVER   DS    0H                                               BS2625
         MVC   0(4,R5),WRACFV          STORE VERSION NAME       WS2004
         B     RET0                    RETURN                   WS2004
*                                                               WS2004
*  LOAD AND EXTRACT RACF VERSION FROM PARAM MODULE              WS2004
*                                                               WS2004
FVERL    DS    0H                                               WS2004
         LOAD  EP=CTSPARM,ERRET=ERRLOAD                         WS2004
         LR    R4,R0                                            WS2004
         CTSADBG LEVEL=(DEBUGC,3,4,5),TYPE=SNAP,ID=1,           WS2004 X
               HDR='** CTS PARAMETERS **',                      WS2004 X
               ADDR=(0(R4),ISTLEN-1(R4))                        WS2004
         USING IST,R4                                           WS2004
*                                                               WS2004
         CTSADBG LEVEL=(DEBUGC,1,2,3,4,5),                      WS2004 X
               'RACFV=_',(ISTRACVR,3)                           WS2004
         L     R5,RACFVERN                                      WS2004
         MVC   0(4,R5),ISTRACVR        STORE VERSION NAME       WS2004
         MVC   WRACFV(4),ISTRACVR      STORE VERSION NAME       WS2004
         DROP  R4
*                                                               WS2004
         DELETE EP=CTSPARM                                      WS2004
*                                                               WS2004
*  IF CTSPARM DOES NOT SPECIFY A VERSION, GET IT FROM RCVT      WS2004
*                                                               WS2004
         CLI   WRACFV,0                CHECK INTERNAL VAR       WS2004
         BNZ   RET0                    RETURN                   WS2004
         B     FVERRCVT                GET VERSION FROM RCVT    WS2004
ERRLOAD  DS    0H                                               WS2004
*                                                               WS2004
FVERRCVT DS    0H                                               WS2004
         MVI   WRACFV,X'FF'            SET NOT FOUND VER IND.   BS2625
         L     R10,CVTPTR              LOAD ADDR CVT
         USING CVT,R10                 ADDRESSABILITY CVT DSECT
*
         L     R8,CVTRAC               LOAD ADDR CVT RACF
         LTR   R8,R8                   ADDR SUPPLIED ?
         BZ    RET16                   NO, RACF 'DOWN'
*-------------------------------------------------------------------
*     PRINT RACF CVT AREA ACCORDING DEBUG LEVEL
*-------------------------------------------------------------------
         LR    R9,R8
         LA    R9,1000(R9)
         CTSADBG TYPE=SNAP,ID=1,HDR='** RACF CVT **',                  1
               ADDR=(0(R8),0(R9)),LEVEL=(DEBUGC,4,5)
         USING RCVT,R8                 ADDRESSABILITY CVT RACF DSECT
*
         CTSADBG 'RCVTID=_',(RCVTID,4),LEVEL=(DEBUGC,1,2,3,4,5)
*---------------------------------------------------------------
*     BLOCK ID  - MUST BE 'RCVT'
*---------------------------------------------------------------
         CLC   =CL4'RCVT',OFFID(R8)    CHECK BLOCK ID
         BNE   RET16                   INVALID BLOCK ID, RACF 'DOWN'
*--------------------------------------------------------------------
*     VERSION
*--------------------------------------------------------------------
         CTSADBG LEVEL=(DEBUGC,1,2,3,4,5),                             X
               'VER=$',(OFFVERS(R8),1)
         CLI   OFFVERS(R8),X'08'
         BNE   RET16                   RELEASE NOT SUPPORTED
*
*-----------------------------------------------------------------
*     STORE VERSION,RELEASE NAMES
*-----------------------------------------------------------------
         L     R4,RACFVERN
         MVC   0(4,R4),OFFVRNM(R8)     STORE VERSION NAME
         MVC   WRACFV(4),OFFVRNM(R8)   STORE VERSION NAME        WS2004
         CTSADBG LEVEL=(DEBUGC,1,2,3,4,5),                             X
               'RACF VER NAME=$',(0(R4),4)
         CLI   WRACFV,0                VER AVAIL?                BS2625
         BNE   RET0                    YES, CONTINUE             BS2625
         MVI   WRACFV,X'FF'            NO, SET NOT FOUND IND.    BS2625
         B     RET0                                              WS2004
*                                                                WS2004
*----------------------------------------------------------------WS2004
*     WORK AREA                                                  WS2004
*----------------------------------------------------------------WS2004
*                                                                WS2004
WRACFV   DC    XL4'00'                                           WS2004
*                                                                WS2004
*--------------------------------------------------------------------
*    RETURN CODES
*--------------------------------------------------------------------
RET0     DS    0H                      0-O.K
         SR    R15,R15
         B     RET
*
RET16    DS    0H                      16-RACF DEACTIVATED BY RVARY CMD
         LA    R15,16
         B     RET
*
RET      DS    0H
         CTSADBG 'EXITING WITH RC=~',(?R15),                           1
               LEVEL=(DEBUGC,1,2,3,4,5)
         BRTRN (15)
         LTORG
DEBUGC   DS    X                       DEBUG LEVEL
***************************************************************
*                                                             *
*        SYSTEM CONTROL BLOCKS MAPPING                        *
*                                                             *
***************************************************************
         PRINT GEN
         CVT   DSECT=YES,LIST=YES
         ICHPRCVT
***************************************************************  WS2004
*                                                             *  WS2004
*        IST MAPPING                                          *  WS2004
*                                                             *  WS2004
***************************************************************  WS2004
IST      DSECT                                                   WS2004
         COPY  CTSAIST                  CTS PARAMETERS BLOCK     WS2004
         END
