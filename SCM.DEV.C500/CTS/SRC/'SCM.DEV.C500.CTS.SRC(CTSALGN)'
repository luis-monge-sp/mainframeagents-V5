         TITLE 'CTSALGN - SWITCH UID IN ADDRESS SPACE'                  00040000
*                                                                       00050000
****************************************************************        00060000
****                                                        ****        00070000
****     CONTROL-SA  RELEASE 1.3.0                          ****        00080000
****                                                        ****        00090000
****     LICENCED MATERIAL - NEW DIMENSION SOFTWARE LTD.    ****        00100000
****                                                        ****        00110000
****************************************************************        00120000
*                                                                       00130000
****************************************************************        00140000
*                                                              *        00150000
* TITLE            : HANDLE RACF USER ENVIRONMENT CREATION,    *        00160000
*                    DELETION AND SWITCH FOR THE ENTIRE        *        00160000
*                    ADDRESS SPACE                             *        00160000
*                                                              *        00170000
* FILE NAME        : CTSALGN                                   *        00180000
*                                                              *        00190000
* AUTHOR           : DORON COHEN                               *        00200000
*                                                              *        00210000
* CREATION DATE    : 01/08/94                                  *        00220000
*                                                              *        00230000
* DESCRIPTION      :                                           *        00240000
*                                                              *        00250000
* ASSUMPTIONS AND                                              *        00260000
*   CONSIDERATIONS : 1. CALLER IS APF AUTHORIZED               *        00270000
*                                                              *        00304000
****************************************************************        00310000
*                                                                       00320000
****************************************************************        00330000
* MOD.ID   WHO      WHEN     DESCRIPTION                       *        00340000
* -------- -------- -------- --------------------------------- *        00350000
* PS0320   GUY      22/10/98 ADDITIONAL "CREATE" MODE FOR ACF2 *        00370000
*                            VERSION 6.2-PERFORM CREATE+SWITCH *        00370000
* PS0322   GUY      27/10/98 CHECK CORRECT RACROUTE RC         *        00370000
* BS2421   YONI     21/02/00 CHECK ACEE IS RETURNED FROM CREATE*        00370000
* BS2422   YONI     28/02/00 BS2421 ALSO FOR CREATE2           *        00370000
* IS10162  AVNER    05/12/17 SUPPORT USERID WITH 8 CHARS       *        00370000
* WS10073  SELIG    08/07/18 UPDATE LOGON STATISTICS WHEN      *        00370000
*                            VERIFYING MS ADMINISTRATOR        *
* BS10100  NURITY   10/04/19 HANDLE SECURITY VIOLATION         *        00370000
****************************************************************        00380000
         GBLC   &AMODUSE                                                00380100
&AMODUSE SETC   'CVT'                                                   00381000
*                                                                       00340000
*---------------------------------------------------------------*       00341000
*        PARAMETER BLOCK MAPPING                                        00342000
*---------------------------------------------------------------*       00343000
PRM      DSECT                                                          00344000
PRMACT   DS    A         CREATE/DELETE/SWITCH/CHECK                     00345000
PRMDBG   DS    A         DEBUG LEVEL                                    00345000
PRMROUT  DS    A         RETURN CODE FROM ROUTER                        00345000
PRMRC    DS    A         RETURN CODE FROM RACF                          00345000
PRMRS    DS    A         REASON CODE FROM RACF                          00345000
PRMSPEC  EQU   *
*
*        CREATE PARAMETERS
*
         ORG   PRMSPEC
*BS10100 PRMUPD   DS    A UPDATE LOGON STATISTICS - INPUT (Y/N) WS10073
PRMCUSR  DS    A         USER NAME    - INPUT                           00345000
PRMCGRP  DS    A         GROUP NAME   - INPUT                           00346000
PRMCACEE DS    A         ACEE POINTER - OUTPUT                          00346000
PRMCUPD  DS    A         UPDATE LOGON STATISTICS - INPUT (Y/N)  WS10073
PRMCAPPL DS    A         APPL NAME               - INPUT        BS10100
         ORG   ,
*
*        SWITCH PARAMETERS
*
         ORG   PRMSPEC
PRMSACE1 DS    A         NEW ACEE POINTER - INPUT                       00346000
PRMSACE2 DS    A         OLD ACEE POINTER - OUTPUT                      00346000
         ORG   ,
*
*        DELETE PARAMETERS
*
         ORG   PRMSPEC
PRMDACEE DS    A         ACEE POINTER     - INPUT/OUTPUT                00346000
         ORG   ,
*
*        CHECK PARAMETERS
*
         ORG   PRMSPEC
PRMHACEE DS    A         ACEE POINTER     - OUTPUT                      00346000
         ORG   ,
*                                                                       00480000
*--------------------------------------------------------------*        00490000
*        THE CODE STARTS HERE                                           00370000
*--------------------------------------------------------------*        00510000
CTSALGN  CSECT                                                          00390000
CTSALGN  RMODE ANY                                                      00400000
CTSALGN  AMODE ANY                                                      00410000
*                                                                       00421000
*--------------------------------------------------------------*        00422000
*        INITIALIZE                                                     00423000
*--------------------------------------------------------------*        00424000
         BEGIN *,12                                                     00530000
         CTSEQUR
         CTSLEVEL                                                       00540000
*                                                                       00541000
         LR    R10,R1
         USING PRM,R10
*                                                                       00541000
         CTSAMOD 31,R=R2               MOVE INTO AMODE 31               00560000
         ST    R2,AMODE                SAVE OLD AMODE                   00570000
*                                                                       01611000
         L     R4,PRMDBG               R4 -> DEBUG LEVEL (4 BYTES)
         XC    DBGLEVEL,DBGLEVEL       STORE DEBUG LEVEL
         ICM   R4,15,0(R4)             DEBUG LEVEL
         BNP   NODEBUG                 SKIP DEBUG LVL IF MINUS
         STC   R4,DBGLEVEL             STORE DEBUG LEVEL (1 BYTE)
NODEBUG  DS    0H
*                                                                       01611000
*---------------------------------------------------------------*       01612000
*        ACT ACCORDING TO OPCODE                                        00870000
*---------------------------------------------------------------*       01614000
         L     R4,PRMACT
         CLC   =CL8'CREATE',0(R4)
         BE    CREATE
         CLC   =CL8'CREATE2',0(R4)                               PS0320
         BE    CREATE2                                           PS0320
         CLC   =CL8'DELETE',0(R4)
         BE    DELETE
         CLC   =CL8'SWITCH',0(R4)
         BE    SWITCH
         CLC   =CL8'CLEAR',0(R4)
         BE    CLEAR
         CLC   =CL8'CHECK',0(R4)
         BE    CHECK
         B     ERROPCOD
*                                                                       00870000
*****************************************************************       01611000
*                                                               *       01612000
*        CREATE                                                 *       01612000
*                                                               *       01612000
*****************************************************************       01611000
*                                                                       01614000
CREATE   DS    0H
         CTSADBG LEVEL=(DBGLEVEL,1),'CREATE'
         MVI   UPDLGNST,C'N'           SET FIELD TO 'N'         WS10073
*BS10100 L     R4,PRMUPD               GET UPDATE STATS PARM    WS10073
         L     R4,PRMCUPD     BS10100  GET UPDATE STATS PARM    WS10073
         CLI   0(R4),C'Y'              IS IT 'Y'?               WS10073
         BNE   DONTUPDT                NO, CONTINUE THERE       WS10073
         MVI   UPDLGNST,C'Y'           SET FIELD TO 'Y'         WS10073
DONTUPDT DS    0H                                               WS10073
         CTSADBG LEVEL=(DBGLEVEL,1),'UPDATE LOGON STATS=_',     WS10073X
               (UPDLGNST,1)
*                                                                       01611000
*---------------------------------------------------------------*       01612000
*        CALCULATE EXACT LENGTHS                                        00870000
*---------------------------------------------------------------*       01614000
*                                                                       01611000
*        USER NAME                                                      01611000
*                                                                       01611000
         MVC   USERNM,BLANKS                                            00881400
         L     R4,PRMCUSR                                               00881300
         LA    R1,USERNM                                                00881500
         LA    R2,L'USERNM                                              00881500
LOOPUSR  DS    0H                                                       00881600
         CLI   0(R4),C' '                                               00881600
         BL    ENDLUSR                                                  00881600
         MVC   0(1,R1),0(R4)
         LA    R1,1(R1)
         LA    R4,1(R4)
         BCT   R2,LOOPUSR
ENDLUSR  DS    0H                                                       00881600
         S     R1,=A(USERNM)                                            00881600
         STC   R1,USERL                                                 00882100
*                                                                       01611000
*        GROUP NAME                                                     01611000
*                                                                       01611000
         MVC   GROUPNM,BLANKS                                           00881400
         L     R4,PRMCGRP                                               00881300
         LA    R1,GROUPNM                                               00881500
         LA    R2,L'GROUPNM                                             00881500
LOOPGRP  DS    0H                                                       00881600
         CLI   0(R4),C' '                                               00881600
         BL    ENDLGRP                                                  00881600
         MVC   0(1,R1),0(R4)
         LA    R1,1(R1)
         LA    R4,1(R4)
         BCT   R2,LOOPGRP
ENDLGRP  DS    0H                                                       00881600
         S     R1,=A(GROUPNM)                                           00881600
         STC   R1,GROUPL                                                00882100
*                                                               BS10100 01611000
*        APPL  NAME                                             BS10100 01611000
*                                                               BS10100 01611000
         MVC   APPLNAME,BLANKS                                  BS10100 00881400
         L     R4,PRMCAPPL                                      BS10100 00881300
         LA    R1,APPLNAME                                      BS10100 00881500
         LA    R2,L'APPLNAME                                    BS10100 00881500
LOOPAPPL DS    0H                                               BS10100 00881600
         CLI   0(R4),C' '                                       BS10100 00881600
         BL    ENDLAPPL                                         BS10100 00881600
         MVC   0(1,R1),0(R4)                                    BS10100
         LA    R1,1(R1)                                         BS10100
         LA    R4,1(R4)                                         BS10100
         BCT   R2,LOOPAPPL                                      BS10100
ENDLAPPL DS    0H                                               BS10100 00881600
*                                                                       00882900
         CTSADBG LEVEL=(DBGLEVEL,1),'USER=~~/_ (~)',                   X
               (USERNM,USERNM+4,USERNM,8,UID)
         CTSADBG LEVEL=(DBGLEVEL,1),'GROUP=~~/_ (~)',                  X
               (GROUPNM,GROUPNM+4,GROUPNM,8,GID)
         CTSADBG LEVEL=(DBGLEVEL,1),'APPL=~~/_ )',              BS10100X
               (APPLNAME,APPLNAME+4,APPLNAME,8)                 BS10100
*                                                                       00882900
         TR    USERNM,LOW2UPR
         TR    GROUPNM,LOW2UPR
         TR    APPLNAME,LOW2UPR                                 BS10100
*                                                                       00885600
*---------------------------------------------------------------*       00885700
*        CREATE ACEE                                                    00885800
*---------------------------------------------------------------*       00885900
*                                                                       01611000
*        CLEAR RACF WORK AREA                                           01611000
*                                                                       01611000
         L     R0,=A(RACWORK)                                           05730000
         LA    R1,L'RACWORK            SET LENGTH                       05740000
         XR    R15,R15                 PADD WITH X'00'                  05750000
         MVCL  R0,R14                  CLEAR                            05760000
         L     R4,PRMCACEE
         CLI   GROUPL,0
         BE    ACEECRE2
*                                                                       01611000
*        CREATE ACEE  (WITH GROUP PARAMETER)                            01611000
*                                                                       01611000
         CLI   UPDLGNST,C'Y'           UPDATE LOGON STATS = 'Y'?WS10073
         BE    ACEECRE3                YES, CONTINUE THERE      WS10073
*
ACEECRE1 RACROUTE REQUEST=VERIFY,                                      X00886000
               ENVIR=CREATE,           CREATE AN ACEE                  X00886100
               RELEASE=1.8,                                            X00886100
               LOG=ASIS,               LOG ONLY FAILED REQUESTS        X00886300
               STAT=NO,                NO STATISTICS                   X00886300
               APPL=APPLNAME,          APPL NAME                BS10100X
               LOC=BELOW,              ACEE SHOULD BE BELOW 16M LINE   X00886300
               ACEE=(R4),              ADDR OF ACEE ADDRESS            X00886500
               USERID=USERID,          USERID                          X00886600
               GROUP=GROUPID,          GROUPID                         X00886600
               PASSCHK=NO,             IGNORE PASSWORD PROCESSING      X00886700
               WORKA=RACWORK           RACROUTE WORK AREA               00886900
         LM    R2,R3,ACEECRE1+4
         B     CREATRC
*
* START WS10073               START WS10073               START WS10073
ACEECRE3 RACROUTE REQUEST=VERIFY,                                      X00886000
               ENVIR=CREATE,           CREATE AN ACEE                  X00886100
               RELEASE=1.8,                                            X00886100
               LOG=ASIS,               LOG ONLY FAILED REQUESTS        X00886300
               STAT=ASIS,              UPDATE STATISTICS               X00886300
               APPL=APPLNAME,          APPL NAME                BS10100X
               LOC=BELOW,              ACEE SHOULD BE BELOW 16M LINE   X00886300
               ACEE=(R4),              ADDR OF ACEE ADDRESS            X00886500
               USERID=USERID,          USERID                          X00886600
               GROUP=GROUPID,          GROUPID                         X00886600
               PASSCHK=NO,             IGNORE PASSWORD PROCESSING      X00886700
               WORKA=RACWORK           RACROUTE WORK AREA               00886900
         LM    R2,R3,ACEECRE3+4
         B     CREATRC
* END WS10073                  END WS10073                  END WS10073
*                                                                       01611000
*        CREATE ACEE  (NO GROUP PARAMETER)                              01611000
*                                                                       01611000
ACEECRE2 DS    0H                                               WS10073
         CLI   UPDLGNST,C'Y'           UPDATE LOGON STATS = 'Y'?WS10073
         BE    ACEECRE5                YES, CONTINUE THERE      WS10073
*
*WS10073 ACEECRE2 RACROUTE REQUEST=VERIFY,                            X 00886000
ACEECRE4 RACROUTE REQUEST=VERIFY,                                      X00886000
               ENVIR=CREATE,           CREATE AN ACEE                  X00886100
               RELEASE=1.8,                                            X00886100
               LOG=ASIS,               NO LOG                          X00886300
               STAT=NO,                NO STATISTICS                   X00886300
               APPL=APPLNAME,          APPL NAME                BS10100X
               LOC=BELOW,              ACEE SHOULD BE BELOW 16M LINE   X00886300
               ACEE=(R4),              ADDR OF ACEE ADDRESS            X00886500
               USERID=USERID,          USERID                          X00886600
               PASSCHK=NO,             OLD PASSWORD                    X00886700
               WORKA=RACWORK           RACROUTE WORK AREA               00886900
*WS10073 LM    R2,R3,ACEECRE2+4
         LM    R2,R3,ACEECRE4+4                                 WS10073
         B     CREATRC
*
* START WS10073               START WS10073               START WS10073
ACEECRE5 RACROUTE REQUEST=VERIFY,                                      X00886000
               ENVIR=CREATE,           CREATE AN ACEE                  X00886100
               RELEASE=1.8,                                            X00886100
               LOG=ASIS,               NO LOG                          X00886300
               STAT=ASIS,              UPDATE STATISTICS               X00886300
               APPL=APPLNAME,          APPL NAME                BS10100X
               LOC=BELOW,              ACEE SHOULD BE BELOW 16M LINE   X00886300
               ACEE=(R4),              ADDR OF ACEE ADDRESS            X00886500
               USERID=USERID,          USERID                          X00886600
               PASSCHK=NO,             OLD PASSWORD                    X00886700
               WORKA=RACWORK           RACROUTE WORK AREA               00886900
         LM    R2,R3,ACEECRE5+4
         B     CREATRC
* END WS10073                  END WS10073                  END WS10073
*                                                                       01611000
*---------------------------------------------------------------*       01612000
*        SET RETURN CODE                                                00870000
*---------------------------------------------------------------*       01614000
*                                                                       00887100
CREATRC  DS    0H
         CTSADBG LEVEL=(DBGLEVEL,1),                                   X
               'CREATE ROUTER RC=# RACF RC=# REASON=#',                X
               (?R15,?R2,?R3)
*                                                                       00887100
         L     R4,PRMROUT
         ST    R15,0(R4)
         L     R4,PRMRC
         ST    R2,0(R4)
         L     R4,PRMRS
         ST    R3,0(R4)
         L     R4,PRMCACEE       GET ACEE POINTER                BS2421
         L     R4,0(R4)          POINT TO ACEE     (????)        BS2421
         LTR   R4,R4             IS IT ZERO?                     BS2421
         BZ    ERRACEE1          YES - RETURN ERROR              BS2421
         CLC   =CL4'ACEE',0(R4)  EYE CATCHER EXIST?              BS2421
         BNE   ERRACEE2          NO - ERROR                      BS2421
         XR    R15,R15
         B     EXIT
*                                                                PS0320 00870000
* PS0320 - ADD "CREATE2" MODE                                    PS0320 00870000
*****************************************************************PS0320 01611000
*                                                               *PS0320 01612000
*        CREATE2 - THIS MODE WAS ADDED ESPECIALLY FOR ACF2 6.2. *PS0320 01612000
*                  IN VERSION 6.2 OF ACF2, A UCB IS CREATED     *PS0320 01612000
*                  FOR AN ACEE ONLY IF HE IS THE PRIMARY ACEE   *PS0320 01612000
*                  (THE CURRENTLY ACTIVE ONE). ALL OTHER ACEES  *PS0320 01612000
*                  ARE ASSIGNED A MCB.                          *PS0320 01612000
*                  AUTHORIZATION FOR ACF2 COMMANDS IS CHECKED   *PS0320 01612000
*                  ONLY AGAINST THE UCB AND NOT AGAINST MCBS.   *PS0320 01612000
*                                                               *PS0320 01612000
*                  IN THE REGULAR "CREATE" MODE, WE CREATED A   *PS0320 01612000
*                  NEW ACEE WITHOUT USING THE RACROUTE OPTION   *PS0320 01612000
*                  TO DO ASXB ACEE SWITCHING. WE DID THE        *PS0320 01612000
*                  SWITCH OURSELVES. THIS CAUSED ALL NEWLY      *PS0320 01612000
*                  CREATED ACEES TO BE GIVEN MCB INSTEAD OF UCB.*PS0320 01612000
*                  AS A RESULT OF THAT ACF2 AUTHORIZATIONS WERE *PS0320 01612000
*                  NOT EFFECTED BY THE LOGIN SWITCH.            *PS0320 01612000
*                                                               *PS0320 01612000
*                  TO SOLVE THIS, WE USE THE ASXB ACEE SWITCHING*PS0320 01612000
*                  OF RACROUTE, NOT BEFORE KEEPING THE OLD ACEE.*PS0320 01612000
*                  AFTER WE CREATE THE NEW ACEE, WE RESTORE THE *PS0320 01612000
*                  OLD ONE BACK. THE RESULT IS THE SAME AS THE  *PS0320 01612000
*                  OLD "CREATE" MODE, PLUS WE GAIN A UCB FOR THE*PS0320 01612000
*                  NEW ACEE INSTEAD OF MCB.                     *PS0320 01612000
*                                                               *PS0320 01612000
*****************************************************************PS0320 01611000
*                                                                PS0320 01614000
CREATE2  DS    0H                                                PS0320
         CTSADBG LEVEL=(DBGLEVEL,1),'CREATE2'                    PS0320
         MVI   UPDLGNST,C'N'           SET FIELD TO 'N'         WS10073
*BS10100 L     R4,PRMUPD               GET UPDATE STATS PARM    WS10073
         L     R4,PRMCUPD     BS10100  GET UPDATE STATS PARM    WS10073
         CLI   0(R4),C'Y'              IS IT 'Y'?               WS10073
         BNE   DONTUPD2                NO, CONTINUE THERE       WS10073
         MVI   UPDLGNST,C'Y'           SET FIELD TO 'Y'         WS10073
DONTUPD2 DS    0H                                               WS10073
         CTSADBG LEVEL=(DBGLEVEL,1),'UPDATE LOGON STATS=_',     WS10073X
               (UPDLGNST,1)
*                                                                PS0320 01611000
*---------------------------------------------------------------*PS0320 01612000
*        CALCULATE EXACT LENGTHS                                 PS0320 00870000
*---------------------------------------------------------------*PS0320 01614000
*                                                                PS0320 01611000
*        USER NAME                                               PS0320 01611000
*                                                                PS0320 01611000
         MVC   USERNM,BLANKS                                     PS0320 00881400
         L     R4,PRMCUSR                                        PS0320 00881300
         LA    R1,USERNM                                         PS0320 00881500
         LA    R2,L'USERNM                                       PS0320 00881500
LOOPUSR2 DS    0H                                                PS0320 00881600
         CLI   0(R4),C' '                                        PS0320 00881600
         BL    ENDLUSR2                                          PS0320 00881600
         MVC   0(1,R1),0(R4)                                     PS0320
         LA    R1,1(R1)                                          PS0320
         LA    R4,1(R4)                                          PS0320
         BCT   R2,LOOPUSR2                                       PS0320
ENDLUSR2 DS    0H                                                PS0320 00881600
         S     R1,=A(USERNM)                                     PS0320 00881600
         STC   R1,USERL                                          PS0320 00882100
*                                                                PS0320 01611000
*        GROUP NAME                                              PS0320 01611000
*                                                                PS0320 01611000
         MVC   GROUPNM,BLANKS                                    PS0320 00881400
         L     R4,PRMCGRP                                        PS0320 00881300
         LA    R1,GROUPNM                                        PS0320 00881500
         LA    R2,L'GROUPNM                                      PS0320 00881500
LOOPGRP2 DS    0H                                                PS0320 00881600
         CLI   0(R4),C' '                                        PS0320 00881600
         BL    ENDLGRP2                                          PS0320 00881600
         MVC   0(1,R1),0(R4)                                     PS0320
         LA    R1,1(R1)                                          PS0320
         LA    R4,1(R4)                                          PS0320
         BCT   R2,LOOPGRP2                                       PS0320
ENDLGRP2 DS    0H                                                PS0320 00881600
         S     R1,=A(GROUPNM)                                    PS0320 00881600
         STC   R1,GROUPL                                         PS0320 00882100
*                                                                PS0320 00882200
*                                                               BS10100 01611000
*        APPL  NAME                                             BS10100 01611000
*                                                               BS10100 01611000
         MVC   APPLNAME,BLANKS                                  BS10100 00881400
         L     R4,PRMCAPPL                                      BS10100 00881300
         LA    R1,APPLNAME                                      BS10100 00881500
         LA    R2,L'APPLNAME                                    BS10100 00881500
LOOPAPL2 DS    0H                                               BS10100 00881600
         CLI   0(R4),C' '                                       BS10100 00881600
         BL    ENDLAPL2                                         BS10100 00881600
         MVC   0(1,R1),0(R4)                                    BS10100
         LA    R1,1(R1)                                         BS10100
         LA    R4,1(R4)                                         BS10100
         BCT   R2,LOOPAPL2                                      BS10100
ENDLAPL2 DS    0H                                               BS10100 00881600
*                                                                PS0320 00882900
         CTSADBG LEVEL=(DBGLEVEL,1),'USER=~~/_ (~)',             PS0320X
               (USERNM,USERNM+4,USERNM,8,UID)                    PS0320
         CTSADBG LEVEL=(DBGLEVEL,1),'GROUP=~~/_ (~)',            PS0320X
               (GROUPNM,GROUPNM+4,GROUPNM,8,GID)                 PS0320
         CTSADBG LEVEL=(DBGLEVEL,1),'APPL=~~/_ )',              BS10100X
               (APPLNAME,APPLNAME+4,APPLNAME,8)                 BS10100
*                                                                PS0320 00882900
         TR    USERNM,LOW2UPR                                    PS0320
         TR    GROUPNM,LOW2UPR                                   PS0320
         TR    APPLNAME,LOW2UPR                                 BS10100
*                                                                PS0320 00885600
*---------------------------------------------------------------*PS0320 00885700
*        SAVE OLD ACEE ADDRESSES FROM ASXB AND TCB               PS0320 00885800
*---------------------------------------------------------------*PS0320 00885900
*                                                                PS0320 01611000
         L     R2,PSAAOLD-PSA                                    PS0320
         L     R2,ASCBASXB-ASCB(,R2)                             PS0320
         ST    R2,ADDRASXB                    KEEP ASXB ADDRESS  PS0320
         L     R2,ASXBSENV-ASXB(,R2)                             PS0320
         ST    R2,BCKASENV                    KEEP ASXB ACEE     PS0320
*                                                                PS0320 01611000
         L     R2,PSATOLD-PSA                                    PS0320
         L     R2,TCBSENV-TCB(,R2)                               PS0320
         ST    R2,BCKTSENV                    KEEP TCB ACEE      PS0320
*                                                                PS0320 00885600
*---------------------------------------------------------------*PS0320 00885700
*        CREATE ACEE                                             PS0320 00885800
*---------------------------------------------------------------*PS0320 00885900
*                                                                PS0320 01611000
*        CLEAR RACF WORK AREA                                    PS0320 01611000
*                                                                PS0320 01611000
         L     R0,=A(RACWORK)                                    PS0320 05730000
         LA    R1,L'RACWORK            SET LENGTH                PS0320 05740000
         XR    R15,R15                 PADD WITH X'00'           PS0320 05750000
         MVCL  R0,R14                  CLEAR                     PS0320 05760000
*                                                                PS0320 01611000
*        CLEAR ASXBSENV - SO RACROUTE WILL "AGREE"               PS0320 01611000
*                         TO DO THE ACEE SWITCHING               PS0320 01611000
*                                                                PS0320 01611000
         L     R2,ADDRASXB                                       PS0320
         MODESET MODE=SUP,KEY=ZERO                               PS0320
         XC    ASXBSENV-ASXB(4,R2),ASXBSENV-ASXB(R2)             PS0320
         MODESET MODE=PROB,KEY=NZERO                             PS0320
*                                                                PS0320 01611000
*                                                                PS0320 01611000
         L     R4,PRMCACEE                                       PS0320
         CLI   GROUPL,0                                          PS0320
         BE    ACEECR22                                          PS0320
*                                                                PS0320 01611000
*        CREATE ACEE  (WITH GROUP PARAMETER)                     PS0320 01611000
*                                                                PS0320 01611000
         CLI   UPDLGNST,C'Y'           UPDATE LOGON STATS = 'Y'?WS10073
         BE    ACEECR23                YES, CONTINUE THERE      WS10073
*
ACEECR21 RACROUTE REQUEST=VERIFY,                                PS0320X00886000
               ENVIR=CREATE,           CREATE AN ACEE            PS0320X00886100
               RELEASE=1.8,                                      PS0320X00886100
               LOG=ASIS,               LOG ONLY FAILED REQUESTS  PS0320X00886300
               STAT=NO,                NO STATISTICS             PS0320X00886300
               APPL=APPLNAME,          APPL NAME                BS10100X
               LOC=BELOW,              ACEE SHOULD BE BELOW 16M  PS0320X00886300
               USERID=USERID,          USERID                    PS0320X00886600
               GROUP=GROUPID,          GROUPID                   PS0320X00886600
               PASSCHK=NO,             IGNORE PASSWORD PROCESSINGPS0320X00886700
               WORKA=RACWORK           RACROUTE WORK AREA        PS0320 00886900
         LM    R2,R3,ACEECR21+4                                  PS0322
         B     CRE2TRC                                           PS0320
*
* START WS10073               START WS10073               START WS10073
ACEECR23 RACROUTE REQUEST=VERIFY,                                PS0320X00886000
               ENVIR=CREATE,           CREATE AN ACEE            PS0320X00886100
               RELEASE=1.8,                                      PS0320X00886100
               LOG=ASIS,               LOG ONLY FAILED REQUESTS  PS0320X00886300
               STAT=ASIS,              UPDATE STATISTICS         PS0320X00886300
               APPL=APPLNAME,          APPL NAME                BS10100X
               LOC=BELOW,              ACEE SHOULD BE BELOW 16M  PS0320X00886300
               USERID=USERID,          USERID                    PS0320X00886600
               GROUP=GROUPID,          GROUPID                   PS0320X00886600
               PASSCHK=NO,             IGNORE PASSWORD PROCESSINGPS0320X00886700
               WORKA=RACWORK           RACROUTE WORK AREA        PS0320 00886900
         LM    R2,R3,ACEECR23+4                                  PS0322
         B     CRE2TRC                                           PS0320
* END WS10073                  END WS10073                  END WS10073
*                                                                PS0320 01611000
*        CREATE ACEE  (NO GROUP PARAMETER)                       PS0320 01611000
*                                                                PS0320 01611000
ACEECR22 DS    0H                                               WS10073
         CLI   UPDLGNST,C'Y'           UPDATE LOGON STATS = 'Y'?WS10073
         BE    ACEECR25                YES, CONTINUE THERE      WS10073
*
*WS10073 ACEECR22 RACROUTE REQUEST=VERIFY,                      PS0320X 00886000
ACEECR24 RACROUTE REQUEST=VERIFY,                        PS0320 WS10073X00886000
               ENVIR=CREATE,           CREATE AN ACEE            PS0320X00886100
               RELEASE=1.8,                                      PS0320X00886100
               LOG=ASIS,               NO LOG                    PS0320X00886300
               STAT=NO,                NO STATISTICS             PS0320X00886300
               APPL=APPLNAME,          APPL NAME                BS10100X
               LOC=BELOW,              ACEE SHOULD BE BELOW 16M  PS0320X00886300
               USERID=USERID,          USERID                    PS0320X00886600
               PASSCHK=NO,             OLD PASSWORD              PS0320X00886700
               WORKA=RACWORK           RACROUTE WORK AREA        PS0320 00886900
*WS10073 LM    R2,R3,ACEECR22+4                                  PS0322
         LM    R2,R3,ACEECR24+4                          PS0322 WS10073
         B     CRE2TRC                                           PS0320
*
* START WS10073               START WS10073               START WS10073
ACEECR25 RACROUTE REQUEST=VERIFY,                                      X00886000
               ENVIR=CREATE,           CREATE AN ACEE                  X00886100
               RELEASE=1.8,                                            X00886100
               LOG=ASIS,               NO LOG                          X00886300
               STAT=ASIS,              UPDATE STATISTICS               X00886300
               APPL=APPLNAME,          APPL NAME                BS10100X
               LOC=BELOW,              ACEE SHOULD BE BELOW 16M        X00886300
               USERID=USERID,          USERID                          X00886600
               PASSCHK=NO,             OLD PASSWORD                    X00886700
               WORKA=RACWORK           RACROUTE WORK AREA               00886900
         LM    R2,R3,ACEECR25+4
         B     CRE2TRC
* END WS10073                  END WS10073                  END WS10073
*                                                                PS0320 01611000
*---------------------------------------------------------------*PS0320 01612000
*        RESTORE OLD ACEE TO ASXB AND TCB AND SET RETURN CODE    PS0320 00870000
*---------------------------------------------------------------*PS0320 01614000
*                                                                PS0320 00887100
CRE2TRC  DS    0H                                                PS0320
*                                                                PS0320 01611000
*        RESTORE OLD ACEE TO ASXB AND TCB                        PS0320 01611000
*                                                                PS0320 01611000
         STM   R0,R15,ALLREGS                   KEEP REGS BEFORE PS0320
         L     R2,ADDRASXB                                       PS0320
         MVC   0(4,R4),ASXBSENV-ASXB(R2)        RETURN NEW ACEE  PS0320
         MODESET MODE=SUP,KEY=ZERO                               PS0320
         MVC   ASXBSENV-ASXB(4,R2),BCKASENV     RESTORE ASXB ACEEPS0320
         L     R2,PSATOLD-PSA                                    PS0320
         MVC   TCBSENV-TCB(4,R2),BCKTSENV       RESTORE TCB ACEE PS0320
         MODESET MODE=PROB,KEY=NZERO                             PS0320
         LM    R0,R15,ALLREGS                   REST. REGS AFTER PS0320
*                                                                PS0320 01611000
*                                                                PS0320 01611000
         CTSADBG LEVEL=(DBGLEVEL,1),                             PS0320X
               'CREATE ROUTER RC=# RACF RC=# REASON=#',          PS0320X
               (?R15,?R2,?R3)                                    PS0320
*                                                                PS0320 00887100
         L     R4,PRMROUT                                        PS0320
         ST    R15,0(R4)                                         PS0320
         L     R4,PRMRC                                          PS0320
         ST    R2,0(R4)                                          PS0320
         L     R4,PRMRS                                          PS0320
         ST    R3,0(R4)                                          PS0320
         L     R4,PRMCACEE       GET ACEE POINTER                BS2422
         L     R4,0(R4)          POINT TO ACEE     (????)        BS2422
         LTR   R4,R4             IS IT ZERO?                     BS2422
         BZ    ERRACEE1          YES - RETURN ERROR              BS2422
         CLC   =CL4'ACEE',0(R4)  EYE CATCHER EXIST?              BS2422
         BNE   ERRACEE2          NO - ERROR                      BS2422
         XR    R15,R15                                           PS0320
         B     EXIT                                              PS0320
*                                                                       00870000
*****************************************************************       01611000
*                                                               *       01612000
*        SWITCH                                                *        01612000
*                                                               *       01612000
*****************************************************************       01611000
SWITCH   DS    0H
         CTSADBG LEVEL=(DBGLEVEL,1),'SWITCH'
*                                                                       00887100
*---------------------------------------------------------------*       00887400
*        VALIDATE NEW ACEE                                              00887100
*---------------------------------------------------------------*       00887400
*
         L     R3,PRMSACE1
         L     R3,0(R3)
         LTR   R3,R3
         BZ    ERRACEE1
         CLC   =CL4'ACEE',0(R3)
         BNE   ERRACEE2
*                                                                       00887100
*---------------------------------------------------------------*       00887400
*        RETURN OLD ACEE                                                00887100
*---------------------------------------------------------------*       00887400
*
         L     R2,PSAAOLD-PSA
         L     R2,ASCBASXB-ASCB(,R2)
*IS10162 LA    R5,ASXBUSER-ASXB(,R2)                             WS2005
         LA    R5,ASXBUSR8-ASXB(,R2)                            IS10162
         LA    R2,ASXBSENV-ASXB(,R2)
         L     R4,PRMSACE2
         MVC   0(4,R4),0(R2)
*                                                                       00887100
*---------------------------------------------------------------*       00887400
*        SET NEW ACEE IN ASXB                                           00887100
*---------------------------------------------------------------*       00887400
*                                                                       00887100
         MODESET MODE=SUP,KEY=ZERO
         ST    R3,0(R2)
*IS10162 MVC   0(7,R5),ACEEUSRI-ACEE(R3)                         WS2005
         MVC   0(8,R5),ACEEUSRI-ACEE(R3)                        IS10162
         MODESET MODE=PROB,KEY=NZERO
*                                                                       00887100
*---------------------------------------------------------------*       00887400
*        SWITCH WAS DONE                                                00887100
*---------------------------------------------------------------*       00887400
*                                                                       00887100
         XR    R15,R15
         B     EXIT
*                                                                       00870000
*****************************************************************       01611000
*                                                               *       01612000
*        SWITCH                                                *        01612000
*                                                               *       01612000
*****************************************************************       01611000
CHECK    DS    0H
         CTSADBG LEVEL=(DBGLEVEL,1),'CHECK'
*                                                                       00887100
*---------------------------------------------------------------*       00887400
*        RETURN OLD ACEE                                                00887100
*---------------------------------------------------------------*       00887400
*
         L     R2,PSAAOLD-PSA
         L     R2,ASCBASXB-ASCB(,R2)
         LA    R2,ASXBSENV-ASXB(,R2)
         L     R4,PRMHACEE
         MVC   0(4,R4),0(R2)
         XR    R15,R15
         B     EXIT
*                                                                       00870000
*****************************************************************       01611000
*                                                               *       01612000
*        CLEAR                                                 *        01612000
*                                                               *       01612000
*****************************************************************       01611000
CLEAR    DS    0H
         CTSADBG LEVEL=(DBGLEVEL,1),'CLEAR'
*                                                                       00870000
         L     R2,PSAAOLD-PSA
         L     R2,ASCBASXB-ASCB(,R2)
         LA    R2,ASXBSENV-ASXB(,R2)
         MODESET MODE=SUP,KEY=ZERO
         XC    0(4,R2),0(R2)
         MODESET MODE=PROB,KEY=NZERO
*                                                                       00870000
         XR    R15,R15
         B     EXIT
*                                                                       00870000
*****************************************************************       01611000
*                                                               *       01612000
*        DELETE                                                 *       01612000
*                                                               *       01612000
*****************************************************************       01611000
*                                                                       01614000
DELETE   DS    0H
         CTSADBG LEVEL=(DBGLEVEL,1),'DELETE'
*                                                                       00887100
*---------------------------------------------------------------*       00887200
*        DELETE ACEE                                                    00887300
*---------------------------------------------------------------*       00887400
*                                                                       01611000
*        AVOID DELETING A WRONG ACEE (WRONG POINTER/ACTIVE)             01611000
*                                                                       01611000
         L     R3,PRMSACE1
         CTSADBG LEVEL=(DBGLEVEL,1),'ACEE=~',(0(R3))
*                                                                       01611000
         L     R3,0(R3)
         LTR   R3,R3
         BZ    ERRACEE1
         CLC   =CL4'ACEE',0(R3)
         BNE   ERRACEE2
*
         L     R2,PSAAOLD-PSA
         L     R2,ASCBASXB-ASCB(,R2)
         L     R2,ASXBSENV-ASXB(,R2)
         CR    R2,R3
         BE    ERRACEE3
*                                                                       01611000
*        CLEAR RACF WORK AREA                                           01611000
*                                                                       01611000
         L     R0,=A(RACWORK)                                           05730000
         LA    R1,L'RACWORK            SET LENGTH                       05740000
         XR    R15,R15                 PADD WITH X'00'                  05750000
         MVCL  R0,R14                  CLEAR                            05760000
*                                                                       01611000
*        DELETE ACEE                                                    01611000
*                                                                       01611000
         L     R4,PRMDACEE
ACEEDEL  RACROUTE REQUEST=VERIFY,                                      X00887600
               ENVIR=DELETE,           DELETE AN ACEE                  X00887700
               RELEASE=1.8,                                            X00886100
               LOG=ASIS,               NO LOG                          X00886300
               STAT=NO,                NO STATISTICS                   X00886300
               ACEE=(R4),              ADDR OF ACEE ADDRESS            X00887800
               WORKA=RACWORK           RACROUTE WORK AREA               00887900
*                                                                       00888000
*        SET RETURN CODES                                               00888000
*                                                                       00888000
         LM    R2,R3,ACEEDEL+4
         CTSADBG LEVEL=(DBGLEVEL,10),                                  X
               'DELETE ROUTER RC=# RACF RC=# REASON=#',                X
               (?R15,?R2,?R3)
         L     R4,PRMROUT
         ST    R15,0(R4)
         L     R4,PRMRC
         ST    R2,0(R4)
         L     R4,PRMRS
         ST    R3,0(R4)
*                                                                       00888000
         XR    R15,R15
         B     EXIT
*                                                                       00888000
*---------------------------------------------------------------*       00888100
*        ERR CODES                                                      00888200
*---------------------------------------------------------------*       00889000
*                                                                       00888000
ERROPCOD DS    0H
         LA    R15,8
         B     EXIT
ERRACEE1 DS    0H
         LA    R15,12
         B     EXIT
ERRACEE2 DS    0H
         LA    R15,16
         B     EXIT
ERRACEE3 DS    0H
         LA    R15,20
         B     EXIT
*                                                                       00888000
*---------------------------------------------------------------*       00888100
*        FINISH                                                         00888200
*---------------------------------------------------------------*       00889000
EXIT     DS    0H                                                       01615000
*                                                                       01630100
         LR    R5,R15                                                   01631000
         L     R2,AMODE                                                 01620000
         CTSAMOD RESTORE,R=R2                                           01630000
         LR    R15,R5                                                   01631000
*                                                                       01630100
         BRTRN (15)                                                     01640000
*                                                                       01650000
*---------------------------------------------------------------*       01660000
*        CONSTANTS                                                      01670000
*---------------------------------------------------------------*       01680000
BLANKS   DC    256C' '
LOW2UPR  DC    256AL1(*-LOW2UPR)       TRANSLATE TABLE TO UPPERCASE
         ORG   LOW2UPR
         DC    64C' '
         ORG   LOW2UPR+C'A'-X'40'
         DC    C'ABCDEFGHI'
         ORG   LOW2UPR+C'J'-X'40'
         DC    C'JKLMNOPQR'
         ORG   LOW2UPR+C'S'-X'40'
         DC    C'STUVWXYZ'
         ORG   ,
*                                                                       01650000
*---------------------------------------------------------------*       01660000
*        WORKAREA                                                       01670000
*---------------------------------------------------------------*       01680000
AMODE    DS    A               ENTRY AMODE                              01690000
DBGLEVEL DS    X
UPDLGNST DS    C               UPDATE LOGON STATISTICS (Y / N)  WS10073
*                                                                       01001000
         CNOP  0,4
UID      DC    XL3'0'
USERID   DS    0XL9                                                     01010000
USERL    DS    X                                                        01010000
USERNM   DS    CL8                                                      01020000
*                                                                       01030000
         CNOP  0,4
GID      DC    XL3'0'
GROUPID  DS    0XL9                                                     01010000
GROUPL   DS    X                                                        01010000
GROUPNM  DS    CL8                                                      01020000
*                                                                       01030000
APPLNAME DS    CL8                APPL NAME                     BS10100
*                                                                       01030000
RACWORK  DS    XL512                                                    01036000
BCKASENV DS    AL4                                               PS0320
BCKTSENV DS    AL4                                               PS0320
ALLREGS  DS    CL64                                              PS0320
ADDRASXB DS    AL4                                               PS0320
*                                                                       01830000
         LTORG                                                          01950000
*                                                                       01950100
*---------------------------------------------------------------*       01950200
*        CONTROL BLOCKS MAPPING                                         01950300
*---------------------------------------------------------------*       01950400
         PRINT NOGEN                                                    01951000
CVT      CVT   DSECT=YES,PREFIX=YES,LIST=YES                            01952000
         IHAPSA                                                         01952000
         IHAASCB                                                        01952000
         IHAASXB                                                        01952000
         IHAACEE                                                 WS2005 01952000
         IKJTCB                                                  PS0320
         END                                                            01960000
