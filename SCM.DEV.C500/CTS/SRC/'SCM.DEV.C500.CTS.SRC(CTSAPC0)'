CTSAPC0 TITLE 'CTSAPC0-CONTROL-M XMEM POST PC ROUTINE'
**********************************************************************
*                                                                    *
*                                                                    *
*                           CTSAPC0                                  *
*                           -------                                  *
*                                                                    *
*                                                                    *
*                    WRITEN BY MIRCEA TOMA                           *
*                                                                    *
*                                                                    *
*                                                                    *
*   FUNCTION:  XMEM POST PC ROUTINE.                                 *
*                                                                    *
*   PARAMETERS:                                                      *
*              R1: ADDRESS OF THE ECB TO BE POSTED                   *
*                                                                    *
*   RETURN-CODES:                                                    *
*                   0   SUCCESSFULL ENDING                           *
*                   8   ERROR                                        *
*                                                                    *
*                                                                    *
*                                                                    *
**********************************************************************
* IS10085 29.05.13 NURITY ABEND S0D3 WHEN RETURNING TO A REUABLE     *
*                         ADDRESS SPACE (BS10034)                    *
*SAS2IBMN 12.12.16 NURITY CORRECT POST PARAMETERS.                   *
* IS10188 14.10.24 AVNERL MOVE OUR MODULES ABOVE 16M LINE            *
**********************************************************************
**********************************************************************
**********************************************************************
*                                                                    *
*                      IS10085 DESCRIPTION                           *
*                      ===================                           *
*                                                                    *
* BACKGROUND                                                         *
* ----------                                                         *
* REUSABLE ADDRESS SPACES ARE EASILY SUPPORTED WITH STACKING PCS     *
* WHERE THE ENVIRONMENT IS SAVED IN THE LINKGAE STACK WHEN THE PC    *
* RECEIVES CONTROL AND RESTORED WHEN THE PC RETURNS WITH THE PR      *
* COMMAND.                                                           *
* UNFORTUNATELY, IT IS TOO COMPLICATED TO CHANGE OUR PCS TO BE       *
* STACKING PCS. THE PCS CAN BE CALLED FROM EXITS FROM DIFFERENT      *
* ENVIRONMENTS AND WE CANNOT GUARANTEE THAT ALL ENVIRONMENTS ARE     *
* CHNAGED TO USE STACKING PCS.                                       *
*                                                                    *
* THEREFORE, WE ADD THE ABILITY TO BE CALLED FROM A REUSABLE         *
* ADDRESS SPACE BY USING THE PTI, SSAIR, ETC. COMMANDS INSTEAD OF    *
* PT, SSAR, ETC. WITH PT, SSAR, ETC. YOU NEED TO PROVIDE THE ASN     *
* (ADDRESS SPACE NUMBER) OF THE ADDRESS SPACE, WHILE WITH PTI,       *
* SSAIR ETC. YOU NEED TO PROVIDE THE ASN AND THE ASTEIN              *
* (ASN-SECOND-TABLE-ENTRY INSTANCE NUMBER) WHICH UNIQELY IDENTIFIES  *
* THE INSTANCE OF THE REUSABE ADDRESS SPACE.                         *
*                                                                    *
*                                                                    *
* LOGIC                                                              *
* -----                                                              *
* THE PTI COMMAND HAS TO BE USED TO RETURN TO A REUABLE ADDRESS      *
* SOACE. THIS COMMAND REQUIRES THE SASN, PKM (PSW KEY MASK) AND      *
* ASTEIN. R3, RECEIVED AT ENTRY TO THE PC, CONTAINS THE SASN AND PKM *
* ONLY. TO RETRIEVE THE SASN AND ASTEIN WE USE THE ESAIR COMMAND.    *
* THIS INFORMATION IS OR-ED WITH THE PKM IN R3 TO CONSTRUCT THE DATA *
* FOR THE PTI COMAMND.                                               *
*                                                                    *
**********************************************************************
         CVT    DSECT=YES,LIST=YES
         IHAPSA
         CTSEQUR
         EJECT
CTSAPC0  CSECT
CTSAPC0  AMODE ANY                                              IS10188
CTSAPC0  RMODE ANY                                              IS10188
         BALR   R12,0
         USING  *,R12
*
*        LEVEL OF THIS MODULE
*
*SAS2IBMN CTSLEVEL      CTSAPC0,1.4.0
         CTSLEVEL                                              SAS2IBMN
**********************************************************************
****                                                              ****
****       SA-AGENT    RELEASE 1.4.0                              ****
****                                                              ****
****       LICENCED MATERIAL - 4TH DIMENSION SOFTWARE LTD.        ****
****                                                              ****
**********************************************************************
         SPACE  3
         LR    R6,R1              GET ECB TO BE POSTED
         LR    R8,R14             KEEP R14
         LR    R10,R3             KEEP R3 (DESTROYED BY GETMAIN) IN R10
*                                 R10 = PKM + SASN.
*
         EPAR  R2                 GET PRIMARY ASID
         LOCASCB ASID=(R2)        GET ASCB
         LR    R11,R1             KEEP ASCB FOR SETLOCK
         LR    R5,R1                        FOR POST
         LR    R7,R1                        FOR GETMAIN/FREEMAIN
         MODESET EXTKEY=ZERO,SAVEKEY=(2)
LOCOBT   SETLOCK OBTAIN,TYPE=CML,ASCB=(11),MODE=UNCOND,REGS=USE,       *
               RELATED=LOCREL
         LA    R4,0
         GETMAIN RU,LV=64,BRANCH=YES
         LR    R9,R1
         STM   R0,R15,0(R9)
*SAS2IBMN POST  (R6),ASCB=(R5),ERRET=POSTERR,BRANCH=YES
         POST  (R6),ASCB=(R5),ERRET=POSTERR,LINKAGE=BRANCH     SAS2IBMN
POSTERR  LM    R0,R15,0(R9)
         LA    R4,0
         LR    R7,R11
         FREEMAIN RU,LV=64,A=(R9),BRANCH=YES
LOCREL   SETLOCK RELEASE,TYPE=CML,ASCB=(11),REGS=USE,RELATED=LOCOBT
         MODESET KEYADDR=(2)
         LA    R15,0
*IS10085 PT    R10,R8
         ESAIR R3                  EXTRACT SASN + INSTANCE.     IS10085
         IILL  R10,0               LEAVE ONLY PKM IN R10.       IS10085
         OR    R3,R10              GET PSW MASK TO R3.          IS10085
         PTI   R3,R8               RETURN.                      IS10085
*OSTERR  EQU   *
         BR    R14
         LTORG
         END
