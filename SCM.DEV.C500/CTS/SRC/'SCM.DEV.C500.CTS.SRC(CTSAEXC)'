          START                                                         00000101
*                                                                       00000901
****************************************************************        00000201
****                                                        ****        00000301
****     CONTROL-SA  RELEASE 1.3.0                          ****        00000401
****                                                        ****        00000501
****     LICENCED MATERIAL - NEW DIMENSION SOFTWARE LTD.    ****        00000601
****                                                        ****        00000701
****************************************************************        00000801
*                                                                       00000901
****************************************************************        00001001
*                                                              *        00001101
* TITLE            : CTSAEXC                                   *        00001201
*                                                              *        00001301
* FILE NAME        : CTSAEXC                                   *        00001401
*                                                              *        00001501
* AUTHOR           : DORON COHEN                               *        00001601
*                                                              *        00001701
* CREATION DATE    : 21/07/94                                  *        00001801
*                                                              *        00001901
* DESCRIPTION      : COMMAND PROCESSOR TO EXECUTE COMMAND      *        00002001
*                    PROCESSSOR (AUTHORIZED ONE'S )            *        00002101
*                    IN THE DYNAMIC TSO ENVIRONMENT            *        00002101
*                                                              *        00002101
* ASSUMPTIONS AND                                              *        00002201
*   CONSIDERATIONS : THIS PROGRAM IS REENTRANT.      IS10184   *        00002301
*                                                              *        00002401
****************************************************************        00002501
*                                                                       00002601
****************************************************************        00002701
* MOD.ID   WHO      WHEN     DESCRIPTION                       *        00002801
* -------- -------- -------- --------------------------------- *        00002901
* BS2428   YONI     14/08/00 FIX TSS RC=8 (CTSTEXC FOR TSS)    *
* IS10184  NURITY   31/08/21 IMPROVE SCRIPTS PERFORMANCE       *
*                            (CHANGE TO BE RENT.)              *
* IS10188  NURITY   06/10/24 MOVE OUR MODULES ABOVE 16M LINE   *
****************************************************************        00010001
*                                                                       00020001
*IS10184 CTSAEXC  CSECT ,
CTSAEXC  AMODE ANY
*IS10188 CTSAEXC  RMODE 24
CTSAEXC  RMODE ANY                                             IS10188
*
***************************************************************
*        INITIALIZE                                           *
***************************************************************
*
         CTSEQUR
*IS10184 BEGIN *,R12
*IS10188 CTSAEXC  CTSBGNR R12,LV=#WALEN,CLEAR=NO,EQUR=YES      IS10184
CTSAEXC  CTSBGNR R12,LV=#WALEN,CLEAR=NO,EQUR=YES,      IS10184 IS10188 *
               GMTYPE=RC,LOC=24  LOC=24 BECAUSE OF DBGAREA     IS10188
         USING WA,R13                                          IS10184
         CTSLEVEL
*
         LR    R10,R1
         USING CPPL,R10
*
         XC    WASTART(#WACLNLN),WASTART                       IS10184
*
*IS10184 XC    RC,RC
*
         L     R8,CPPLECT
         USING ECT,R8
*
*        IF WE WERE ACTIVATED WITH DEBUG ON, SET DEBUG LEVEL
*
         CLC   ECTPCMD,=CL8'CTSAEXCD'
         BNE   DEBUGOK
         MVC   DEBUG,=F'1'
DEBUGOK  DS    0H
*
         CTSADBG LEVEL=(DBGLVL,1),                                     X
               'PRIMARY COMMAND IN ECT IS _',(ECTPCMD,8),              >10184
               RAREA=RAREA,DBGAREA=DBGAREA                     IS10184
*
         L     R9,CPPLCBUF
         BAL   R14,DUMPCBUF
*
***************************************************************
*        SCAN THE COMMAND BUFFER                              *
***************************************************************
*
         LA    R1,4(R9)              R1 -> START OF TEXT
         ST    R1,TEXTSTRT           SAVE IT
         AH    R1,2(R9)              R1 -> START OF ARGUMENT 1
         ST    R1,ARG1STRT           SAVE IT
         LH    R1,0(R9)              R1 = LENGTH OF TOTAL BUFFER
         SH    R1,=H'4'              R1 = LENGTH OF TEXT
         ST    R1,TEXTLEN            SAVE IT
         SH    R1,2(R9)              R1 = LENGTH OF ARGUMENTS
         ST    R1,ARGSLEN            SAVE IT
         BNP   ERRNOARG              NO ARGUMENTS ERROR
*
*        LOCATE THE END OF THE 1ST ARGUMNET (THE REAL COMMAND)
*
         L     R5,ARG1STRT
         L     R6,ARGSLEN
LOOPA1   DS    0H
         CLI   0(R5),C' '
         BE    ENDOFA1
         LA    R5,1(R5)
         BCT   R6,LOOPA1
ENDOFA1  DS    0H
         ST    R5,ARG1END
*
*        LOCATE THE START OF THE 2ND ARGUMNET (THE REAL ARGUMENTS)
*
LOOPA2   DS    0H
         CLI   0(R5),C' '
         BNE   ENDOFA2
         LA    R5,1(R5)
         BCT   R6,LOOPA2
ENDOFA2  DS    0H
         ST    R5,ARG2STRT
*
*        BLANK OUT THE ORIGINAL COMMAND FROM THE BUFFER (OUR OWN CP)
*
         L     R1,ARG1STRT
         L     R2,TEXTSTRT
         SR    R1,R2
         BNP   SKIPB
         BCTR  R1,0
         MVC   0(*-*,R2),=CL256' '
         EX    R1,*-6
SKIPB    DS    0H
*
*        COPY COMMAND NAME
*
COPYPRG  DS    0H
         MVC   PRG,=CL8' '
         L     R2,ARG1STRT
         L     R1,ARG1END
         SR    R1,R2
         CH    R1,=H'8'
         BH    ERRNOCMD
         BCTR  R1,0
         MVC   PRG(*-*),0(R2)
         EX    R1,*-6
         TR    PRG,LOW2UPR
*
*        UPDATE ECT PRIMARY COMMAND NAME
*
         MVC   ECTPCMD,PRG
*
*        UPDATE COMMAND BUFFER ARGUMENTS OFFSET
*
         L     R5,ARG2STRT           R5 -> START OF ARG2
         S     R5,TEXTSTRT           R5 = OFFSET OF ARG2
         STH   R5,2(R9)              UPDATE NEW OFFSET
         BAL   R14,DUMPCBUF
*
***************************************************************
*        ACTIVATING TSS COMMAND VIA SPECIFIC TSS INTERFACE
***************************************************************
*
         CTSADBG LEVEL=(DBGLVL,1),                               BS2428X
               'PRG=_',(PRG,8),                                  BS2428>
               RAREA=RAREA,DBGAREA=DBGAREA                     IS10184
         CLC   PRG(8),=CL8'TSS     '                             BS2428
         BNE   CHKCMD                                            BS2428
         L     R2,TEXTSTRT                                       BS2428
*                                                                BS2428
         CTSADBG LEVEL=(DBGLVL,1),                               BS2428X
               'TEXTLEN=#,TEXTSTRT=~,TEXT=_',                    BS2428X
               (TEXTLEN,TEXTSTRT,(R2),200),                      BS2428>
               RAREA=RAREA,DBGAREA=DBGAREA                     IS10184
*                                                                BS2428
         XC    CALL10(CALL10L),CALL10                          IS10108
         CALL  CTSTEXC,(DEBUG,TEXTSTRT,TEXTLEN,(R10)),           BS2428>
               LINKINST=BASR,MF=(E,CALL10)                     IS10184
         ST    R15,RC                                            BS2428
         CTSADBG LEVEL=(DBGLVL,1),                               BS2428X
               'RC #',(?R15),                                    BS2428>
               RAREA=RAREA,DBGAREA=DBGAREA                     IS10184
         B     EXIT                                              BS2428
*
***************************************************************
*        CHECK COMMAND AUTHORIZATION
***************************************************************
*
CHKCMD   DS    0H
         MVC   FUNC,=CL8'CHKCMD'
         XC    CALL10(CALL10L),CALL10                          IS10108
         CALL  CTSATSO,(FUNC,RC,RS,AB,DEBUG,PRG),                      >
               LINKINST=BASR,MF=(E,CALL10)                     IS10184
         CTSADBG LEVEL=(DBGLVL,1),                                     X
               'CHKCMD RC=# #/#/#',(?R15,RC,RS,AB),                    >
               RAREA=RAREA,DBGAREA=DBGAREA                     IS10184
         LTR   R15,R15
         BZ    ACTAUTH
         CH    R15,=H'4'
         BE    ACTNAUTH
         CTSADBG TYPE=WTO,                                             X
               'IKJTBLS ERROR - RC=# (#/#/#) - COMMAND IGNORED',       X
               (?R15,RC,RS,AB),                                        >
               RAREA=RAREA,DBGAREA=DBGAREA                     IS10184
         LA    R15,128
         ST    R15,RC
         B     EXIT
*
***************************************************************
*        ACTIVATE NON AUTHROIZED COMMAND
***************************************************************
*
ACTNAUTH DS    0H
         LR    R1,R10
*IS10184 LINK  EPLOC=PRG
         MVC   LINKL(#LINKML),LINKM                            IS10184
         LA    R3,PRG                                          IS10184
         LINK  EPLOC=(R3),SF=(E,LINKL)                         IS10184
         ST    R15,RC
         CTSADBG LEVEL=(DBGLVL,1),                                     X
               'RC FROM _ #',(PRG,8,?R15),                             >
               RAREA=RAREA,DBGAREA=DBGAREA                     IS10184
         B     EXIT
*
***************************************************************
*        ACTIVATE AUTHORIZED COMMAND UNDER CONTROL-SA         *
***************************************************************
ACTAUTH  DS    0H
*
*        CHECK ENVIRONMENT - ARE WE UNDER CONTROL-SA ?
*
         MVC   FUNC,=CL8'CHKENV'
         XC    CALL10(CALL10L),CALL10                          IS10108
         CALL  CTSATSO,(FUNC,RC,RS,AB,DEBUG),                          >
               LINKINST=BASR,MF=(E,CALL10)                     IS10184
         CTSADBG LEVEL=(DBGLVL,1),                                     X
               'CHKENV RC=# #/#/#',(?R15,RC,RS,AB),                    >
               RAREA=RAREA,DBGAREA=DBGAREA                     IS10184
         LTR   R15,R15
         BNZ   ACTAUTH1
*
*        IF NOT ALREADY AUTHORIZED, SET AUTHORIZATION BIT IN JSCB
*
         MVI   FLAGAUTH,C'Y'
         TESTAUTH FCTN=1                                                00040000
         LTR   R15,R15                                                  00050000
         BZ    DONEAUTH                                                 00060001
         MVI   FLAGAUTH,C'N'
         CTSAATH AUTH=ON
         CTSADBG LEVEL=(DBGLVL,1),                                     X
               'AUTHORIZED...',                                        >
               RAREA=RAREA,DBGAREA=DBGAREA                     IS10184
DONEAUTH DS    0H
*
*        ACTIVATE
*
         LR    R1,R10
*IS10184 LINK  EPLOC=PRG
         MVC   LINKL(#LINKML),LINKM                            IS10184
         LA    R3,PRG                                          IS10184
         LINK  EPLOC=(R3),SF=(E,LINKL)                         IS10184
         ST    R15,RC
         CTSADBG LEVEL=(DBGLVL,1),                                     X
               'RC FROM _ #',(PRG,8,?R15),                             >
               RAREA=RAREA,DBGAREA=DBGAREA                     IS10184
*
*        IF NOT ALREADY AUTHORIZED, SET AUTHORIZATION BIT IN JSCB
*
         CLI   FLAGAUTH,C'Y'
         BE    DONEDAUT
         CTSAATH AUTH=OFF
         CTSADBG LEVEL=(DBGLVL,1),                                     X
               'UNAUTHORIZED ...',                                     >
               RAREA=RAREA,DBGAREA=DBGAREA                     IS10184
DONEDAUT DS    0H
         B     EXIT
*
***************************************************************
*        ACTIVATE AUTHORIZED COMMAND USING TSO SERVICES       *
***************************************************************
*
ACTAUTH1 DS    0H
         L     R5,ARG1STRT
         L     R6,TEXTSTRT             * CALC LEN OF AUTHCMD
         A     R6,TEXTLEN              * CALC LEN OF AUTHCMD
         SR    R6,R5                   * CALC LEN OF AUTHCMD
         ST    R6,AUTHCLEN             * CALC LEN OF AUTHCMD
         MVC   FUNC,=CL8'CMDAUTH'
         XC    CALL10(CALL10L),CALL10                          IS10108
         CALL  CTSATSO,(FUNC,RC,RS,AB,DEBUG,(5),AUTHCLEN),             >
               LINKINST=BASR,MF=(E,CALL10)                     IS10184
         CTSADBG LEVEL=(DBGLVL,1),                                     X
               'CMDAUTH RC=# #/#/#',(?R15,RC,RS,AB),                   >
               RAREA=RAREA,DBGAREA=DBGAREA                     IS10184
         CH    R15,=H'4'               COMMAND ENEDED NOT OK ?
         BE    EXIT                    YES, THE RC ALREADY SET
         BL    AUTHRC                  NO, SET ZERO RC
         CTSADBG TYPE=WTO,                                             X
               'IKJEFTSR ERROR - RC=# (#/#/#) - COMMAND IGNORED',      X
               (?R15,RC,RS,AB),                                        >
               RAREA=RAREA,DBGAREA=DBGAREA                     IS10184
         LA    R15,132                 TSR FAILURE
AUTHRC   DS    0H
         ST    R15,RC                  UPDATE RC
         CTSADBG LEVEL=(DBGLVL,1),                                     X
               'RC FROM _ #',(PRG,8,?R15),                             >
               RAREA=RAREA,DBGAREA=DBGAREA                     IS10184
         B     EXIT
*
*******************************************************
*********  DEAD CODE (INCLUDED HERE FOR FFU) **********
*******************************************************
*IS10184  ATTCMD   DS    0H
*IS10184 NOP   EXIT
*IS10184 XC    CMDECB,CMDECB           PREPARE ECB
*IS10184 ATTACH EPLOC=PRG,ECB=CMDECB
*IS10184 LTR   R15,R15                 SUBTASK ATTACHED OK ?
*IS10184 BNZ   BADATT                  NO - EXIT WITH ERROR
*IS10184 ST    R1,CMDTCB               FOR DETACH
*IS10184 WAIT  ECB=CMDECB              WAIT FOR TSO
*IS10184 LA    R2,CMDTCB               R2 -> SAVETCB -> CMD TCB
*IS10184 DETACH (2)                    DETACH THE TASK
*IS10184 XR    R15,R15
*IS10184 ICM   R15,B'0111',CMDECB+1    GET STEP RETURN CODE
*IS10184 B     EXIT
*IS10184   BADATT   DS    0H
*IS10184 LA    R15,130
*IS10184 NOP   EXIT
*******************************************************
******************* END OF DEAD CODE ******************
*******************************************************
*
***************************************************************
*        DUMP COMMAND BUFFER ROUTINE                          *
***************************************************************
*
DUMPCBUF DS    0H
         STM   R14,R4,SAVEREGS
         LH    R2,0(R9)
         LH    R3,2(R9)
         LR    R4,R2
         SH    R4,=H'4'
         SH    R4,=H'4'
         CTSADBG LEVEL=(DBGLVL,1),                                     X
               'CPPLCBUF AT ~,#,#,_',                                  X
               (?R9,?R2,?R3,4(R9),?R4),                                >
               RAREA=RAREA,DBGAREA=DBGAREA                     IS10184
         LM    R14,R4,SAVEREGS
         BR    R14
*IS10184 - MOVED TO WA - SAVEREGS DS    18F
*
***************************************************************
*        FINISH                                               *
***************************************************************
*
ERRNOCMD DS    0H
         CTSADBG LEVEL=(DBGLVL,1),                                     X
               'NO COMMAND',                                           >
               RAREA=RAREA,DBGAREA=DBGAREA                     IS10184
         XC    RC,RC
         B     EXIT
ERRNOARG DS    0H
         CTSADBG LEVEL=(DBGLVL,1),                                     X
               'NO ARGUMENTS',                                         >
               RAREA=RAREA,DBGAREA=DBGAREA                     IS10184
         MVC   RC,=F'4'
         B     EXIT
*
EXIT     DS    0H
         L     R15,RC
*IS10184 BRTRN (15)
         CTSBRTR (R15),LV=#WALEN                               IS10184
*
***************************************************************
*     CONSTANTS
***************************************************************
*
         LTORG ,
         DROP  ,
LINKM    LINK  EPLOC=,SF=L                                     IS10184
#LINKML  EQU   *-LINKM                                         IS10184
*
LOW2UPR  DC    256AL1(*-LOW2UPR)       TRANSLATE TABLE TO UPPERCASE
         ORG   LOW2UPR
         DC    64C' '
         ORG   LOW2UPR+C'A'-X'40'
         DC    C'ABCDEFGHI'
         ORG   LOW2UPR+C'J'-X'40'
         DC    C'JKLMNOPQR'
         ORG   LOW2UPR+C'S'-X'40'
         DC    C'STUVWXYZ'
         ORG   ,
*
***************************************************************
*     WORK AREA
***************************************************************
*
WA       DSECT                                                 IS10184
         DS    18F                     SAVE AREA               IS10184
WASTART  DS    0X
TEXTLEN  DS    F
ARGSLEN  DS    F
TEXTSTRT DS    A
ARG1STRT DS    A
ARG1END  DS    A
ARG2STRT DS    A
PRG      DS    CL8
FUNC     DS    CL8
CMDL     DS    F
RC       DS    F
RS       DS    F
AB       DS    F
FLAGAUTH DS    C
DEBUG    DC    F'0'
         ORG   DEBUG+3
DBGLVL   DS    X
         ORG   ,
CMDTCB   DS    A
CMDECB   DS    A
AUTHCLEN DS    F
#WACLNLN EQU   *-WA                    LENGTH TO CLEAN .       IS10184
*
SAVEREGS DS    18F                                             IS10184
LINKL    LINK  EPLOC=,SF=L                                     IS10184
CALL10   DS    10A                     CALL PARMLIST (10 PARMS)IS10184
CALL10L  EQU   *-CALL10                                        IS10184
         DS    0D                                              IS10184
RAREA    DS    XL512                   USED BY CTSADBG         IS10184
DBGAREA  DS    XL2048                  USED BY CTSADBG         IS10184
*                                                              IS10184
#WALEN   EQU   *-WA                    LENGTH OF AREA          IS10184
*
***************************************************************
*        CONTROL BLOCKS MAPPING                               *
***************************************************************
*
         IKJCPPL
         IKJECT
         END
