         START
*
****************************************************************
****                                                        ****
****     CONTROL-SA  RELEASE 4.0.00                         ****
****                                                        ****
****     LICENSED MATERIAL - SAILPOINT TECHNOLOGIES         ****
****                                                        ****
****************************************************************
*
****************************************************************
*                                                              *
* TITLE            : CTS2GSO                                   *
*                                                              *
* FILE NAME        : CTS2GSO                                   *
*                                                              *
* AUTHOR           : AVNER LIVNY                               *
*                                                              *
* CREATION DATE    : 02/1/2003                                 *
*                                                              *
* DESCRIPTION      : GET GSO DEFAULTS                          *
*                                                              *
* ASSUMPTIONS AND                                              *
*   CONSIDERATIONS :                                           *
*                                                              *
****************************************************************
*                                                              *
* FUNCTION:                                                    *
* ---------                                                    *
*                                                              *
*  THIS MODULE RETURNS VALUES FROM THE ACF2 CVT BASED ON AN    *
*  INPUT PARAMETER.                                            *
*                                                              *
* INPUT:                                                       *
* ------                                                       *
*                                                              *
*  CURRENTLY, THE PFIELD PARAMETER CAN BE SET TO THE FOLLOWING *
*  AND WILL RETURN THE VALUE OF THE REQUESTED FIELD:           *
*                                                              *
*    PFRC - RETURN THE VALUE OF ACCYPFRC:                      *
*           0 - PASSWORD EXPIRED                               *
*           4 - NO PASSWORD EXPIRED                            *
*    PSRQ - RETURN THE VALUE OF ACCF8LDP:                      *
*           0 - INSERT COMMAND MUST HAVE PASSWORD OPERAND      *
*               (PSWDREQ)                                      *
*           4 - INSERT COMMAND DOES NOT NEED PASSWORD OPERAND  *
*               (NOPSWDREQ)                                    *
*                                                              *
* OUTPUT:                                                      *
* -------                                                      *
*                                                              *
*    REGISTER 15:                                              *
*      SEE ABOVE FOR RETURN VALUES FOR EACH DIFFERENT 'FIELD'  *
*      PARAMETER INPUT VALUE                                   *
*                                                              *
****************************************************************
*
****************************************************************
*                                                              *
* CHANGE ACTIVITY :                                            *
* -----------------                                            *
*                                                              *
* MOD.ID   WHO      WHEN     DESCRIPTION                       *        00340000
* -------- -------- -------- --------------------------------- *        00350000
* IS10147A SELIGT 27/06/2017 ADD SUPPORT FOR PASSWORD PHRASE   *
* IS10188  AVNERL   06/10/24 MOVE OUR MODULES ABOVE 16M LINE   *
*                                                              *
****************************************************************
*
PARMLIST DSECT                                                 IS10147A
PFIELD   DS    A                       PFRC OR PSRQ OR ...     IS10147A
*
R0       EQU   0                       TEMPORARY WORK REGISTER
R1       EQU   1                       TEMPORARY WORK REGISTER
R2       EQU   2                       WORK REGISTER
R3       EQU   3                       WORK REGISTER
R4       EQU   4                       POINTER TO ACF2 CVT
R5       EQU   5                       WORK REGISTER
R6       EQU   6                       WORK REGISTER
R7       EQU   7                       WORK REGISTER
R8       EQU   8                       WORK REGISTER
R9       EQU   9                       WORK REGISTER
R10      EQU   10                      WORK REGISTER
R11      EQU   11                      POINTER TO PARAMETER LIST
R12      EQU   12                      PROGRAM BASE REGISTER
R13      EQU   13                      LOCAL SAVE AREA (GETMAINED)
R14      EQU   14                      RETURN ADDRESS
R15      EQU   15                      RETURN CODE
*
CTS2GSO  CSECT
CTS2GSO  AMODE 31
*IS10188 CTS2GSO  RMODE 24
CTS2GSO  RMODE ANY                                             IS10188
*
****************************************************************
*        INITIALIZE
****************************************************************
*
*        SAVE REGISTERS AND ESTABLISH ADDRESSABILITY
*
         SAVE  (14,12),,CTS2GSO-&SYSDATE-&SYSTIME
         LR    R12,R15             ESTABLISH BASE REGISTER
         USING CTS2GSO,R12         ESTABLISH ADDRESSABILITY
         CTSLEVEL ,
         LR    R11,R1              SAVE PTR TO PARAMETER LIST  IS10147A
         USING PARMLIST,R11        ESTABLISH ADDRESSABILITY    IS10147A
*
*        OBTAIN ACF2 GSO RECORD
*
         XR    R15,R15             SET RC=0 AS DEFAULT         IS10147A
         ACFINCVT R4,NONE=RETURN   GET POINTER TO ACF2 CVT
         USING ACCVT,R4            ESTABLISH ADDRESSABILITY
         L     R2,PFIELD           POINT TO FIELD PARAMETER    IS10147A
         CLC   =CL4'PFRC',0(R2)    DID USER WANT ACCYPFRC?     IS10147A
         BE    RETPFRC             YES, CONTINUE THERE         IS10147A
         CLC   =CL4'PSRQ',0(R2)    DID USER WANT ACCF8LDP?     IS10147A
         BE    RETPSRQ             YES, CONTINUE THERE         IS10147A
         ABEND 1,DUMP              NONE, MAJOR PROBLEM         IS10147A
*
RETPFRC  DS    0H                                              IS10147A
         XR    R15,R15             SET RC=0 AS DEFAULT
         TM    ACCYFLG,ACCYPFRC    PASSWORD FORCED (EXPIRED)?
         BNZ   RETURN              YES, SET RC=0 AND RETURN TO CALLER
         LA    R15,4               NO (NO-EXP), SET RC=4
         B     RETURN              RETURN TO CALLER            IS10147A
*                                                              IS10147A
RETPSRQ  DS    0H                                              IS10147A
         XR    R15,R15             SET RC=0 AS DEFAULT         IS10147A
         TM    ACCFLG8,ACCF8LDP    PASSWORD REQUIRED (PSWDREQ)?IS10147A
         BO    RETURN              YES, SET RC=0 (PSWDREQ) AND RETURN S
         LA    R15,4               NO, SET RC=4 (NOPSWDREQ)    IS10147A
         B     RETURN              RETURN TO CALLER            IS10147A
*                                                              IS10147A
RETURN   DS    0H
         RETURN (14,12),T,RC=(15)  RESTORE REGISTERS AND RETURN
         DROP  R4,R11,R12          DISCONNECT ADDRESSABILITY
*
****************************************************************
*        CONSTANTS
****************************************************************
*
         LTORG ,
*
****************************************************************
*        ACF2 MAIN CONTROL BLOCK
****************************************************************
*
         ACCVT
         END
