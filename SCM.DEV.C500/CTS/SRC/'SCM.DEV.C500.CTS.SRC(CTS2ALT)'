****************************************************************
****                                                        ****
****     CONTROL-SA  RELEASE 2.1.0                          ****
****                                                        ****
****     LICENCED MATERIAL - NEW DIMENSION SOFTWARE LTD.    ****
****                                                        ****
****************************************************************
*
****************************************************************
*                                                              *
* TITLE            : ACF2 LOGONID RECORDS EXTRACTION (ACALT)   *
*                                                              *
* FILE NAME        : CTS2ALT                                   *
*                                                              *
* AUTHOR           : YISHAY YOVEL                              *
*                                                              *
* CREATION DATE    : 03/06/97                                  *
*                                                              *
* DESCRIPTION      : GET LOGON ID RECORD INFORMATION           *
*                                                              *
* PARAMETERS       :                                           *
*                                                              *
* INPUT            : LID - LID OR LID MASK                     *
*                    USER - CURRENT LID (GET NEXT) OR NULL     *
*                    UID - UIDMASK FILTER (OPTIONAL)           *
*                    GETF - GET FIELDS NDICATOR (Y/N)          *
*                    APIC - FIELD LIST TO RETREIVE (COUNT)     *
*                    APIL - FIELD LIST TO RETREIVE (VECTOR)    *
*                    BUFL - OUTPUT BUFFER LENGTH (ARES)        *
*                                                              *
* OUTPUT           : USER - RETURNED LID (CURRENT)             *
*                    BUF  - AREAS BUFFER                       *
*                    RC   - RETURN CODE                        *
*                    MSG  - ERR MSG                            *
* DEPENDENCIES     : ACCVT - ACF2 CVT CB                       *
*                    ACALT - ALT API CB                        *
*                                                              *
* CHANGE ACTIVITY :                                            *
* 01/11/16 SAS2IBMT ST SAS/C TO IBM C CONVERSION PROJECT:      *
*                     THIS ROUTINE SHOULD RETURN RC (R15) 0    *
*                                                              *
****************************************************************
****************************************************************
*        PARAMETERS BLOCK                                      *
****************************************************************
*
PARM       DSECT
PARMLID    DS    A                       LOGON ID PREFIX
PARMUID    DS    A                       UIDMASK
PARMUSER   DS    A                       RETURNED USER
PARMGETF   DS    A                       GET FIELDS
PARMAPIC   DS    A                       API FIELDS COUNT
PARMAPIL   DS    A                       API FIELDS LIST
PARMBUFL   DS    A                       ARE BUFFER LENGTH
PARMBUF    DS    A                       ARE BUFFER ADDRESS
PARMRC     DS    A                       RC
PARMMSG    DS    A                       MSG
*
NULL       EQU   C' '
CTS2ALT    CSECT
CTS2ALT    AMODE 31
CTS2ALT    RMODE ANY
           BEGIN
           CTSEQUR
           LR    R11,R1
           USING PARM,R11
****************************************************************
*  RESET ACALT PARM CONTROL BLOCK                              *
****************************************************************
           XC    ACALT(ACALTLEN),ACALT
P1         DS    0H
****************************************************************
*  BUILD ACALT PARM CONTROL BLOCK                              *
****************************************************************
           MVI   ACAFCN,X'02'    - ACALT REQUEST
           MVI   ACASFCN,ACASFRT - RETURN FORMATTED RECORD
           L     R4,PARMLID
           CLI   L'ACALID-1(R4),C'*'
           BNE   SKIP_LIDMASK_SET
           OI    ACACNTL,ACACKGE - READ UNDER LOGONID MASK
SKIP_LIDMASK_SET DS 0H
           L     R4,PARMUID
           CLI   0(R4),NULL
           BE    NOUID
           MVC   ACAUID,0(R4)
           CLI   L'ACAUID-1(R4),C'*'
           BNE   SKIP_UIDMASK_SET
           OI    ACACNTL,ACACUMSK - READ UNDER UID MASK
SKIP_UIDMASK_SET DS 0H
NOUID      DS    0H
           L     R4,PARMGETF
           CLI   0(R4),C'Y'
           BE    GET_FIELDS_OK
           OI    ACACNTL,ACACNRET - RETURN NO FIELDS
           B     SKIP_GET_FIELDS
GET_FIELDS_OK DS  0H
           L     R4,PARMAPIC
           CLC   0(4,R4),=F'0'
           BE    NO_SELECTIVE_FIELDS
           MVC   ACAFLDC,0(R4)     - SET SPECIFIC FIELDS LIST COUNT
           MVC   ACAFLDB,PARMAPIL  - SET SPECIFIC FIELDS LIST VECTOR
           OI    ACACNTL,ACACLRET  - INDICATE SPECIFIC FIELDS LIST
NO_SELECTIVE_FIELDS DS 0H
           MVC   ACARETB,PARMBUF   - SET ARE BUFFER
           L     R4,PARMBUFL
           MVC   ACARETL,0(R4)  - SET ARE BUFFER LENGTH
SKIP_GET_FIELDS DS 0H
           MVC   ACAMSG,=A(SVCMSG) - SET SVC MESSAGE BUFFER
           L     R4,PARMLID
           MVC   ACALID,0(R4)      - SET LID TO USE
           L     R4,PARMUSER
           CLI   0(R4),NULL
           BE    SKIP_SET_ACASLID
           MVC   ACASLID,0(R4)      - SET LID TO USE
SKIP_SET_ACASLID DS 0H
           ACFINCVT R4,NONE=EXIT_NOACF2
           USING  ACCVT,R4
****************************************************************
*  ISSUE ACFSVC ACALT CALL                                     *
****************************************************************
           ACFSVC ACALT,TYPE=A,NONE=EXIT_NOACF2,CVT=HAVE
           B     *+4(R15)
           B     EXIT_OK
           B     EXIT_ACCESS_DENIED
           B     EXIT_CONTROL_BLOCK_ERROR
           B     EXIT_NOACF2
           B     EXIT_RC
****************************************************************
*  ACCESS DENIED: ACF2 EOF                                     *
****************************************************************
EXIT_ACCESS_DENIED DS 0H
           LA    R15,4
           B     EXIT_RC
****************************************************************
*  CB ERROR: INTERNAL ERROR                                    *
****************************************************************
EXIT_CONTROL_BLOCK_ERROR DS 0H
           LA    R15,8
           B     EXIT_RC
****************************************************************
*  ACF2 NOT ACTIVE                                             *
****************************************************************
EXIT_NOACF2        DS   0H
           LA    R15,12
           B     EXIT_RC
****************************************************************
*  API CAL ENDED OK                                            *
****************************************************************
EXIT_OK    DS   0H
           L    R4,PARMUSER
           MVC  0(L'ACASLID,R4),ACASLID
           XR   R15,R15
           B    EXIT_RC
****************************************************************
*  RETURN WITH RC                                              *
****************************************************************
EXIT_RC    DS   0H
           L    R14,PARMRC
           ST   R15,0(R14)
           L    R14,PARMMSG
           MVC  0(124,R14),SVCMSG+4
           BRTRN 0                                             SAS2IBMT
SVCMSG     DS    CL128
           ACALT DSECT=NO
           ACCVT
           END
