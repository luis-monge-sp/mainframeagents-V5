         START
*                                                                       00000101
****************************************************************        00000201
****                                                        ****        00000301
****     CONTROL-SA  RELEASE 1.3.0                          ****        00000401
****                                                        ****        00000501
****     LICENCED MATERIAL - NEW DIMENSION SOFTWARE LTD.    ****        00000601
****                                                        ****        00000701
****************************************************************        00000801
*                                                                       00000901
****************************************************************        00001001
*                                                              *        00001101
* TITLE            : INITIALIZE CTSA ADDRESS SPACE ENVIRONMENT *        00001201
*                                                              *        00001301
* FILE NAME        : CTSAINI                                   *        00001401
*                                                              *        00001501
* AUTHOR           : DORON COHEN                               *        00001601
*                                                              *        00001701
* CREATION DATE    : 25/7/94                                   *        00001801
*                                                              *        00001901
* DESCRIPTION      : INITIALIZE AUTHORIZATION SERVICES AND     *        00002001
*                    TSO ENVIRONMENT FOR CONTROL-SA ADDRESS    *        00002101
*                    SPACE                                     *        00002101
*                                                              *        00002101
* ASSUMPTIONS AND                                              *        00002201
*   CONSIDERATIONS : 1. CALLER IS APF AUTHORIZED               *        00002301
*                                                              *        00002401
****************************************************************        00002501
*                                                                       00002601
****************************************************************        00002701
* MOD.ID   WHO      WHEN     DESCRIPTION                       *        00002801
* -------- -------- -------- --------------------------------- *        00002901
* PS0117   DORON    23/6/97  ALLOW NON APF CALLER              *        00004001
* IS0045   AVNERL   17/3/03  FIX DIAG DEBUG LEVEL HANDLING.    *        00004001
* IS0272   MICHAEL  10/6/04  UPDATE C31B LIBS WITH OLD FIXES   *
* IS10188  AVNERL   06/10/24 MOVE OUR MODULES ABOVE 16M LINE   *
****************************************************************        00010001
*                                                                       00020001
*
****************************************************************
*        PARAMETER LIST
****************************************************************
*
PARM     DSECT
PARMOP   DS    A                       OPCODE  (INIT/TERM)
PARMDBG  DS    A                       DBG LEVEL
PARMRC   DS    A                       RETURN CODE
PARMRS   DS    A                       REASON CODE
*
CTSAINI  CSECT
CTSAINI  AMODE 31
*IS10188 CTSAINI  RMODE 24
CTSAINI  RMODE ANY                                             IS10188
*
****************************************************************
*        INITIALIZE
****************************************************************
*
         BEGIN
         CTSEQUR
         LR    R10,R1
         USING PARM,R10
         L     R1,PARMDBG              R1 -> DEBUG LEVEL (4 BYTES)
         XC    DEBUG,DEBUG             STORE DEBUG LEVEL
         ICM   R1,15,0(R1)             DEBUG LEVEL
         BNP   NODEBUG                 SKIP DEBUG LVL IF MINUS
*IS0045  STC   R1,DEBUG                STORE DEBUG LEVEL (1 BYTE)
         ST    R1,DEBUG                STORE DEBUG LEVEL(4 BYTES!IS0045
NODEBUG  DS    0H
*
         L     R1,PARMOP
         CLC   =CL8'INIT',0(R1)
         BE    INIT
         CLC   =CL8'TERM',0(R1)
         BE    TERM
         B     ERROP
***
****************************************************************
***                                                          ***
***         OPCODE = INIT                                    ***
***                                                          ***
****************************************************************
*                                                              *
*  RETURN AND REASON CODES TABLE                               *
*  -----------------------------                               *
*                                                              *
*   +----------------------------+-------------+------------+  *
*   | R15                        |  RC         |  REASON    |  *
*   +----------------------------+-------------+------------+  *
*   |                            |             |            |  *
*   | 0 - OK                     | 0           | 0          |  *
*   |                            |             |            |  *
*   +----------------------------+-------------+------------+  *
*   |                            |             |            |  *
*   | 4 - ALREADY INITIALIZED    | 4           | 0          |  *
*   |                            |             |            |  *
*   +----------------------------+-------------+------------+  *
*   |                            | RC          | REASON     |  *
*   | 8 - TSO ENVIRONMENT ERROR  | FROM        | FROM       |  *
*   |                            | IKJTSOEV    | IKJTSOEV   |  *
*   +----------------------------+-------------+------------+  *
*   |                            |             |            |  *
*   |12 - AUTHORIZATION SERVICES | RC FROM     | 0          |  *
*   |     ERROR                  | CTSAAUT     |            |  *
*   |                            |             |            |  *
*   +----------------------------+-------------+------------+  *
*   |                            |             |            |  *
*   |20 - INVALID OPCODE         | 20          | 0          |  *
*   |                            |             |            |  *
*   +----------------------------+-------------+------------+  *
*                                                              *
****************************************************************
*
INIT     DS    0H
*
****************************************************************
*        CREATE SVC AUTHORIZAION SERVICES ENVIRONMENT
****************************************************************
*
         CTSADBG LEVEL=(DBGL,1),                                       X
               'CALLING SVC CREATION'
         LINK  EP=CTSAAUT,PARAM=(SVCCREAT)
         CTSADBG LEVEL=(DBGL,1),                                       X
               'AUTH SVC CREATE R15=#',(?R15)
*
         MVI   SVCCRETD,C'N'           SVC NOT CREATED !    PS0117
         CH    R15,=H'4'               NON APF ?            PS0117
         BE    SKIPAPF1                YES, SKIP SVC SETUP  PS0117
         LTR   R15,R15
         BNZ   ERRSVC
         MVI   SVCCRETD,C'Y'           SET SVC CREATED FLAG PS0117
SKIPAPF1 DS    0H                                           PS0117
*
****************************************************************
*        TSO ENVIRONMENT CREATION
****************************************************************
*
*        CLEAR APF BIT
*
         CLI   SVCCRETD,C'Y'                                PS0117
         BNE   SKIP1                                        PS0117
         CTSAATH AUTH=OFF
         CTSADBG LEVEL=(DBGL,1),                                       X
               'CLEAR AUTH BIT RC = #',(?R15)
SKIP1    DS    0H                                           PS0117
*
*        INITIALIZE DYNAMIC TSO ENVIRONMENT
*
         LINK  EP=CTSATSO,PARAM=(FUNCINIT,RC,RS,AB,DEBUG)
         CTSADBG LEVEL=(DBGL,1),                                       X
               'TSO INIT R15=# RC=# RS=# AB=#',(?R15,RC,RS,AB)
         LR    R5,R15                  SAVE DURING AUTH BIT GAMES
*
*        SET APF BIT
*
         CLI   SVCCRETD,C'Y'                                PS0117
         BNE   SKIP2                                        PS0117
         CTSAATH AUTH=ON
         CTSADBG LEVEL=(DBGL,1),                                       X
               'SET AUTH BIT RC = #',(?R15)
SKIP2    DS    0H                                           PS0117
*
*        CHECK RC FROM TSO INIT
*
         LTR   R15,R5
         BZ    SKIPTSO
         CH    R15,=H'128'
         BNE   ERRTSO
         CH    R15,RC
         BE    ERRTSO
SKIPTSO  DS    0H
         XC    RS,RS
         XC    RC,RC
         XR    R15,R15
         B     EXIT
***
****************************************************************
***                                                          ***
***         OPCODE = TERM                                    ***
***                                                          ***
****************************************************************
***
TERM     DS    0H
         CTSADBG LEVEL=(DBGL,1),                                       X
               'CALLING SVC DELETION'
*
*PS0117  LINK  EP=CTSASVC,PARAM=(SVCDELET)
         LINK  EP=CTSAAUT,PARAM=(SVCDELET)                  PS0117
         CTSADBG LEVEL=(DBGL,1),                                       X
               'SVC DELETE R15=#',(?R15)
*
         XC    RS,RS
         XC    RC,RC
         XR    R15,R15
         B     EXIT
***
****************************************************************
***                                                          ***
***         HANDLE ERRORS                                    ***
***                                                          ***
****************************************************************
***
ERRALRDY DS    0H
         CTSADBG LEVEL=(DBGL,1),                                       X
               'ALREADY INITIALIZED'
         XC    RS,RS
         MVC   RC,=F'4'
         LA    R15,4
         B     EXIT
*
ERRTSO   DS    0H
         CTSADBG LEVEL=(DBGL,1),                                       X
               'TSO ENVIRONMENT CREATION ERROR'
         XC    RS,RS
         MVC   RC,=F'8'
         LA    R15,8
         B     EXIT
*
ERRSVC   DS    0H
         CTSADBG LEVEL=(DBGL,1),                                       X
               'AUTH SVC CREATION ERROR'
         ST    R15,RS
         MVC   RC,=F'12'
         LA    R15,12
         B     EXIT
*
ERROP    DS    0H
         CTSADBG LEVEL=(DBGL,1),                                       X
               'INVALID OPCODE'
         XC    RS,RS
         MVC   RC,=F'20'
         LA    R15,20
         B     EXIT
*
****************************************************************
*        FINISH
****************************************************************
*
EXIT     DS    0H
*
*        RETURN RC
*
         L     R1,PARMRC
         MVC   0(4,R1),RC
         L     R1,PARMRS
         MVC   0(4,R1),RS
         BRTRN (15)
*
****************************************************************
*        CONSTANTS
****************************************************************
*
SVCCREAT DC    CL8'CREATE'
SVCDELET DC    CL8'DELETE'
FUNCINIT DC    CL8'INIT'
         LTORG ,
*
****************************************************************
*        VARIABLES
****************************************************************
*
SVCCRETD DS    C
RC       DS    F
RS       DS    F
AB       DS    F
DEBUG    DS    F
         ORG   DEBUG+3
DBGL     DS    FL1
         ORG   ,
*
         END
