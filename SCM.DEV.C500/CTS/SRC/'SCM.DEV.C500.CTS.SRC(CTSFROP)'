          START
*                                                                       00000101
****************************************************************        00000201
****                                                        ****        00000301
****     CONTROL-SA  RELEASE 1.3.0                          ****        00000401
****                                                        ****        00000501
****     LICENCED MATERIAL - NEW DIMENSION SOFTWARE LTD.    ****        00000601
****                                                        ****        00000701
****************************************************************        00000801
*                                                                       00000901
****************************************************************        00001001
*                                                              *        00001101
* TITLE            : GET RSS PARAMS                            *        00001201
*                                                              *        00001301
* FILE NAME        : CTSFROP                                   *        00001401
*                                                              *        00001501
* AUTHOR           : ALEXANDER SHVARTSMAN                      *        00001601
*                                                              *        00001701
* CREATION DATE    : 26/07/94                                  *        00001801
*                                                              *        00001901
* DESCRIPTION      :                                           *        00002001
*                                                              *        00002101
*                                                              *        00002101
* INPUT            : DEBUG       -  DEBUG LEVEL                *
*                                                              *
* OUTPUT           : PRSS        -  RSS PARAMS STRUCTURE       *
*                                                              *
* RETURN VALUE     : 0 - O.K.                                  *
*                    16- RACF IS NOT ACTIVE                    *
*                                                              *
* CALLED BY        : GTRSPRM                                   *
*                                                              *
* CALLS TO         : NONE                                      *
*                                                              *
* ASSUMPTIONS AND                                              *        00002201
*   CONSIDERATIONS : CALLER MUST BE AUTHORIZED                 *        00002301
*                                                              *        00002401
* MOD.ID   WHO      WHEN     DESCRIPTION                       *
* -------- -------- -------- --------------------------------- *
* IS10188  AVNERL   06/10/24 MOVE OUR MODULES ABOVE 16M LINE   *
****************************************************************        00002501
*                                                                       00002601
PARM     DSECT
DEBUG    DS    A                       DEBUG LEVEL
PRSS     DS    A                       ADDR RSS PARAMS STRUCTURE
***************************************************************
*
         CTSARSS                       RSS PARAMS DSECT
*
***************************************************************
*-----------------------------------------------------------------
*    OFFSETS IN ICHPRCVT DSECT FOR SOME FIELDS
*-----------------------------------------------------------------
OFFID    EQU   0                       RCVTID   FIELD
OFFSTAT  EQU   X'35'                   RCVTSTAT FIELD
OFFFLGS  EQU   X'99'                   RCVTFLGS FIELD
OFFVERS  EQU   X'AC'                   RACFVERS FIELD
OFFVRNM  EQU   X'268'                  RCVTVRMN FIELD
*-----------------------------------------------------------------
CTSFROP  CSECT
CTSFROP  AMODE 31
*IS10188 CTSFROP  RMODE 24       (FOR CTSADBG)
CTSFROP  RMODE ANY                                              IS10188
*
*        INITALIZE
*
         BEGIN *
         CTSEQUR
         CTSLEVEL
         LR    R10,R1
         USING PARM,R10
         L     R11,DEBUG               R11-> DEBUG LEVEL (4 BYTES)
         XC    DEBUGL,DEBUGL           STORE DEBUG LEVEL
         ICM   R11,15,0(R11)           DEBUG LEVEL
         BNP   NODEBUG                 SKIP DEBUG LVL IF MINUS
         STC   R11,DEBUGL              STORE DEBUG LEVEL (1 BYTE)
NODEBUG  DS    0H
         CTSADBG 'ENTER DEBUG LEVEL=#',(?R11),                         1
               LEVEL=(DEBUGL,1,2,3,4,5)
         L     R11,PRSS
         USING RSSPARM,R11
*
         L     R10,CVTPTR              LOAD ADDR CVT
         USING CVT,R10                 ADDRESSABILITY CVT DSECT
*
         L     R8,CVTRAC               LOAD ADDR CVT RACF
         LTR   R8,R8                   ADDR SUPPLIED ?
         BZ    RET16                   NO, RACF 'DOWN'
*-------------------------------------------------------------------
*     PRINT RACF CVT AREA ACCORDING DEBUG LEVEL
*-------------------------------------------------------------------
         LR    R9,R8
         LA    R9,1000(R9)
         CTSADBG TYPE=SNAP,ID=1,HDR='** RACF CVT **',                  1
               ADDR=(0(R8),0(R9)),LEVEL=(DEBUGL,1,2,3,4,5)
*
*-------------------------------------------------------------------
*        RCVT PROCESSING
*-------------------------------------------------------------------
         USING RCVT,R8                 ADDRESSABILITY CVT RACF DSECT
*
         CTSADBG 'RCVTID=_',(RCVTID,4),LEVEL=(DEBUGL,1,2,3,4,5)
*---------------------------------------------------------------
*     BLOCK ID  - MUST BE 'RCVT'
*---------------------------------------------------------------
         CLC   =CL4'RCVT',OFFID(R8)    CHECK BLOCK ID
         BNE   RET16                   INVALID BLOCK ID, RACF 'DOWN'
*---------------------------------------------------------------
*     GLOBAL MAX PASSWORD INTERVAL VALUE
*---------------------------------------------------------------
         SR    R5,R5
         IC    R5,RCVTPINV
         ST    R5,RSSPINV
         CTSADBG 'RCVTPINV=#',(?R5),LEVEL=(DEBUGL,1,2,3,4,5)
*---------------------------------------------------------------
*     NUMBER ATTEMPTS BEFORE REVOKING USERID
*---------------------------------------------------------------
         SR    R5,R5
         IC    R5,RCVTRVOK
         ST    R5,RSSRVOK
         CTSADBG 'RCVTRVOK=#',(?R5),LEVEL=(DEBUGL,1,2,3,4,5)
*---------------------------------------------------------------
*     PASSWORD RULES
*---------------------------------------------------------------
         SR    R3,R3                  MIN PASSWORD LEN
         MVC   PSWDRULE(80),RCVTSNTX
         LA    R5,PSWDRULE
         USING RCVTSNTD,R5
         LA    R6,8                   MAX NUMBER OF RULES
RULELOOP DS    0H
         MVC   RULE(10),0(R5)
         TR    RULE+2(8),TRTTBL
         SR    R7,R7
         SR    R8,R8
         IC    R7,RCVTSLEN             STARTING LENGTH VALUE
         IC    R8,RCVTELEN             ENDING   LENGTH VALUE
         CTSADBG '(#:#) _',(?R7,?R8,RULE+2,10),LEVEL=(DEBUGL,1,2,3,4,5)
         LTR   R7,R7                   | DEFINITION
         BZ    RULELP1                 |
         LTR   R3,R3                   |   MINIMUM
         BZ    RULEPM                  |
*
         CTSADBG 'MIN LEN(R3)=#  CURRENT LEN(R7)=#',(?R3,?R7),         1
               LEVEL=(DEBUGL,1,2,3,4,5)
*
         CR    R3,R7                   |     PASSWORD
         BL    RULELP1                 |
RULEPM   DS    0H                      |
         LR    R3,R7                   |       LENGTH
RULELP1  DS    0H
         LA    R5,10(R5)
         BCT   R6,RULELOOP
*---------------------------------------------------------------
*     MINIMUM PASSWORD LENGTH
*---------------------------------------------------------------
         CTSADBG 'MIN LEN(R3)=#',(?R3),                                1
               LEVEL=(DEBUGL,1,2,3,4,5)
         ST    R3,RSSMLEN
*
*--------------------------------------------------------------------
*    RETURN CODES
*--------------------------------------------------------------------
RET0     DS    0H                      0-O.K
         SR    R15,R15
         B     RET
*
RET16    DS    0H                      RACF NOT ACTIVE
         LA    R15,16
         B     RET
*
RET      DS    0H
         CTSADBG 'EXITING WITH RC=~',(?R15),                           1
               LEVEL=(DEBUGL,1,2,3,4,5)
         BRTRN (15)
         LTORG
DEBUGL   DS    X                       DEBUG LEVEL
PSWDRULE DS    CL80
RULE     DS    CL10
TRTTBL   DC    256AL1(*-TRTTBL)
         ORG   TRTTBL+X'00'
         DC    C'*'
         ORG   TRTTBL+X'40'
         DC    C'*'
         ORG   ,
*
***************************************************************
*                                                             *
*        SYSTEM CONTROL BLOCKS MAPPING                        *
*                                                             *
***************************************************************
         PRINT GEN
         CVT   DSECT=YES,LIST=YES
         ICHPRCVT
         END
