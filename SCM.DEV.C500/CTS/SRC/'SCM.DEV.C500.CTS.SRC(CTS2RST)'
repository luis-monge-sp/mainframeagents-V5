         TITLE 'CTS2RST - GET ACF2 RESTRICT ATTRIBUTE'                  00040000
*                                                                       00050000
****************************************************************        00060000
****                                                        ****        00070000
****     CONTROL-SA  RELEASE 4.0.02                         ****        00080000
****                                                        ****        00090000
****     LICENCED MATERIAL - SAILPOINT                      ****        00100000
****                                                        ****        00110000
****************************************************************        00120000
*                                                                       00130000
****************************************************************        00140000
*                                                              *        00150000
* TITLE            : VERIFY THAT THE PASSWORD IS INDEED THE    *        00160000
*                    SPECIFIED USERS PASSWORD.                 *        00160000
* FILE NAME        : CTS2RST                                   *        00180000
* AUTHOR           : AVNER LIVNY                               *        00200000
* CREATION DATE    : 05/07/2017                                *        00220000
****************************************************************        00310000
*                                                                       00320000
****************************************************************        00330000
* MOD.ID   WHO      WHEN     DESCRIPTION                       *        00340000
* -------- -------- -------- --------------------------------- *        00350000
* IS10147  AVNERL & 06/08/17 EMPTY PASSWORD FIELD              *
*          SELIGT                                              *
* BS10087  SELIGT   22/05/18 IDENTIFY WHETHER A LOGONID IS A   *
*                            RESTRICT LOGONID.                 *
*                            USERID NOT COPIED CORRECTLY FROM  *
*                            INPUT PARAMETER (ALSO CLEAN UP    *
*                            ROUTINE BY DELETING UNNECESSARY   *
*                            COMMENT LINES)                    *
****************************************************************        00380000
*                                                                       00480000
*--------------------------------------------------------------*        00490000
*        START OF PROGRAM                                               00370000
*--------------------------------------------------------------*        00510000
CTS2RST  CSECT                                                          00390000
CTS2RST  RMODE 31                                                       00400000
CTS2RST  AMODE ANY                                                      00410000
*                                                                       00421000
*--------------------------------------------------------------*        00422000
*        INITIALIZE                                                     00423000
*--------------------------------------------------------------*        00424000
         BEGIN *                                                        00530000
         CTSEQUR
         CTSLEVEL
*                                                                       00541000
         LR    R10,R1                  R10 CONTAINS POINTER TO PARMS
         USING PRM,R10                 R10 POINTS TO INPUT PARMS
*
         L     R4,PRMDBG               R4 -> DEBUG LEVEL (4 BYTES)
         XC    DBGLEVEL,DBGLEVEL       STORE DEBUG LEVEL
         ICM   R4,15,0(R4)             DEBUG LEVEL
         BNP   NODEBUG                 SKIP DEBUG LVL IF MINUS
         STC   R4,DBGLEVEL             STORE DEBUG LEVEL (1 BYTE)
NODEBUG  DS    0H
         L     R1,PRMREST          -> RESTRICT PARAMETER
         MVI   0(R1),C'N'          SET IT TO N BY DEFAULT
         ACFINCVT R4,NONE=NORSS        GET ACF2 CVT              WS2565
         ST    R4,ACCVTADR         SAVE ACCVT ADDRESS.          IS10066
         USING  ACCVT,R4                                         WS2565
*
PRPACALT DS    0H                  PREPARE FOR ACALT CALL
         XC    AREBUF,AREBUF       CLEAR ARE BUFFER AREA
         XC    ACALT(ACALTLEN),ACALT                                    00885600
         MVI   ACAFCN,X'02'         ACALT REQUEST                       00885600
         MVI   ACASFCN,ACASFRT      RETURN FORMATTED RECORD             00885600
         MVI   ACACNTL,0            READ UNDER SPECIFIC LOGONID         00885600
* ABOVE NO FLAG OPTION IS NOT DOCUMENTED YET. DOC BUG WAS OPENED AT CA.
         L     R1,PRMUSER           -> LID TO BE USED
*BS10087 MVC   ACALID,0(R1)         SET LID TO USE                      00885600
*BS10087 OC    ACALID,=CL8' '       SET THE REST TO BLANKS              00885600
         MVC   ACALID,=CL8' '       INITIALIZE USERID FIELD     BS10087
         LA    R9,ACALID            R9 - PTR TO USERID FIELD    BS10087
         LA    R2,L'ACALID          R2 - MAX LEN OF USERID FIELDBS10087
LOOPUSR  DS    0H                                               BS10087
         CLI   0(R1),C' '           IS I/P CHAR BLANK OR X'00'? BS10087
         BNH   ENDLUSR              IF YES, STOP LOOPING        BS10087
         MVC   0(1,R9),0(R1)        MOVE 1 CHAR FROM I/P TO O/P BS10087
         LA    R1,1(,R1)            ADVANCE INPUT PARM POINTER  BS10087
         LA    R9,1(,R9)            ADVANCE ACALID POINTER      BS10087
         BCT   R2,LOOPUSR           SUBTRACT AND LOOP           BS10087
ENDLUSR  DS    0H                                               BS10087
         MVC   ACAFLDC,=F'1'        # OF FIELDS TO RETRIEVE
         LA    R1,RESTRICT          -> FIELD TO BE RETRIEVED
         ST    R1,ACAFLDB           -> FIELDS TO RETRIEVE.
         OI    ACACNTL,ACACLRET     IND SPECIFIC FIELDS LIST            00885600
         LA    R1,AREBUF
         ST    R1,ACARETB           SET ARE BUFFER                      00885600
         LHI   R1,L'AREBUF
         ST    R1,ACARETL           OUTPUT BUFFER LENGTH
         LA    R1,SVCMSG
         ST    R1,ACAMSG            SET AREA FOR MSG.
*
*** CALL ACF2
*
         ACFSVC ACALT,TYPE=A,NONE=NORSS,CVT=HAVE                        00885600
         LR    R9,R15
*
         CTSADBG LEVEL=(DBGLEVEL,1),                                   X
               'ACALT RC=# MSG=_',(?R15,SVCMSG,72),                    X
               RAREA=RAREA,DBGAREA=DBGAREA
         CTSADBG LEVEL=(DBGLEVEL,1),TYPE=SNAP,                 IS10147BX
               HDR='ACFSVC ACALT INPUT',                       IS10147BX
               ADDR=(CONST,CONSTEND),                          IS10147BX
               RAREA=RAREA,DBGAREA=DBGAREA                     IS10147B
         CTSADBG LEVEL=(DBGLEVEL,1),TYPE=SNAP,                         X
               HDR='ACFSVC ACALT OUTPUT',                              X
               ADDR=(AREBUF,AREBUF+L'AREBUF-1),                        X
               RAREA=RAREA,DBGAREA=DBGAREA
*
         LR    R15,R9
         LTR   R15,R15                 ALTRC OK ?
         BZ    DACHECK                 YES - ANALYZE PARMS
********************* IS10002 STARTS **********************************
* ACF2 RETURNS ACF00151 WHEN THERE'S NOTHING TO RETURN.
***********************************************************************
         CLC   SVCMSG+4(8),=CL8'ACF00151' IF THIS MESSAGE:     IS10147B
*                      NO SIGNIFICANT DATA AVAILABLE FOR RETURNIS10147B
         BE    DARC0                   NOTHING TO PROCESS.     IS10147B
         B     DARC4                   SET RETURN CODE AND RETURN. 0066
DACHECK  DS    0H
         LA    R5,AREBUF           R5 -> ACF2 SVC OUTPUT.       IS0041
         USING ACARE,R5                BASE REG OF ARE          IS0041
         CLC   AREANAME,=CL8'RESTRICT'                          IS10066
         BNE   NOREST
*IS10147B TM   AREVALUE,X'01'          IS RESTRICT ON?
*IS10147B BZ   NOREST                  NO, REST IS OFF
         CLI   AREVALUE,X'01'          IS RESTRICT ON?         IS10147B
         BNE   NOREST                  NO, REST IS OFF         IS10147B
         L     R1,PRMREST          -> RESTRICT PARAMETER
         MVI   0(R1),C'Y'          SET IT TO Y
         CTSADBG LEVEL=(DBGLEVEL,1),'RESTRICT IS ON.'           IS0041  00885600
         B     DARC0                                          IS10147B
NOREST   DS    0H                                               IS0041
         CTSADBG LEVEL=(DBGLEVEL,1),'RESTRICT IS OFF.'          IS0041  00885600
*                                                               IS10066
**   SET RETURN CODE AND RETURN                                 IS10066
*                                                               IS10066
DARC0    DS    0H                  DATA RETRIEVED SUCCESSFULLY. IS10066
         LHI   R15,0                                            IS10066
         B     DAEND                                            IS10066
DARC4    DS    0H                  USER DOES NOT EXIST.         IS10066
         LHI   R15,4                                            IS10066
         B     DAEND                                            IS10066
NORSS    DS    0H
         CTSMSG1  CODE=CTS331E,PLANT=(4,ACF2),                         X
               ROUT=WTO,WTOCON=CTS
*BS10087 CTSADBG TYPE=SNAP,ID=1,HDR='NORSS - CVT **',                  X
               ADDR=(0(,R2),0(,R4)),                                   X
               RAREA=RAREA,DBGAREA=DBGAREA
         LHI   R15,8               ACF2 IS DOWN
         B     DAEND
DAEND    DS    0H                                               IS10066
         BRTRN (15)                                             IS10066
*---------------------------------------------------------------*       01660000
*        WORKAREA                                                       01670000
*---------------------------------------------------------------*       01680000
         LTORG ,                                                IS10066
ACF2     DC    CL4'ACF2'
RESTRICT DC    CL8'RESTRICT'   KEYWORD TO BE RETRIEVED FROM ACF2
*
CONST    DS    0D
ACCVTADR DS    A                   -> ACF2 ACCVT.
DBGLEVEL DS    X
SVCMSG   DS    CL128              ACF2 MSG WHEN ERROR           IS0041
         DC    C'ACALT PARM LIST:'                             IS10147B
         ACALT DSECT=NO                                         IS0041
CONSTEND EQU   *                                               IS10147B
*
         ACNTRY DSECT=NO           ACNTRY PARAMETER LIST AREA  IS10147A
*                                                               IS10066
         DS    0D
AREBUF   DS    XL256               ACF SVC RETURN AREA.         IS10066
RAREA    DS    XL512               USED BY CTSADBG AND CTSMSG1  IS10066
DBGAREA  DS    XL2048                                           IS10066 01950100
*
*---------------------------------------------------------------*       00341000
*        INPUT/OUTPUT PARAMETERS OF ROUTINE                             00342000
*---------------------------------------------------------------*       00343000
PRM      DSECT           INPUT PARAMETERS BLOCK.                        00344000
PRMUSER  DS    A         USER TO BE CHECKED WHETHER RESTRICT            00345000
PRMREST  DS    A         RESTRICT ON/OFF                                00345000
PRMDBG   DS    A         DEBUG LEVEL                                    00345000
*---------------------------------------------------------------*       01950200
*        CONTROL BLOCKS MAPPING                                         01950300
*---------------------------------------------------------------*       01950400
         PRINT GEN
CVT      CVT   DSECT=YES,PREFIX=YES,LIST=YES
*
*BS10087 ICHSAFP ,
*
*BS10087 ICHPRCVT ,
*
         ACARE DSECT=YES,TYPE=ARE                               IS0041
*
         ACCVT ,                                                IS0041
*
*BS10087 IHAACEE ,                                              IS0041
*
*BS10087 IRRPRXTW ,                RACROUTE RESULT AREA MAPPING WS2460
*
         END
