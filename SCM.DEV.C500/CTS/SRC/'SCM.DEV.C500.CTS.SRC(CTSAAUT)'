         START
*
****************************************************************
****                                                        ****
****     CONTROL-SA  RELEASE 1.3.0                          ****
****                                                        ****
****     LICENCED MATERIAL - NEW DIMENSION SOFTWARE LTD.    ****
****                                                        ****
****************************************************************
*
****************************************************************
*                                                              *
* TITLE            : CTSAAUT                                   *
*                                                              *
* FILE NAME        : CTSAAUT                                   *
*                                                              *
* AUTHOR           : MEIZY , DORON                             *
*                                                              *
* CREATION DATE    : 21/7/94                                   *
*                                                              *
* DESCRIPTION      : HANDLE SVC AUTHORIZATION SEVICES          *
*                                                              *
* ASSUMPTIONS AND                                              *
*   CONSIDERATIONS : 1. CALLER IS APF AUTHORIZED               *
*                    2. CALLER IS IN AMODE 31                  *
*                                                              *
****************************************************************
*
****************************************************************
* MOD.ID   WHO      WHEN     DESCRIPTION                       *
* -------- -------- -------- --------------------------------- *
*                   DD/MM/YY                                   *
*                                                              *
****************************************************************
*
****************************************************************
*        PARAMETER BLOCK                                       *
****************************************************************
*
PARM     DSECT
PARMOPT  DS    A
*
****************************************************************
*        SETUP AUTHORIZATION SVC VIA SVC SCREENING             *
****************************************************************
*                                                              *
* RETURN CODES (R15)                                           *
*                                                              *
*        0 - OK                                                *
*        4 - CALLER NOT APF AUTHORIZED                         *
*        8 - ENVIRONMENT ALREADY SET/CLEARED                   *
*       12 - GETMAIN/FREEMAIN FAILURE                          *
*       16 - INVALID OPTION                                    *
*       20 - INVLAID SVC MODULE                                *
*                                                              *
****************************************************************
*
CTSAAUT  CSECT
CTSAAUT  AMODE 31
CTSAAUT  RMODE ANY
*
*        INITIALIZE
*
         BEGIN
         CTSEQUR
         CTSLEVEL
         LR    R10,R1
         USING PARM,R10
*
*        CHECK OPTION
*
         L     R1,PARMOPT
         CLC   =CL8'CREATE',0(R1)
         BE    CREATE
         CLC   =CL8'DELETE',0(R1)
         BE    DELETE
         B     ERROPT
*
****************************************************************
*        CREATE AUTHORIZATION SERVICES ENVIRONMENT             *
****************************************************************
*
*        SET SVC SCREENING TABLE
*
CREATE   DS    0H
         TESTAUTH FCTN=1               ARE WE AUTHORIZED?
         LTR   R15,R15
         BNZ   ERRAUTH
*
         LOAD  EP=CTSASVC
         LR    R5,R0
         CLC   =CL8'CTSASVC',4(R5)
         BNZ   ERRLMOD
*
         L     R4,PSATOLD-PSA          R4 -> CURRENT TCB
         USING TCB,R4                  ADDRESSABILITY
         CLC   TCBSVCA2,=A(0)
         BNZ   ERRACT
*
         GETMAIN RC,LV=SCRNTABL,SP=253 OBTAIN TABLE IN SQA
         LTR   R15,R15                 ARE WE OK ?
         BNZ   ERRSTOR                 NO, FAIL REQUEST
         LR    R3,R1                       AND IN OUR OWN REG3
*
         MODESET MODE=SUP,KEY=ZERO     WE NEED KEY0 FOR TCB/SCRN TABLE
*
*        SET SCREENING TABLE
*
         LR    R0,R3                   DEST ADDR
         LA    R1,SCRNTABL             DEST LEN
         LA    R14,SCRNTAB             SOURCE ADDR
         LR    R15,R1                  SOURCE LEN
         MVCL  R0,R14                  COPY
*
*        SET TCB FLAGS
*
         ST    R5,0(R3)                SET SVC ROUTINE ADDRESS
         ST    R3,TCBSVCA2             SCREEN TABLE ADDR
         OI    TCBFLGS7,TCBSVCSP       SVC SCREENING PROPAGATION
         OI    TCBFLGS7,TCBSVCS        SVC SCREENING ACTIVE
*
         MODESET MODE=PROB,KEY=NZERO   RETURN TO NORMAL
*
         DROP  R4                      TCB NO LONGER NEEDED
         XR    R15,R15                 EVERYTHING WENT JUST FINE
         B     EXIT
*
****************************************************************
*        CLEAR AUTHORIZATION SERVICES ENVIRONMENT              *
****************************************************************
*
DELETE   DS    0H
         TESTAUTH FCTN=1                 ARE WE AUTHORIZED?
         LTR   R15,R15
         BNZ   ERRAUTH
*
         L     R4,PSATOLD-PSA            R4 -> CURRENT TCB
         USING TCB,R4                    MAP IT
         CLC   TCBSVCA2,=A(0)            SCREEN TABLE ADDR
         BE    ERRACT                    NO, NOTHING TO CLEAR
         L     R3,TCBSVCA2               R3 -> SCREENING TABLE
*
         L     R5,0(R3)                  R5 -> SVC MODULE
         CLC   =CL8'CTSASVC',4(R5)
         BNZ   ERRLMOD
*
*        CLEAR TCB FLAGS
*
*
         MODESET MODE=SUP,KEY=ZERO       NEED KEY0 FOR TCB UPDATE
         NI    TCBFLGS7,X'FF'-TCBSVCS    SVC SCREENING ACTIVE
         NI    TCBFLGS7,X'FF'-TCBSVCSP   SVC SCREENING PROPAGATION
         XC    TCBSVCA2,TCBSVCA2         SCREEN TABLE ADDR
         MODESET MODE=PROB,KEY=NZERO     RETURN TO NORMAL
*
         DROP  R4                        WE ARE DONE WITH THE TCB
*
*        FREE SVC SCREENING TABLE
*
         FREEMAIN RC,LV=SCRNTABL,SP=253,A=(R3) FREE IT
         LTR   R15,R15                   OK ?
         BNZ   ERRSTOR                   NO, ERROR ...
*
*        DELETE SVC MODULE
*
         DELETE EP=CTSASVC
         B     EXIT
*
****************************************************************
*        SET ERROR CODE                                        *
****************************************************************
*
ERRAUTH  DS    0H
         LA    R15,4
         B     EXIT
*
ERRACT   DS    0H
         LA    R15,8
         B     EXIT
*
ERRSTOR  DS    0H
         LA    R15,12
         B     EXIT
*
ERROPT   DS    0H
         LA    R15,16
         B     EXIT
ERRLMOD  DS    0H
         LA    R15,20
         B     EXIT
*
****************************************************************
*        FINISH                                                *
****************************************************************
*
EXIT     DS    0H
         BRTRN (15)
*
****************************************************************
*        CONSTANTS                                             *
****************************************************************
*
         LTORG
SCRNTAB  DS    0D
         DC    A(0)                SVC ROUTINE
         DC    X'C0000000'         TYPE 3, NO LOCKS
SVCLST   DC    256X'80'            ALL SVCS ARE OK
         ORG   SVCLST+144          SVC 144 IS SCREENED
         DC    X'00'
         ORG   ,
SCRNTABL EQU   *-SCRNTAB
*
****************************************************************
*        CONTROL BLOCKS MAPPING                                *
****************************************************************
*
         IEZJSCB
         IHAPSA
         IKJTCB
         END
