          START
*                                                                       00000101
****************************************************************        00000201
****                                                        ****        00000301
****     CONTROL-SA  RELEASE 2.1.0                          ****        00000401
****                                                        ****        00000501
****     LICENCED MATERIAL - NEW DIMENSION SOFTWARE LTD.    ****        00000601
****                                                        ****        00000701
****************************************************************        00000801
*                                                                       00000901
****************************************************************        00001001
*                                                              *        00001101
* TITLE            : TIME SERVICE FOR TSS ONLINE INTERCEPTOR   *        00001201
*                                                              *        00001301
* FILE NAME        : CTSATIM                                   *        00001401
*                                                              *        00001501
* AUTHOR           : ALEXANDER SHVARTSMAN                      *        00001601
*                                                              *        00001701
* CREATION DATE    : 12/06/97                                  *        00001801
*                                                              *        00001901
* DESCRIPTION      : THE PROGRAM PROVIDES VARIOUS TIME         *        00002001
*                    SERVICES FOR TSS ONLINE INTERCEPTOR       *        00002101
*                                                              *        00002101
* ASSUMPTIONS AND                                              *        00002201
*   CONSIDERATIONS :                                           *        00002301
*                                                              *        00002401
* MOD.ID   WHO      WHEN     DESCRIPTION                       *
* -------- -------- -------- --------------------------------- *
* IS10188  AL       06/10/24 MOVE OUR MODULES ABOVE 16M LINE   *
****************************************************************        00002501
*                                                                       00002601
PARM     DSECT
*
*        COMMON PARMATER LIST
*
PARMDBG  DS    A
PARMMODE DS    A              CALL MODE
PARMSPEC EQU   *
*
*        PARAMETERS FOR 'GETT'
*
         ORG   PARMSPEC
PARMTMST DS    A              TIMESTAMP ADDRESS          - OUTPUT
         ORG   ,
*
*        PARAMETERS FOR 'DIFF'
*
         ORG   PARMSPEC
PARMCTST DS    A              CURRENT TIMESTAMP ADDRESS  - INPUT
PARMTTST DS    A              TABLE'S TIMESTAMP ADDRESS  - INPUT
PARMDIFT DS    A              DIFFTIME                   - OUTPUT
         ORG   ,
*
***************************************************************
*
***************************************************************
CTSATIM  CSECT
CTSATIM  AMODE 31
*IS10188 CTSATIM  RMODE 24       (FOR CTSADBG)
CTSATIM  RMODE ANY                                              IS10188
*
*        INITALIZE
*
         BEGIN *
         CTSEQUR
         CTSLEVEL
         LR    R10,R1
         USING PARM,R10
*
         L     R1,PARMDBG              R1 -> DEBUG LEVEL (4 BYTES)
         XC    DBGLVL,DBGLVL           STORE DEBUG LEVEL
         ICM   R1,15,0(R1)             DEBUG LEVEL
         BNP   NODEBUG                 SKIP DEBUG LVL IF MINUS
         STC   R1,DBGLVL               STORE DEBUG LEVEL (1 BYTE)
NODEBUG  DS    0H
*
*       CHECK MODE
*
         L     R2,PARMMODE
         CTSADBG LEVEL=(DBGLVL,1),'MODE=_',(0(R2),4)
         CLC   0(4,R2),=CL8'GETT'
         BE    GETT
         CLC   0(4,R2),=CL8'DIFF'
         BE    DIFFT
         CTSADBG LEVEL=(DBGLVL,1),'ERROR, UNDEFINED MODE'
         B     EXIT
*
***************************************************************
*        GETT MODE                                            *
***************************************************************
*
GETT     DS    0H
         L     R5,PARMTMST             TIMESTAMP ADDRESS
         USING TIMESTMP,R5
         TIME  BIN
         ST    R0,TIME
         ST    R1,DATE
*
         CTSADBG LEVEL=(DBGLVL,1),                                     X
               'GETT: DATE=# TIME=#',(DATE,TIME)
*
         B     EXIT
*
***************************************************************
*        DIFF  MODE                                           *
***************************************************************
*
DIFFT    DS    0H
         L     R6,PARMDIFT             DIFFTIME
         L     R5,PARMCTST             CURRENT TIMESTAMP ADDRESS
         USING TIMESTMP,R5
         MVC   WTIME,TIME
         MVC   WDATE,DATE
*
         CTSADBG LEVEL=(DBGLVL,1),                                     X
               'DIFFT: CURRENT DATE=# TIME=#',(WDATE,WTIME)
*
         DROP  R5
         L     R5,PARMTTST             TABLE'S TIMESTAMP ADDRESS
         USING TIMESTMP,R5
*
         CTSADBG LEVEL=(DBGLVL,1),                                     X
               'DIFFT: TABLE   DATE=# TIME=#',(DATE,TIME)
*
         CLC   WDATE,DATE
         BE    DIFFTEQD
*
***************************************************************
*     THE DATES ARE NOT EQUAL                                 *
***************************************************************
*
         L     R7,MIDNGT
         S     R7,TIME
         A     R7,WTIME
         ST    R7,0(R6)
         CTSADBG LEVEL=(DBGLVL,1),                                     X
               'DIFFT: DIFFT=#',(?R7)
         B     EXIT
*
***************************************************************
*     THE DATES ARE EQUAL                                     *
***************************************************************
*
DIFFTEQD DS    0H
         L     R7,WTIME
         S     R7,TIME
         ST    R7,0(R6)
         CTSADBG LEVEL=(DBGLVL,1),                                     X
               'DIFFT: DIFFT=#',(?R7)
         B     EXIT
*
***************************************************************
*                                                             *
* EXIT - RETURN TO CALLING PROGRAM                            *
*                                                             *
***************************************************************
*
EXIT     DS    0H
         BRTRN 0
         LTORG ,
*
***************************************************************
*        WORK AREAS AND CONSTANTS                             *
***************************************************************
*
DBGLVL   DC    X'00'
WTIME    DS    F
WDATE    DS    F
NUM100TH EQU   24*60*60*100       NUMBER OF 1/100 SEC IN 24 HRS
MIDNGT   DC    A(NUM100TH)
*
*        TIMESTAMP FORMAT
*
TIMESTMP DSECT
TIME     DS    F
DATE     DS    F
         END
