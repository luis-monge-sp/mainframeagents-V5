         TITLE 'CTS2RXF - FREE ALL ACFRPTRX DATA BUFFERS'
*                                                                       00000901
****************************************************************        00000201
****                                                        ****        00000301
****     MF CONNECTOR RELEASE 4.0.00                        ****        00000401
****                                                        ****        00000501
****     (C) COPYRIGHT 2019 SAILPOINT TECHNOLOGIES, INC.,   ****        00000601
****     ALL RIGHTS RESERVED.                               ****        00000701
****                                                        ****        00000801
****************************************************************        00000901
*                                                                       00000901
****************************************************************        00001001
*                                                              *        00001101
* TITLE            : CTS2RXF                                   *        00001201
*                                                              *        00001501
* AUTHOR           : SELIG TESSLER                             *        00001601
*                                                              *        00001701
* CREATION DATE    : 20 NOVEMBER 2019                          *        00001801
*                                                              *        00001901
* DESCRIPTION      : FREE ALL ACFRPTRX DATA BUFFERS            *        00002001
*                                                              *        00002101
* ASSUMPTIONS AND                                              *        00002201
*   CONSIDERATIONS :                                           *        00002301
*                                                              *        00002401
****************************************************************        00002501
*--------------------- CTS V 4.0.00 ---------------------------*
* WS10075  20.11.19 ST MANAGE PERMISSIONS AS ACCOUNT AND GROUP *
*                      ATTRIBUTES                              *
* WS10076  05.12.20 ST CHANGE ALL LRA... FIELD NAMES TO RUO... *
*                      ALSO CHECK RUOB EYECATCHER              *
* BS10111  23.11.20 NY OBSOLETE - DATA BUFFERS ABOVE THE BAR.  *
* IS10188  14.10.24 AL ABOVE 16M. ONLY 4 LINKCARD #SOM.        *
****************************************************************        00002501
*
*--------------------------------------------------------------*
*        STANDARD ENTRY LINKAGE
*--------------------------------------------------------------*
CTS2RXF  CSECT
CTS2RXF  AMODE 31
CTS2RXF  RMODE 24
*                                                                       00020001
         LHI   15,0                    NULLIFY THE...          BS10111
         BR    14                      ...ROUTINE.             BS10111
         DC    C'CTS2RXF - THIS MODULE IS OBSOLETE'            BS10111
*
         AGO   .END                    SKIP THE WHOLE ROUTINE  BS10111
         USING CTS2RXF,R15
         SAVE  (14,12),,*
         CTSEQUR
         CTSLEVEL
*
         LR    R12,R15                 SET BASE REGISTER
         DROP  R15
         USING CTS2RXF,R12             ESTABLISH ADDRESSABILITY
*--------------------------------------------------------------*
*        GETMAIN WORK AREA AND CHAIN SAVE AREAS
*--------------------------------------------------------------*
         LR    R10,R1                  R10 -> PARAMETER LIST
         USING PRM,R10                 R10 POINTS TO INPUT PARMS
         GETMAIN RC,LV=WALEN           GETMAIN WORK AREA
         CHI   R15,0                   HOW WAS GETMAIN?
         BNE   RC12                    GETMAIN FAILED
*
******** XC    0(WALEN,R1),0(R1)       CLEAR WORK AREA
         LR    R2,R1                   COPY WORK AREA ADDRESS
         LHI   R3,WALEN                GET WORK AREA LENGTH
         SLR   R15,R15                 ZERO SOURCE LEN
         MVCL  R2,R14                  CLEAR WORK AREA
         ST    R13,4(,R1)              SET PTR TO HSA
         ST    R1,8(,R13)              SET PTR TO LSA
         LR    R13,R1                  SET PTR TO SAVE AREA
         USING WA,R13
*--------------------------------------------------------------------
*        GET THE PARAMETERS AND START WORKING
*--------------------------------------------------------------------
         MVI   DBGLEVEL,0              ZERO DEBUG LEVEL
         L     R1,PRMDBG               POINT TO DEBUG LEVEL
         LA    R1,0(,R1)               CLEAR AMODE BIT
         CHI   R1,0                    DEBUG LEVEL PROVIDED?
         BE    NODEBUG                 NO, CONTINUE THERE
         L     R1,0(,R1)               GET DEBUG LEVEL
         CHI   R1,0                    VALID DEBUG LEVEL?
         BNH   NODEBUG                 NO, NO DEBUG LEVEL
         STC   R1,DBGLEVEL             SAVE DEBUG LEVEL
         CHI   R1,77                   I/O DIAG IS ALSO REQUESTED?
         BNE   NODEBUG                 NO, CONTINUE THERE
         MVI   DBGLEVEL,1              RESET DEBUG LEVEL TO 1
NODEBUG  DS    0H
*
         L     R1,PRMERBLK             POINT TO RXE ERROR BLOCK
         LA    R1,0(,R1)               CLEAR AMODE BIT
         CHI   R1,0                    IS THE POINTER PRESENT?
         BE    RC4                     PARAMETER ERROR
         ST    R1,ERRBLKA              SAVE ERROR BLOCK ADDRESS
*
         USING RXE,R1                  ESTABLISH ADDRESSABILITY
         LHI   R15,0
         ST    R15,RXEERROR            INITIALIZE THESE FIELDS
         MVI   RXEPGM,X'00'            "  "  "  "  "  "  "  "
         MVI   RXEFUNC,X'00'           "  "  "  "  "  "  "  "
         MVI   RXEMSG,X'00'            "  "  "  "  "  "  "  "
         MVI   RXEDLINE,X'00'          "  "  "  "  "  "  "  "
         DROP  R1                      DISCONNECT ADDRESSABILITY
*
*                                      ALL FIELDS IN THE NEXT  WS10076
*                                      ~15 LINES WHICH BEGAN   WS10076
*                                      WITH LRA WERE CHANGED   WS10076
*                                      TO RUO.                 WS10076
         ICM   R8,15,PRMLIDRA          POINT TO 1ST LID RULE AREA BLK
         BZ    RC4                     PARAMETER ERROR
         USING RUO,R8                  ESTABLISH ADDRESSABILITY
*
FREELOOP DS    0H
         CLC   RUOHEYEC,=CL4'RUOB'     EYECATCHER PRESENT?     WS10076S
         BNE   RC8                     NO, DISPLAY ERROR MSG   WS10076S
         L     R3,RUOHNEXT             SAVE NEXT BLOCK IN R3
         L     R0,RUOHLEN              R0 = LENGTH OF THIS BLOCK
         LR    R7,R0                   COPY LENGTH FOR DEBUG MESSAGE
         LR    R1,R8                   R1 = ADDRESS OF THIS BLOCK
         LHI   R4,0                                            WS10076N
         IC    R4,RUOHSP               TAKE THE SUBPOOL NUMBER WS10076N
         LHI   R5,0                                            WS10076N
         IC    R5,RUOHKEY              TAKE THE KEY.           WS10076N
         SLL   R5,4                    SHIFT KEY TO ITS PLACE  WS10076N
*WS10076N STORAGE RELEASE,LENGTH=(R0),ADDR=(R1),SP=#RUOSP,KEY=8,       *
               LINKAGE=SYSTEM
*WS10076N
         STORAGE RELEASE,LENGTH=(R0),ADDR=(R1),SP=(R4),KEY=(R5),       *
               LINKAGE=SYSTEM
         CHI   R15,0                   WAS RELEASE SUCCESSFUL?
         BNE   RC8                     NO, RETURN TO CALLER
         SRL   R5,4                    FOR DEBUG               WS10076N
         CTSADBG TYPE=PRTDBG,LEVEL=(DBGLEVEL,1),                       *
               'RELEASED RUO AT ADDRESS=~  LENGTH=#  SP=#  KEY=#',     *
               (?R8,?R7,?R4,?R5),                                      *
               RAREA=RAREA,DBGAREA=DBGAREA
         LTR   R8,R3                   IS POINTER TO NEXT BLOCK ZERO?
         BNZ   FREELOOP                NO, GO BACK UP TO FREE NEXT BLK
         B     RC0                     YES, SET RC=0 & RETURN TO CALLER
*--------------------------------------------------------------------
*        SET THE RETURN CODE AND RETURN TO THE CALLER
*--------------------------------------------------------------------
RC0      DS    0H
         LHI   R15,0                   PROGRAM SUCCESSFUL
         B     RETURN
*
RC4      DS    0H
         LHI   R15,4                   INPUT PARAMETER ERROR
         B     RETURN
*
*                                      ALL FIELDS IN THE NEXT  WS10076
*                                      ~10 LINES WHICH BEGAN   WS10076
*                                      WITH LRA WERE CHANGED   WS10076
*                                      TO RUO.                 WS10076
RC8      DS    0H
         CTSAN2C CHARRC                TRANSLATE RC (R15)
         MVC   MSGACT,=CL7'RELEASE'
         MVC   M251CBNM,=CL8'RUO'
         LR    R15,R8                  BLOCK ADDRESS
         CTSAN2C M251ADDR,MODE=HEX     TRANSLATE ADDRESS
         L     R15,RUOHLEN
         CTSAN2C NUMCHAR,LEN=7         TRANSLATE LENGTH
         LHI   R15,#RUOSP
         CTSAN2C SPNUM,LEN=3           TRANSLATE SP NUM
*
         CTSMSG1 CODE=CTS251E,ROUT=(WTO,NO),RAREA=RAREA,               *
               PLANT=(7,MSGACT,8,M251CBNM,8,M251ADDR,7,NUMCHAR,3,SPNUM,*
               ,2,CHARRC)
*
**       RETURN THE MESSAGE TO THE CALLER IN THE ERROR BLOCK.
**
**       THE MESSAGE WAS CREATED BY CTSMSG1 IN RAREA:
**         - 2 BYTES = MESSAGE LENGTH
**         - MESSAGE TEXT
*
         ICM   R1,15,ERRBLKA           POINT TO RXE ERROR BLOCK
         BZ    SKIPRXE                 NOT PRESENT, SKIP THIS
*
         USING RXE,R1                  ESTABLISH ADDRESSABILITY
         LHI   R15,1
         ST    R15,RXEERROR            SET ERROR INDICATION
*
         MVC   RXEPGM(8),=CL8'CTS2RXF' SET PROGRAM NAME
         MVI   RXEPGM+8,X'00'
         MVI   RXEFUNC,X'00'
*
         LH    R15,RAREA               R15 = MESSAGE LENGTH
         CHI   R15,L'RXEMSG-1          ENOUGH SPACE FOR MESSAGE?
         BNH   MRRETMSG                YES, CONTINUE
         LHI   R15,L'RXEMSG-1          NO, TRUNCATE
MRRETMSG DS    0H
         AHI   R15,-1                  DECREMENT FOR EXECUTED MVC
         EX    R15,MRMVMSG             MOVE THE MESSAGE
         B     *+L'*+L'MRMVMSG
MRMVMSG  MVC   RXEMSG(0),RAREA+2       MOVE THE MESSAGE
         LA    R15,RXEMSG+1(R15)       POINT TO CHAR AFTER MSG TEXT
         MVI   0(R15),X'00'            PUT C-TYPE TERMINATOR
         MVI   RXEDLINE,X'00'
         DROP  R1
*
SKIPRXE  DS    0H
         LHI   R15,8                   STORAGE RELEASE ERROR
         B     RETURN
*
RC12     DS    0H
         LHI   R15,12                  WORK AREA GETMAIN ERROR
         B     OUT
*
RETURN   DS    0H
         LR    R5,R15                  SAVE RC
         LR    R1,R13                  GET POINTER TO GETMAINED AREA
         L     R13,4(,R13)             GET POINTER TO HSA
         FREEMAIN RC,LV=WALEN,A=(R1)   FREEMAIN WORK AREA
         LR    R15,R5                  RESTORE RC
*
OUT      DS    0H
         RETURN (14,12),RC=(15)
         DROP  ,
         LTORG ,
*
         CTSAN2C WORK=YES,RENT=CONST
*
*--------------------------------------------------------------------
*        WORK AREA, INPUT PARAMETERS, AND CONTROL BLOCK DSECTS
*--------------------------------------------------------------------
WA       DSECT
SA       DS    18F                     SAVE AREA
*
ERRBLKA  DS    A                       PTR TO RXE ERROR BLOCK
MSGACT   DS    CL7                     'RELEASE' FOR CTS251E
M251CBNM DS    CL8                     CONTROL BLOCK NAME
M251ADDR DS    CL8                     ADDRESS FOR CTS251E
NUMCHAR  DS    CL8                     CTSAN2C OUTPUT
SPNUM    DS    CL3                     SUBPOOL NUMBER IN CHAR (FOR MSG)
CHARRC   DS    CL2                     RC IN CHAR (FOR CTSMSG1)
DBGLEVEL DS    XL1                     DEBUG LEVEL FOR THIS ROUTINE
         CTSAN2C WORK=YES,RENT=VAR
         DS    0D
RAREA    DS    XL512                   USED BY CTSADBG
DBGAREA  DS    XL2048                  USED BY CTSADBG
*
WALEN    EQU   *-WA                    LENGTH OF WORK AREA
*
*
PRM      DSECT                         INPUT PARAMETERS
PRMLIDRA DS    A                       PTR TO LIDRA BLOCK
PRMERBLK DS    A                       PTR TO RXE ERROR BLOCK
PRMDBG   DS    A                       PTR TO DEBUG LEVEL
*
*
         COPY CTSARUOB                                          WS10076
*
         COPY CTS2RXE
.END     ANOP                                                   BS10111
         END
