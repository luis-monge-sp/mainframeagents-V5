         TITLE 'CTSADDI - RETURN DD INFORMATION'                        00040000
*                                                                       00050000
****************************************************************        00060000
****                                                        ****        00070000
****     CONTROL-SA  RELEASE 4.0.00                         ****        00080000
****                                                        ****        00090000
****     (C) COPYRIGHT 2015 SAILPOINT TECHNOLOGIES, INC.,   ****        00100000
****     ALL RIGHTS RESERVED.                               ****        00110000
****                                                        ****        00110000
****************************************************************        00120000
*                                                                       00130000
****************************************************************        00140000
*                                                              *        00150000
* TITLE            : RETURN DATASET NAME AND MEMBER NAME, IF   *        00160000
*                    EXISTS, FROM A DD STATEMENT.              *        00160000
*                                                              *        00170000
* FILE NAME        : CTSADDI                                   *        00180000
*                                                              *        00190000
* AUTHOR           : NURIT YAHALOM                             *        00200000
*                                                              *        00210000
* CREATION DATE    : 22/09/16                                  *        00220000
*                                                              *        00230000
* DESCRIPTION      : THIS ROUTINE REPLACES THE SAS/C OSDDINFO  *        00240000
*                    SERVICE, EXCEPT THAT IT DOES NOT RETURN   *        00230000
*                    THE DCB ATTRIBUTES OF THE DATASET.        *        00230000
*                    THE ROUTINE DOES NOT CHECK IF THE DATASET *        00230000
*                    EXISTS.                                   *        00230000
*                    IF THE DD STATEMENT POINTS TO A           *        00270000
*                    CONCATENATION, THE ROUTINE RETURNS THE    *        00250000
*                    DETAILS OF THE FIRST DD STATEMENT IN THE  *        00250000
*                    CONCATENATION.                            *        00250000
*                                                              *        00250000
* PARAMETERS       :  1. DDNAME - (I) - SHOULD END WITH X'00'  *        00240000
*                        OR A BLANK. MANDATORY.                *        00250000
*IS10174              2. AREA FOR DSN -(O) - CL44. MANDATORY   *        00250000
*IS10174              3. AREA FOR DSN LENGTH (O) - WORD -      *        00250000
*IS10174                 OPTIONAL.                             *        00250000
*IS10174              4. AREA FOR MEMBER NAME - (O) - CL8.     *        00250000
*IS10174                 OPTIONAL.                             *        00250000
*IS10174              5. AREA FOR MEMBER NAME LENGTH (O) -     *        00250000
*IS10174                 WORD - OPTIONAL.                      *        00250000
*IS10174              6. DEBUG LEVEL - (I) - F. OPTIONAL.      *        00250000
*                     2. NUMBER OF REQUSTED PARAMETERS. (I)    IS10174  00250000
*                        OPTIONAL.                             IS10174  00250000
*                     3. REQUESTED PARAMETERS ARRAY. (I)       IS10174  00250000
*                        MANDATORY WHEN NUMBER OF PARAMETERS   IS10174  00250000
*                        IS SPECIFIED AND IS NOT 0.            IS10174  00250000
*                     4. DEBUG LEVEL - (I) - F. OPTIONAL.      *        00250000
*                                                              *        00250000
*                     REQUESTED PARMAETERS ARRARY STRUCTURE:   *        00250000
*                     THE ARRAY MAY CONTAIN SEVERAL PARAMETERS.*        00250000
*                     EACH PARAMETER HAS 3 ENTRIES IN THE      *        00250000
*                     ARRAY:                                   *        00250000
*                     -> ATTRIBUTE NAME.  (I, CL8)             *        00250000
*                     -> OUTPUT AREA.     (O, VARIABLE)        *        00250000
*                     -> RC.              (O, F)               *        00250000
*                     THE STRUCTURE IS MAPPED BY PRMATTR..     *        00250000
*                     THE CALLER SHOULD PROVIDE THE OUTPUT     *        00250000
*                     AREA FOR EACH ATTRIBUTE ACCORDING TO     *        00250000
*                     THE DETAILS IN TABLE ATTRLIST BELOW.     *        00250000
*                                                              *        00250000
*                                                              *        00250000
* ERTURN CODES     :  0 - OK.                                  *        00240000
*                     4 - SOME OF THE PARAMETERS ARE NOT       *        00250000
*                         SUPPORTED.                           *        00250000
*                     8 - RDJFCB FAILED.                       *        00250000
*                    12 - GETMAIN FAILED.                      *        00250000
*                    16 - PARAMETERS ERROR.                    *        00250000
*                    20 - FAILED TO RETRIEVE SOME PARAMETERS   *        00250000
*                                                              *        00250000
*                                                              *        00250000
* ASSUMPTIONS AND                                              *        00260000
*   CONSIDERATIONS :  1. MISSING PARAMETERS SHOULD BE          *        00304000
*                        SPECIFIED AS 0.                       *        00304000
*                                                              *        00304000
*                     2. DDNAME AND ATTRIBUTE NAMES SHOULD     *        00304000
*                        BE SPECIFIED IN UPPERCASE.            *        00304000
*                                                              *        00304000
*                     2. WHEN THE NUMBER OF REQUESTED          *        00304000
*                        PARAMETERS IS 0 OR NOT SPECIFIED,     *        00304000
*                        THE RETURN CODE INDICATES WHETHER     *        00304000
*                        RDJFCB WAS SUCCESSFUL OR NOT          *        00304000
*                        (WHETHER THE DDNAME EXISTS).          *        00304000
*                                                              *        00304000
*                     3. IF THE NUMBER OF REQUESTED            *        00304000
*                        PARAMETERS IS 0 OR NOT SPECIFIED,     *        00304000
*                        THE RETURN CODE INDICATES WHETHER     *        00304000
*                        RDJFCB WAS SUCCESSFUL OR NOT, WHICH   *        00304000
*                        IS ACTUALLY WHETHER THE DDNAME EXISTS.*        00304000
*                                                              *        00304000
****************************************************************        00310000
*                                                                       00320000
****************************************************************        00330000
* MOD.ID   WHO      WHEN     DESCRIPTION                       *        00340000
* -------- -------- -------- --------------------------------- *        00350000
*          SAS2IBMN 22/09/16 INITIAL VERSION                   *
* IS10174  NURITY   27/12/17 DYNAMIC EXECOUT SUPPORT.          *
* WS10075  NURITY   02/12/19 INCORRECT RETURN CODE BECAUSE     *
*                            MAXRC IS NOT SET.
****************************************************************        00380000
CTSADDI  CSECT                                                          00390000
CTSADDI  RMODE ANY                                                      00400000
CTSADDI  AMODE ANY                                                      00410000
*
         USING CTSADDI,R15
         SAVE  (14,12),,*                                               00530000
         CTSLEVEL
*
         LR    R12,R15                 SET BASE REGISTER.
         LA    R11,CONST               R11 -> CONSTANTS AREA   IS10174
         DROP  R15
*IS10174 USING CTSADDI,R12
         USING (CTSADDI,CTSADDIE),R12                          IS10174
         USING (CONST,CONSTE),R11                              IS10174
*
*
**   ALLOCATE WORK AREA AND CHAIN SAVE AREAS.
*
         LR    R10,R1                  R10 -> PARMS LIST.
         USING PRM,R10                 R10 POINTS TO INPUT PARMS
         GETMAIN RC,LV=WALEN,LOC=24
         CHI   R15,0
         JNE   RC12                    GETMAIN FAILED.
*
         LR    R3,R1                   SAVE AREA ADDRESS.      WS10075
         LR    R0,R1                                           WS10075
         LA    R1,WALEN                                        WS10075
         LHI   R15,0                                           WS10075
         MVCL  R0,R14                  ZERO AREA.              WS10075
         LR    R1,R3                   RESTORE AREA ADDRESS.   WS10075
*
         ST    R13,4(,R1)
         ST    R1,8(,R13)
         LR    R13,R1
         USING WA,R13
*
**   TAKE DEBUG LEVEL.
*
         MVI   DBGLEVEL,0              ZERO DEBUG LEVEL.
         L     R4,PRMDBG               R4 -> DEBUG LEVEL (4 BYTES)
         LA    R4,0(,R4)               CLEAR AMODE BIT.
         CHI   R4,0                    DEBUG LEVEL PROVIDED ?
         JE    NODEBUG
         L     R4,0(,R4)               R4 = DEBUG LEVEL
         CHI   R4,0                    VALID DEBUG LEVEL ?
         JNH   NODEBUG                 ..NO - NO DEBUG.
         STC   R4,DBGLEVEL             STORE DEBUG LEVEL (1 BYTE)
NODEBUG  DS    0H
*IS10174 L     R3,PRMDDN
*IS10174 L     R4,PRMDBG
*IS10174 CTSADBG LEVEL=(DBGLEVEL,1,2,3,4,5,6),                         X00885600
               'PARMS = ~ / _, ~, ~, ~ / #',                           X00885600
               (?R3,0(R3),1,PRMDSN,PRMMEM,?R4,0(R4)),                  X00885600
               RAREA=RAREA,DBGAREA=DBGAREA
*
**   TAKE DDNAME.
*
         L     R4,PRMDDN               R4 -> DDNAME
         LA    R4,0(,R4)               CLEAR AMODE BIT.
         CHI   R4,0                    DDNAME PROVIDED ?
*IS10174 JE    RC4                     ..NO - ERROR.
         JE    RC16                    ..NO - ERROR.           IS10174
*IS10174 LR    R1,R4                   R1 -> DDNAME (FOR LOOP)
*IS10174 MVC   DDNAME,BLANK44
*IS10174 LA    R3,L'DDNAME             R2 CONTAINS MAX LEN OF USERID
*IS10174 LOOPDDN  DS    0H
*IS10174 CLI   0(R1),C' '              IS CHAR BLANK\X'00' OR LOWER?
*IS10174 JNH   ENDDDN                  ..YES - END OF DDNAME.
*IS10174 LA    R1,1(,R1)               ADVANCE PARM POINTER
*IS10174 BCT   R3,LOOPDDN              SUBTRACT 1 FROM COUNTER & RELOOP
*IS10174 ENDDDN   DS    0H
*IS10174 SR    R1,R4                   R1 = DDNAME LENGTH.
*IS10174 JNP   RC4                      ..NO DDNAME.
*IS10174 AHI   R1,-1                   FOR EX.
*IS10174 EX    R1,MVDDN
*IS10174 B     *+L'*+L'MVDDN
*IS10174 MVDDN    MVC   DDNAME(*-*),0(R4)
         LR    R0,R4                   R0 -> PARAMETER NAME.   IS10174
         BAS   R14,GETPARM                                     IS10174
         CHI   R1,0                    DDNAME PROVIDED ?       IS10174
         JE    RC16                    ..NO - ERROR.           IS10174
         MVC   DDNAME,PARMNAME         TAKE DDNAME.            IS10174
         CTSADBG LEVEL=(DBGLEVEL,1,2,3,4,5,6),                         X00885600
               'DDNAME = _ ',(DDNAME,8),                               X00885600
               RAREA=RAREA,DBGAREA=DBGAREA
*IS10174
*IS10174 CHECK IF DSN AREA WAS PROVIDED
*IS10174
*IS10174 L     R4,PRMDSN               R4 -> AREA FOR DSN.
*IS10174 LA    R4,0(,R4)               CLEAR AMODE BIT.
*IS10174 CHI   R4,0                    AREA FOR DSN PROVIDED ?
*IS10174 JE    RC4                     ..NO - ERROR.
*
**   PREPARE TO RETRIEVE THE DATA
**   1. SET THE EXIT LIST.
**   2. SET THE DCB
*
EXLSTU   USING EXLST,EXLAREA
         MVI   EXLSTU.EXLCODES,EXLLASTE+EXLRJFCB
         LA    R1,JFCBAREA
         STCM  R1,B'0111',EXLSTU.EXLENTRB
         DROP  EXLSTU
*
         MVC   DCBAREA(DCBML),DCBM
         LA    R5,DCBAREA
DCBU     USING IHADCB,R5
         MVC   DCBU.DCBDDNAM,DDNAME
         LA    R1,EXLAREA
         STCM  R1,B'0111',DCBU.DCBEXLSA
*
**   RETRIEVE THE DATA USING RDJFCB
*
         MVC   RDJFCBL(RDJFCBML),RDJFCBM
         RDJFCB ((R5)),MF=(E,RDJFCBL)
         CTSADBG LEVEL=(DBGLEVEL,1,2,3,4,5,6),                         X00885600
               'RDJFCB: RC = #',(?R15),                                X00885600
               RAREA=RAREA,DBGAREA=DBGAREA
         CHI   R15,0
         JNE   RC8
         DROP  DCBU
*
JFCBU    USING JFCB,JFCBAREA
*IS10174 CTSADBG LEVEL=(DBGLEVEL,1,2,3,4,5,6),                         X00885600
               'RDJFCB: DSN = _    IND = $   MEMBER = _',              X00885600
               (JFCBU.JFCBDSNM,8,JFCBU.JFCBIND1,1,JFCBU.JFCBELNM,8),   X
               RAREA=RAREA,DBGAREA=DBGAREA
         MVC   SNAPHDR(L'SNAPHDRM),SNAPHDRM                    IS10174
         MVC   SNAPHDDN,DDNAME                                  IS10174
         LA    R0,SNAPHDR                                      IS10174
         CTSADBG TYPE=SNAP,ID=1,HDR=(R0),                      IS10174 X
               ADDR=(JFCBAREA,JFCBAREA+JFCBALEN-1),            IS10174 X
               LEVEL=(DBGLEVEL,1,2,3,4,5,6),                   IS10174 X
               RAREA=RAREA,DBGAREA=DBGAREA                     IS10174
*
** IS10174 - START
*
**  RETURN REQUESTED ATTRIBUE.
**
**  FOR EACH ATTRIBUTE IN INPUT PARMS, LOOK FOR THE ATTRIBUTES LIST.
**  - IF THE ATTRIBUTE EXISTS IN THE LIST, BRANCH TO THE PROCESSING
**    ROUTINE POINTED BY THE ENTRY.
**  - IF THE ATTRIBUTE DOES NOT EXIST IN THE LIST, SET A RETURN CODE
**    IN THE CORRESPONDING PARM RC AND PROCESS THE NEXT PARAMETER.
**
*
         L     R4,PRMATTR#
         CHI   R4,0
         JE    RC0                     NOTHING IS REQUESTED.
         L     R3,0(,R4)               R3 = NUMBER OF REQUESTED ATTR.
         CHI   R3,0
         JE    RC0                     NOTHING IS REQUESTED.
         CTSADBG LEVEL=(DBGLEVEL,1,2,3,4,5,6),                         X00885600
               '# ATTRIBUTES REQUESTED',(?R3),                         X00885600
               RAREA=RAREA,DBGAREA=DBGAREA
*
**   RETURN REQUESTED ATTRIBUTES.
*
         L     R9,PRMATTRL             R9 -> REQUESTED ATTR. LIST.
         CHI   R9,0
         JE    RC16                    NO ATTRIBUTES LIST - ERROR.
*
         USING PRMATTR,R9
ATTRLOOP DS    0H
         CTSADBG LEVEL=(DBGLEVEL,1,2,3,4,5,6),                         X00885600
               'PARM = ~, ~, ~',(PRMANAME,PRMAOUTA,PRMARC),            X00885600
               RAREA=RAREA,DBGAREA=DBGAREA
         L     R0,PRMANAME             R0 -> REQUESTED ATTRIBUTE.
         CHI   R0,0                    ATTRIBUTE EXISTS ?
         JE    RC16                    ..NO - ERROR.
         BAS   R14,GETPARM             GET THE ATTRIBUTE NAME.
         CTSADBG LEVEL=(DBGLEVEL,1,2,3,4,5,6),                         X00885600
               'REQUESTED ATTRIBUTE = _',(PARMNAME,8),                 X00885600
               RAREA=RAREA,DBGAREA=DBGAREA
*
         LA    R8,ATTRLIST             R8 -> ATTRIBUTES LIST.
         USING ATTRENT,R8
         LHI   R0,ATTRL#
LK4ATTR  DS    0H                      LOOK FOR PROCESS ROUTINE.
         CLC   ATTRNAME,PARMNAME       FOUND ?
         JNE   ATTRNEXT                ..NO - CHECK  NEXT ENTRY.
*                                      ..YES ->
         L     R4,PRMAOUTA             R4 -> OUTPUT AREA,
         LA    R4,0(,R4)
         CHI   R4,0                    PARAMETER PROVIDED ?
         JE    RC16                    ..NO - ERROR.
         L     R15,ATTRRTN             R15 -> PROCESS ROUTINE.
         BASR  R14,R15
         L     R4,PRMARC               R4 -> RC AREA.
         LA    R4,0(,R4)
         CHI   R4,0                    PARAMETER PROVIDED ?
         JE    RC16                    ..NO - ERROR.
         ST    R15,0(,R4)              RETURN ATTRIBUTE RC.
*WS10075 J     PARMNEXT                SKIP TO NEXT PARM.
         J     SETMAXRC                SET MAX RC, IF NEEDED.  WS10075
ATTRNEXT DS    0H
         LA    R8,ATTRENTL(,R8)
         BCT   R0,LK4ATTR              CHECK NEXT ENTRY.
*
*WS10075 L     R6,PRMARC               PARM NOT FOUND, SET RC AND CONT.
         L     R4,PRMARC               PARM NOT FOUND, SET RC  WS10075
         LA    R4,0(,R4)
         CHI   R4,0                    PARAMETER PROVIDED ?
         JE    RC16                    ..NO - ERROR.
         LHI   R15,4
*WS10075 ST    R15,0(,R6)              INDICATE PARM NOT SUPPORTED.
         ST    R15,0(,R4)              INDICATE PARM NOT SUPP. WS10075
SETMAXRC DS    0H                                              WS10075
         C     R15,MAXRC               RC IS HIGHER THAN MAX ? WS10075
         JNH   PARMNEXT                ..NO - CONTINUE.        WS10075

         ST    R15,MAXRC               SET AS MAX RC.
*
PARMNEXT DS    0H                      PROCESS NEXT PARAMETER.
         LA    R9,PRMALEN(,R9)         R9 -> NEXT PARM NAME.
         BCT   R3,ATTRLOOP             GO PROCESS IT.
         J     SETRC                   ALL PROCESSED - OUT.
** IS10174 - END
*
**   RETURN THE DSN.
**
**   FOR NAMES SPECIFIED WITH QUOTES, THE QUOTES ARE NOT SAVES IN THE
**   JFCB. THEREFORE, IF THE NAME CONTAINS BLANKS, IT WILL BE
**   TRUNCATED AT THE FIRST BLANLK.
**   THE SAME IS DONE BY SVC 99 INFORMATION RETRIVAL.
*
GETDSN   DS    0H                                              IS10174
*IS10174 L     R4,PRMDSN
         MVC   0(44,R4),JFCBU.JFCBDSNM
*IS10174 L     R8,PRMDSNL              R8 -> AREA FOR DSN LENGTH,
*IS10174 CHI   R8,0                    REQUESTED ?
*IS10174 BE    GETMEM                  ..NO - GET MEMBER NAME.
*IS10174 LA    R1,44(,R4)              R1 -> END OF DSN.
*IS10174 TRT   0(44,R4),TRTBLANK       LOOK FOR BLANK.
*IS10174 SR    R1,R4                   R1 = DSN LENGTH.
*IS10174 ST    R1,0(,R8)               RETURN DSN LENGTH.
         CTSADBG LEVEL=(DBGLEVEL,1,2,3,4,5,6),                 IS10174 X00885600
               'RET DSN = _',(0(R4),44),                       IS10174 X00885600
               RAREA=RAREA,DBGAREA=DBGAREA                     IS10174
         LHI   R15,0                   SET ATTRIBUTE RC. .     IS10174
         BR    R14                     RETURN                  IS10174
*
**   RETURN THE MEMBER NAME.
*
GETMEM   DS    0H
         TM    JFCBU.JFCBIND1,JFCPDS   IS IT A PDS MEMBER ?
IS10174  JNO   ENDMEM                  ..NO - RETURN.
         JO    RETMEM                  ..YES - RETURN IT.      IS10174
         MVC   0(8,R4),BLANK8                                  IS10174
         J     ENDMEM                                          IS10174
*IS10174 L     R4,PRMMEM
*IS10174 CHI   R4,0
*IS10174 JE    ENDMEM
RETMEM   DS    0H                                              IS10174
         MVC   0(8,R4),JFCBU.JFCBELNM
*IS10174 L     R8,PRMMEML              R8 -> AREA FOR DSN LENGTH,
*IS10174 CHI   R8,0                    REQUESTED ?
*IS10174 BE    ENDMEM                  ..NO - GET MEMBER NAME.
*IS10174 LA    R1,8(,R4)               R1 -> END OF MEMBER NAME.
*IS10174 TRT   0(8,R4),TRTBLANK        LOOK FOR BLANK.
*IS10174 SR    R1,R4                   R1 = DSN LENGTH.
*IS10174 ST    R1,0(,R8)               RETURN MEMBER NAME LENGTH.
*IS10174 J     ENDMEM
*IS10174 DROP  JFCBU
ENDMEM   DS    0H
         CTSADBG LEVEL=(DBGLEVEL,1,2,3,4,5,6),                 IS10174 X00885600
               'RET MEM = _',(0(R4),8),                        IS10174 X00885600
               RAREA=RAREA,DBGAREA=DBGAREA                     IS10174
         LHI   R15,0                   SET ATTRIBUTE RC. .     IS10174
         BR    R14                     RETURN                  IS10174
**  IS10174 - START
*
**   RETURN VIO INDICATION ( Y / N )
*
GETVIO   DS    0H
         TM    JFCBU.JFCFLGS1,JFCVRDS  VIO ?
         JO    VIOY                    ..YES - RETURN IT.
         MVI   0(R4),C'N'
         J     ENDVIO
VIOY     DS    0H
         MVI   0(R4),C'Y'
ENDVIO   DS    0H
         CTSADBG LEVEL=(DBGLEVEL,1,2,3,4,5,6),                         X00885600
               'RET VIO = _',(0(R4),1),                                X00885600
               RAREA=RAREA,DBGAREA=DBGAREA
         LHI   R15,0                   SET ATTRIBUTE RC. .
         BR    R14                     RETURN
*
**   RETURN LRECL
*
GETLRECL DS    0H
         MVC   0(2,R4),JFCBU.JFCLRECL
         CTSADBG LEVEL=(DBGLEVEL,1,2,3,4,5,6),                         X00885600
               'RET LRECL = $',(0(R4),2),                              X00885600
               RAREA=RAREA,DBGAREA=DBGAREA
         LHI   R15,0                   SET ATTRIBUTE RC. .
         BR    R14                     RETURN
*
**   RETURN BLKSIZE
*
GETBLKSZ DS    0H
         MVC   0(2,R4),JFCBU.JFCBLKSI
         CTSADBG LEVEL=(DBGLEVEL,1,2,3,4,5,6),                         X00885600
               'RET BLKSIZE = $',(0(R4),2),                            X00885600
               RAREA=RAREA,DBGAREA=DBGAREA
         LHI   R15,0                   SET ATTRIBUTE RC. .
         BR    R14                     RETURN
*
**   RETURN RECFM ( F/V/U/?[B][S][A/M] )
*
GETRECFM DS    0H
         MVC   0(4,R4),BLANK8          INIT OUTPUT FIELD.
         MVI   0(R4),C'?'              RECFM UNKNOWN.
*
         LHI   R0,0
         TM    JFCBU.JFCRECFM,JFCUND
         JM    CHKF
         MVI   0(R4),C'U'
         LA    R4,1(,R4)
         J     RETA
CHKF     DS    0H
         TM    JFCBU.JFCRECFM,JFCFIX
         JZ    CHKV
         MVI   0(R4),C'F'
         J     RETB
CHKV     DS    0H
         TM    JFCBU.JFCRECFM,JFCVAR
         JZ    RETB
         MVI   0(R4),C'V'
RETB     DS    0H
         LA    R4,1(,R4)
         TM    JFCBU.JFCRECFM,JFCRFB   BLOCKED ?
         JZ    RETS
         MVI   0(R4),C'B'
         LA    R4,1(,R4)
RETS     DS    0H
         TM    JFCBU.JFCRECFM,JFCRFS   STANDARD /SPANNED ?
         JZ    RETA
         MVI   0(R4),C'S'
         LA    R4,1(,R4)
RETA     DS    0H
         TM    JFCBU.JFCRECFM,JFCASA   ASA CODES  ?
         JZ    RETC
         MVI   0(R4),C'A'
         J     ENDRECFM
RETC     DS    0H
         TM    JFCBU.JFCRECFM,JFCMAC   MACHINE CODES ?
         JZ    ENDRECFM
         MVI   0(R4),C'C'
         J     ENDRECFM
ENDRECFM DS    0H
         CTSADBG LEVEL=(DBGLEVEL,1,2,3,4,5,6),                         X00885600
               'RET RECFM = _',(0(R4),4),                              X00885600
               RAREA=RAREA,DBGAREA=DBGAREA
         LHI   R15,0                   SET ATTRIBUTE RC. .
         BR    R14                     RETURN
*
**   SPACE TYPE (C/T/B/?)
*
GETTSPC  DS    0H
         MVI   0(R4),C'?'              SPACE TYPE UHKNOWN
*
         LHI   R0,0
         IC    R0,JFCBU.JFCBCTRI
         SRL   R0,6                    LEAVE ONLY...
         SLL   R0,6                       ...U / F / V
         CHI   R0,JFCBCYL              CYL ?
         JNE   CHKTRK
         MVI   0(R4),C'C'
         B     ENDTSPC
CHKTRK   DS    0H
         CHI   R0,JFCBTRK              TRK ?
         JNE   CHKAVB
         MVI   0(R4),C'T'
         B     ENDTSPC
CHKAVB   DS    0H
         CHI   R0,JFCBAVR              AVERAGE BLKSIZE ?
         JNE   ENDTSPC
         MVI   0(R4),C'B'
ENDTSPC  DS    0H
         CTSADBG LEVEL=(DBGLEVEL,1,2,3,4,5,6),                         X00885600
               'RET TSPACE = _',(0(R4),1),                             X00885600
               RAREA=RAREA,DBGAREA=DBGAREA
         LHI   R15,0                   SET ATTRIBUTE RC. .
         BR    R14                     RETURN
*
**   PRIMARY SPACE QUANTITY
*
GETPSPC  DS    0H
         LHI   R0,0
         ICM   R0,B'0111',JFCBU.JFCBPQTY
         ST    R0,0(,R4)
         CTSADBG LEVEL=(DBGLEVEL,1,2,3,4,5,6),                         X00885600
               'RET P-SPACE = #',(0(R4)),                              X00885600
               RAREA=RAREA,DBGAREA=DBGAREA
         LHI   R15,0                   SET ATTRIBUTE RC. .
         BR    R14                     RETURN
*
**   SECONDARY SPACE QUANTITY
*
GETSSPC  DS    0H
         LHI   R0,0
         ICM   R0,B'0111',JFCBU.JFCBSQTY
         ST    R0,0(,R4)
         CTSADBG LEVEL=(DBGLEVEL,1,2,3,4,5,6),                         X00885600
               'RET S-SPACE = #',(0(R4)),                              X00885600
               RAREA=RAREA,DBGAREA=DBGAREA
         LHI   R15,0                   SET ATTRIBUTE RC. .
         BR    R14                     RETURN
*
**   AVERAGE BLKSIZE
*
GETAVGB  DS    0H
         LHI   R0,0
         ICM   R0,B'0111',JFCBU.JFCBDRLH
         ST    R0,0(,R4)
         CTSADBG LEVEL=(DBGLEVEL,1,2,3,4,5,6),                         X00885600
               'RET AVG-BLK = #',(0(R4)),                              X00885600
               RAREA=RAREA,DBGAREA=DBGAREA
         LHI   R15,0                   SET ATTRIBUTE RC. .
         BR    R14                     RETURN
**  IS10174 - END
*
         DROP  JFCBU                                           IS10174
*
**   SET RETURN CODE AND RETURN
*
SETRC    DS    0H                                              IS10174
         L     R15,MAXRC               TAKE MAX RC.            IS10174
         CHI   R15,4                   UNSUPPORTED ATTRIBUTES? WS10075
         JH    RC20                    ..NO - ERROR, RC = 20.  WS10075
         J     RETURN                                          IS10174
RC0      DS    0H                      OK
         LHI   R15,0
         J     RETURN
RC4      DS    0H                      UNSUPPORTED ATTRIBUTES.
         LHI   R15,4
         J     RETURN
RC8      DS    0H                      RDJFCB FAILED.
         LHI   R15,8
         J     RETURN
RC12     DS    0H                      GETMAIN ERROR.
         LHI   R15,12
         J     OUT
RC16     DS    0H                      PARAMETERS ERROR.       IS10174
         LHI   R15,16                                          IS10174
         J     RETURN                                          IS10174
RC20     DS    0H                      ERROR HANDLING ATTR.    IS10174
         LHI   R15,20                                          IS10174
         J     RETURN                                          IS10174
*
RETURN   DS    0H
         CTSADBG LEVEL=(DBGLEVEL,1,2,3,4,5,6),                         X00885600
               'RETURN: RC = #',(?R15),                                X00885600
               RAREA=RAREA,DBGAREA=DBGAREA
         LR    R5,R15                  SAVE RC
         LR    R1,R13                  R1 -> ALLOCTED AREA.
         L     R13,4(,R13)
         FREEMAIN RC,LV=WALEN,A=(R1)
         LR    R15,R5                  RESTORE RC.
*
OUT      DS    0H
         RETURN (14,12),RC=(15)
         DROP  ,
** IS10174 - START
*******************************************************************
**
**   GET THE PARAMETER INTO A 8-CHAR FIELD, PADDED WITH BLANKS,
**   IF NEEDED.
**
**   INPUT:
**   R0 -> INPUT PARM.
**   R14-> RETURN ADDRESS.
**
**   OUTPUT:
**   PARMNAME = PARAMETER.
**   R1       = PARAMETER LENGTH.
**
**   R1-R2 ARE DESTROYES BY THIS ROUTINE.
**
*******************************************************************
GETPARM  DS    0H
         BASR  R15,0
         USING (*,GETPARME),R15
         USING (CONST,CONSTE),R11
         USING WA,R13
         XR    R0,R14                  SWITCH...
         XR    R14,R0                     ...R0...
         XR    R0,R14                        ...AND R14.
         MVC   PARMNAME,BLANK8
         LA    R1,8(,R14)              R1 -> END OF PARM
         TRT   0(8,R14),TRTCHAR        LOOK FOR FIRST NON-CHAR.
         LA    R14,0(,R14)             CLEAR THE HIGH-ORDER BIT.
         SR    R1,R14                  R1 = PARM LENGTH.
         JZ    GPEND                   0 = NOTHING TO MOVE.
         AHI   R1,-1                   FOR EX.
         EX    R1,MVPARM
         J     *+L'*+L'MVPARM
MVPARM   MVC   PARMNAME(*-*),0(R14)    RETURN PARM             IS10174
         AHI   R1,1                    RESTORE PARM LENGTH.
         TR    PARMNAME,TRUCASE        TRANSLATE TO UPPERCASE  IS10174
GPEND    DS    0H
         LR    R14,R0                  RESTORE R14.
         BR    R14                     RETURN
         DROP  ,
GETPARME EQU   *
** IS10174 - END
*
CTSADDIE EQU   *                       END OF PROGRAM. NEEDED  IS10174
*                                      ...FOR USING.           IS10174
*
*******************************************************************
*
**   CONSTANTS
*
*******************************************************************
CONST    DS    0D                                              IS10174
         LTORG ,
*IS10174 BLANK44  DC    CL44' '
BLANK8   DC    CL8' '                                          IS10174
DCBM     DCB   MACRF=R,DSORG=PS                                         00320000
DCBML    EQU   *-DCBM
*
RDJFCBM  RDJFCB 0,MF=L
RDJFCBML EQU   *-RDJFCBM
*
SNAPHDRM DC    C'** JFCB FOR DD '                              IS10174
*
*IS10174 - START
*
**   SUPPORTED ATTRIBUTES
**   EACH ENTRY CONTAINS THE ATTRIBUTE NAME AND THE ADDRESS
**   OF ITS PROCESS ROUTINE.
**   THE ENTRIES ARE MAPPED BY ATTRENT DSECT.
**
**   IF MORE ATTRIBUTES SHOULD BE SUPPROTED, AN ENTRY SHOULD BE
**   ADDED FOR EACH, CONTAINING THE ATTRIBUTE NAME AND THE ADDRESS
**   OF THE PROCESSING "ROUTINE'.  THE COMMENT FOR THE ENTRY
**   SHOULD PROVIDE THE ATTRIBTE REQUESTED AND THE OUTPUT AREA
**   FORMAT AND LENGTH. IF THE VALUE RETURNED IS AN INDICATION,
**   THE COMMENT SHOULD ALSO CONTAIN THE POSSIBLE OUTPUT VALUES.
**
**   ATTRIBUTE NAME MAY CONTAIN LETTERS, DIGITS, #, @, - AND _
**
*
ATTRLIST DS    0F
         DC    CL8'DSNAME',A(GETDSN)   DSNAME (CL44)
         DC    CL8'MEMBER',A(GETMEM)   MEMBER NAME (CL8)
         DC    CL8'VIO',A(GETVIO)      VIO INDICATION (CL1)
*                                      OUTPUT VALUES: Y / N / ?
         DC    CL8'LRECL',A(GETLRECL)  LRECL   (H)
         DC    CL8'BLKSIZE',A(GETBLKSZ) BLKSIZE (H)
         DC    CL8'RECFM',A(GETRECFM)  RECFM      (CL4)
         DC    CL8'PSPACE',A(GETPSPC)  PRIMARY SPACE. (F)
         DC    CL8'SSPACE',A(GETSSPC)  SECONDARY SPACE (F)
         DC    CL8'TSPACE',A(GETTSPC)  SPACE TYPE  (CL1)
*                                      OUTPUT VALUES: C / T / B / ?
         DC    CL8'AVGBLK',A(GETAVGB)  AVERAGE BLKSIZE. (F)
ATTRL#   EQU   (*-ATTRLIST)/ATTRENTL
*IS10174 - END
*
*IS10174 TRTBLANK DC    256AL1(0)
*IS10174 ORG   TRTBLANK+C' '
*IS10174 DC    AL1(1)
*IS10174 ORG   ,
*
TRTCHAR  DC    256AL1(*-TRTCHAR)                               IS10174
         ORG   TRTCHAR                                         IS10174
         DC    AL1(1)                                          IS10174
         ORG   TRTCHAR+C'#'                                    IS10174
         DC    AL1(0)                                          IS10174
         ORG   TRTCHAR+C'@'                                    IS10174
         DC    AL1(0)                                          IS10174
         ORG   TRTCHAR+C'-'                                    IS10174
         DC    AL1(0)                                          IS10174
         ORG   TRTCHAR+C'_'                                    IS10174
         DC    AL1(0)                                          IS10174
         ORG   TRTCHAR+X'81'           LOWERCASE A-I           IS10174
         DC    9AL1(0)                                         IS10174
         ORG   TRTCHAR+X'91'           LOWERCASE J-R           IS10174
         DC    9AL1(0)                                         IS10174
         ORG   TRTCHAR+X'A2'           LOWERCASE S-Z           IS10174
         DC    8AL1(0)                                         IS10174
         ORG   TRTCHAR+C'A'                                    IS10174
         DC    9AL1(0)                                         IS10174
         ORG   TRTCHAR+C'J'                                    IS10174
         DC    9AL1(0)                                         IS10174
         ORG   TRTCHAR+C'S'                                    IS10174
         DC    8AL1(0)                                         IS10174
         ORG   TRTCHAR+C'0'                                    IS10174
         DC    10AL1(0)                                        IS10174
         ORG   ,                                               IS10174
*
TRUCASE  DC    256AL1(*-TRUCASE)                               IS10174
         ORG   TRUCASE+X'81'           LOWERCASE A-I           IS10174
         DC    C'ABCDEFGHI'                                    IS10174
         ORG   TRUCASE+X'91'           LOWERCASE J-R           IS10174
         DC    C'JKLMNOPQR'                                    IS10174
         ORG   TRUCASE+X'A2'           LOWERCASE S-Z           IS10174
         DC    C'STUVWXYZ'                                     IS10174
         ORG   ,                                               IS10174
*
CONSTE   EQU   *                                               IS10174
*******************************************************************
*
**   WORK AREA
*
*******************************************************************
WA       DSECT
SA       DS    18F
*
DDNAME   DS    CL8
PARMNAME DS    CL8                                             IS10174
*
RDJFCBL  DS    0F,XL(RDJFCBML)
DCBAREA  DS    0F,XL(DCBML)
EXLAREA  DS    0F,XL(EXLLENTH)
JFCBAREA DS    0F,XL176
JFCBALEN EQU   *-JFCBAREA                                      IS10174
DBGLEVEL DS    X
*
MAXRC    DS    F                       MAX RC.                 IS10174
SNAPHDR  DS    AL1(L'SNAPHDRM+8)                               IS10174
SNAPHDRT DS    CL(L'SNAPHDRM)                                  IS10174
SNAPHDDN DS    CL8                                             IS10174
SNAPHDRE EQU   *                                               IS10174
*
         DS    0D
RAREA    DS    XL512                   USED BY CTSADBG.
DBGAREA  DS    XL2048                                                   01950100
WALEN    EQU   *-WA
*
**   INTERNAL DSECTS
*
         CTSEQUR ,
*
*IS10174 - START
*
**   SUPPORTED ATTRIBUTES LIST ENTRY STRUCTURE
*
ATTRENT  DSECT                         REQUESTED KEYWORDS TABLE.
ATTRNAME DS    CL8                     ATTRIBUTE NAME.
ATTRRTN  DS    A                       -> PROCESS ROUTINE.
ATTRENTL EQU   *-ATTRENT               LENGTH OF ENTRY.
*
*IS10174 - END
*
PRM      DSECT
PRMDDN   DS    A    (M)         -> DDNAME.
*IS10174 PRMDSN   DS    A    (M)  (CL44) -> AREA FOR DSN.
*IS10174 PRMDSNL  DS    A    (O)         -> AREA FOR DSN LENGTH.
*IS10174 PRMMEM   DS    A    (O)  (CL8)  -> AREA FOR MEMBER NAME.
*IS10174 PRMMEML  DS    A    (O)         -> AREA FOR MEMBER NAME LEN.
PRMATTR# DS    A    (M)         -> NUMBER OF ATTRIBUTES.       IS10174
PRMATTRL DS    A    (M)         -> PARM ATTRIBUTES LIST (SEE   IS10174
*                                  BELOW).                     IS10174
PRMDBG   DS    A    (M)         -> DEBUG LEVEL.
*
*IS10174 - START
*
**   ATTRIBUTE ENTRY IN PARM ATTRIBUTES LIST
**
**   PRMATTRL POINTES TO AN ARRAY OF POINTER. EACH SET OF 4
**   POINTERS IS CONSIDERRED AN "ENTRY" FOR A SPECIFIC ATTRIBUTE.
**   EACH "ENTRY" CONTIANS THE FOLLOWING POINTERS:
**
**   - ATTRIBUTE NAME.
**   - OUTPUT  AREA
**   - OUTPUT LENGTH (RELEVANT FOR CHARACTER TYPE ONLY)
**   - RETURN CODE FOR THIS ATTRIBUTE.
*
PRMATTR  DSECT                         ATTRIBUTES LISTE "ENTRY" .
PRMANAME DS    A                       -> ATTRIBUTE NAME (C STRING)
PRMAOUTA DS    A                       -> OUTPUT AREA.
PRMARC   DS    A                       -> RC.
PRMALEN  EQU   *-PRMATTR
*
*IS10174 - END
*
**   SYSTEM DSECTS
*
         DCBD  DSORG=PS
*
         IHAEXLST ,
*
JFCB     DSECT
         IEFJFCBN LIST=YES
         END
