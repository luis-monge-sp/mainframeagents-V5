          START
*                                                                       00000101
****************************************************************        00000201
****                                                        ****        00000301
****     CONTROL-SA  RELEASE 1.3.0                          ****        00000401
****                                                        ****        00000501
****     LICENCED MATERIAL - NEW DIMENSION SOFTWARE LTD.    ****        00000601
****                                                        ****        00000701
****************************************************************        00000801
*                                                                       00000901
****************************************************************        00001001
*                                                              *        00001101
* TITLE            : TSO SERVICES MODULES                      *        00001201
*                                                              *        00001301
* FILE NAME        : CTSATSO                                   *        00001401
*                                                              *        00001501
* AUTHOR           : DORON COHEN                               *        00001601
*                                                              *        00001701
* CREATION DATE    : 21/07/94                                  *        00001801
*                                                              *        00001901
* DESCRIPTION      : THE PROGRAM PROVIDES VARIOUS TSO          *        00002001
*                    SERVICES UNDER THE TSO DYNAMIC ENVIRONMENT*        00002101
*                                                              *        00002101
* ASSUMPTIONS AND                                              *        00002201
*   CONSIDERATIONS : 1. TSO/E 2.3 IS REQUIRED                  *        00002301
*                    2. CTSAINI HAS INITIALIZED THE AS         *        00002401
*                                                              *        00002401
*                                                              *        00002401
* *** ATTENTION ***                                            *        00002401
* THIS PROGRAM WAS CHANGED BY IS10184 TO BE REENTRANT,         *        00002401
*                                                              *        00002401
*                                                              *        00002401
****************************************************************        00002501
*
****************************************************************
* MOD.ID   WHO      WHEN     DESCRIPTION                       *
* -------- -------- -------- --------------------------------- *
* IS10172  SELIGT   25/04/18 TSO/E ENVIRONMENT INITIALIZATION  *
*                            FAILURE (SEE DETAILS BELOW)       *
* IS10183  THOMAS   06/05/21 DON'T PRINT THE BUFFER AS IT MAY  *
*                            INCLUDE PASSWORD                  *
* IS10184  NURITY   31/08/21 IMPROVE SCRIPTS PERFORMANCE       *
* IS10188  AVNERL   06/10/24 MOVE OUR MODULES ABOVE 16M LINE   *
****************************************************************
*                                                                       00002601
PARM     DSECT
*
*        COMMON PARMATER LIST
*
PARMFUNC DS    A              CALL TYPE
PARMRC   DS    A
PARMRS   DS    A
PARMAB   DS    A
PARMDBG  DS    A
PARMSPEC EQU   *
*
*        PARAMETERS FOR 'CMD'
*
         ORG   PARMSPEC
PARMCMD  DS    A
         ORG   ,
*
*        PARAMETERS FOR 'CMDAUTH'
*
         ORG   PARMSPEC
PARMAUTH DS    A
PARMATHL DS    A
         ORG   ,
*
*        PARAMETERS FOR 'CHKCMD'
*
         ORG   PARMSPEC
PARMNAME DS    A
         ORG   ,
*
*        PARAMETERS FOR 'CHKPROG'
*
         ORG   PARMSPEC
PARMPROG DS    A
         ORG   ,
*
***************************************************************
*
***************************************************************
*IS10184 CTSATSO  CSECT
CTSATSO  AMODE 31
*IS10188 CTSATSO  RMODE 24       (FOR CTSADBG)
CTSATSO  RMODE ANY                                             IS10188
*
*        INITALIZE
*
*IS10184 BEGIN *,R12
*IS10184 CTSEQUR
*IS10188 CTSATSO CTSBGNR R12,R11,LV=WALEN,CLEAR=NO,EQUR=YES    IS10184
CTSATSO  CTSBGNR R12,R11,LV=WALEN,CLEAR=NO,EQUR=YES,   IS10184 IS10188 *
               GMTYPE=RC,LOC=24                                IS10188
         USING WA,R13                                          IS10184
         CTSLEVEL
         LR    R10,R1
         USING PARM,R10
*
         XC    WASTART(WACLNLEN),WASTART                       IS10184
*
         L     R1,PARMDBG              R1 -> DEBUG LEVEL (4 BYTES)
         XC    DBGLVL,DBGLVL           STORE DEBUG LEVEL
         ICM   R1,15,0(R1)             DEBUG LEVEL
         BNP   NODEBUG                 SKIP DEBUG LVL IF MINUS
         STC   R1,DBGLVL               STORE DEBUG LEVEL (1 BYTE)
NODEBUG  DS    0H
*
         L     R1,PARMRC
         XC    0(4,R1),0(R1)
         L     R1,PARMRS
         XC    0(4,R1),0(R1)
         L     R1,PARMAB
         XC    0(4,R1),0(R1)
*
*       CHECK TYPE
*
         L     R2,PARMFUNC
         CTSADBG LEVEL=(DBGLVL,1),'TYPE=_',(0(R2),8),                  >
               RAREA=RAREA,DBGAREA=DBGAREA                     IS10184  00037500
         CLC   0(8,R2),=CL8'CHKENV'
         BE    CHKENV
         CLC   0(8,R2),=CL8'INIT'
         BE    INIT
         CLC   0(8,R2),=CL8'CMD'
         BE    CMDISREG
         CLC   0(8,R2),=CL8'CMDAUTH'
         BE    CMDAUTH
         CLC   0(8,R2),=CL8'CHKCMD'
         BE    CHKCMD
         CLC   0(8,R2),=CL8'CHKPROG'
         BE    CHKPROG
         CTSADBG LEVEL=(DBGLVL,1),'ERROR, UNDEFINED TYPE',             >
               RAREA=RAREA,DBGAREA=DBGAREA                     IS10184  00037500
         LA    R15,16
         B     EXIT
*
***************************************************************
*                                                             *
*        CHKENV CALL                                          *
*                                                             *
***************************************************************
*                                                             *
* DESCRIPTION                                                 *
* -----------                                                 *
*                                                             *
*  CHECK IF TSO ENVIRONMENT SERVICE AVAILABLE                 *
*                                                             *
***************************************************************
*
CHKENV   DS    0H
*
         L     R2,PSATOLD-PSA
         L     R2,TCBJSTCB-TCB(,R2)
         L     R2,TCBJPQ-TCB(,R2)
*
         USING CDENTRY,R2
LOOPJPA  DS    0H
         CTSADBG LEVEL=(DBGLVL,1),                                     X
               'PROG _ AT CDE ~',(CDNAME,8,?R2),                       >
               RAREA=RAREA,DBGAREA=DBGAREA                     IS10184  00037500
         CLC   TSOEVNAM,CDNAME
         BE    FOUNDJPA
         ICM   R2,15,CDCHAIN
         BNZ   LOOPJPA
         DROP  R2
*
         CTSADBG LEVEL=(DBGLVL,1),                                     X
               'PROG _ NOT FOUND',(TSOEVNAM,8),                        >
               RAREA=RAREA,DBGAREA=DBGAREA                     IS10184  00037500
         LA    R15,128
         B     EXIT
*
FOUNDJPA DS    0H
         CTSADBG LEVEL=(DBGLVL,1),                                     X
               'PROG _ FOUND',(TSOEVNAM,8),                            >
               RAREA=RAREA,DBGAREA=DBGAREA                     IS10184  00037500
         XR    R15,R15
         B     EXIT
*
***************************************************************
*                                                             *
*        INITIALIZE CALL                                      *
*                                                             *
***************************************************************
*                                                             *
* DESCRIPTION                                                 *
* -----------                                                 *
*                                                             *
*  CALL THE TSO/E ENVIRONMENT SERVICE TO ESTABLISH A TSO/E    *
*  ENVIRONMENT IN THIS PROGRAM'S ADDRESS SPACE.               *
*                                                             *
*                                                             *
* PARAMETERS TO IKJTSOEV :                                    *
* ------------------------                                    *
*                                                             *
*   PARM1 IS RESERVED                                         *
*   PARM2 IS A FULLWORD THAT WILL CONTAIN THE                 *
*            RETURN CODE FROM IKJTSOEV.                       *
*   PARM3 IS A FULLWORD THAT WILL CONTAIN THE                 *
*            REASON CODE ON RETURN FROM IKJTSOEV.             *
*   PARM4 IS A FULLWORD THAT WILL CONTAIN THE                 *
*            ABEND CODE, IF AN ABEND OCCURS DURING            *
*            TSO/E ENVIRONMENT SERVICE PROCESSING.            *
*   PARM5 IS A FULLWORD THAT WILL CONTAIN THE                 *
*            ADDRESS OF THE CPPL.                             *
*                                                             *
***************************************************************
*
INIT     DS    0H
*                                                               WS2005
*  LOAD AND EXTRACT OPTION TO FORCE TSO TMP USAGE               WS2005
*                                                               WS2005
INITL    DS    0H                                               WS2005
         LOAD  EP=CTSPARM,ERRET=ERRLOAD                         WS2005
         LR    R4,R0                                            WS2005
         CTSADBG LEVEL=(DBGLVL,3,4,5),TYPE=SNAP,ID=1,           WS2005 X
               HDR='** CTS PARAMETERS **',                      WS2005 X
               ADDR=(0(R4),ISTLEN-1(R4)),                       WS2005 >
               RAREA=RAREA,DBGAREA=DBGAREA                     IS10184  00037500
         USING IST,R4                                           WS2005
*                                                               WS2005
         CTSADBG LEVEL=(DBGLVL,1,2,3,4,5),                      WS2005 X
               'FORCE TSO TMP USAGE=$',(ISTTSTMP,1),            WS2005 >
               RAREA=RAREA,DBGAREA=DBGAREA                     IS10184  00037500
         MVC   WFTSOTMP(1),ISTTSTMP    FORCE TSO RMP USAGE      WS2005
         DROP  R4                                               WS2005
*                                                               WS2005
         DELETE EP=CTSPARM                                      WS2005
*                                                               WS2005
*  IF CTSPARM DOES NOT SPECIFY A FORCE TSO TMP USAGE GOTO LOAD  WS2005
*                                                               WS2005
         CLI   WFTSOTMP,0              FORCE TSO RMP USAGE ?    WS2005
         BE    INITLLD                 LOAD                     WS2005
         LA    R15,128                 EMULATION MODULE NOT     WS2005
         B     EXIT                    FOUND. EXIT              WS2005
ERRLOAD  DS    0H                                               WS2005
INITLLD  DS    0H                                               WS2005
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * IS10172
* PERIODICALLY, WE SAW THE FOLLOWING MESSAGES WHEN THE AGENT    IS10172
* WAS BROUGHT UP:                                               IS10172
* +IKJ56231I FILE SYSTSIN NOT ALLOCATED, SYSTEM OR INSTALLATION IS10172
*            ERROR+                                             IS10172
* +IKJ56231I DYNAMIC ALLOCATION REASON CODE IS X'00000254'      IS10172
*                                                               IS10172
* AT THE SAME TIME, IF THE PLT_COMP_OS_CLI DIAGNOSTIC WAS SET,  IS10172
* WE SAW THE FOLLOWING PRTDBG MESSAGE:                          IS10172
* CTSATSO: 0/00000024/0000012C/00000254/0                       IS10172
* CTSAINI: TSO INIT R15=0 RC=36 RS=300 AB=596                   IS10172
*                                                               IS10172
* THIS MEANS THAT THE IKJTSOEV TSO/E ENVIRONMENT WAS NOT        IS10172
* ESTABLISHED/INITIALIZED BECAUSE ANOTHER TASK IS HOLDING THE   IS10172
* TIOT (SYSZTIOT) RESOURCE. (THIS IS PROBABLY ONE OF THE        IS10172
* ECAAMGR PROGRAM TASKS.)  OUR APPROACH IS TO RETRY THE         IS10172
* INITIALIZATION REQUEST. WE WILL WAIT 2 SECONDS AND RETRY THE  IS10172
* REQUEST UP TO 5 TIMES (FOR A TOTAL OF 10 SECONDS). WHEN WE    IS10172
* ARE SUCCESSFUL, WE WILL DISPLAY A WTO MESSAGE INFORMING THE   IS10172
* USER THAT THE IKJTSOEV ENVIRONMENT WAS INITIALIZED. (BY THE   IS10172
* WAY, IN IBM PTF OA3122, IBM USES THE SAME APPROACH. THEY      IS10172
* RETRY THE INITIALIZATION REQUEST UP TO 5 TIMES.)              IS10172
*                                                               IS10172
* PLEASE NOTE: WE ONLY DO THIS WHEN RC=36 RSN=300 OR 400 AND    IS10172
* THE DYNAMIC ALLOCATION ERROR IS HEX 254 (DECIMAL 596).        IS10172
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * IS10172
         LOAD  EPLOC=TSOEVNAM,ERRET=NOTSOEV
*IS10172 LR    R15,R0
         ST    R0,TSOEVPTR         SAVE IKJTSOEV EPA            IS10172
         LHI   R4,5                BCT REG - NUMBER OF RETRIES  IS10172
TSOEVLP  DS    0H
         L     R15,TSOEVPTR        GET IKJTSOEV EPA             IS10172
*IS10184 LA     R1,PARMEV
*IS10184 BALR   R14,R15
         CALL  (15),(PARM1,PARM2,PARM3,PARM4,PARM5),VL,        IS10184 >
               MF=(E,CALL6P)                                   IS10184
         CTSADBG LEVEL=(DBGLVL,1),                                     X
               '~/~/~/~/~',(PARM1,PARM2,PARM3,PARM4,PARM5),            >
               RAREA=RAREA,DBGAREA=DBGAREA                     IS10184  00037500
*
         OC    PARM2,PARM2
*IS10172 BZ    EXIT
         BZ    TSOEVMSG            GO ISSUE IKJTSOEV WTO MSG    IS10172
*                                                               IS10172
*        WE NOW CHECK FOR RC=36 RS=300/400 AB=596               IS10172
*                                                               IS10172
         CLC   PARM2,=F'36'        IS THIS RC 36?               IS10172
         BNE   TSOEVERR            NO, DO NOT RETRY             IS10172
         CLC   PARM4,=F'596'       IS THIS ABEND CODE 596?      IS10172
         BNE   TSOEVERR            NO, DO NOT RETRY             IS10172
         CLC   PARM3,=F'300'       IS THIS RSN 300 (SYSTSIN)?   IS10172
         BE    TSOEVBCT            YES, GO DO RETRY             IS10172
         CLC   PARM3,=F'400'       IS THIS RSN 400 (SYSTSPRT)?  IS10172
         BNE   TSOEVERR            NO, DO NOT RETRY             IS10172
TSOEVBCT DS    0H                                               IS10172
*IS10184STIMERM SET,ID=ID,DINTVL=DINTVL,WAIT=YES WAIT 2 SECONDSIS10172
         STIMERM SET,ID=(R3),DINTVL=DINTVL,WAIT=YES, WAIT 2 SE IS10184 >
               MF=(E,STIMERML)                                 IS10184
         BCT   R4,TSOEVLP          GO UP AND RETRY INIT REQUEST IS10172
TSOEVERR DS    0H                                               IS10172
         L     R1,PARMRC
         MVC   0(4,R1),PARM2
         L     R1,PARMRS
         MVC   0(4,R1),PARM3
         L     R1,PARMAB
         MVC   0(4,R1),PARM4
*
         DELETE EPLOC=TSOEVNAM
*
         B     EXIT
*
*        INITIALIZATION IMPOSSIBLE
*
NOTSOEV  DS    0H
         CTSADBG LEVEL=(DBGLVL,1),                                     X
               'LOADING _ HAS FAILED',(TSOEVNAM,8),                    >
               RAREA=RAREA,DBGAREA=DBGAREA                     IS10184  00037500
         LA    R15,128
         B     EXIT
*                                                               IS10172
*        IKJTSOEV INITIALIZATION SUCCESSFUL MESSAGE             IS10172
*                                                               IS10172
TSOEVMSG DS    0H                                               IS10172
         CTSMSG1 CODE=CTS240I,ROUT=(WTO),WTOCON=CTS             IS10172
         SLR   R15,R15             SET R15 TO ZERO              IS10172
         B     EXIT                RETURN TO CALLER             IS10172
*
***************************************************************
*                                                             *
*        CHKCMD CALL                                          *
*                                                             *
***************************************************************
*                                                             *
* DESCRIPTION :                                               *
* -------------                                               *
*                                                             *
* CHECK IF A CERTAIN COMMNAD IS AUTHORIZED USING TABLE LOOK-UP*
* SERVICE (TO SEE IF THE COMMAND IS FOUND IN THE AUTHORIZED   *
* COMMAND TABLE.                                              *
*                                                             *
*                                                             *
* FOUR VALID TABLE NAMES                                      *
* ----------------------                                      *
*                                                             *
*   AUTHCMD   - AUTHORIZED COMMAND TABLE (IKJEFTE2)           *
*   AUTHPGM   - AUTHORIZED PROGRAM TABLE (IKJEFTE8)           *
*   AUTHTSF   - AUTHORIZED PROGRAMS SUPPORTED THROUGH         *
*               THE TSO SERVICE FACILITY (IKJEFTAP)           *
*   NOTBKGND  - COMMANDS NOT SUPPORTED IN THE                 *
*               BACKGROUND (IKJEFTNS)                         *
*                                                             *
*                                                             *
* POSSIBLE RETURN CODES ARE:                                  *
* --------------------------                                  *
*                                                             *
*    0 - COMMAND FOUND                                        *
*    4 - COMMAND NOT FOUND                                    *
*    8 - TABLE NOT FOUND                                      *
*   20 - ERROR IN REQUEST                                     *
*        IN THIS CASE, THE ABEND CODE AND ABEND REASON CODE   *
*        FIELDS  ARE SET UP TO THE APPROPRIATE INFORMATION.   *
*                                                             *
***************************************************************
*
CHKCMD   DS    0H
*
*        SET UP THE PARAMETER LIST FOR IKJTBLS USING IKJTLS
*
         LA    R2,AUTHCMD              SEARCH THE AUTHORIZED COMMAND
*IS10184 ST     R2,TLSPTAB              TABLE (IKJEFTE2)
         ST    R2,WATLS+(TLSPTAB-TLS)   TABLE (IKJEFTE2)       IS10184
         CTSADBG LEVEL=(DBGLVL,1),                                     X
               'AUTHCMD=_',(0(R2),8),                                  >
               RAREA=RAREA,DBGAREA=DBGAREA                     IS10184  00037500
*
         L     R2,PARMNAME             SEARCH FOR A PROGRAM NAME
*IS10184 ST     R2,TLSPCMD
         ST    R2,WATLS+(TLSPCMD-TLS)                         IS10184
         CTSADBG LEVEL=(DBGLVL,1),                                     X
               'COMMAND=_',(0(R2),8),                                  >
               RAREA=RAREA,DBGAREA=DBGAREA                     IS10184  00037500
*
***  IS10184 - START
*        LA    R2,TLSABND
*        ST    R2,TLSPABND             PASS FIELD FOR ABEND CODE
*        LA    R2,TLSREAS
*        ST    R2,TLSPREAS             PASS FIELD FOR ABEND REASON CODE
         LA    R2,WATLS+(TLSABND-TLS)
         ST    R2,WATLS+(TLSPABND-TLS) PASS FIELD FOR ABEND CODE
         LA    R2,WATLS+(TLSREAS-TLS)
         ST    R2,WATLS+(TLSPREAS-TLS) PASS FIELD FOR ABEND REASON CODE
***  IS10184 - END
*
*        CALL IKJTBLS
*
*IS10184 LA     R1,TLSPARM              R1 POINTS TO PARAMETER LIS
         LA    R1,WATLS+(TLSPARM-TLS) R1 POINTS TO PARM LIST  IS10184
         LINK  EP=IKJTBLS              INVOKE TABLE LOOK-UP SERVICE
         L     R1,PARMRC
         ST    R15,0(R1)
         CTSADBG LEVEL=(DBGLVL,1),                                     X
               'TBLS RC =# ',?R15,                                     >
               RAREA=RAREA,DBGAREA=DBGAREA                     IS10184  00037500
         B     EXIT
*
***************************************************************
*                                                             *
*        CHKPROG CALL                                         *
*                                                             *
***************************************************************
*                                                             *
* DESCRIPTION :                                               *
* -------------                                               *
*                                                             *
* CHECK IF A CERTAIN PROGRAM IS AUTHORIZED USING TABLE LOOK-UP*
* SERVICE (TO SEE IF THE PROGRAM IS FOUND IN THE AUTHORIZED   *
* PROGRAMS TABLE.                                             *
*                                                             *
*                                                             *
* POSSIBLE RETURN CODES ARE:                                  *
* --------------------------                                  *
*                                                             *
*    0 - PROGRAM FOUND                                        *
*    4 - PROGRAM NOT FOUND                                    *
*    8 - TABLE NOT FOUND                                      *
*   20 - ERROR IN REQUEST                                     *
*        IN THIS CASE, THE ABEND CODE AND ABEND REASON CODE   *
*        FIELDS  ARE SET UP TO THE APPROPRIATE INFORMATION.   *
*                                                             *
***************************************************************
*
CHKPROG  DS    0H
*
*        SET UP THE PARAMETER LIST FOR IKJTBLS USING IKJTLS
*
         LA    R2,AUTHPGM              SEARCH THE AUTHORIZED COMMAND
*IS10184 ST     R2,TLSPTAB              TABLE (IKJEFTE2)
         ST    R2,WATLS+(TLSPTAB-TLS) TABLE (IKJEFTE2)        IS10184
         CTSADBG LEVEL=(DBGLVL,1),                                     X
               'AUTHPRG=_',(0(R2),8),                                  >
               RAREA=RAREA,DBGAREA=DBGAREA                     IS10184  00037500
*
         L     R2,PARMPROG             SEARCH FOR A PROGRAM NAME
*IS10184 ST     R2,TLSPCMD
         ST    R2,WATLS+(TLSPCMD-TLS)                         IS10184
         CTSADBG LEVEL=(DBGLVL,1),                                     X
               'PROG=_',(0(R2),8),                                     >
               RAREA=RAREA,DBGAREA=DBGAREA                     IS10184  00037500
*
*IS10184 LA    R2,TLSABND
*IS10184 ST    R2,TLSPABND             PASS FIELD FOR ABEND CODE
*IS10184 LA    R2,TLSREAS
*IS10184 ST    R2,TLSPREAS             PASS FIELD FOR ABEND REASON CODE
         LA    R2,WATLS+(TLSABND-TLS)                          IS10184
         ST    R2,WATLS+(TLSPABND-TLS) FIELD FOR ABEND CODE    IS10184
         LA    R2,WATLS+(TLSREAS-TLS)                          IS10184
         ST    R2,WATLS+(TLSPREAS-TLS) FIELD FOR ABEND REASON  IS10184
*
*        CALL IKJTBLS
*
*IS10184 LA     R1,TLSPARM              R1 POINTS TO PARAMETER LIS
         LA    R1,WATLS+(TLSPARM-TLS) R1 POINTS TO PARM LIST  IS10184
         LINK  EP=IKJTBLS              INVOKE TABLE LOOK-UP SERVICE
         L     R1,PARMRC
         ST    R15,0(R1)
         CTSADBG LEVEL=(DBGLVL,1),                                     X
               'TBLS RC =# ',?R15,                                     >
               RAREA=RAREA,DBGAREA=DBGAREA                     IS10184  00037500
         B     EXIT
*
***************************************************************
*                                                             *
*        CMD CALL                                             *
*                                                             *
***************************************************************
*                                                             *
* DESCRITION:                                                 *
* -----------                                                 *
*                                                             *
* ACTIVATE A NON AUTHORIZED COMMND/CLIST/REXX                 *
*                                                             *
***************************************************************
CMDISREG DS    0H
         L     R3,PARMCMD              R3 -> COMMAND BUFFER
         LTR   R3,R3                   CMD PASSED ?
         BZ    ERRNCMD                 NO, ERROR
         CTSADBG LEVEL=(DBGLVL,1),                                     X
               'CMD BUFFER AT ~ LEN=# CMD=_',                          X
               (?R3,0(R3),4(R3),8),                                    >
               RAREA=RAREA,DBGAREA=DBGAREA                     IS10184  00037500
         L     R4,0(R3)
         LTR   R4,R4                   NULL CMD ?
         BNP   ERRNCMD                 YES, CAN DO NOTHING
         BCTR  R4,0                    ADJUST FOR THE BLANK TRIMMING
LOOPBLNK DS    0H
         LA    R1,4(R4,R3)
         CLI   0(R1),C' '
         BNE   ENDBLNK
         BCT   R4,LOOPBLNK
ENDBLNK  DS    0H
         LA    R4,1(R4)                RESTORE TO REAL LENGTH
         ST    R4,0(R3)
*
*        PREPARE TO CALL IKJEFTSR TO INVOKE THE COMMAND
*
*IS10183 CTSADBG LEVEL=(DBGLVL,1),                                     X
               'CMD BUFFER=#,_',(0(R3),4(R3),0(R3))
         MVC   CMDLEN,0(R3)
*IS10184 LA    R1,4(R3)
*IS10184 ST    R1,PARMTSRC
         LA    R5,4(R3)                                        IS10184
*
*        TURN OFF AUTHORIZATION
*
         XC    AUTHFLAG,AUTHFLAG
         TESTAUTH FCTN=1
         LTR   R15,R15
         BNZ   AUTHOFF
         MVC   AUTHFLAG,=F'1'
         CTSAATH AUTH=OFF
AUTHOFF  DS    0H
*
*        CALL IKJEFTSR
*
         MVC   FLAGS,FLAGSINI                                  IS10184
         MVC   RESFLAGS,=XL2'0001' <===  SET UNAUTHORIZED ENVIRONMENT
*IS10184 LA     R1,PARMTSR
*IS10184 LINK   EP=IKJEFTSR
         L     R15,FLCCVT-PSA          R15 -> CVT              IS10184
         L     R15,CVTTVT-CVTMAP(,R15) R15 -> TSVT             IS10184
         L     R15,TSVTASF-TSVT(,R15)  R15 -> IKJEFTSR         IS10184
         CALL  (15),(FLAGS,(R5),CMDLEN,RETCODE,                IS10184 >
               RSNCODE,ABNDCODE),VL,                           IS10184 >
               LINKINST=BASR,MF=(E,CALL6P)                     IS10184
         CTSADBG LEVEL=(DBGLVL,1),                                     X
               'TSR R15=#, RC=#, REASON=#, ABEND=#',                   X
               (?R15,RETCODE,RSNCODE,ABNDCODE),                        >
               RAREA=RAREA,DBGAREA=DBGAREA                     IS10184  00037500
         LR    R5,R15
*
*        RESTORE AUTHORIZATION
*
         OC    AUTHFLAG,AUTHFLAG
         BZ    AUTHOK
         XC    AUTHFLAG,AUTHFLAG
         CTSAATH AUTH=ON
AUTHOK   DS    0H
*
*        SET RC
*
         L     R1,PARMRC
         MVC   0(4,R1),RETCODE
         L     R1,PARMRS
         MVC   0(4,R1),RSNCODE
         L     R1,PARMAB
         MVC   0(4,R1),ABNDCODE
         LR    R15,R5
         B     EXIT
*
***************************************************************
*                                                             *
*        CMDAUTH CALL                                         *
*                                                             *
***************************************************************
*                                                             *
* DESCRITION:                                                 *
* -----------                                                 *
*                                                             *
* ACTIVATE AN AUTHORIZED COMMND/CLIST/REXX                    *
*                                                             *
***************************************************************
CMDAUTH  DS    0H
         L     R3,PARMAUTH             R3 -> COMMAND BUFFER
         LTR   R3,R3                   CMD PASSED ?
         BZ    ERRNCMD                 NO, ERROR
         L     R4,PARMATHL
         LTR   R4,R4                   CMD LENGTH PASSED ?
         BZ    ERRNCMD                 NO, ERROR
         CTSADBG LEVEL=(DBGLVL,1),                                     X
               'CMDAUTH BUFFER AT ~ LEN=# CMD=_',                      X
               (?R3,0(R4),0(R3),8),                                    >
               RAREA=RAREA,DBGAREA=DBGAREA                     IS10184  00037500
         L     R4,0(R4)
         LTR   R4,R4
         BNP   ERRNCMD
         BCTR  R4,0                    ADJUST FOR THE BLANK TRIMMING
ALUPBLNK DS    0H
         LA    R1,0(R4,R3)
         CLI   0(R1),C' '
         BNE   AEDBLNK
         BCT   R4,ALUPBLNK
AEDBLNK  DS    0H
         LA    R4,1(R4)                RESTORE TO REAL LENGTH
*
*           CALL IKJEFTSR TO INVOKE THE AUTHORIZED TSO CP
*
         CTSADBG LEVEL=(DBGLVL,1),                                     X
               'CMDAUTH BUFFER=#,_',(?R4,0(R3),?R4),                   >
               RAREA=RAREA,DBGAREA=DBGAREA                     IS10184  00037500
         ST    R4,CMDLEN
*IS10184 ST    R3,PARMTSRC
*
         MVC   FLAGS,FLAGSINI                                  IS10184
         XC    RESFLAGS,RESFLAGS  <=== AUTHORIZED ENVIRONMENT
*IS10184 LA     R1,PARMTSR
*IS10184 LINK   EP=IKJEFTSR
         L     R15,FLCCVT-PSA         R15 -> CVT               IS10184
         L     R15,CVTTVT-CVTMAP(,R15) R15 -> TSVT             IS10184
         L     R15,TSVTASF-TSVT(,R15)  R15 -> IKJEFTSR         IS10184
         CALL  (15),(FLAGS,(R3),CMDLEN,RETCODE,                IS10184 >
               RSNCODE,ABNDCODE),VL,                           IS10184 >
               LINKINST=BASR,MF=(E,CALL6P)                     IS10184
         CTSADBG LEVEL=(DBGLVL,1),                                     X
               'TSR R15=#, RC=#, REASON=#, ABEND=#',                   X
               (?R15,RETCODE,RSNCODE,ABNDCODE),                        >
               RAREA=RAREA,DBGAREA=DBGAREA                     IS10184  00037500
         LR    R5,R15
*
*        SET RC
*
         L     R1,PARMRC
         MVC   0(4,R1),RETCODE
         L     R1,PARMRS
         MVC   0(4,R1),RSNCODE
         L     R1,PARMAB
         MVC   0(4,R1),ABNDCODE
         LR    R15,R5
         B     EXIT
*
***************************************************************
*        ERROR MESSAGES
***************************************************************
*
ERRNCMD  DS    0H
         LA    R15,4
         B     EXIT
*
***************************************************************
*                                                             *
* EXIT - RETURN TO CALLING PROGRAM                            *
*                                                             *
***************************************************************
*
EXIT     DS    0H
*IS10184 BRTRN (15)
         CTSBRTR (R15),LV=WALEN                                IS10184
*
***************************************************************
*        CONSTANTS                                            *
***************************************************************
*
         LTORG
         DROP  ,                                               IS10184
TBLANK   DC    256X'00'
         ORG   TBLANK+C' '
         DC    C' '
         ORG   ,
TNBLANK  DC    256AL1(*-TNBLANK)
         ORG   TNBLANK+C' '
         DC    X'00'
         ORG   ,
*
LWR2UPR  DC    256AL1(*-LWR2UPR)
         ORG   LWR2UPR+C'A'-X'40'
         DC    C'ABCDEFGHI'
         ORG   LWR2UPR+C'J'-X'40'
         DC    C'JKLMNOPQR'
         ORG   LWR2UPR+C'S'-X'40'
         DC    C'STUVWXYZ'
         ORG   ,
SUPNPRT  DC    256AL1(*-SUPNPRT)
         ORG   SUPNPRT
         DC    64C' '
         ORG   ,
*
DINTVL   DC    CL8'00000200'           WAIT FOR 2 SECONDS      IS10184
TSOEVNAM DC    CL8'IKJTSOEV'                                   IS10184
*
FLAGSINI DS    0F              FULLWORD OF FLAGS               IS10184
         DC    XL2'0001'       ESTABLISH UNAUTHORIZED ENV      IS10184
         DC    X'01'           PRODUCE DUMP IF FUNC ABENDS     IS10184
         DC    X'01'           INVOKE A TSO/E CMD,REXX OR CLISTIS10184
FLGSINIL EQU   *-FLAGSINI                                      IS10184
*
*IS10184 LTORG
*IS10184 DROP  ,
*
*************************************************************  IS10184
*                                                           *  IS10184
*        MAPPING OF IKJTBLS PARAMETERS                      *  IS10184
*                                                           *  IS10184
*  THIS DEFINES THE TLS FIELDS INTHE PROGRAM CONSTANS AREA  *  IS10184
*                                                           *  IS10184
*************************************************************  IS10184
         IKJTLS                                                IS10184
*
***************************************************************
*        WORKAREA                                             *
***************************************************************
*
WA       DSECT                                                 IS10184
         DS    18F                     SAVE AREA               IS10184
WASTART  EQU   *                                               IS10184
DBGLVL   DC    X'00'
AUTHFLAG DC    F'0'
WFTSOTMP DS    X                       FORCE TSO TMP USAGE   WS2005
*
*        COMMNAD PARSING
*
C1START  DS    A
C1END    DS    A
C1LEN    DS    A
W1START  DS    A
W1END    DS    A
W1LEN    DS    A
*
******+********************************************************
*                                                             *
* PARAMETERS USED TO INVOKE THE                               *
* TSO SERVICE FACILITY                                        *
*                                                             *
***************************************************************
*
*IS10184 PARMTSR  DS    0F
*IS10184 DC    A(FLAGS)
*IS10184 PARMTSRC DC    A(0)
*IS10184 DC    A(CMDLEN)
*IS10184 DC    A(RETCODE)
*IS10184 DC    A(RSNCODE)
*IS10184 DC    A(ABNDCODE+X'80000000')
*
CALL6P   CALL  ,(,,,,,),VL,MF=L                                IS10184
*
FLAGS    DS    0F                      FULLWORD OF FLAGS
*IS10184 RESFLAGS DC    XL2'0001'    ESTABLISH UNAUTHORIZED ENV
*IS10184 ABFLAGS  DC    X'01'        PRODUCE DUMP IF FUNC ABENDS
*IS10184 FNCFLAGS DC    X'01'        INVOKE A TSO/E CMD,REXX OR CLIST
RESFLAGS DS    XL2            ESTABLISH UNAUTHORIZED ENV       IS10184
ABFLAGS  DS    X              PRODUCE DUMP IF FUNC ABENDS      IS10184
FNCFLAGS DS    X              INVOKE A TSO/E CMD,REXX OR CLIST IS10184
***   END OF FLAGS FIELD.
*
RETCODE  DS    F                       FUNCTION RETURN CODE
RSNCODE  DS    F                       FUNCTION REASON CODE
ABNDCODE DS    F                       FUNCTION ABEND CODE
*IS10184  ECB      DC    F'0'
CMDLEN   DC    F'0'
*
***************************************************************
*
* PARAMETERS USED TO INVOKE THE TSO/E ENVIRONMENT SERVICE
*
***************************************************************
*IS10184 TSOEVNAM DC    CL8'IKJTSOEV'
PARMEV   DS    0F
         DC    A(PARM1)
         DC    A(PARM2)
         DC    A(PARM3)
         DC    A(PARM4)
         DC    A(PARM5+X'80000000')
*
PARM1    DS    F                       RESERVED FIELD
PARM2    DS    F                       RETURN CODE FIELD
PARM3    DS    F                       REASON CODE FIELD
PARM4    DS    F                       FUNCTION ABEND CODE
PARM5    DS    F                       CPPL ADDRESS
*
TSOEVPTR DS    F                       IKJTSOEV EPA             IS10172
ID       DS    F                       STIMERM ID FIELD         IS10172
STIMERML STIMERM SET,MF=L                                      IS10184
*IS10184 DINTVL   DC    CL8'00000200'  WAIT FOR 2 SECONDS       IS10172
*IS10184 RAREA    DS    64F            WORK AREA FOR CTSMSG1    IS10172
*
***************************************************************
*                                                             *
*        AREA FOR IKJTBLS PARAMETERS                          *
*                                                             *
***************************************************************
*IS10184 - REPLACED WITH A DEFINITION FOR IKJBLS PARAMETER
*IS10184   AREA.  THE MAPPING IS DONE USING IKJTLS.
*
*IS10184 TLS       DS    0D  BEGIN TLS ON DOUBLE WORD BDY
*IS10184 TLSTAB    DS    CL8 TABLE TO SEARCH
*IS10184 TLSCMD    DS    CL8 COMMAND OR PROGRAM TO SEARCH FOR
*IS10184 TLSABND   DS    F   ABEND CODE IF SERVICE FAILS
*IS10184 TLSREAS   DS    F   ABEND REASON CODE IF SERVICE FAILS
*IS10184 TLSEND    DS    0D  ASSURE TLS ENDS ON DOUBLE WORD BOUNDARY
*IS10184 *
*IS10184 TLSPARM   DS    0D  BEGIN PARAMETERS ON DOUBLE WORD BOUNDARY
*IS10184 TLSPTAB   DS    A   ADDRESS OF TABLE TO SEARCH
*IS10184 TLSPCMD   DS    A   ADDRESS OF COMMAND OR PROG TO SEARCH FOR
*IS10184 TLSPABND  DS    A   ADDRESS OF ABEND CODE
*IS10184 TLSPREAS  DS    A   ADDRESS OF ABEND REASON CODE
*IS10184 TLSPEND   DS    0D  ASSURE TLSPARM ENDS ON DOUBLEWORD BOUNDARY
*IS10184 *
*IS10184 AUTHCMD   DC  CL8'AUTHCMD '
*IS10184 AUTHPGM   DC  CL8'AUTHPGM '
*IS10184 AUTHTSF   DC  CL8'AUTHTSF '
*IS10184 NOTBKGND  DC  CL8'NOTBKGND'
WATLS    DS    0D,XL(TLSPEND-TLS)                             IS10184
*
WACLNLEN EQU   *-WASTART              LENGTH TO RESET         IS10184
*
***************************************************************
*                                                             *
*        CTSADBG AREAS                                        *
*                                                             *
***************************************************************
         DS    0D
RAREA    DS    XL512                   USED BY CTSADBG         IS10184
DBGAREA  DS    XL2048                  USED BY CTSADBG         IS10184
WALEN    EQU   *-WA                                            IS10184
*
*
*
***************************************************************
*                                                             *
*        SYSTEM CONTROL BLOCKS MAPPING                        *
*                                                             *
***************************************************************
         PRINT NOGEN
         IHAPSA
         CVT   DSECT=YES
         IKJTCB
         IKJTSVT
         IHACDE
***************************************************************  WS2005
*                                                             *  WS2005
*        IST MAPPING                                          *  WS2005
*                                                             *  WS2005
***************************************************************  WS2005
IST      DSECT                                                   WS2005
         COPY  CTSAIST                  CTS PARAMETERS BLOCK     WS2005
         END
