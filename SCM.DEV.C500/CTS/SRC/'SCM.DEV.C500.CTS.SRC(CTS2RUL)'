****************************************************************
****                                                        ****
****     CONTROL-SA  RELEASE 3.0.0                          ****
****                                                        ****
****     LICENCED MATERIAL - NEW DIMENSION SOFTWARE LTD.    ****
****                                                        ****
****************************************************************
*
****************************************************************
*                                                              *
* TITLE            : ACF2 ACCESS RULES HANDLING (ACRULE)       *
*                                                              *
* FILE NAME        : CTS2RUL                                   *
*                                                              *
* AUTHOR           : GUY SHAVITT                               *
*                                                              *
* CREATION DATE    : 12/01/98                                  *
*                                                              *
* DESCRIPTION      : HANDLE ACCESS RULES OPERATIONS            *
*                                                              *
* PARAMETERS       :                                           *
*                                                              *
* INPUT            : OP   - RET/INS/UPD/DEL RULE OBJECT        *
*                    KEY  - KEY OR KEY MASK                    *
*                    CKEY - CURRENT KEY (GET NEXT) OR NULL     *
*                    KEYT - KEY TYPE (SPECIFIC OR PREFIX)      *
*                    BUFL - SOURCE BUFFER LENGTH               *
*                                                              *
* INPUT / OUTPUT   : BUF  - 32K RULE OBJECT BUFFER             *
*                                                              *
* OUTPUT           : CKEY - RETURNED KEY (CURRENT)             *
*                    BUF2 - RULE SOURCE BUFFER                 *
*                    RC   - RETURN CODE                        *
*                    MSG  - ERR MSG                            *
* DEPENDENCIES     : ACCVT  - ACF2 CVT CB                      *
*                    ACRULE - RULE API CB                      *
*                                                              *
* RETURN CODES     : 0 - OK                                    *
*                    1 - INVALID OPERATION CODE                *
*                    2 - OUTPUT SOURCE BUFFER TOO SMALL        *
*                    3 - RULE OBJECT DECOMPILE FAILED          *
*                    4 - ACCESS DENIED / EOF                   *
*                    5 - ACFRDS FAILED                         *
*                    8 - CONTROL BLOCK ERROR                   *
*                   12 - ACF2 NOT ACTIVE                       *
*                                                              *
* CHANGE ACTIVITY :                                            *
* 01/11/16 SAS2IBMT ST SAS/C TO IBM C CONVERSION PROJECT:      *
*                     THIS ROUTINE SHOULD RETURN RC (R15) 0    *
*                                                              *
****************************************************************
****************************************************************
*        PARAMETERS BLOCK                                      *
****************************************************************
*
PARM       DSECT
PARMOP     DS    A                       OPERATION
PARMKEY    DS    A                       KEY PREFIX
PARMCKEY   DS    A                       RETURNED KEY
PARMKEYT   DS    A                       KEY TYPE (SPECIFIC OR PREFIX)
PARMBUF    DS    A                       RULE OBJECT BUFFER ADDRESS
PARMBUF2   DS    A                       RULE SOURCE BUFFER ADDRESS
PARMBUFL   DS    A                       RULE SOURCE BUFFER SIZE
PARMRC     DS    A                       RC
PARMMSG    DS    A                       MSG
*
NULL       EQU   C' '
CTS2RUL    CSECT
CTS2RUL    AMODE 31
CTS2RUL    RMODE ANY
           BEGIN
           CTSEQUR
           LR    R11,R1
           USING PARM,R11
****************************************************************
*  RESET ACRULE PARM CONTROL BLOCK                             *
****************************************************************
           XC    ACRULE(ACRLEN),ACRULE
P1         DS    0H
****************************************************************
*  BUILD ACRULE PARM CONTROL BLOCK                             *
****************************************************************
           MVI   ACRFCN,X'03'        ACALT REQUEST
           MVC   ACRRULAD,PARMBUF    SET RULE BUFFER
           MVC   ACRRULEN,=F'32768'  SET RULE BUFFER LENGTH
           MVC   ACRMSG,=A(SVCMSG)   SET SVC MESSAGE BUFFER
           XC    SVCMSG,SVCMSG       CLEAR THE MESSAGE BUFFER  @WS2333
           L     R4,PARMKEY
           MVC   ACRPFX,0(R4)        SET KEY TO USE
****************************************************************
*  CHECK WHICH OPERATION IS REQUESTED                          *
****************************************************************
           L     R4,PARMOP
           CLC   0(3,R4),=CL3'RET'   RETRIEVE OPERATION ?
           BNE   NOT_RET
****************************************************************
*  HANDLE A RETRIEVE OPERATION                                 *
****************************************************************
           MVI   ACRSFCN,ACRSRET     RETURN RULE OBJECT RECORD
           L     R4,PARMKEYT                          @WS2333
           CLI   0(R4),C'P'          READ PREFIX ?    @WS2333
           BNE   SKIP_KEYMASK_SET
           OI    ACRFLGS,ACRFGNXT    READ UNDER KEY MASK
SKIP_KEYMASK_SET DS 0H
           L     R4,PARMCKEY
           CLI   0(R4),NULL
           BE    ISSUE_SVC
           MVC   ACRLAST,0(R4)       SET KEY TO USE
           B     ISSUE_SVC
NOT_RET          DS 0H
           CLC   0(3,R4),=CL3'INS'   INSERT OPERATION ?
           BNE   NOT_INS
           OI    ACRFLGS,ACRFVLD     REJECT IF RULE ALREADY EXISTS
           B     INS_UPD
NOT_INS          DS 0H
           CLC   0(3,R4),=CL3'UPD'   UPDATE OPERATION ?
           BNE   NOT_UPD
****************************************************************
*  HANDLE A INSERT/UPDATE OPERATION                            *
****************************************************************
INS_UPD          DS 0H
           MVI   ACRSFCN,ACRSINS     INSERT OR REPLACE RULE OBJECT
           B     ISSUE_SVC
NOT_UPD          DS 0H
           CLC   0(3,R4),=CL3'DEL'   DELETE OPERATION ?
           BNE   INV_OP
****************************************************************
*  HANDLE A DELETE OPERATION                                   *
****************************************************************
           MVI   ACRSFCN,ACRSDEL     DELETE RULE OBJECT RECORD
           B     ISSUE_SVC
INV_OP     LA    R15,1               INVALID OPERATION CODE
           B     EXIT_RC
ISSUE_SVC        DS 0H
           ACFINCVT R4,NONE=EXIT_NOACF2
           USING  ACCVT,R4
****************************************************************
*  ISSUE ACFSVC ACRULE CALL                                    *
****************************************************************
           ACFSVC ACRULE,TYPE=A,NONE=EXIT_NOACF2,CVT=HAVE
           B     *+4(R15)
           B     EXIT_OK
           B     EXIT_ACCESS_DENIED
           B     EXIT_CONTROL_BLOCK_ERROR
           B     EXIT_NOACF2
           B     EXIT_RC
****************************************************************
*  ACCESS DENIED: ACF2 EOF                                     *
****************************************************************
EXIT_ACCESS_DENIED DS 0H
           LA    R15,4
           B     EXIT_RC
****************************************************************
*  CB ERROR: INTERNAL ERROR                                    *
****************************************************************
EXIT_CONTROL_BLOCK_ERROR DS 0H
           LA    R15,8
           B     EXIT_RC
****************************************************************
*  ACF2 NOT ACTIVE                                             *
****************************************************************
EXIT_NOACF2        DS   0H
           LA    R15,12
           B     EXIT_RC
****************************************************************
*  API CAL ENDED OK                                            *
****************************************************************
EXIT_OK    DS   0H
           XR   R15,R15
           L    R4,PARMOP
****************************************************************
*  IF NOT RETRIEVE OPERATION SKIP REST OF PROCESSING           *
****************************************************************
           CLC  0(3,R4),=CL3'RET'
           BNE  EXIT_RC
           L    R4,PARMCKEY
           MVC  0(L'ACRLAST,R4),ACRLAST
****************************************************************
*  DECOMPILE THE RULE OBJECT                                   *
****************************************************************
           MVC  OBJBUF,PARMBUF
           MVC  SRCBUF,PARMBUF2
           L    R4,PARMBUFL
           MVC  SRCLEN(L'SRCLEN),0(R4)
           LA   R1,DCMPLPRM
           LINK EP=ACFDCMPL
           LTR  R15,R15
           BZ   DCMPL_OK
           C    R15,=F'04'
           BE   TOOSMALL
           LA   R15,3
           B    EXIT_RC
TOOSMALL   DS   0H
           LA   R15,2
           B    EXIT_RC
****************************************************************
*  WRITE THE RULE SOURCE TO THE ACF2OUT DATASET                *
****************************************************************
DCMPL_OK   DS   0H
           MVC  RDSSRC,PARMBUF2
           LA   R1,RDSPARM
           LINK EP=ACFRDS
           LTR  R15,R15
           BZ   EXIT_RC
           LA   R15,5
           B    EXIT_RC
****************************************************************
*  RETURN WITH RC                                              *
****************************************************************
EXIT_RC    DS   0H
           L    R14,PARMRC
           ST   R15,0(R14)
           L    R14,PARMMSG
           MVC  0(124,R14),SVCMSG+4
           BRTRN 0                                             SAS2IBMT
SVCMSG     DS    CL128
DCMPLPRM   DS    0F
OBJBUF     DS    A
SRCBUF     DS    A
SRCLEN     DS    A
SEQINC     DC    A(SEQ)
SEQ        DC    F'01'
RDSPARM    DS    0F
RDSSRC     DS    A
RDSDD      DC    A(DDNAME)
DDNAME     DC    CL8'ACF2OUT '
           ACRULE DSECT=NO
           ACCVT
           END
