         TITLE 'CTSFRXF - EXTRACT A FIELD FROM RACF USER/GROUP PROFILE' 00040000
*                                                                       00050000
****************************************************************        00060000
****                                                        ****        00070000
****     CONTROL-SA  RELEASE 3.1.02                         ****        00080000
****                                                        ****        00090000
****     LICENCED MATERIAL - NEW DIMENSION SOFTWARE LTD.    ****        00100000
****                                                        ****        00110000
****************************************************************        00120000
*                                                                       00130000
****************************************************************        00140000
*                                                              *        00150000
* TITLE            : EXTRACT A FIELD FROM RACF USER/GROUP      *        00160000
*                    PROFILE                                   *        00170000
*                                                              *        00170000
* FILE NAME        : CTSFRXF                                   *        00180000
*                                                              *        00190000
* AUTHOR           : DORON COHEN                               *        00200000
*                                                              *        00210000
* CREATION DATE    : 28/12/94                                  *        00220000
*                                                              *        00230000
* DESCRIPTION      :                                           *        00240000
*                                                              *        00250000
* ASSUMPTIONS AND                                              *        00260000
*   CONSIDERATIONS : 1. CALLER IS APF AUTHORIZED               *        00270000
*                                                              *        00304000
****************************************************************        00310000
*                                                                       00320000
****************************************************************        00330000
* MOD.ID   WHO      WHEN     DESCRIPTION                       *        00340000
* -------- -------- -------- --------------------------------- *        00350000
* R0004    DORON    28/5/94  USE EXTWOFF FOR EXTWOPT OFFSET    *        00370000
*                            INSTEAD OF THE DSECT OFFSET       *        00370000
* BS10011  AVN&SHM  16JUL08  ONLI ABENDS WITH S878.            *        00370000
****************************************************************        00380000
         GBLC   &AMODUSE                                                00380100
&AMODUSE SETC   'CVT'                                                   00381000
*                                                                       00340000
*---------------------------------------------------------------*       00341000
*        PARAMETER BLOCK MAPPING                                        00342000
*---------------------------------------------------------------*       00343000
*                                                                       00480000
PARM     DSECT                                                          00344000
PARMCLS  DS    A               CLASS (USER/GROUP)                       00345000
PARMENT  DS    A               ENTITY                                   00347000
PARMSEG  DS    A               SEGEMENT                                 00345000
PARMFLD  DS    A               FIELD NAME                               00345000
PARMRETA DS    A               RETURN AREA ADDRESS                      00345000
PARMRETL DS    A               RETURN AREA LENGTH                       00345000
PARMDBG  DS    A               DEBUG LEVEL                              00345000
PARMR15  DS    A               SAF RC                                   00345000
PARMRC   DS    A               RACF RC                                  00345000
PARMRS   DS    A               RACF REASON                              00345000
*                                                                       00480000
*--------------------------------------------------------------*        00490000
*        THE CODE STARTS HERE                                           00370000
*--------------------------------------------------------------*        00510000
*                                                                       00480000
CTSFRXF  CSECT                                                          00390000
CTSFRXF  RMODE ANY                                                      00400000
CTSFRXF  AMODE ANY                                                      00410000
*
*        INITIALIZE
*
         BEGIN *                                                        00530000
         CTSEQUR
         CTSLEVEL                                                       00540000
         LR    R10,R1
         USING PARM,R10
         CTSAMOD 31,R=R2               MOVE INTO AMODE 31               00560000
         ST    R2,AMODE                SAVE OLD AMODE                   00570000
*
*        SET DEBUG LEVEL
*
         L     R1,PARMDBG              R1 -> DEBUG LEVEL (4 BYTES)
         XC    DBGLEVEL,DBGLEVEL       STORE DEBUG LEVEL
         ICM   R1,15,0(R1)             DEBUG LEVEL
         BNP   NODEBUG                 SKIP DEBUG LVL IF MINUS
         STC   R1,DBGLEVEL             STORE DEBUG LEVEL (1 BYTE)
NODEBUG  DS    0H
*
*        INITIALIZE RETURN CODES
*
         XC    SAFRC,SAFRC
         XC    RACFRC,RACFRC
         XC    RACFRS,RACFRS
*
*        ESTABLISH PARAMETERS
*
         L     R3,PARMCLS
         MVC   CLASS,0(R3)
         TR    CLASS,LOW2UPR
*
         L     R4,PARMENT
         MVC   ENTITY,0(R4)
         TR    ENTITY,LOW2UPR
*
         L     R5,PARMFLD
         MVC   FLDNAME,0(R5)
         TR    FLDNAME,LOW2UPR
*
         L     R5,PARMSEG
         MVC   SEGMENT,0(R5)
         TR    SEGMENT,LOW2UPR
*
*        CALL RACF TO EXTRACT REQUIRED INFO
*
         CTSADBG LEVEL=(DBGLEVEL,1),                                   X
               'EXTRACT CLASS=_ ENTITY=_ FLD=_ SEG=_',                 X
               (CLASS,8,ENTITY,8,FLDNAME,8,SEGMENT,8)
*
EXTRACT  RACROUTE REQUEST=EXTRACT,TYPE=EXTRACT,                        X00886000
               RELEASE=1.8,                                            X00886200
               CLASS=CLASS,                                            X00886100
               ENTITY=ENTITY,                                          X00886100
               FIELDS=RACFLDS,                                         X00886100
               SEGMENT=SEGMENT,                                        X00886100
               WORKA=RACWORK                                            00886900
*
         LM    R2,R3,EXTRACT+4
         LR    R5,R1                KEEP EXTWKEA ADDR IN TIME   BS10011
         ST    R15,SAFRC
         ST    R2,RACFRC
         ST    R3,RACFRS
         CTSADBG LEVEL=(DBGLEVEL,1),                                   X
               'EXTRACT ROUTER RC=# RACF RC=# REASON=#',               X
               (?R15,?R2,?R3)
         LTR   R15,R15
         BNZ   ERRCALL
*BS10011 LR    R5,R1                TOO LATE FOR R1...          BS10011
         USING EXTWKEA,R5
         CTSADBG LEVEL=(DBGLEVEL,1),                            R0004  X
               'EXTRACT AREA AT ~/~,~',(?R5,EXTWSP,EXTWOFF)     R0004
*
*        COPY THE FIELD VALUE BACK TO THE CALLER
*
         LH    R8,EXTWOFF             R8 = EXTWOPT OFFSET       R0004
         AR    R8,R5                  R8 -> EXTWOPT AREA        R0004
*R0004   L     R6,EXTWOPT             LEGNTH
         L     R6,0(R8)               R6 = LEGNTH OF VALUE      R0004
         L     R15,PARMRETL           R15 -> RET AREA LENGTH
         C     R6,0(R15)              ENOUGH SPACE IN RET AREA ?
         BNH   LENOK                  YES, RET ENTIRE AREA
         L     R6,0(R15)              NO, FILL WHAT WE HAVE
LENOK    DS    0H
         L     R7,PARMRETA
         CTSADBG LEVEL=(DBGLEVEL,1),                                   X
               'VALUE LEN=# VALUE AREA=~',(?R6,?R7)
*
         LTR   R6,R6
         BNP   SKIPCOPY
*R0004   LA    R14,EXTWOPT+4           R14 -> COPY SOURCE
         LA    R14,4(R8)               R14 -> COPY SOURCE       R0004
         LR    R15,R6                  R15 = COPY LEN
         LR    R0,R7                   R0 -> COPY TARGET
         LR    R1,R15                  R1  = COPY LEN
         MVCL  R0,R14                  COPY !
SKIPCOPY DS    0H
*
*        SAVE LENGTH OF RETURNED AREA
*
         L     R15,PARMRETL
         ST    R6,0(R15)
*
*        FREE THE STORAGE
*
         XR    R3,R3
         ICM   R3,1,EXTWSP             R3 = SUPOOL
         XR    R4,R4
*BS10011 ICM   R4,1,EXTWLN             R4 = LENGTH (WRONG 1 BYTE)
         ICM   R4,7,EXTWLN             R4=LEN WHICH IS 3 BYTES BS10011
         CTSADBG LEVEL=(DBGLEVEL,1),                                   X
               'FREE AREA AT ~/~,~',(?R5,EXTWSP,EXTWOFF)
         CTSADBG LEVEL=(DBGLEVEL,1),                           BS10011 X
               'FREEMAIN ADDR=~ LENGTH=~  SP=~  ',             BS10011 X
               (?R5,?R4,?R3)                                   BS10011
         DROP  R5
         FREEMAIN RU,SP=(3),LV=(4),A=(5)
         XR    R15,R15
         B     RETURN
*
*        EXTRACT ERRORS
*
ERRCALL  DS    0H
         LA    R15,8
         B     RETURN
*
*        FINISH
*
RETURN   DS    0H                                                       01615000
         LR    R5,R15
*
         L     R1,PARMR15
         MVC   0(4,R1),SAFRC
         L     R1,PARMRC
         MVC   0(4,R1),RACFRC
         L     R1,PARMRS
         MVC   0(4,R1),RACFRS
*
         L     R2,AMODE                                                 01620000
         CTSAMOD RESTORE,R=R2                                           01630000
*
         LR    R15,R5                                                   01631000
         BRTRN (15)                                                     01640000
         LTORG                                                          01950000
*
*        WORKAREA
*
AMODE    DS    A               ENTRY AMODE                              01690000
PARMADDR DS    A
DBGLEVEL DS    X
RACWORK  DS    512X                                                     01036000
CLASS    DS    CL8
ENTITY   DS    CL256
SEGMENT  DS    CL8
RACFLDS  DC    A(1)                                                     01036000
FLDNAME  DS    CL8                                                      01036000
SAFRC    DS    F
RACFRC   DS    F
RACFRS   DS    F
LOW2UPR  DC    256AL1(*-LOW2UPR)       TRANSLATE TABLE TO UPPERCASE
         ORG   LOW2UPR
         DC    64C' '
         ORG   LOW2UPR+C'A'-X'40'
         DC    C'ABCDEFGHI'
         ORG   LOW2UPR+C'J'-X'40'
         DC    C'JKLMNOPQR'
         ORG   LOW2UPR+C'S'-X'40'
         DC    C'STUVWXYZ'
         ORG   ,
*                                                                       01950100
*---------------------------------------------------------------*       01950200
*        CONTROL BLOCKS MAPPING                                         01950300
*---------------------------------------------------------------*       01950400
*                                                                       01950100
         IRRPRXTW                                                       01952000
         PRINT NOGEN                                                    01951000
CVT      CVT   DSECT=YES,PREFIX=YES,LIST=YES                            01952000
         END                                                            01960000
