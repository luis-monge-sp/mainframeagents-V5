         TITLE 'CTSFNPS - CHANGE PASSWORD OF A USER'                    00040000
*                                                                       00050000
****************************************************************        00060000
****                                                        ****        00070000
****     CONTROL-SA  RELEASE 3.1.02                         ****        00080000
****                                                        ****        00090000
****     LICENCED MATERIAL - NEW DIMENSION SOFTWARE LTD.    ****        00100000
****                                                        ****        00110000
****************************************************************        00120000
*                                                                       00130000
****************************************************************        00140000
*                                                              *        00150000
* TITLE            : CHANGE A USER'S PASSWORD VIA RACINIT      *        00160000
*                                                              *        00170000
* FILE NAME        : CTSFNPS                                   *        00180000
*                                                              *        00190000
* AUTHOR           : DORON COHEN                               *        00200000
*                                                              *        00210000
* CREATION DATE    : 08/05/94                                  *        00220000
*                                                              *        00230000
* DESCRIPTION      :                                           *        00240000
*                                                              *        00250000
* ASSUMPTIONS AND                                              *        00260000
*   CONSIDERATIONS : 1. CALLER IS APF AUTHORIZED               *        00270000
*                                                              *        00304000
****************************************************************        00310000
*                                                                       00320000
****************************************************************        00330000
* MOD.ID   WHO      WHEN     DESCRIPTION                       *        00340000
* -------- -------- -------- --------------------------------- *        00350000
* PS0149   DORON             ADD OPTIONAL OLD PASSWORD PARAM   *        00370000
*                            AND USE IT WHEN SUPPLIED          *        00370000
* BS10011  AVN&SHM  16JUL08  ONLI ABENDS WITH S878.            *        00370000
****************************************************************        00380000
         GBLC   &AMODUSE                                                00380100
&AMODUSE SETC   'CVT'                                                   00381000
*                                                                       00340000
*---------------------------------------------------------------*       00341000
*        PARAMETER BLOCK MAPPING                                        00342000
*---------------------------------------------------------------*       00343000
PARM     DSECT                                                          00344000
PARMUSER DS    A         USER NAME                                      00345000
PARMNEWP DS    A         NEW PASSWORD                                   00347000
PARMOLDP DS    A         OLD PASSWORD                                   00347000
PARMDBG  DS    A         DEBUG LEVEL                                    00345000
PARMR15  DS    A         RETURN CODE FROM ROUTER                        00345000
PARMRC   DS    A         RETURN CODE FROM RACF                          00345000
PARMRS   DS    A         REASON CODE FROM RACF                          00345000
*                                                                       00480000
*--------------------------------------------------------------*        00490000
*        THE CODE STARTS HERE                                           00370000
*--------------------------------------------------------------*        00510000
CTSFNPS CSECT                                                           00390000
CTSFNPS RMODE ANY                                                       00400000
CTSFNPS AMODE ANY                                                       00410000
*                                                                       00421000
*--------------------------------------------------------------*        00422000
*        INITIALIZE                                                     00423000
*--------------------------------------------------------------*        00424000
         BEGIN *,12                                                     00530000
         CTSEQUR
         CTSLEVEL                                                       00540000
*                                                                       00541000
         LR    R10,R1
         USING PARM,R10
*                                                                       00541000
         CTSAMOD 31,R=R2               MOVE INTO AMODE 31               00560000
         ST    R2,AMODE                SAVE OLD AMODE                   00570000
*                                                                       01611000
         L     R4,PARMDBG              R4 -> DEBUG LEVEL (4 BYTES)
         XC    DBGLEVEL,DBGLEVEL       STORE DEBUG LEVEL
         ICM   R4,15,0(R4)             DEBUG LEVEL
         BNP   NODEBUG                 SKIP DEBUG LVL IF MINUS
         STC   R4,DBGLEVEL             STORE DEBUG LEVEL (1 BYTE)
NODEBUG  DS    0H
*                                                                       01611000
*---------------------------------------------------------------*       01612000
*        CALCULATE EXACT LENGTHS OF USER AND NEWPASS                    00870000
*---------------------------------------------------------------*       01614000
*                                                                       00882900
         L     R4,PARMUSER                                              00881300
         MVC   USERNM,0(R4)                                             00881400
         TR    USERNM,LOW2UPR
         LA    R1,USERNM+L'USERNM                                       00881500
         TRT   USERNM,BLANKTAB                                          00881600
         S     R1,=A(USERNM)                                            00881700
         STC   R1,USERL                                                 00882100
*                                                                       00882900
         L     R4,PARMNEWP                                              00883000
         MVC   NPASTXT,0(R4)                                            00883100
         TR    NPASTXT,LOW2UPR
         LA    R1,NPASTXT+L'NPASTXT                                     00883200
         TRT   NPASTXT,BLANKTAB                                         00883300
         S     R1,=A(NPASTXT)                                           00883400
         STC   R1,NPASLEN                                               00883500
*                                                                       00882900
         XR    R15,R15
         IC    R15,USERL
         CTSADBG LEVEL=(DBGLEVEL,1),'USER=_/~~ (#)',                   X
               (USERNM,8,USERNM,USERNM+4,?R15)
         IC    R15,NPASLEN
         CTSADBG LEVEL=(DBGLEVEL,1),'NPAS=_/~~ (#)',                   X
               (NPASTXT,8,NPASTXT,NPASTXT+4,?R15)
*                                                               *PS0149 01611000
*---------------------------------------------------------------*PS0149 01612000
*        CALCULATE OLD PASSWORD LENGTH, IF IT WAS SUPPLIED      *PS0149 00870000
*---------------------------------------------------------------*PS0149 01614000
*                                                               *PS0149 00882900
         MVC   OPASTXT,=CL8' '       CLEAR OLD PASSWORD         *PS0149
         MVI   OPASLEN,0             CLEAR OLD PASSWORD LEN     *PS0149
         MVI   OPASSUP,C'N'          CLEAR OLD PASSWORD FLAG    *PS0149
         ICM   R4,15,PARMOLDP        OLD PASSWORD SPECIFIED ?   *PS0149 00883000
         BZ    SKIPOLDP              NO - NOTHING TO DO HERE    *PS0149
         MVC   OPASTXT,0(R4)         YES, COPY TO BUFFER        *PS0149 00883100
         TR    OPASTXT,LOW2UPR       MAKE UPPER                 *PS0149
         LA    R1,OPASTXT+L'OPASTXT  LOCATE                     *PS0149 00883200
         TRT   OPASTXT,BLANKTAB          BLANK                  *PS0149 00883300
         S     R1,=A(OPASTXT)        CALC LEN                   *PS0149 00883400
         STC   R1,OPASLEN            SAVE LEN                   *PS0149 00883500
         MVI   OPASSUP,C'Y'          OLD PASSWORD WAS SUPPLIED  *PS0149
*                                                               *PS0149 00882900
         IC    R15,OPASLEN                                      *PS0149
         CTSADBG LEVEL=(DBGLEVEL,1),'OPAS=_/~~ (#)',            *PS0149X
               (OPASTXT,8,OPASTXT,OPASTXT+4,?R15)               *PS0149
*                                                               *PS0149 00882900
SKIPOLDP DS    0H                                               *PS0149
*                                                                       01611000
*---------------------------------------------------------------*       01612000
*        CHECK PARMETERS                                                00870000
*---------------------------------------------------------------*       01614000
*                                                                       01611000
         MVC   STAGE,=F'20'                                             00882900
         XC    R15RCRS,R15RCRS
         XR    R15,R15
*                                                                       01611000
         MVC   R15RCRS,=F'4'
         ICM   R15,1,USERL
         BZ    SOF
         MVC   R15RCRS,=F'8'
         CH    R15,=H'8'
         BH    SOF
*                                                                       01611000
         MVC   R15RCRS,=F'12'
         ICM   R15,1,NPASLEN
         BZ    SOF
         MVC   R15RCRS,=F'16'
         CH    R15,=H'8'
         BH    SOF
*                                                               *PS0149 00882900
         CLI   OPASSUP,C'Y'            OLD PASSWORD SUPP ?      *PS0149 00882900
         BNE   SKIPOCHK                NO, NO CHECKS            *PS0149 00882900
         MVC   R15RCRS,=F'20'                                   *PS0149 00882900
         ICM   R15,1,OPASLEN                                    *PS0149 00882900
         BZ    SOF                                              *PS0149 00882900
         MVC   R15RCRS,=F'24'                                   *PS0149 00882900
         CH    R15,=H'8'                                        *PS0149 00882900
         BH    SOF                                              *PS0149 00882900
         B     PASSREP                 GO DIRECTLY TO PASS CHG  *PS0149 00882900
SKIPOCHK DS    0H                                               *PS0149 00882900
*
*---------------------------------------------------------------*       00885700
*        EXTRACT OLD PASSWORD (ENCRYPTED)                               00885800
*---------------------------------------------------------------*       00885900
*                                                                       00885600
         MVC   STAGE,=F'16'                                             00882900
*                                                                       00885600
EXTRACT  RACROUTE REQUEST=EXTRACT,TYPE=EXTRACT,                        X00886000
               CLASS='USER',           EXTRACT A USER PROFILE          X00886100
               ENTITY=USERNM,          REQUESTED UID                   X00886100
               RELEASE=1.8,            RELEASE NUMBER                  X00886200
               FIELDS=RACFLDS,         FIELDS TO EXTRACT               X00886100
               WORKA=RACWORK           WORKAREA                         00886900
*
         LM    R2,R3,EXTRACT+4
         LR    R5,R1                   KEEP EXTWKEA IN TIME     BS10011
         CTSADBG LEVEL=(DBGLEVEL,1),                                   X
               'EXTRACT ROUTER RC=# RACF RC=# REASON=#',               X
               (?R15,?R2,?R3)
         ST    R15,R15RCRS
         STM   R2,R3,R15RCRS+4
         LTR   R15,R15
         BNZ   SOF
*
*        COPY THE ENCRYPTED PASSWORD TO THE OLD PASSWORD FIELD
*
*BS10011 LR    R5,R1                   TOO LATE FOR R1
         USING EXTWKEA,R5
         CTSADBG LEVEL=(DBGLEVEL,1),                                   X
               'AREA AT ~/~,~ DATA _/_/~~',                            X
               (?R5,EXTWSP,EXTWOFF,EXTWUID,8,EXTWGRP,8,                X
               EXTWOPT+4,EXTWOPT+8)
         MVC   OPASTXT(8),EXTWOPT+4
         MVI   OPASLEN,8                                                00882800
*
*        FREE THE EXTRACT OUTPUT AREA
*
         XR    R3,R3
         ICM   R3,1,EXTWSP
         XR    R4,R4
*BS10011 ICM   R4,1,EXTWLN             FIELD LENGTH IS 3 BYTES
         ICM   R4,7,EXTWLN             AND NOT 1 BYTE AS USED.  BS10011
         CTSADBG LEVEL=(DBGLEVEL,1),                           BS10011 X
               'FREEMAIN ADDR=~ LENGTH=~  SP=~  ',             BS10011 X
               (?R5,?R4,?R3)                                   BS10011
         DROP  R5
         FREEMAIN RU,SP=(3),LV=(4),A=(5)
*
         CTSADBG LEVEL=(DBGLEVEL,1),                                   X
               'OPAS=_/~~',(OPASTXT,8,OPASTXT,OPASTXT+4)
*
*---------------------------------------------------------------*       00885700
*        ENCRYPT THE NEW PASSWORD (LENGTH = 8 TO INCLUDE BLANKS)        00885800
*---------------------------------------------------------------*       00885900
*
         MVC   STAGE,=F'12'                                             00882900
*
         MVI   NPASLEN,8                                                00882800
ENCR     RACROUTE REQUEST=EXTRACT,TYPE=ENCRYPT,                        X00886100
               ENCRYPT=(NPASWRD,INST), ENCRYPTION KEY                  X00886100
               ENTITY=USERNM,          DATA TO ENCRYPT                 X00886100
               WORKA=RACWORK           RACROUTE WORK AREA               00886900
*
         LM    R2,R3,ENCR+4
         CTSADBG LEVEL=(DBGLEVEL,1),                                   X
               'ENCRYPT ROUTER RC=# RACF RC=# REASON=#',               X
               (?R15,?R2,?R3)
         ST    R15,R15RCRS
         STM   R2,R3,R15RCRS+4
         LTR   R15,R15
         BNZ   SOF
*                                                               *PS0149 00882200
*---------------------------------------------------------------*PS0149 00885700
*        REPLACE PASSWORD                                       *PS0149 00885800
*---------------------------------------------------------------*PS0149 00885900
*                                                               *PS0149 00885600
PASSREP  DS    0H                                               *PS0149 00885600
         CTSADBG LEVEL=(DBGLEVEL,1),                                   X
               'NPAS=_/~~',(NPASTXT,8,NPASTXT,NPASTXT+4)
*                                                               *PS0149 00885600
         MVC   STAGE,=F'8'                                              00882900
*                                                               *PS0149 00885600
         CLI   OPASSUP,C'Y'            OLD PASSWORD SUPP ?      *PS0149 00882900
         BE    REPNENCR                NO, NO CHECKS            *PS0149 00882900
*                                                                       00882200
*---------------------------------------------------------------*       00885700
*        REPLACE PASSWORD (BOTH OLD AND NEW PASSWORD ARE ENCRYPTED)     00885800
*---------------------------------------------------------------*       00885900
*                                                                       00885600
CREATE   RACROUTE REQUEST=VERIFY,                                      X00886000
               ENVIR=CREATE,           CREATE AN ACEE                  X00886100
               RELEASE=1.8,            RELEASE NUMBER                  X00886200
               LOG=ASIS,               LOG ONLY FAILED REQUESTS        X00886300
               STAT=NO,                NO STATISTICS                   X00886400
               ENCRYPT=NO,             PASSWORDS ARE ALREADY ENCRYPTED X00886400
               ACEE=ACEEA,             ADDR OF ACEE ADDRESS            X00886500
               USERID=USERID,          USERID                          X00886600
               PASSWRD=OPASWRD,        OLD PASSWORD                    X00886700
               NEWPASS=NPASWRD,        NEW PASSWORD                    X00886700
               WORKA=RACWORK           RACROUTE WORK AREA               00886900
         LM    R2,R3,CREATE+4
         B     SETRC                                            *PS0149 00882900
*                                                               *PS0149 00882200
*---------------------------------------------------------------*PS0149 00885700
*        REPLACE PASSWORD (PASSWORDS NOT ENCRYPTED)             *PS0149 00885800
*---------------------------------------------------------------*PS0149 00885900
*                                                               *PS0149 00885600
REPNENCR DS    0H                                               *PS0149 00885600
CREATEO  RACROUTE REQUEST=VERIFY,                               *PS0149X00886000
               ENVIR=CREATE,           CREATE AN ACEE           *PS0149X00886100
               RELEASE=1.8,            RELEASE NUMBER           *PS0149X00886200
               LOG=ASIS,               LOG ONLY FAILED REQUESTS *PS0149X00886300
               STAT=NO,                NO STATISTICS            *PS0149X00886400
               ACEE=ACEEA,             ADDR OF ACEE ADDRESS     *PS0149X00886500
               USERID=USERID,          USERID                   *PS0149X00886600
               PASSWRD=OPASWRD,        OLD PASSWORD             *PS0149X00886700
               NEWPASS=NPASWRD,        NEW PASSWORD             *PS0149X00886700
               WORKA=RACWORK           RACROUTE WORK AREA       *PS0149 00886900
         LM    R2,R3,CREATEO+4                                  *PS0149 00885600
*                                                                       00887100
*---------------------------------------------------------------        00885700
*        SET RACF RC                                                    00887100
*---------------------------------------------------------------        00885700
*                                                                       00887200
SETRC    DS    0H                                               *PS0149 00882900
         CTSADBG LEVEL=(DBGLEVEL,1),                                   X
               'CREATE ROUTER RC=# RACF RC=# REASON=#',                X
               (?R15,?R2,?R3)
         ST    R15,R15RCRS
         STM   R2,R3,R15RCRS+4
         LTR   R15,R15
         BNZ   SOF
*                                                                       00887100
*---------------------------------------------------------------*       00887200
*        AND CLEANUP                                                    00887300
*---------------------------------------------------------------*       00887400
         MVC   STAGE,=F'4'                                              00882900
DELETE   RACROUTE REQUEST=VERIFY,                                      X00887600
               ENVIR=DELETE,           DELETE AN ACEE                  X00887700
               ACEE=ACEEA,             ADDR OF ACEE ADDRESS            X00887800
               WORKA=RACWORK           RACROUTE WORK AREA               00887900
         LM    R2,R3,DELETE+4
         ST    R15,R15RCRS
         STM   R2,R3,R15RCRS+4
*                                                                       00888000
         CTSADBG LEVEL=(DBGLEVEL,1),                                   X
               'DELETE ROUTER RC=# RACF RC=# REASON=#',                X
               (?R15,?R2,?R3)
*                                                                       00888000
*---------------------------------------------------------------*       00888100
*        FINISH                                                         00888200
*---------------------------------------------------------------*       00889000
         XC    STAGE,STAGE                                              00882900
SOF      DS    0H                                                       01615000
*                                                                       01630100
         LM    R2,R4,R15RCRS
         L     R15,PARMR15
         ST    R2,0(R15)
         L     R15,PARMRC
         ST    R3,0(R15)
         L     R15,PARMRS
         ST    R4,0(R15)
*                                                                       01630100
         L     R2,AMODE                                                 01620000
         CTSAMOD RESTORE,R=R2                                           01630000
*                                                                       01630100
         L     R15,STAGE                                                01631000
         BRTRN (15)                                                     01640000
*                                                                       01650000
*---------------------------------------------------------------*       01660000
*        WORKAREA                                                       01670000
*---------------------------------------------------------------*       01680000
AMODE    DS    A               ENTRY AMODE                              01690000
ACEEA    DS    A               ACEE ADDR                                01000000
DBGLEVEL DS    X
STAGE    DS    F
R15RCRS  DS    3F
*                                                                       01001000
BLANKTAB DC    256AL1(00)
         ORG   BLANKTAB
         DC    65AL1(255)
         ORG   ,
LOW2UPR  DC    256AL1(*-LOW2UPR)       TRANSLATE TABLE TO UPPERCASE
         ORG   LOW2UPR
         DC    64C' '
         ORG   LOW2UPR+C'A'-X'40'
         DC    C'ABCDEFGHI'
         ORG   LOW2UPR+C'J'-X'40'
         DC    C'JKLMNOPQR'
         ORG   LOW2UPR+C'S'-X'40'
         DC    C'STUVWXYZ'
         ORG   ,
*                                                                       01001000
USERIDPD DS     0F
         DS     XL3
USERID   DS     0XL9                                                    01010000
USERL    DS     X                                                       01010000
USERNM   DS     CL8                                                     01020000
*                                                                       01030000
OPASSPD  DS     0F
         DS     XL3
OPASWRD  DS     0XL9                                                    01010000
OPASLEN  DS     X                                                       01032000
OPASTXT  DS     CL8                                                     01033000
*                                                                       01033100
NPASSPD  DS     0F
         DS     XL3
NPASWRD  DS     0XL9                                                    01010000
NPASLEN  DS     X                                                       01032000
NPASTXT  DS     CL8                                                     01033000
*                                                                       01033100
OPASSUP  DS     CL1                                            *PS0149  01033000
*                                                                       01033100
RACWORK  DS    512X                                                     01036000
*                                                                       01830000
RACFLDS  DC    A(1),CL8'PASSWORD'                                       01830000
*                                                                       01830000
         LTORG                                                          01950000
*                                                                       01950100
*---------------------------------------------------------------*       01950200
*        CONTROL BLOCKS MAPPING                                         01950300
*---------------------------------------------------------------*       01950400
*                                                                       01950100
         IRRPRXTW                                                       01952000
*                                                                       01950100
         PRINT NOGEN                                                    01951000
CVT      CVT   DSECT=YES,PREFIX=YES,LIST=YES                            01952000
         END                                                            01960000
