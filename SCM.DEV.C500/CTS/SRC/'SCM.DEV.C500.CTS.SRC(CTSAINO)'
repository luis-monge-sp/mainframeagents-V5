         START
*
****************************************************************
****                                                        ****
****     CONTROL-SA  RELEASE 2.1.0                          ****
****                                                        ****
****     LISENCED MATERIAL - EAGLEEYE CONTROL SOFTWARE LTD. ****
****                                                        ****
****************************************************************
*
****************************************************************
*                                                              *
* TITLE            : CTSAINO                                   *
*                                                              *
* FILE NAME        : CTSAINO                                   *
*                                                              *
* AUTHOR           : DORON COHEN                               *
*                                                              *
* CREATION DATE    : 9/6/97                                    *
*                                                              *
* DESCRIPTION      : SEND NOTIFICATION TO ONLINE INTERCEPTOR   *
*                                                              *
* ASSUMPTIONS AND                                              *
*   CONSIDERATIONS :                                           *
*                                                              *
****************************************************************
*                                                              *
*--------V.2.1.0-----------------------------------------------*
* PS0244  DC 24/02/98 LONG RQC SUPPORT                         *
*--------V.4 --------------------------------------------------*
* IS10188 AL 14/10/24 MOVE OUR MODULES ABOVE 16M LINE          *
****************************************************************
*
CTSAINO  CSECT
*IS10188 CTSAINO  CTSBGNR LV=WORKLEN,EQUR=YES,R12
CTSAINO  CTSBGNR LV=WORKLEN,EQUR=YES,R12,GMTYPE=RC,LOC=RES      IS10188
CTSAINO  AMODE 31
CTSAINO  RMODE ANY
         CTSLEVEL ,
         USING WORKAREA,R13
         LR    R11,R1
         USING PARM,R11
*
*        INITIALIZE RQC
*
*PS0244  XC    RQCAREA,RQCAREA      @ CLEAR LOCAL RQC AREA
         LA    R0,RQCAREA              CLEAR THE AREA           PS0244
         LA    R1,RQCLEN               SET LENGTH               PS0244
         XR    R15,R15                 PADD WITH X'00'          PS0244
         MVCL  R0,R14                  CLEAR                    PS0244
*
         LA    R4,RQCAREA             R4 -> LOCAL RQC AREA
         USING RQC,R4                 MAP RQC
*PS0244  MVI   RQCTYPE,RQCTNOT        RQC TYPE = NOTIFY
         MVI   RQCTYPE,RQCTNOTX       RQC TYPE = EXTENDED NOTIFY PS0244
         TIME  DEC                    GET TIME
         STM   R0,R1,RQCTSTMP         TIMESTAMP RQC
*
*        INITIALIZE EVT WITHIN THE RQC
*
         LA    R5,RQCEVT              R5 -> EVT
         USING EVT,R5                 MAP IT
         MVC   EVTEVT,=CL4'EVT '      SET EYE CATCHER
         MVC   EVTFLAG,=A(EVTFULL)    MARK EVT AS LOADED WITH DATA
         TIME  BIN
         ST    R0,EVTTIME             EVENT TIME STAMP
         ST    R1,EVTDATE             EVENT TIME STAMP
*
*        SET ORIGINATOR FIELDS OF EVT
*
         L     R1,PARMOUID
         MVC   EVTOUID,0(R1)          ORIGINATOR UERID
         L     R1,PARMOJBN
         MVC   EVTOJBN,0(R1)          ORIGINATOR JOBNAME
         L     R1,PARMOPGM
         MVC   EVTOPROG,0(R1)          ORIGINATOR PROGRAM
         L     R1,PARMOTRM
         MVC   EVTOTERM,0(R1)         ORIGINATOR TERMINAL
*
*        EVENT FIELDS OF EVT
*
         L     R1,PARMUSR
         MVC   EVTUSER,0(R1)          EVENT USER
         L     R1,PARMGRP
         MVC   EVTGROUP,0(R1)         EVENT GROUP/PASSWORD
         L     R1,PARMTYP
         MVC   EVTTYPE,0(R1)          EVENT TYPE
         L     R1,PARMACT
         MVC   EVTACT,0(R1)           EVENT ACTION
*
*        SEND NOTIFICATION
*
         L     R9,PARMSSN
         CALL  CTSAPCC,(FUJIFLAG,(R9),JOBNAME,(R4),PCCWORK),           X
               MF=(E,CALLAREA)
*
*        RETURN                                                *
*
EXIT     DS    0H
         CTSBRTR (15),LV=WORKLEN
         LTORG ,
JOBNAME  DC    CL8'CTSAONI'
FUJIFLAG DC    AL1(128)
HEX2CHAR DC    256AL1(*-HEX2CHAR)
         ORG   HEX2CHAR+X'FA'
         DC    CL6'ABCDEF'
         ORG   ,
         DROP  ,
*
****************************************************************
*        WORK AREA
****************************************************************
*
WORKAREA DSECT
SAVEAREA DS    18F
SSNAME   DS    CL4                     SERVER SUBSYS
CALLAREA CALL  ,(,,,,),MF=L            EXECUTE FORM CALL AREA
PCCWORK  DS    XL512                   WORK AREA FOR CTSAPCC ROUTINE
RQCAREA  DS    CL(RQCLEN)              AREA FOR RQC
PACKD    DS    D                       RETURN CODE CONVERSION
CHARRC   DS    CL8                     RETURN CODE CONVERSION
RESERVED DS    16X                     RESERVED AREA
WORKLEN  EQU   *-WORKAREA
*
****************************************************************
*        PARAMETER LIST
****************************************************************
*
PARM     DSECT
PARMSSN  DS    A                       SUBSYSTEM NAME
PARMUSR  DS    A                       USER NAME
PARMGRP  DS    A                       GROUP NAME
PARMTYP  DS    A                       EVENT OBJECT TYPE
PARMACT  DS    A                       EVENT ACTION
PARMOUID DS    A                       CALLER USERID
PARMOJBN DS    A                       CALLER JOBNAME
PARMOTRM DS    A                       CALLER TERM
PARMOPGM DS    A                       CALLER PROGRAM
*
****************************************************************
*        CONTROL-SA CONTROL BLOCKS
****************************************************************
*
RQC      DSECT
         COPY  CTSRQC
EVT      DSECT
         COPY  CTSEVT
         END
