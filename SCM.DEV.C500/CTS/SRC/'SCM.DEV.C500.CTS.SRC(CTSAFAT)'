         TITLE 'CTSAFAT - CHECK RESOURCE PERMISSION USING FASTAUTH'     00040000
*
****************************************************************        00060000
****                                                        ****        00070000
****     CONTROL-SA  RELEASE 4.0.00                         ****        00080000
****                                                        ****        00090000
****     (C) COPYRIGHT 2015 SAILPOINT TECHNOLOGIES, INC.,   ****        00100000
****     ALL RIGHTS RESERVED.                               ****        00110000
****                                                        ****        00110000
****************************************************************        00120000
*
****************************************************************
*                                                              *
* TITLE            : CHECK RESOURCE AUTHRIZATION USING         *
*                    RACROUTE FASTAUTH.                        *
*                                                              *
* FILE NAME        : CTSAFAT                                   *
*                                                              *
* AUTHOR           : NURIT YAHALOM                             *
*                                                              *
* CREATION DATE    : 23/05/19                                  *
*                                                              *
* DESCRIPTION      : CHECK RESOURCE AUTHRIZATION USING         *
*                    RACROUTE FASTAUTH.                        *
*                                                              *
* PARAMETERS       : 1. CLASS NAME (0-8 CHARS) (I)             *
*                         RESOURCE CLASS NAME.                 *
*                         THE NAME HAS TO BE A VALID C STRING  *
*                         OR SHOULD END WITH A BLANK.          *
*                                                              *
*                    2. ENTITY NAME (0-255 CHARS) (I)          *
*                         RESOURCE NAME.                       *
*                         THE NAME HAS TO BE A VALID C STRING  *
*                         OR SHOULD END WITH A BLANK.          *
*                                                              *
*                    3. ACEE - (F) (I)                         *
*                         ADDRESS OF ACEE TO BE USED FOR       *
*                         THE RACCROUTE FASAUTH CALL.          *
*                                                              *
*                    4. DEBUG LEVEL (F) (I)                    *
*                                                              *
*                    5. RACROUTE RETURN CODE (F) (O)           *
*                                                              *
*                    6. RACROUTE RACF RETURN CODE (F) (O)      *
*                                                              *
*                    7. RACROUTE RACF REASON CODE (F) (O)      *
*                                                              *
*                                                              *
* ERTURN CODES     :  0 - ALLOWED.                             *        00240000
*                     4 - NOT DEFINED.                         *        00250000
*                     8 - NOT ALLOWED.                         *        00250000
*                    12 - PARAMETERS ERROR.                    *        00250000
*                    16 - GETMAIN FAILED.                      *        00250000
*                    20 - CALLER IS NOT AUTHORIZED.            *        00250000
*                    24 - RACROUTE FAILED.                     *        00250000
*                                                              *
*                                                              *
* COMMENTS         :  1. THE PROGRAM IS REENTRANT.             *        00240000
*                  :  2. THE CALLER MUST BE APF AUTHORIZED.    *        00240000
*                  :  3. ACCESS CHECKED IS ALWAYS READ.        *        00240000
*                  :  3. THE RESURCE REQUESTED MUST BE         *        00240000
*                        RACLISTED VIA SETROPTS FOR FASTAUTH   *
*                        TO FIND IT.                           *
*                                                              *
****************************************************************
*
****************************************************************
* MOD.ID   WHO      WHEN     DESCRIPTION                       *
* -------- -------- -------- --------------------------------- *
* BS10100  NURITY   23/05/19 NEW VERSION.                      *
*                                                              *
****************************************************************
*
*--------------------------------------------------------------*
*        INITIALIZE
*--------------------------------------------------------------*
CTSAFAT  CSECT
CTSAFAT  RMODE ANY
CTSAFAT  AMODE ANY
*
         USING CTSAFAT,R15
         SAVE  (14,12),,*                                               00530000
         CTSLEVEL
*
         LR    R12,R15                 SET BASE REGISTER.
         DROP  R15
         USING CTSAFAT,R12
*
**   ALLOCATE WORK AREA AND CHAIN SAVE AREAS.
*
         LR    R10,R1                  R10 -> PARMS LIST.
         USING PRM,R10                 R10 POINTS TO INPUT PARMS
         GETMAIN RC,LV=WALEN
         CHI   R15,0
         BNE   RC16                    GETMAIN FAILED.
*
         ST    R13,4(,R1)
         ST    R1,8(,R13)
         LR    R13,R1
         USING WA,R13
*--------------------------------------------------------------------
**   PROCESS THE PARAMETERS
*--------------------------------------------------------------------
*
**   TAKE DEBUG LEVEL.
*
         MVI   DBGLEVEL,0              ZERO DEBUG LEVEL.
         L     R4,PRMDBG               R4 -> DEBUG LEVEL (4 BYTES)
         LA    R4,0(,R4)               CLEAR AMODE BIT.
         CHI   R4,0                    DEBUG LEVEL PROVIDED ?
         BE    NODEBUG
         L     R4,0(,R4)               R4 = DEBUG LEVEL
         CHI   R4,0                    VALID DEBUG LEVEL ?
         BNH   NODEBUG                 ..NO - NO DEBUG.
         STC   R4,DBGLEVEL             STORE DEBUG LEVEL (1 BYTE)
NODEBUG  DS    0H
*
**   RESET RETURN AND REASON CODES, IS REQUESTED.
*
         LHI   R1,0
*
         L     R4,PRMFRC               R4 -> AREA FOR FASTAUTH RC.
         LA    R4,0(,R4)               CLEAR HIGH-ORDER BYTE.
         CHI   R4,0                    REQUESTED ?
         BE    RC12                    ..NO - ERROR.
         ST    R1,0(,R4)               RESET RETURN CODE.
*
         L     R4,PRMFRRC              R4 -> AREA FOR RACF RC.
         LA    R4,0(,R4)               CLEAR HIGH-ORDER BYTE.
         CHI   R4,0                    REQUESTED ?
         BE    RC12                    ..NO - ERROR.
         ST    R1,0(,R4)               RESET RETURN CODE.
*
         L     R4,PRMFRRS              R4 -> AREA FOR RACF RS.
         LA    R4,0(,R4)               CLEAR HIGH-ORDER BYTE.
         CHI   R4,0                    REQUESTED ?
         BE    RC12                    ..NO - ERROR.
         ST    R1,0(,R4)               RESET RETURN CODE.
*
**   TAKE CLASS NAME
*
         MVC   CLASNAME,BLANKS                                          00881400
         L     R4,PRMCLASS             R4 -> CLASS NAME.                00881300
         LA    R4,0(,R4)               CLEAR HIGH-ORDER BYTE.
         CHI   R4,0                    PROVIDED ?
         BE    RC12                    ..NO - ERROR.
         LA    R1,CLASNAME                                              00881500
         LA    R2,L'CLASNAME                                            00881500
LOOPCLAS DS    0H                                                       00881600
         CLI   0(R4),C' '                                               00881600
         BL    ENDLCLAS                                                 00881600
         MVC   0(1,R1),0(R4)
         LA    R1,1(R1)
         LA    R4,1(R4)
         BCT   R2,LOOPCLAS
ENDLCLAS DS    0H                                                       00881600
         TR    CLASNAME,LOW2UPR
*
**   TAKE LOGASIS OR LOGNONE
*
         MVC   LOGPARM,BLANKS                                           00881400
         L     R4,PRMLOG               R4 -> LOGASIS OR LOGNONE         00881300
         LA    R4,0(,R4)               CLEAR HIGH-ORDER BYTE.
         CHI   R4,0                    PROVIDED ?
         BE    RC12                    ..NO - ERROR.
         LA    R1,LOGPARM                                               00881500
         LA    R2,L'LOGPARM                                             00881500
LOOPLOG  DS    0H                                                       00881600
         CLI   0(R4),C' '                                               00881600
         BL    ENDLLOG                                                  00881600
         MVC   0(1,R1),0(R4)
         LA    R1,1(R1)
         LA    R4,1(R4)
         BCT   R2,LOOPLOG
ENDLLOG  DS    0H                                                       00881600
         TR    LOGPARM,LOW2UPR
         CLC   =C'LOGASIS',LOGPARM
         BE    GOODLOG
         CLC   =C'LOGNONE',LOGPARM
         BNE   RC12
GOODLOG  DS    0H                                                       00881600
*
**   TAKE ENTITY NAME
*
         MVC   ENTITY,BLANKS                                            00881400
         L     R4,PRMENTTY             R4 -> ENTITY NAME.               00881300
         LA    R4,0(,R4)               CLEAR HIGH-ORDER BYTE.
         CHI   R4,0                    PROVIDED ?
         BE    RC12                    ..NO - ERROR.
         LA    R1,ENTITY                                                00881500
         LA    R2,L'ENTITY                                              00881500
LOOPENTT DS    0H                                                       00881600
         CLI   0(R4),C' '                                               00881600
         BL    ENDLENTT                                                 00881600
         MVC   0(1,R1),0(R4)
         LA    R1,1(R1)
         LA    R4,1(R4)
         BCT   R2,LOOPENTT
ENDLENTT DS    0H                                                       00881600
         TR    ENTITY,LOW2UPR
*
**   TAKE ACEE ADDRESS
*
         L     R4,PRMACEE              R4 -> ACEE.
         LA    R4,0(,R4)               CLEAR HIGH-ORDER BYTE.
         CHI   R4,0                    PROVIDED ?
         BE    RC12                    ..NO - ERROR.
         ST    R4,ACEEADDR
*
         CTSADBG LEVEL=(DBGLEVEL,1),                                   X00885600
               'CLASS = _  ENTITY = _   ACEE = ~',                     X00885600
               (CLASNAME,8,ENTITY,8,ACEEADDR),                         X
               RAREA=RAREA,DBGAREA=DBGAREA
*--------------------------------------------------------------------
**   DO RACROUTE
*--------------------------------------------------------------------
*
**  SWITCH TO SUP STATE, IF NEEDED AND AUTHORIZED.
**  SUP STATE IS REQURIED FOR CHECKING AUTHORIZATION USING
**  SETROPTS RACLISTED RESOURCES.
*
         TESTAUTH FCTN=1               APF AUTHORIZED ?
         CHI   R15,0
         BNE   RC20                    ..NO - ERROR.
*
         TESTAUTH STATE=YES,RBLEVEL=1  ALREADY IN SUP STATE ?
         CHI   R15,0                   ..YES - NO SWITCH NEEDED.
         BNE   SETSUP
         MVI   ISSUP,X'00'             ALREADY IN SUP STATE.
         B     CHKAUTH
*
SETSUP   DS    0H
         MODESET MODE=SUP
         MVI   ISSUP,X'FF'             PROB -> SUP.
*
**  CALL RACROUTE FASTAUTH
*
CHKAUTH  DS    0H
         L     R4,ACEEADDR
         MVC   RACROUTL(RACROUML),RACROUTM
*
         CLC   =C'LOGASIS',LOGPARM     CALLED WITH LOGASIS?
         BNE   RCRTATNN                NO, CALL WITH LOG=NONE
*                                      YES, CALL WITH LOG=ASIS
         RACROUTE REQUEST=FASTAUTH,                                    X00886000
               RELEASE=7706,           1.13                            X
               WKAREA=FAWKAREA,        FASTAUTH WORK AREA              X
               CLASS=CLASNAME,         CLASS NAME (APPL)               X00886100
               ENTITY=ENTITY,          RESOURCE NAME (SAILPAPL)        X00886300
               WORKA=RAREA,            RACROUTE WORK AREA              X00886900
               ACEE=(R4),              ACEE ADDRESS.                   X00886900
               LOG=ASIS,               LOG THE REQUEST                 X
               MF=(E,RACROUTL)
         B     RCRTRETN                CONTINUE THERE
*
RCRTATNN DS    0H
         RACROUTE REQUEST=FASTAUTH,                                    X00886000
               RELEASE=7706,           1.13                            X
               WKAREA=FAWKAREA,        FASTAUTH WORK AREA              X
               CLASS=CLASNAME,         CLASS NAME (APPL)               X00886100
               ENTITY=ENTITY,          RESOURCE NAME (SAILPAPL)        X00886300
               WORKA=RAREA,            RACROUTE WORK AREA              X00886900
               ACEE=(R4),              ACEE ADDRESS.                   X00886900
               LOG=NONE,               DO NOT LOG THE REQUEST          X
               MF=(E,RACROUTL)
*
RCRTRETN DS    0H
         CLI   ISSUP,0                 HAS TO SWITCH BACK ?
         BE    CHKRC                   ..NO - CHECK RACROUTE RC.
*
         LR    R5,R15
         MODESET MODE=PROB
         LR    R15,R5
*
CHKRC    DS    0H
         CTSADBG LEVEL=(DBGLEVEL,1),                                   X00885600
               'FASTAUTH - RC = # / # / #,   ACEEADDR = ~',            X
               (?R15,RACROUTL+0,RACROUTL+4,?R4),                       X
               RAREA=RAREA,DBGAREA=DBGAREA                              04430000
         CTSADBG LEVEL=(DBGLEVEL,1),                                   X00885600
               'FASTAUTH WORKAERA = ~ / ~ / ~ / ~ / ~',                X
               (FAWKAREA+40,FAWKAREA+44,FAWKAREA+48,FAWKAREA+52,FAWKAREX
               A+56),                                                  X
               RAREA=RAREA,DBGAREA=DBGAREA                              04430000
*
**   RETURN RACROUTE RETURN CODES.
*
         L     R4,PRMFRC               RETURN RACROUTE RETURN CODE.
         ST    R15,0(,R4)
         LM    R2,R3,RACROUTL
         L     R4,PRMFRRC              RETURN RACF RETURN CODE.
         ST    R2,0(,R4)
         L     R4,PRMFRRS              RETURN RACF REASON CODE.
         ST    R3,0(,R4)
*
**   CHECK IF ALLOWED:
**   - ALLOWED - RETURN RC = 0.
**   - NOT DEFINED (SAF RC = 4 AND RACF_RC = 4) - RETURN RC = 4.
**   - NOT ALLOWED (SAF RC = 8 AND RACF_RC = 8) - RETURN RC = 8.
**   - ANY OTHER RC FROM RACROUTE - RETUR RC = 20
*
         CHI   R15,0                   ACEE IS AUTHORIZED TO USE THE    OURCE
*                                      RESOURCE ?
         BE    RETURN                  ..YES - RETURN.
*
         CHI   R15,4                   SAF RC = 4?
         BNE   CHKRC8                  ..NO - ERROR.
*                                      ..YES ... ->
         CHI   R2,4                    RACF RC = 4 ?  (NOT DEFINED)
         BE    RC4                     ..YES - RETURN RC=4
*
CHKRC8   DS    0H
         CHI   R15,8                   SAF RC = 8?
         BNE   RC24                    ..NO - ERROR.
*                                      ..YES ... ->
         CHI   R2,8                    RACF RC = 8 ?  (NOT ALLOWED)
         BE    RC8                     ..YES - RETURN RC=4
         B     RC24                    ..NO - ERROR.
*--------------------------------------------------------------------
**   SET RETURN CODE AND RETURN
*--------------------------------------------------------------------
RC4      DS    0H                      NOT DEFINED.
         LHI   R15,4
         B     RETURN
*
RC8      DS    0H                      NOT ALLOWED.
         LHI   R15,8
         B     RETURN
*
RC12     DS    0H                      PARAMETERS ERROR.
         LHI   R15,12
         B     RETURN
*
RC16     DS    0H                      GETMAIN FAILED.
         LHI   R15,16
         B     OUT
*
RC20     DS    0H                      CALLER IS NOT AUTHORIZED.
         LHI   R15,20
         B     RETURN
*
RC24     DS    0H                      RACROUTE FAILED.
         LHI   R15,24
         B     RETURN
*
RETURN   DS    0H
         CTSADBG LEVEL=(DBGLEVEL,1),                                   X00885600
               'RETURN: RC = #',(?R15),                                X00885600
               RAREA=RAREA,DBGAREA=DBGAREA
         LR    R5,R15                  SAVE RC
         LR    R1,R13                  R1 -> ALLOCTED AREA.
         L     R13,4(,R13)
         FREEMAIN RC,LV=WALEN,A=(R1)   FREE ALLOCATED AREA.
         LR    R15,R5                  RESTORE RC.
*
OUT      DS    0H
         RETURN (14,12),RC=(15)
         DROP  ,
         LTORG ,
         DROP  ,
*--------------------------------------------------------------------
**   CONSTANTS
*--------------------------------------------------------------------
BLANKS   DC    256C' '
LOW2UPR  DC    256AL1(*-LOW2UPR)       TRANSLATE TABLE TO UPPERCASE
         ORG   LOW2UPR
         DC    64C' '
         ORG   LOW2UPR+C'A'-X'40'
         DC    C'ABCDEFGHI'
         ORG   LOW2UPR+C'J'-X'40'
         DC    C'JKLMNOPQR'
         ORG   LOW2UPR+C'S'-X'40'
         DC    C'STUVWXYZ'
         ORG   ,
         DROP  ,
*
RACROUTM RACROUTE REQUEST=FASTAUTH,                                    X00886000
               RELEASE=7706,           1.13                            X
               WKAREA=,                FASTAUTH WORK AREA              X
               CLASS=,                 CLASS NAME (APPL)               X00886100
               ENTITY=,                RESOURCE NAME (SAILPAPL)        X00886300
               APPL=,                  REQUESTING APPLICATION.         X00886600
               WORKA=,                 RACROUTE WORK AREA              X00886900
               ACEE=,                  ACEE ADDRESS.                   X00886900
               LOG=ASIS,               LOG THE REQUEST                 X
               MF=L
RACROUML EQU   *-RACROUTM
*--------------------------------------------------------------------
**   DSECTS
*--------------------------------------------------------------------
*
**   WORK AREA
*
WA       DSECT
SA       DS    18F                     SAVE AREA.
DBGLEVEL DS    XL1                     DEBUG LEVEL
*
CLASNAME DS    CL8
LOGPARM  DS    CL8                    LOGASIS OR LOGNONE
ENTITY   DS    CL256                                                    01010000
ACEEADDR DS    A(0)
FAWKAREA DS    16F
*
ISSUP    DS    X                       0 - SUP   X'FF' - PROB
*
         DS    0D
RACROUTL DS    XL(RACROUML)            AREA FOR RACROUTE.
*
         DS    0D
RAREA    DS    XL512                   USED BY CTSADBG.
DBGAREA  DS    XL2048                                                   01950100
WALEN    EQU   *-WA
*
**   PARAMETERS BLOCK.
*
PRM      DSECT                         PARAMETERS
PRMCLASS DS    A   1. (INPUT)    -> CLASS NAME.
PRMLOG   DS    A   2. (INPUT)    -> LOGASIS OR LOGNONE
PRMENTTY DS    A   3. (INPUT)    -> ENTITY NAME.
PRMACEE  DS    A   4. (INPUT)    -> ACEE ADRESS FOR RACROUTE.
PRMDBG   DS    A   5. (INPUT)    -> DEBUG LEVEL ( F )
PRMFRC   DS    A   6. (OUTPUT)   -> RACROUTE RETURN CODE.
PRMFRRC  DS    A   7. (OUTPUT)   -> RACROUTE RACF RETURN CODE.
PRMFRRS  DS    A   8. (OUTPUT)   -> RACROUTE RACF REASON CODE.
*
         CTSEQUR ,
*
         END
