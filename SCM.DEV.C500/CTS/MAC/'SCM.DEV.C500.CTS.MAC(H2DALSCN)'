         MACRO                                                          00010000
         H2DALSCN &PRMNAME,&PAIR1,&PAIR2, ... ETC.                     +00020000
               &SCNCTR=0,&PARM=,&RET=C,&MSG=NO                          00030000
.*                                                                      00040000
.*--------------------------------------------------------------------* 00050000
.*       D Y N A M I C   A L L O C A T I O N     M A C R O            * 00060000
.*       THIS MACRO SCANS A SUPPLIED LIST FOR A GIVEN VALUE, AND      * 00070000
.*       RETURNS A VALUE IN A GLOBAL SET SYMBOL.                      * 00080000
.*                                                                    * 00090000
.*       MACRO PARAMETERS ARE:                                        * 00100000
.*       &PRMNAME - A NAME WHICH IS ASSOCIATED WITH THE GIVEN VALUE.  * 00110000
.*                  THIS PARAMETER IS OPTIONAL. IT SERVES ONLY IN AN  * 00120000
.*                  ERROR MESSAGE.                                    * 00130000
.*       &PAIRI   - AN ELEMENT OF THE SUPPLIED SCAN LIST. UP TO 254   * 00140000
.*                  PAIRS ARE ALLOWED.                                * 00150000
.*                  A PAIR CONSISTS OF (STRING,RETURN-VALUE).         * 00160000
.*                  STRING MAY BE ANY LENGTH, INCLUDING A NULL STRING.* 00170000
.*                  RETURN-VALUE MAY BE OMITTED, IN WHICH CASE A NULL * 00180000
.*                  OR ZERO VALUE IS RETURNED.                        * 00190000
.*                  ELSE, RETURN-VALUE MAY BE ANY VALUE VALID FOR     * 00200000
.*                  SETA OR SETC OPERATIONS, DEPENDINGON &RET.        * 00210000
.*                  N O T E :  THIS PARAMETER LIST IS OPTIONAL.       * 00220000
.*                  WHEN NO PAIR IS CODED,  &SCNCTR IS NUMBER OF      * 00230000
.*                  ELEMENTS SUPPLIED IN &H2DLIST+&H2DLRTV VECTORS.   * 00240000
.*                  &H2DLIST CONTAINS STRINGS; &H2DLRTV CONTAINS      * 00250000
.*                  THE MATCHING RETURN-VALUES.                       * 00260000
.*       &SCNCTR  - SUPPLIES NUMBER OF ELEMENTS IN &H2DLIST+&H2DLRTV. * 00270000
.*                  DEFAULT IS 0.                                     * 00280000
.*       &PARM=   - THE GIVEN VALUE. THERE ARE NO RESTRICTIONS ON     * 00290000
.*                  TYPE, SIZE OR VALUE OF THIS PARAMETER.            * 00300000
.*       &RET=    - TYPE OF GLOBAL VARIABLE SYMBOL IN WHICH VALUE IS  * 00310000
.*                  RETURNED.                                         * 00320000
.*                  RET=A -  VALUE IS RETURNED IN A SETA SYMBOL.      * 00330000
.*                  RET=C -  VALUE IS RETURNED IN A SETC SYMBOL.      * 00340000
.*       &MSG     - INDICATES WHETER AN ERROR MESSAGE SHOULD BE       * 00350000
.*                  ISSUED WHEN SCAN FAILS.                           * 00360000
.*                  MSG=NO  - DO NOT ISSUE MESSAGE.                   * 00370000
.*                  MSG=YES - ISSUE MESSAGE (WITH &PRMNAME / &PARM).  * 00380000
.*                  MSG=ANY VALUE OTHER THAN 'NO' IS EQU TO MSG=YES.  * 00390000
.*                                                                    * 00400000
.*       WHEN SCAN FAILS A MESSAGE MAY BE ISSUED, &H2DLERR IS SET TO  * 00410000
.*       '1' TO INDICATE FAILURE, &H2DLRTA IS SET TO 0 AND &H2DLRTC   * 00420000
.*       IS SET TO A NULL STRING.                                     * 00430000
.*       WHEN RETURN-VALUE REQUESTED IS A NULL STRING, &H2DLNUL       * 00440000
.*       IS SET TO '1', &H2DLRTA IS SET TO 0 AND &H2DLRTC IS SET      * 00450000
.*       TO A NULL STRING.                                            * 00460000
.*--------------------------------------------------------------------* 00470000
.*                                                                    * 00480000
.*------------                                                          00490000
.*       SET  SYMBOL  DEFINITIONS                                       00500000
.*------------                                                          00510000
         GBLC  &H2DLIST(254)                 STRING ELEMENTS, OPTIONAL  00520000
         GBLC  &H2DLRTV(254)                 RETURN-VALUE ELEMENTS, OPT 00530000
         GBLA  &H2DLRTA                      FOR RETURNED VALUE, RET=A  00540000
         GBLC  &H2DLRTC                      FOR RETURNED VALUE, RET=C  00550000
         GBLB  &H2DLERR,&H2DLNUL             RETURN INDICATIONS         00560000
.*                                                                      00570000
         LCLA  &I,&N                                                    00580000
.*------------                                                          00590000
.*       INIT  SET  SYMBOLS                                             00600000
.*------------                                                          00610000
&H2DLERR SETB  0                                                        00620000
&H2DLNUL SETB  0                                                        00630000
&H2DLRTC SETC  ''                                                       00640000
&H2DLRTA SETA  0                                                        00650000
.*------------                                                          00660000
.*       SELECT  LIST  TO  BE  SCANNED                                  00670000
.*------------                                                          00680000
         AIF   (N'&SYSLIST GT 1).SCNLST                                 00690000
         AIF   (&SCNCTR GT 0).SCNVEC                                    00700000
         AGO   .ERNPAIR                                                 00710000
.*------------                                                          00720000
.*       SCAN  THE  SPECIFIED  LIST  (SYSLIST)                          00730000
.*------------                                                          00740000
.SCNLST  ANOP                                                           00750000
&N       SETA  N'&SYSLIST                                               00760000
&I       SETA  2                             INDEX OVER 1ST ELEMENT OF  00770000
.*                                           SYSLIST, WHICH IS PRMNAME  00780000
.LSTLP   ANOP                                                           00790000
         AIF   ('&SYSLIST(&I,1)' EQ '&PARM').LSTFND                     00800000
&I       SETA  &I+1                                                     00810000
         AIF   (&I LE &N).LSTLP                                         00820000
         AGO   .ERNOFND                                                 00830000
.LSTFND  ANOP                                                           00840000
         AIF   (N'&SYSLIST(&I) LT 2).ERETNUL                            00850000
         AIF   ('&SYSLIST(&I,2)' EQ '').ERETNUL                         00860000
         AIF   ('&RET' EQ 'A').LSTRETA                                  00870000
         AIF   ('&RET' EQ 'C').LSTRETC                                  00880000
         AGO   .ERINVRT                                                 00890000
.LSTRETA ANOP                                                           00900000
&H2DLRTA SETA  &SYSLIST(&I,2)                RETURN NUMBER              00910000
         MEXIT                                                          00920000
.LSTRETC ANOP                                                           00930000
&H2DLRTC SETC  '&SYSLIST(&I,2)'              RETURN STRING              00940000
         MEXIT                                                          00950000
.*------------                                                          00960000
.*       SCAN  VECTOR  H2DLIST                                          00970000
.*------------                                                          00980000
.SCNVEC  ANOP                                                           00990000
&I       SETA  1                             INIT INDEX                 01000000
.VECLP   ANOP                                                           01010000
         AIF   ('&H2DLIST(&I)' EQ '&PARM').VECFND                       01020000
&I       SETA  &I+1                                                     01030000
         AIF   (&I LE &SCNCTR).VECLP                                    01040000
         AGO   .ERNOFND                                                 01050000
.VECFND  ANOP                                                           01060000
         AIF   ('&H2DLRTV(&I)' EQ '').ERETNUL                           01070000
         AIF   ('&RET' EQ 'A').VECRETA                                  01080000
         AIF   ('&RET' EQ 'C').VECRETC                                  01090000
         AGO   .ERINVRT                                                 01100000
.VECRETA ANOP                                                           01110000
&H2DLRTA SETA  &H2DLRTV(&I)                  RETURN NUMBER              01120000
         MEXIT                                                          01130000
.VECRETC ANOP                                                           01140000
&H2DLRTC SETC  '&H2DLRTV(&I)'                RETURN STRING              01150000
         MEXIT                                                          01160000
.*------------                                                          01170000
.*       ERROR  HANDLING                                                01180000
.*------------                                                          01190000
.ERNOFND ANOP                                SCAN FOR &PARM FAILD       01200000
         AIF   (T'&MSG EQ 'O').OVMSG                                    01210000
         AIF   ('&PRMNAME' EQ '').NOPRMSG                               01220000
         MNOTE 8,'*** H2DALSCN *** "&PRMNAME=&PARM" IS ILLEGAL'         01230000
         AGO   .OVMSG                                                   01240000
.NOPRMSG ANOP                                                           01250000
         MNOTE 8,'*** H2DALSCN *** "&PARM" IS ILLEGAL VALUE'            01260000
.OVMSG   ANOP                                                           01270000
&H2DLERR SETB  1                                                        01280000
         MEXIT                                                          01290000
.ERETNUL ANOP                                RETURN NULL INDICATION     01300000
&H2DLNUL SETB  1                                                        01310000
         MEXIT                                                          01320000
.ERNPAIR ANOP                                NO PAIR SPECIFIED          01330000
         MNOTE 32,'*** H2DALSCN *** SCAN LIST CONTAINS NO PAIR'         01340000
         MEXIT                                                          01350000
.ERINVRT ANOP                                RET IS INVALID             01360000
         MNOTE 32,'*** H2DALSCN *** RET=&RET IS INVALID'                01370000
         MEXIT                                                          01380000
         MEND                                                           01390000
