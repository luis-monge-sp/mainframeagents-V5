.*   ARLEND  MACRO 26.6.90  (SOURCE:N.W)
.*   PARAMETERS:
.*     RC       - IF WE WANT TO SAVE RETURN CODE FROM R15
.*     FREE     - YES - FREEMAIN; NO - DON'T FREE
.*     FREETYPE - TYPE OF FREEMAIN (R,RC,RU) RELEVENT IF FREE = YES
.*     LEN      - LENGTH OF WORKAREA TO FREE RELEVENT IF FREE = YES
.*     AREA     - THE POINTER TO AREA TO BE FREE
.*     ERRLABEL - LABEL TO JUMP OVER IF FAILED FREEMAIN
.*
.*   NOTICE: AREA FOR FREE MAIN CAN'T BE R13!!
.*
         MACRO
&NAME    ARLEND &RC=YES,&FREE=NO,&FREETYPE=RC,&LEN=,&AREA=,            *
               &ERRLABEL=FAILFREE,&SAVEREG=R5
         AIF   ('&AREA' EQ '').CONT
         AIF   ('&AREA'(1,1) EQ '(').REG
         L     R13,&AREA+4
         AGO   .FREE
.REG     ANOP
&TMP     SETC '&AREA'(2,4)
         L     R13,4(,&TMP         GET ADDRESS OF PREVIOUS SAVE AREA
.FREE    AIF   ('&FREE' EQ 'NO').CONT
         ARLFREEM FREETYPE=&FREETYPE,LEN=&LEN,AREA=&AREA,              *
               ERRLABEL=&ERRLABEL,SAVEREG=&SAVEREG
.CONT    ANOP
         AIF   ('&RC' NE 'YES').CONT1
         ST    R15,16(,R13)
.CONT1   ANOP
         LM    R14,R12,12(R13)
         BR    R14
         MEND
