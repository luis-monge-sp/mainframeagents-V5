         MACRO
&NAME    CTSAAMD  &AMODE,&AMODSAV=,&CMDTAB=,&R=R14
.*********************************************************************
.***                                                              ****
.***   SAVE, TEST, SET AND RESTORE ADDTRESSING MODE.              ****
.***                                                              ****
.***   MF CONNECTOR RELESAE 4.0.XX                                ****
.***                                                              ****
.***   (C) COPYRIGHT 2015 SAILPOINT TECHNOLOGIES, INC.,           ****
.***   ALL RIGHTS RESERVED.                                       ****
.***                                                              ****
.*********************************************************************
.*********************************************************************
.***                                                              ****
.***  SAVE/SET AMODE USING THE TAM/SAM COMMANDS.                  ****
.***                                                              ****
.***  PARAMETERS:                                                 ****
.***  -----------                                                 ****
.***     AMODE  : SAVE    - SAVE CURRENT AMODE IN AMODSAV (AS CC) ****
.***              RESTORE - SWITCH AMODE TO SAVED AMODE.          ****
.***              24      - SET AMODE 24                          ****
.***              31      - SET AMODE 31                          ****
.***              64      - SET AMODE 64                          ****
.***              TEST    - TEST CURRENT AMODE AND SET CC:        ****
.***                         BC  0 -> 24                          ****
.***                         BC  1 -> 31                          ****
.***                         BC  3 -> 64                          ****
.***              TESTSAVD- TEST THE SAVED AMODE:                 ****
.***                         BC  0 -> 24                          ****
.***                         BC  1 -> 31                          ****
.***                         BC  3 -> 64                          ****
.***              DEFCMDT - DEFINE THE COMMANDS TABLE.            ****
.***                        IF RESTORE WILL BE USED, A CALL TO    ****
.***                        THE MACRO SHOULD BE ADDED TO THE      ****
.***                        CONSTANTS AREA IN ORDER TO DEFINE THE ****
.***                        COMMANDS TABLE.                       ****
.***                                                              ****
.***     AMODSAV: NAME - 4-BYTES LENGTH VARIABLE:                 ****
.***              (RX) - REGISTER NUMBER.                         ****
.***                     MANDATORY FOR SAVE, RESTORE AND TESTSAVD.****
.***                     OPTIONAL FOR 24, 31, AND 64.             ****
.***                     - FOR SAVE, THE CURRENT AMODE (CC) IS    ****
.***                       SAVE DIN THIS FIELD.                   ****
.***                     - WHEN SPECIFIED WITH 24, 31, 64 OR SAVE ****
.***                       THE CURRENT AMODE (CC) WILL BE SAVED   ****
.***                       IN THIS FIELD.                         ****
.***                     - FOR RESTORE, THE CC SAVED IN THIS      ****
.***                       FIELD IS USED FOR RESTORING THE        ****
.***                       AMODE.                                 ****
.***                     - FOR TESTSAVD, THE CC SAVED IN THIS     ****
.***                       FIELD IS SET IN PSW TO BE CHECKED BY   ****
.***                       THE CALLER.                            ****
.***                                                              ****
.***                                                              ****
.***     CMDTAB : NAME OF COMMANDS TABLE TO CREATE (FOR DEFCMDT)  ****
.***              OR TO BE USED (FOR RESTORE).                    ****
.***                                                              ****
.***     R      : WORK REGISTER.                                  ****
.***                                                              ****
.***                                                              ****
.***  CALLING EXMAPLES                                            ****
.***  ----------------                                            ****
.***     CTSAAMD SAVE,AMODSAV=AMODE     SAVE CURRENT AMODE.       ****
.***                                                              ****
.***     CTSAAMD RESTORE,AMODSAV=AMODE,CMDTAB=AMDCMD              ****
.***                                    RESTORE TO SAVED AMODE.   ****
.***                                                              ****
.***     CTSAAMD RESTORE,AMODSAV=AMODE  RESTORE TO SAVED AMODE.   ****
.***                                    THE COMMANDS TABLE WILL   ****
.***                                    BE DEFINED IN THE CODE.   ****
.***                                                              ****
.***     CTSAAMD 31,AMODSAV=AMODE       SAVE CURRENT AND SWITCH   ****
.***                                    TO 31.                    ****
.***                                                              ****
.***                                                              ****
.***     CTSAAMD 64,R=R1                SWITCH TO 64 AND USE R1   ****
.***                                    AS WORK REGISTER.         ****
.***                                                              ****
.***     CTSAAMD TEST                   BRANCH ACCODING TO        ****
.***     BC    0,AM24                   CURRENT AMODE.            ****
.***     BC    1,AM31                                             ****
.***     BC    3,AM64                                             ****
.***                                                              ****
.***     CTSAAMD DEFCMDT,CMDTAB=AMDCMD  DEFINE THE COMMANDS TABLE ****
.***                                    FOR RESTORE.              ****
.***                                                              ****
.***                                                              ****
.***  LOGIC                                                       ****
.***  -----                                                       ****
.***  THE MACRO USES THE TAM, SAM24, SAM31 AND SAM64 Z/OS         ****
.***  COMMANDS TO TEST AND SET AMODE.                             ****
.***                                                              ****
.***  THE TAM (TEST ADDRESSING MODE) SETS A CONDITION CODE IN     ****
.***  PSW ACCORDING TO THE CURRENT AMODE.                         ****
.***  CC 0 - AMODE 24.                                            ****
.***  CC 1 - AMODE 31.                                            ****
.***  CC 3 - AMODE 64.                                            ****
.***                                                              ****
.***  WHEN CALLED WITH TEST, THE MACRO ISSUES THE TAM COMMAND     ****
.***  AND THE CALLER SHOULD TEST THE CC.                          ****
.***                                                              ****
.***  WHEN CALLED WITH SAVE, OR WHEN CALLED WITH 24, 31 OR 64     ****
.***  WITH A NAME SPECIFIED FOR AMODSAV, THE MACRO ISSUES THE     ****
.***  TAM COMMAND AND SAVES THE CC IN THE FIELD POINTED BY        ****
.***  AMODSAV.                                                    ****
.***                                                              ****
.***  WHEN CALLED WITH RESTORE, THE MACRO USES THE SAVED CC TO    ****
.***  CALCULATE AN INDEX TO THE COMMANDS TABLE, AND EXECUTES      ****
.***  THE COMMAND.                                                ****
.***                                                              ****
.***  WHEN CALLED WITH TESTSAVD, THE MACRO TAKES THE SAVED CC     ****
.***  AND SETS IT IN THE PSW TO BE CHECKED BY THE CALLER.         ****
.***                                                              ****
.***                                                              ****
.***  THE COMMANDS TABLE                                          ****
.***  ------------------                                          ****
.***  THE COMMANDS TABLE HAS TO BE CREATED IF RESTORE WILL BE     ****
.***  USED. THIS TABLE CONTAINS THE SAM24, SAM31 AND SAM64        ****
.***  COMMANDS IN ENTIRES CORRESPONDING TO THE CC RETURNED BY     ****
.***  THE TAM COMMAND.                                            ****
.***                                                              ****
.***                                                              ****
.*********************************************************************
.*
.* CHANGE ACTIVITY:
.* ----------------
.*
.* ID      DATE     NAME DESCRIPTION
.* ------  -------- ---- ------------------------------------------
.* BS10111 22.11.20 NY   NEW
.*
.*********************************************************************
         GBLC  &GCMD
         LCLC  &LCMD,&SC,&SP,&RA
         AIF   (T'&AMODE EQ 'O').NOAMD
         AIF   ('&AMODE' EQ 'DEFCMDT').DEFTAB
         AIF   (T'&NAME EQ 'O').NONAME
&NAME    DS    0H
.NONAME  ANOP
.*
.**  PREPARE COMMANDS ACCORDING TO SAVE ENTITY (WORD / REG)
.*
         AIF   (T'&AMODSAV EQ 'O').VERTYPE
         AIF   ('&AMODSAV'(1,1) EQ '(').AMR
&SC      SETC  'ST'
&SP      SETC  '&R,&AMODSAV'
&RA      SETC  ''
         AGO   .VERTYPE
.AMR     ANOP
&SC      SETC  'LR'
&SP      SETC  '&AMODSAV,&R'
&RA      SETC  'R'
.*
.VERTYPE ANOP
         AIF   ('&AMODE' EQ 'TEST').TESTC
         AIF   ('&AMODE' EQ '24').CHKSAVE
         AIF   ('&AMODE' EQ '31').CHKSAVE
         AIF   ('&AMODE' EQ '64').CHKSAVE
         AIF   ('&AMODE' EQ 'SAVE').VERAMDS
         AIF   ('&AMODE' EQ 'RESTORE').VERAMDS
         AIF   ('&AMODE' EQ 'TESTSAVD').VERAMDS
         AGO   .ERRUNKN
.*
.**  VERIFY AMODSAV - OPTIONAL FOR 24/31/64.  MANDATORY FOR THE REST.
.*
.CHKSAVE ANOP
         AIF   (T'&AMODSAV NE 'O').SAVE  NEED TO SAVE
         AGO   .SET                    JUST SET
.VERAMDS ANOP
         AIF   (T'&AMODSAV EQ 'O').ERRAMDS AMODSAV IS MISSING.
         AIF   ('&AMODE' EQ 'SAVE').SAVE
         AIF   ('&AMODE' EQ 'RESTORE').RST
         AIF   ('&AMODE' EQ 'TESTSAVD').TESTS
         AGO   .ERRUNKN
.*
.**  SAVE
.*
.SAVE    ANOP
         TAM   ,                   TEST ADDRESSING MODE
         IPM   &R                  RETRIEVE CC AND PROGRAM MASK
         NILH  &R,X'3000'          LEAVE ONLY CC.
         &SC   &SP                 SAVE TAM TEST RESULTS.
         AIF   ('&AMODE' EQ 'SAVE').OUT
.*
.**  SET
.*
.SET     ANOP
         AIF   ('&AMODE' EQ '24').SET24
         AIF   ('&AMODE' EQ '31').SET31
         AIF   ('&AMODE' EQ '64').SET64
         AGO   .ERRUNKN
.SET24   ANOP
         SAM24                     SET AMODE 24.
         AGO   .OUT
.SET31   ANOP
         SAM31                     SET AMODE 31.
         AGO   .OUT
.SET64   ANOP
         SAM64                     SET AMODE 64.
         AGO   .OUT
.*
.**  TEST CURRENT AMODE.
.*
.TESTC   ANOP
         TAM   ,                   TEST ADDRESSING MODE
         AGO   .OUT
.*
.**  TEST SAVED AMODE.
.*
.TESTS   ANOP
         IPM   &R
         NILH  &R,X'0F00'          LEAVE ONLY PROGRAMMASK
         O&RA  &R,&AMODSAV         R = SAVED AMODE + PM.
         SPM   &R                  SET TAM CC IN PSE.
         AGO   .OUT
.*
.**  RESTORE
.*
.RST     ANOP
         AIF   (T'&CMDTAB NE 'O').RSTUCM  IF PROVIDED - USE.
         AIF   (T'&GCMD  NE 'O').RSTUGL   ELSE, IF ALREADY CREATED, USE
&GCMD    SETC  'CMDT'.'&SYSNDX'           ELSE, CREATE INLINE
&LCMD    SETC  '&GCMD'
         J     &LCMD.CME               BRANCH AROUND TABLE.
         AGO   .DEFCM
.RSTUGL  ANOP
&LCMD    SETC  '&GCMD'
         AGO   .DORST
.RSTUCM  ANOP
&LCMD    SETC  '&CMDTAB'
.DORST   ANOP
         L&RA  &R,&AMODSAV         R = SAVED AMODE (TAM RC)
         SRL   &R,28               MOVE CC TO THE RIGHT.
         MHI   &R,&LCMD.CEL        USE THE RC (0/1/3) AS INDEX
*                                  TO THE SET AMODE COMMANDS TABLE.
         EX    0,&LCMD.TB(&R)      RESTORE AMODE BY EXECUTING THE
*                                  APPROPRIATE SAM COMMAND.
         AGO   .OUT
.*
.**   DEFINE THE CMDTAB FOR RESTORE.
.*
.DEFTAB  ANOP
         AIF   (T'&CMDTAB EQ 'O').ERRNOCM
&LCMD    SETC  '&CMDTAB'
.DEFCM   ANOP
*
**  SAM COMMAND TABLE TO BE USED TO RESTORE THE CALLER AMODE.
**  THE APPROPRIATE COMMAND IS EXECUTED ACCORDING TO THE RC
**  RECEIVED AS A RESULT OF THE TAM COMMAND PERFORMED AT THE
**  BEGINING OF THE PROGRAM.
*
&LCMD.TB DS    0H                  FORCE COMMAND ALIGNMENT.
&LCMD.24 SAM24                       TAM RC = 0
&LCMD.CEL EQU L'&LCMD.24
&LCMD.31 SAM31                       TAM RC = 1
         DS    XL(&LCMD.CEL)'00' NO TAM RC.
&LCMD.64 SAM64                       TAM RC = 3
&LCMD.CME EQU *
         AIF   ('&AMODE' EQ 'RESTORE').DORST
         AGO   .OUT
.*
.**   ERRORS
.*
.NOAMD   ANOP
         MNOTE 8,'AMODE PARAMETER IS MISSING'
         AGO   .OUT
.*
.ERRAMDS ANOP
         MNOTE 8,'AMODSAV PARAMETER IS MISSING OR EMPTY'
         AGO   .OUT
.*
.ERRUNKN ANOP
         MNOTE 8,'UNKNOWN REQUEST &AMODE'
         AGO   .OUT
.*
.ERRNOCM ANOP
         MNOTE 8,'CMDTAB PARAMETER IS MISSING OR EMPTY'
         AGO   .OUT
.*
.**   RETURN
.*
.OUT     ANOP
         MEXIT
         MEND
