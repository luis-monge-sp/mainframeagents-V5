         MACRO                                                          00010000
         CTSAPCC  &P,&R0=0,&R1=0,&R15=0,&WORKREG=R2,&XMMHDR=            00020000
.*********************************************************************  00030000
.***                                                              ****  00040000
.***       CONTROL-SA PC ROUTINES CALLING                         ****  00050000
.***                                                              ****  00060000
.***       CONTROL-SA  RELEASE 1.2.1                              ****  00070000
.***                                                              ****  00080000
.***       LICENCED MATERIAL - NEW DIMENSION SOFTWARE LTD.        ****  00090000
.***                                                              ****  00100000
.*********************************************************************  00110000
.* IS10085 29.05.13 NURITY ABEND S0D3 WHEN RETURNING TO A REUABLE    *  00110101
.*                         ADDRESS SPACE. (BS10034)                  *  00110200
.*********************************************************************  00110300
.*********************************************************************  00110400
.*                                                                   *  00110500
.*                      IS10085 DESCRIPTION                          *  00110600
.*                      ===================                          *  00110700
.*                                                                   *  00110800
.* BACKGROUND                                                        *  00110900
.* ----------                                                        *  00111000
.* REUSABLE ADDRESS SPACES ARE EASILY SUPPORTED WITH STACKING PCS    *  00112000
.* WHERE THE ENVIRONMENT IS SAVED IN THE LINKGAE STACK WHEN THE PC   *  00113000
.* RECEIVES CONTROL AND RESTORED WHEN THE PC RETURNS WITH THE PR     *  00113100
.* COMMAND.                                                          *  00113200
.* UNFORTUNATELY, IT IS TOO COMPLICATED TO CHANGE OUR PCS TO BE      *  00113300
.* STACKING PCS. THE PCS CAN BE CALLED FROM EXITS FROM DIFFERENT     *  00113400
.* ENVIRONMENTS AND WE CANNOT GUARANTEE THAT ALL ENVIRONMENTS ARE    *  00113500
.* CHNAGED TO USE STACKING PCS.                                      *  00113600
.*                                                                   *  00113700
.* THEREFORE, WE ADD THE ABILITY TO BE CALLED FROM A REUSABLE        *  00113800
.* ADDRESS SPACE BY USING THE PTI, SSAIR, ETC. COMMANDS INSTEAD OF   *  00113900
.* PT, SSAR, ETC. WITH PT, SSAR, ETC. YOU NEED TO PROVIDE THE ASN    *  00114000
.* (ADDRESS SPACE NUMBER) OF THE ADDRESS SPACE, WHILE WITH PTI,      *  00114100
.* SSAIR ETC. YOU NEED TO PROVIDE THE ASN AND THE ASTEIN             *  00114200
.* (ASN-SECOND-TABLE-ENTRY INSTANCE NUMBER) WHICH UNIQELY IDENTIFY   *  00114300
.* THE INSTANCE OF THE REUSABE ADDRESS SPACE.                        *  00114400
.*                                                                   *  00114500
.*                                                                   *  00114600
.* LOGIC                                                             *  00114700
.* -----                                                             *  00114800
.* THE ASTEIN IN PROVIDED THE HIGH-ORDET HALF OF THE REGISTER (BITS  *  00114900
.* 0-31). THIS REQUIRES SAVING THE FULL 64-BITS REGISTERS SO THIS    *  00115000
.* MACRO WAS CHANGED TO SAVE THE ENVIRONMENT USING BAKR. BAKR SAVES  *  00115100
.* THE 64-BIT REGISTERS, THE ERTURN PSW CONTAINING THE RETURN        *  00115200
.* ADDRESS AD AMODE FROM ITS FIRST OPERAND, AND THE PRIMARY AND      *  00115300
.* SECONDARY ADDRESS SPACE NUMBER AND INSTANCES.                     *  00115400
.*                                                                   *  00115500
.* THE MACRO RETURNS USING THE PR COMMAND WHICH RESTORES THE 64-BIT  *  00115600
.* REGISTERS. BUT, THE PR DONE ON AN ENTRY CREATED BY BAKR DOES NOT  *  00115700
.* RESTORE THE SECONDARY ADDRESS SPACE (CHANGED BY THE CALL TO THE   *  00115800
.* PC). THEREFORE, AFTER THE PC RETURNS, AND BEFORE THE PR COMMAND   *  00115900
.* WE RESTORE THE SECOND ADDRESS SPACE NUMBER AND INSTANCE FROM THE  *  00116000
.* LINKAGE STACK ENTRY AND RESET THE SECONDARY ADDRESS SPACE USING   *  00116100
.* THE SSAIR COMMAND.                                                *  00116200
.*                                                                   *  00116300
.*********************************************************************  00116400
*IS10085 STM   R14,R12,12(R13)    SAVE REGISTERS                        00116500
*IS10085 ESAR  &WORKREG                                                 00116600
*IS10085 ST    &WORKREG,16(,R13)                                        00116700
         LA    &WORKREG,PCE&SYSNDX WHERE TO RETURN AFTER PR.    IS10085 00116800
         BSM   &WORKREG,0          ...ANDA ADD AMODE.           IS10085 00116900
         BAKR  &WORKREG,0          SAVE THE ENVIRONMENT.        IS10085 00117000
*                                                                       00118000
         LA    &WORKREG,&XMMHDR                                         00119000
         L     &WORKREG,XMMLXIND-XMMHDR(&WORKREG)                       00120000
         LA    R0,&P                                                    00130000
         OR    &WORKREG,R0                                              00140000
         LA    R0,&R0                                                   00150000
         LA    R1,&R1                                                   00160000
         LA    R15,&R15                                                 00170000
         PC    0(&WORKREG)                                              00180000
*IS10085 L     R14,12(,R13)                                             00190000
*IS10085 L     &WORKREG,16(,R13)                                        00200000
*IS10085 SSAR  &WORKREG                                                 00210000
*IS10085 LM    R2,R12,28(R13)                                           00220000
         LHI   &WORKREG,0          SET EXTRACT CODE = 0.        IS10085 00230000
         ESTA  R4,&WORKREG         R4 = PKM + SASN              IS10085 00240000
*                                  R5 = EAX + PASN              IS10085 00250000
         LHI   &WORKREG,5          SET EXTRACT CODE = 5.        IS10085 00250100
         ESTA  R4,&WORKREG         R4 = S-ASTEIN + PKM + SASN   IS10085 00250200
*                                  R5 = P-ASTEIN + EAX + PASN   IS10085 00250300
         SSAIR R4                  RESTORE SECONDAY ASN (WILL   IS10085 00250400
*                                  NOT BE DONE BY PR BECASUE IT IS10085 00250500
*                                  IS A BAKR ENTRY).            IS10085 00250600
         PR    ,                   RESTORE REGISTERS.           IS10085 00250700
PCE&SYSNDX DS  0H                                               IS10085 00250800
         MEND                                                           00250900
