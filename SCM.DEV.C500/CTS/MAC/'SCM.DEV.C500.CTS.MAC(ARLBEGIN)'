.*   ARLBEGIN MACRO 27.8.90  (SOURCE:N.W)
.*
.*  DEFINE START OF PROGRAM PRINTS OUT DATE OF COMPILATION AND
.*  NAME OF PROGRAMMER DATE OF LAST CHANGE AND REGISTER EQUATES.
.*  SAVES ALL THE REQUESTED REGISTER AND DEFINE A BASE REGISTER
.*  FOR THE PROGRAM (UP TO 3) THERE IS ALSO THE OPTION TO DO GETMAIN
.*  FROM THIS MACRO (IF GET=YES)
.*  PARAMETERS:
.*     PROGRMER - NAME OF THE PROGRAMER
.*     BASE1    - FIRST BASE REGISTER OF THE PROGRAM
.*     BASE2    - SECOND BASE REGISTER OF THE PROGRAM IF NEEDED
.*     BASE3    - THIRD BASE REGISTER OF THE PROGRAM IF NEEDED
.*     LASTCHNG - DATE OF LAST UPDATE IN THE PROGRAM
.*     WATYPE   - GET  - GETMAIN FOR WORK AREA
.*              - PARM - WORK AREA AS PARAMETER TO THE PROGRAM
.*              - DEF  - WORK AREA DEFINED IN THE PROGRAM
.*     GETTYPE  - TYPE OF GETMAIN (R,RC,RU) RELEVENT IF WATYPE=GET
.*     LEN      - LENGTH OF WORKAREA RELEVENT IF WATYPE=GET
.*     ERRLABEL - LABEL TO JUMP OVER IF FAILED IN GETMAIN
.*     WAPTR    - REGISTER TO POINT ON THE WORKAREA
.*     WANAME   - NAME OF DSECT OF WORK AREA
.*     PARMNUM  - NUMBER OF PARAMETER IF WATYPE=PARM
.*
        MACRO
&NAME    ARLBEGIN &PROGRMER=,&BASE1=R12,&BASE2=,&BASE3=,               *
               &LASTCHNG=,&WATYPE=GET,&GETTYPE=RC,&LEN=,               *
               &ERRLABEL=FAILGET,&WAPTR=R13,&WANAME=WA,&PARMNUM=1
         PRINT GEN
&NAME    CSECT
*
* REGISTERS EQUATES
*
         ARLEQUR
         STM   R14,R12,12(R13)
*
         BALR  &BASE1,0
         USING BASE,&BASE1
BASE     DS    0H
         AIF ('&BASE2' EQ '').CONT
         USING BASE+4096,&BASE2
         LA    &BASE2,4095(,&BASE1)
         LA    &BASE2,1(,&BASE2)
         AIF ('&BASE3' EQ '').CONT
         USING BASE+8192,&BASE3
         LA    &BASE3,4095(,&BASE2)
         LA    &BASE3,1(,&BASE3)
.CONT    ANOP
         B     S&SYSNDX
         DC    C'&NAME ,&SYSDATE ,&LASTCHNG ,&PROGRMER'
         DS    0D                            WAS 0H
S&SYSNDX EQU   *
         AIF ('&WATYPE' EQ 'GET').GET
         AIF ('&WATYPE' EQ 'DEF').DEF
         AIF ('&WATYPE' EQ 'PARM').PARM
         AGO .END
.*
.PARM    ANOP
         L     R2,4*(&PARMNUM-1)(R1)
         AGO   .NEXT
.*
.GET     ANOP
         LR    R3,R1                  SAVE VALUE OF R1
         ARLGETM   GETTYPE=&GETTYPE,LEN=&LEN,ERRLABEL=&ERRLABEL
         LR    R2,R1                  SAVE POINTER TO AREA
         LR    R1,R3                  RESTORE R1
         AGO   .NEXT
.*
.DEF     ANOP
         LA    R2,&WANAME
.NEXT    ST    R13,4(,R2)             ADDRESS OF PREVIOUS SAVEAREA
         ST    R2,8(,R13)             ADDRESS OF NEXT SAVEAREA IN OLD
         LR    &WAPTR,R2
         AIF ('&WANAME' EQ '').END
         USING &WANAME,&WAPTR
.END      MEND
