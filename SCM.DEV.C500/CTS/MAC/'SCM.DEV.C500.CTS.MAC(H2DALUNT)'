         MACRO                                                          00010000
         H2DALUNT &UNTNAME,&PAIR1,&PAIR2, ... ETC.                     +00020000
               &KEY=,&#=,&LEN=,&PARM=,&TYPE=                            00030000
.*--------------------------------------------------------------------* 00040000
.*       D Y N A M I C    A L L O C A T I O N    M A C R O            * 00050000
.*       H 2 D A L U N T                                              * 00060000
.*       THIS MACRO DETERMINES WHETHER A GIVEN TEXT UNIT IS REQUESTED;* 00070000
.*       IF REQUESTED, MACRO STORES TEXT UNIT NAME IN A GLOBAL SETC   * 00080000
.*       VECTOR, AND OPTIONALLY DEFINES/MODIFIES TEXT UNIT FIELDS.    * 00090000
.*       INVOKED MACROS:                                              * 00100000
.*       H2DALSCN - SCANS LIST & TRANSLATES. TRANSLATED VALUE IS      * 00110000
.*                  RETURNED IN A GLOBAL SETC OR SETA SYMBOL.         * 00120000
.*       MACRO PARAMETERS ARE:                                        * 00130000
.*       &UNTNAME - MNEUMONIC FOR UNIT NAME. SHOULD BE CODED.         * 00140000
.*       &PAIRI   - AN ELEMENT OF A SCAN LIST. WHEN PARAMETER STANDS  * 00150000
.*                  FOR A BIT COMBINATION, SCAN LIST CONSISTS OF      * 00160000
.*                  ALL POSSIBLE COMBINATIONS.UP TO 254 PAIRS ARE     * 00170000
.*                  ALLOWED. SCAN LIST IS OPTIONAL.                   * 00180000
.*                  A PAIR CONSISTS OF (MNEUMONIC,BIT-COMBINATION) OR * 00190000
.*                  (MNEUMONIC,BIT-NUMBER).                           * 00200000
.*                  MNEUMONIC STANDS FOR THE MATCHING BIT-COMBINATION * 00210000
.*                  OR BIT-NUMBER.                                    * 00220000
.*                  BIT-COMBINATION IS AN HEXADECIMAL NUMBER (EVEN    * 00230000
.*                  NUMBER OF DIGITS).                                * 00240000
.*                  BIT-NUMBER IS A DECIMAL NUMBER RANGING 1 TO 8,    * 00250000
.*                  STANDING FOR A BIT POSITION IN A BYTE (FROM LEFT  * 00260000
.*                  TO RIGHT).                                        * 00270000
.*                  MNEUMONIC MAY BE A NULL STRING.                   * 00280000
.*                  BIT REPRESENTATION MAY NOT.                       * 00290000
.*       &KEY     - UNIT KEY. AN HEXADECIMAL 4 DIGIT NUMBER.          * 00300000
.*                  SHOULD BE CODED.                                  * 00310000
.*       &#       - NUMBER OF LEN/PARM COMBINATIONS. OBLIGATORY.      * 00320000
.*                  #=N       N IS NUMBER OF COMBINATIONS;            * 00330000
.*                  #=(D,L,H) D IS DEFAULT FOR NUMBER OF COMBINATIONS;* 00340000
.*                            L IS LOWEST NUMBER;                     * 00350000
.*                            H IS HIGHEST VALID NUMBER;              * 00360000
.*       &LEN     - LENGTH OF PARAMETER FIELD. OBLIGATORY, WHEN #^=0. * 00370000
.*                  THIS IS MAXIMUM LENGTH.                           * 00380000
.*       &PARM    - VALUE FOR PARAMETER FIELD. OBLIGATORY.            * 00390000
.*                  PARM=    IGNORE UNIT.                             * 00400000
.*                  PARM=*    TEXT UNIT WITH NULL PARM FIELD. LEN/PARM* 00410000
.*                            COMBINATIONS ACCORDING TO #=N OR D.     * 00420000
.*                  PARM=(*,M) TEXT UNIT WITH NULL PARM FIELD. M IS   * 00430000
.*                            NUMBER OF LEN/PARM COMBINATIONS (M>=0). * 00440000
.*                  PARM="    SAME AS PARM=* PLUS A ZERO KEY FIELD.   * 00450000
.*                  PARM=(",M) SAME AS PARM=(*,M) + A ZERO KEY FIELD. * 00460000
.*                  PARM=YES  OR PARM=Y WHEN #=0 MEANS TEXT UNIT WHICH* 00470000
.*                            HAS NO PARM FIELD.                      * 00480000
.*                  PARM=STRING  WHEN PARM FIELD IS A CHARACTER STRING* 00490000
.*                  PARM=NUMBER  WHEN PARM FIELD IS A NUMBER.         * 00500000
.*                  PARM=MNEUMONIC FOR A BIT COMBINATION.             * 00510000
.*                  PARM=(VALUE1,VALUE2,...) FOR MORE THAN 1 PARM.    * 00520000
.*       &TYPE    - PARAMETER TYPE:                                   * 00530000
.*                  C - FOR A CHARACTER STRING;                       * 00540000
.*                  A - FOR A NUMBER;                                 * 00550000
.*                  X - FOR AN HEXADECIMAL BIT COMBINATION;           * 00560000
.*                  B - FOR A BIT-POSITION BIT COMBINATION;           * 00570000
.*                  R - PARM IS TO BE RETURNED IN TEXT UNIT;          * 00580000
.*--------------------------------------------------------------------* 00590000
.*                                                                      00600000
.*--------------------------------------------------------------------* 00610000
.*       S E T    S Y M B O L    D E F I N I T I O N S                * 00620000
.*--------------------------------------------------------------------* 00630000
         GBLA  &H2DLINX                      UNIT VECTOR INDEX          00640000
         GBLB  &H2DLBPT                      ON- FIELDS=PTR             00650000
         GBLB  &H2DLBMF                      ON- MF=E                   00660000
         GBLB  &H2DLBKY                      ON- FIELDS=UNT OR KEY      00670000
         GBLB  &H2DLB#                       ON- FIELDS=UNT             00680000
         GBLB  &H2DLBLN                      ON- FIELDS=UNT OR LEN      00690000
         GBLB  &H2DLBPR                      ON- FIELDS=UNT, LEN OR PRM 00700000
         GBLB  &H2DLBUN                      ON- FIELDS=UNT/KEY/LEN/PRM 00710000
         GBLC  &H2DLUNT(100)                 UNIT VECTOR FOR UNITS REQ  00720000
         GBLC  &H2DLPRF                      PREFIX                     00730000
         GBLC  &H2DLIST(254)                 PARM VECTOR FOR H2DALSCN   00740000
         GBLC  &H2DLRTV(254)                 VALUES VECTOR FOR H2DALSCN 00750000
         GBLB  &H2DLERR,&H2DLNUL             INDICATIONS RETURNED FROM  00760000
.*                                           H2DALSCN                   00770000
         GBLC  &H2DLRTC                      VALUE RETURNED BY H2DALSCN 00780000
         GBLA  &H2DLRTA                      VALUE RETURNED BY H2DALSCN 00790000
         LCLA  &K,&N,&SCNCTR                                            00800000
         LCLA  &NUM,&I,&OFFSET,&J,&MVIOFST,&#DFLT,&#MIN,&#MAX           00810000
         LCLB  &RSRVPRM,&B(8)                                           00820000
         LCLC  &C                                                       00830000
.*--------------------------------------------------------------------* 00840000
.*       I N I T I A L I Z A T I O N    &    C H E C K S              * 00850000
.*--------------------------------------------------------------------* 00860000
.*                                                                      00870000
.*-------------                                                         00880000
.*       STORE  TEXT  UNIT  NAME                                        00890000
.*-------------                                                         00900000
         AIF   (T'&PARM EQ 'O').MEND         TEXT UNIT IS NOT REQUESTED 00910000
         AIF   ('&UNTNAME' EQ '').ERUNTIN    ERROR- NO NAME WAS SPEC.   00920000
         AIF   (NOT &H2DLBPT).OSTUN                                     00930000
&H2DLINX SETA  &H2DLINX+1                                               00940000
&H2DLUNT(&H2DLINX) SETC '&UNTNAME'           NAME WILL BE USED IN PTR   00950000
.OSTUN   ANOP                                                           00960000
         AIF   (NOT &H2DLBUN).MEND           IF NO TEXT UNIT, MEND      00970000
.*-------------                                                         00980000
.*       GATHER  ELEMENTS  IN  SCAN  LIST  VECTOR                       00990000
.*-------------                                                         01000000
&N       SETA  N'&SYSLIST                                               01010000
&SCNCTR  SETA  &N-1                          NUMBER OF PAIRS IN SCNLIST 01020000
         AIF   (&N LT 2).OLIST                                          01030000
&I       SETA  2                                                        01040000
.LSTLOOP ANOP                                                           01050000
         AIF   (N'&SYSLIST(&I) LE 0).LSTNUL                             01060000
&H2DLIST(&I-1) SETC '&SYSLIST(&I,1)'                                    01070000
         AIF   (N'&SYSLIST(&I) EQ 1).LSTRTNL                            01080000
&H2DLRTV(&I-1) SETC '&SYSLIST(&I,2)'                                    01090000
         AIF   (N'&SYSLIST(&I) GT 2).ERLSTIN                            01100000
         AGO   .LSTNXT                                                  01110000
.LSTNUL  ANOP                                                           01120000
&H2DLIST(&I-1) SETC ''                                                  01130000
.LSTRTNL ANOP                                                           01140000
&H2DLRTV(&I-1) SETC ''                                                  01150000
.LSTNXT  ANOP                                                           01160000
&I       SETA  &I+1                                                     01170000
         AIF   (&I LE &N).LSTLOOP                                       01180000
.OLIST   ANOP                                                           01190000
.*-------------                                                         01200000
.*       COMPUTE  NUMBER  OF  PARAMETERS  IN  TEXT  UNIT                01210000
.*-------------                                                         01220000
         AIF   (N'&# EQ 1).#1                # MUST BE N; #=N           01230000
         AIF   (N'&# EQ 3).#3                #=(DFLT,MIN,MAX)           01240000
         AGO   .ER#INV                       ERROR- # IS INVALID        01250000
.#1      ANOP                                                           01260000
&#DFLT   SETA  &#                                                       01270000
&#MIN    SETA  &#                                                       01280000
&#MAX    SETA  &#                                                       01290000
         AGO   .COMPNUM                                                 01300000
.#3      ANOP                                                           01310000
&#DFLT   SETA  &#(1)                                                    01320000
&#MIN    SETA  &#(2)                                                    01330000
&#MAX    SETA  &#(3)                                                    01340000
         AIF   (&#DFLT LT &#MIN OR &#DFLT GT &#MAX).ER#INV              01350000
.COMPNUM ANOP                                                           01360000
         AIF   ('&PARM(1)' EQ '*' OR '&PARM(1)' EQ '"').#PNS            01370000
         AIF   (&#MAX EQ 0 AND                                         +01380000
               ('&PARM' EQ 'YES' OR '&PARM' EQ 'Y')).#PO                01390000
.#PS     ANOP                                PARM IS GIVEN VALUE        01400000
&NUM     SETA  N'&PARM                                                  01410000
         AGO   .CHK#RNG                                                 01420000
.#PO     ANOP                                NO PARM IN THIS UNIT       01430000
&NUM     SETA  0                                                        01440000
         AGO   .CHK#RNG                                                 01450000
.#PNS    ANOP                                PARM R.F.F.U.              01460000
         AIF   (N'&PARM EQ 1).#PNS1          PARM=* | PARM="            01470000
         AIF   (N'&PARM EQ 2).#PNS2          PARM=(*,N) | PARM=(",N)    01480000
         AGO   .ERPRMIN                      ERROR- PARM FORMAT INVALID 01490000
.#PNS1   ANOP                                                           01500000
&NUM     SETA  &#DFLT                                                   01510000
         AGO   .CHK#RNG                                                 01520000
.#PNS2   ANOP                                                           01530000
&NUM     SETA  &PARM(2)                                                 01540000
.CHK#RNG ANOP                                                           01550000
         AIF   (&NUM LT &#MIN OR &NUM GT &#MAX).ERNUMIN                 01560000
.*-------------                                                         01570000
.*       CHECK  IF  PARM  IS  RESERVED                                  01580000
.*-------------                                                         01590000
        AIF    ('&PARM(1)' NE '*' AND '&PARM(1)' NE '"').ORSRVCK        01600000
&RSRVPRM SETB  1                             PARM(1)=* OR "             01610000
.ORSRVCK ANOP                                                           01620000
.*-------------                                                         01630000
.*       CHECK  KEY                                                     01640000
.*-------------                                                         01650000
         AIF   (K'&KEY NE 4).ERKEYIN         ERROR- INVALID KEY         01660000
.*-------------                                                         01670000
.*       CHECK  TYPE  SPECIFIED                                         01680000
.*-------------                                                         01690000
         AIF   ('&TYPE' EQ 'R' AND NOT &RSRVPRM).ERPRMIN                01700000
.*--------------------------------------------------------------------* 01710000
.*       D E F I N E  /  M O D I F Y    T E X T    U N I T            * 01720000
.*--------------------------------------------------------------------* 01730000
.*                                                                      01740000
         AIF   (&H2DLBMF).MFE                H2DLBMF=1 -> MF=E          01750000
.*                                           H2DLBMF=0 -> MF=L          01760000
.*-------------                                                         01770000
.*       LIST  FORM : DEFINE  TEXT  UNIT                                01780000
.*-------------                                                         01790000
&H2DLPRF&UNTNAME DS    0H - TEXT UNIT ORIGIN                            01800000
.MFLKEY  ANOP                                DEFINE TEXT UNIT KEY:      01810000
         AIF   ('&PARM(1)' EQ '"').NULLKY                               01820000
         DC    AL2(X'&KEY') -  KEY                                      01830000
         AGO   .OMFLKEY                                                 01840000
.NULLKY  ANOP                                                           01850000
         DC    AL2(*-*) -  A NULL KEY                                   01860000
.OMFLKEY ANOP                                                           01870000
         DC    AL2(&NUM) -  # OF LEN/PARM COMBINATION                   01880000
         AIF   (&NUM EQ 0).MEND              TERMINATION HERE           01890000
.*                                           HANDLE LEN AND PARM:       01900000
         AIF   (&LEN LE 0).ERLEN             ERROR- LEN IS ZERO         01910000
&I       SETA  1                                                        01920000
.MFLPRML ANOP                                                           01930000
         DC    AL2(&LEN) -  LEN OF PARM                                 01940000
         AIF   (&RSRVPRM).MFLPRMR                                       01950000
         AIF   ('&PARM(&I)' EQ '').ERPRMII   ERROR- A NULL SUBPARM      01960000
         AIF   ('&TYPE' EQ 'A').MFLPRMA                                 01970000
         AIF   ('&TYPE' EQ 'C').MFLPRMC                                 01980000
         AIF   ('&TYPE' EQ 'X').MFLPRMX                                 01990000
         AIF   ('&TYPE' EQ 'B').MFLPRMB                                 02000000
         AGO   .ERTYPIN                                                 02010000
.MFLPRMA ANOP                                                           02020000
         DC    AL&LEN.(&PARM(&I)) -  PARAMETER FIELD                    02030000
         AGO   .MFLINX                                                  02040000
.MFLPRMC ANOP                                                           02050000
         DC    CL&LEN.'&PARM(&I)' -  PARAMETER FIELD                    02060000
         AGO   .MFLINX                                                  02070000
.MFLPRMX ANOP                                                           02080000
         AIF   (&SCNCTR LE 0).ERLSTIN                                   02090000
         H2DALSCN &UNTNAME,SCNCTR=&SCNCTR,PARM=&PARM(&I),RET=C          02100000
         AIF   (&H2DLERR).ERPRMII                                       02110000
         DC    XL&LEN.'&H2DLRTC' -  PARAMETER FIELD                     02120000
&K       SETA  K'&H2DLRTC                                               02130000
         AIF   (&K/2 NE &LEN).ERSCNLN                                   02140000
         AIF   (&K/2*2 NE &K).ERSCNLN                                   02150000
         AGO   .MFLINX                                                  02160000
.MFLPRMB ANOP                                                           02170000
&J       SETA  1                                                        02180000
.MFLPBZR ANOP                                                           02190000
&B(&J)   SETB  0                                                        02200000
&J       SETA  &J+1                                                     02210000
         AIF   (&J LE 8).MFLPBZR                                        02220000
&K       SETA  K'&PARM(&I)                                              02230000
&J       SETA  1                                                        02240000
.MFLPBLP ANOP                                                           02250000
&C       SETC  '&PARM(&I)'(&J,1)                                        02260000
         AIF   ('&UNTNAME' EQ 'RECFM' AND '&C' EQ 'U').MFLPBRC          02270000
         AIF   (&SCNCTR LE 0).ERLSTIN                                   02280000
         H2DALSCN &UNTNAME,SCNCTR=&SCNCTR,PARM=&C,RET=A                 02290000
         AIF   (&H2DLERR).ERPRMII                                       02300000
         AIF   (&H2DLNUL).ERSCNUL                                       02310000
&B(&H2DLRTA) SETB 1                                                     02320000
         AGO   .MFLPBIX                                                 02330000
.MFLPBRC ANOP                                SPECIAL RECFM=U HANDLING:  02340000
&B(1)    SETB  1                                                        02350000
&B(2)    SETB  1                                                        02360000
.MFLPBIX ANOP                                                           02370000
&J       SETA  &J+1                                                     02380000
         AIF   (&J LE &K).MFLPBLP                                       02390000
         DC    BL1'&B(1)&B(2)&B(3)&B(4)&B(5)&B(6)&B(7)&B(8)' -  PARM    02400000
         AGO   .MFLINX                                                  02410000
.MFLPRMR ANOP                                                           02420000
         DC    XL&LEN.'0' -  RESERVE STORAGE FOR PARM                   02430000
.MFLINX  ANOP                                                           02440000
&I       SETA  &I+1                                                     02450000
         AIF   (&I LE &NUM).MFLPRML                                     02460000
         AGO   .MEND                                                    02470000
.*-------------                                                         02480000
.*       EXECUTE  FORM : MODIFY  TEXT  UNIT                             02490000
.*-------------                                                         02500000
.MFE     ANOP                                EXECUTE FROM:              02510000
.MFEKEY  ANOP                                IF REQUIRED, MODIFY KEY    02520000
         AIF   (NOT &H2DLBKY).OMFEKEY                                   02530000
         AIF   ('&PARM(1)' EQ '"').NULEKY                               02540000
         LA    0,X'&KEY' -  ON KEY                                      02550000
         AGO   .SMFEKEY                                                 02560000
.NULEKY  ANOP                                                           02570000
         SLR   0,0 -  KEY SET TO ZERO                                   02580000
.SMFEKEY ANOP                                                           02590000
         STH   0,&H2DLPRF&UNTNAME -  STORE IN TEXT UNIT                 02600000
.OMFEKEY ANOP                                                           02610000
.MFE#    ANOP                                IF REQUIRED, MODIFY #:     02620000
         AIF   (NOT &H2DLB#).OMFE#                                      02630000
         LA    0,&NUM -  ON # OF LEN/PARM COMBINATION                   02640000
         STH   0,&H2DLPRF&UNTNAME+2 -  STORE IN TEXT UNIT               02650000
.OMFE#   ANOP                                                           02660000
         AIF   (&NUM EQ 0).MEND              TERMINATE HERE             02670000
.*                                           IF REQUIRED, MODIFY LEN    02680000
.*                                           & PARM:                    02690000
         AIF   (NOT &H2DLBLN AND NOT &H2DLBPR).MEND                     02700000
         AIF   (&LEN LE 0).ERLEN             ERROR- LEN IS ZERO         02710000
&I       SETA  1                                                        02720000
&OFFSET  SETA  4                                                        02730000
.MFEPRML ANOP                                LOOP FOR PARM & LEN        02740000
.MFELEN  ANOP                                CANAGE LEN:                02750000
         AIF   (NOT &H2DLBLN).OMFELEN                                   02760000
         LA    0,&LEN -  ON LEN OF PARM                                 02770000
         STH   0,&H2DLPRF&UNTNAME+&OFFSET -  STORE IN TEXT UNIT         02780000
.OMFELEN ANOP                                                           02790000
&OFFSET  SETA  &OFFSET+2                                                02800000
.MFEPRM  ANOP                                MODIFY PARM:               02810000
         AIF   (NOT &H2DLBPR).MFEINX                                    02820000
         AIF   (&RSRVPRM).MFEPRMR                                       02830000
         AIF   ('&PARM(&I)' EQ '').ERPRMII   ERROR- A NULL SUBPARM      02840000
         AIF   ('&TYPE' EQ 'A').MFEPRMA                                 02850000
         AIF   ('&TYPE' EQ 'C').MFEPRMC                                 02860000
         AIF   ('&TYPE' EQ 'X').MFEPRMX                                 02870000
         AIF   ('&TYPE' EQ 'B').MFEPRMB                                 02880000
         AGO   .ERTYPIN                                                 02890000
.MFEPRMC ANOP                                                           02900000
.MFEPRMA ANOP                                                           02910000
         AIF   ('&PARM(&I)'(1,1) EQ '(').MFEPAR                         02920000
         MVC   &H2DLPRF&UNTNAME+&OFFSET.(&LEN),&PARM(&I) -  MOVE PARM   02930000
         AGO   .MFEINX                                                  02940000
.MFEPAR  ANOP                                                           02950000
         MVC   &H2DLPRF&UNTNAME+&OFFSET.(&LEN),0&PARM(&I) -  MOVE PARM  02960000
         AGO   .MFEINX                                                  02970000
.MFEPRMX ANOP                                                           02980000
         AIF   (&SCNCTR LE 0).ERLSTIN                                   02990000
         H2DALSCN &UNTNAME,SCNCTR=&SCNCTR,PARM=&PARM(&I),RET=C          03000000
         AIF   (&H2DLERR).ERPRMII                                       03010000
&K       SETA  K'&H2DLRTC                                               03020000
         AIF   (&K/2 NE &LEN).ERSCNLN                                   03030000
         AIF   (&K/2*2 NE &K).ERSCNLN                                   03040000
&J       SETA  1                                                        03050000
&MVIOFST SETA  &OFFSET                                                  03060000
.MFEPXLP ANOP                                                           03070000
&C       SETC  '&H2DLRTC'(&J,2)                                         03080000
         MVI   &H2DLPRF&UNTNAME+&MVIOFST,X'&C' -  MOVE NXT BYTE IN PARM 03090000
&MVIOFST SETA  &MVIOFST+1                                               03100000
&J       SETA  &J+2                                                     03110000
         AIF   (&J LT &K).MFEPXLP                                       03120000
         AGO   .MFEINX                                                  03130000
.MFEPRMB ANOP                                                           03140000
&J       SETA  1                                                        03150000
.MFEPBZR ANOP                                                           03160000
&B(&J)   SETB  0                                                        03170000
&J       SETA  &J+1                                                     03180000
         AIF   (&J LE 8).MFEPBZR                                        03190000
&K       SETA  K'&PARM(&I)                                              03200000
&J       SETA  1                                                        03210000
.MFEPBLP ANOP                                                           03220000
&C       SETC  '&PARM(&I)'(&J,1)                                        03230000
         AIF   ('&UNTNAME' EQ 'RECFM' AND '&C' EQ 'U').MFEPBRC          03240000
         AIF   (&SCNCTR LE 0).ERLSTIN                                   03250000
         H2DALSCN &UNTNAME,SCNCTR=&SCNCTR,PARM=&C,RET=A                 03260000
         AIF   (&H2DLERR).ERPRMIN                                       03270000
         AIF   (&H2DLNUL).ERSCNUL                                       03280000
&B(&H2DLRTA) SETB 1                                                     03290000
         AGO   .MFEPBIX                                                 03300000
.MFEPBRC ANOP                                                           03310000
&B(1)    SETB  1                                                        03320000
&B(2)    SETB  1                                                        03330000
.MFEPBIX ANOP                                                           03340000
&J       SETA  &J+1                                                     03350000
         AIF   (&J LE &K).MFEPBLP                                       03360000
         MVI   &H2DLPRF&UNTNAME+&OFFSET,B'&B(1)&B(2)&B(3)&B(4)&B(5)&B(6+03370000
               )&B(7)&B(8)' -  MOVE PARM (1 BYTE) TO TEXT UNIT          03380000
         AGO   .MFEINX                                                  03390000
.MFEPRMR ANOP                                                           03400000
         XC    &H2DLPRF&UNTNAME+&OFFSET.(&LEN),&H2DLPRF&UNTNAME+&OFFSET+03410000
                -  CLEAR PARM                                           03420000
.MFEINX  ANOP                                                           03430000
&OFFSET  SETA  &OFFSET+&LEN                                             03440000
&I       SETA  &I+1                                                     03450000
         AIF   (&I LE &NUM).MFEPRML                                     03460000
         AGO   .MEND                                                    03470000
.*--------------------------------------------------------------------* 03480000
.*       E R R O R S    H A D L I N G                                 * 03490000
.*--------------------------------------------------------------------* 03500000
.ERNUMIN MNOTE 8,'*** H2DALUNT *** TEXTUNIT "&UNTNAME" - NUMBER OF SUBP+03510000
               ARAMETERS &NUM IS ILLEGAL FOR THIS UNIT'                 03520000
         MEXIT                                                          03530000
.ERPRMIN MNOTE 8,'*** H2DALUNT *** TEXTUNIT "&UNTNAME" - &PARM IS INVAL+03540000
               ID VALUE FOR THIS UNIT'                                  03550000
         MEXIT                                                          03560000
.ERPRMII MNOTE 8,'*** H2DALUNT *** TEXTUNIT "&UNTNAME" - SUBPARM &I &PA+03570000
               RM(&I) IS INVALID FOR THIS UNIT'                         03580000
         MEXIT                                                          03590000
.ERUNTIN MNOTE 32,'*** H2DALUNT *** TEXTUNIT "&UNTNAME" IS INVALID'     03600000
         MEXIT                                                          03610000
.ERKEYIN MNOTE 32,'*** H2DALUNT *** TEXTUNIT "&UNTNAME" - KEY=&KEY IS I+03620000
               NVALID'                                                  03630000
         MEXIT                                                          03640000
.ER#INV  MNOTE 32,'*** H2DALUNT *** TEXTUNIT "&UNTNAME" - #=&# IS INVAL+03650000
               ID'                                                      03660000
         MEXIT                                                          03670000
.ERLEN   MNOTE 32,'*** H2DALUNT *** HTEXTUNIT=&UNTNAME - LEN=&LEN IS IN+03680000
               VALID'                                                   03690000
         MEXIT                                                          03700000
.ERTYPIN MNOTE 32,'*** H2DALUNT *** TEXTUNIT "&UNTNAME" - TYPE=&TYPE IS+03710000
                INVALID'                                                03720000
         MEXIT                                                          03730000
.ERSCNUL MNOTE 32,'*** H2DALUNT *** TEXTUNIT "&UNTNAME" - "&C" MATCHING+03740000
                A NULL VALUE'                                           03750000
         MEXIT                                                          03760000
.ERLSTIN MNOTE 32,'*** H2DALUNT *** TEXTUNIT "&UNTNAME" - SCANNING LIST+03770000
                MISSING WHEN TYPE=X OR B'                               03780000
         MEXIT                                                          03790000
.ERSCNLN MNOTE 32,'*** H2DALUNT *** TEXTUNIT "&UNTNAME" - "&H2DLRTC" IN+03800000
               VALID FOR THIS UNIT'                                     03810000
.MEND    MEND                                                           03820000
